<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>


 '定型Ｐ_エクセルブックのプロパティを取得する
Private Sub PfixGetExcelBookProperty(myZstrPrptyVal() As String, ByVal myXobjBook As Object)
    Erase myZstrPrptyVal
    ReDim myZstrPrptyVal(9) As String
    With myXobjBook
        myZstrPrptyVal(1) = .BuiltinDocumentProperties("Title").Value       'タイトル
        myZstrPrptyVal(2) = .BuiltinDocumentProperties("Subject").Value     'サブタイトル
        myZstrPrptyVal(3) = .BuiltinDocumentProperties("Author").Value      '作成者
        myZstrPrptyVal(4) = .BuiltinDocumentProperties("Keywords").Value    'キーワード
        myZstrPrptyVal(5) = .BuiltinDocumentProperties("Comments").Value    'コメント
        myZstrPrptyVal(6) = .BuiltinDocumentProperties("Category").Value    '分類
        myZstrPrptyVal(7) = .BuiltinDocumentProperties("Manager").Value     '管理者
        myZstrPrptyVal(8) = .BuiltinDocumentProperties("Company").Value     '会社名
        myZstrPrptyVal(9) = .BuiltinDocumentProperties("Hyperlink base").Value  'ハイパーリンクの基点
    End With
End Sub

 '定型Ｐ_エクセルブックのプロパティを設定する
Private Sub PfixSetExcelBookProperty(ByVal myXobjBook As Object, ByRef myZstrPrptyVal As Variant)
    With myXobjBook
        .BuiltinDocumentProperties("Title").Value = myZstrPrptyVal(1)       'タイトル
        .BuiltinDocumentProperties("Subject").Value = myZstrPrptyVal(2)     'サブタイトル
        .BuiltinDocumentProperties("Author").Value = myZstrPrptyVal(3)      '作成者
        .BuiltinDocumentProperties("Keywords").Value = myZstrPrptyVal(4)    'キーワード
        .BuiltinDocumentProperties("Comments").Value = myZstrPrptyVal(5)    'コメント
        .BuiltinDocumentProperties("Category").Value = myZstrPrptyVal(6)    '分類
        .BuiltinDocumentProperties("Manager").Value = myZstrPrptyVal(7)     '管理者
        .BuiltinDocumentProperties("Company").Value = myZstrPrptyVal(8)     '会社名
        .BuiltinDocumentProperties("Hyperlink base").Value = myZstrPrptyVal(9)  'ハイパーリンクの基点
    End With
End Sub

 '定型Ｆ_エクセルブックの親フォルダを取得する
Private Function PfncobjGetParentFolderOfExcelBook(ByVal myXobjBook As Object) As Object
    Set PfncobjGetParentFolderOfExcelBook = Nothing
    On Error GoTo ExitPath
  Dim myXstrFullName As String: myXstrFullName = myXobjBook.FullName
    On Error GoTo 0
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrDirPath As String: myXstrDirPath = myXobjBook.Path
    Set PfncobjGetParentFolderOfExcelBook = myXobjFSO.GetFolder(myXstrDirPath)
    Set myXobjFSO = Nothing
ExitPath:
End Function

 '定型Ｆ_エクセルブックの親親フォルダを取得する
Private Function PfncobjGetGrandParentFolderOfExcelBook(ByVal myXobjBook As Object) As Object
    Set PfncobjGetGrandParentFolderOfExcelBook = Nothing
    On Error GoTo ExitPath
  Dim myXstrFullName As String: myXstrFullName = myXobjBook.FullName
    On Error GoTo 0
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrDirPath As String: myXstrDirPath = myXobjBook.Path
  Dim myXobjPrntDir As Object: Set myXobjPrntDir = myXobjFSO.GetFolder(myXstrDirPath)
    Set PfncobjGetGrandParentFolderOfExcelBook = myXobjPrntDir.ParentFolder
    Set myXobjPrntDir = Nothing: Set myXobjFSO = Nothing
ExitPath:
End Function

 '定型Ｆ_エクセルブックの親フォルダパスを取得する
Private Function PfncstrGetParentFolderPathOfExcelBook(ByVal myXobjBook As Object) As String
    PfncstrGetParentFolderPathOfExcelBook = Empty
    On Error GoTo ExitPath
  Dim myXstrFullName As String: myXstrFullName = myXobjBook.FullName
    On Error GoTo 0
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfncstrGetParentFolderPathOfExcelBook = myXobjBook.Path
    Set myXobjFSO = Nothing
ExitPath:
End Function

 '定型Ｆ_エクセルブックの親親フォルダパスを取得する
Private Function PfncstrGetGrandParentFolderPathOfExcelBook(ByVal myXobjBook As Object) As String
    PfncstrGetGrandParentFolderPathOfExcelBook = Empty
    On Error GoTo ExitPath
  Dim myXstrFullName As String: myXstrFullName = myXobjBook.FullName
    On Error GoTo 0
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrDirPath As String: myXstrDirPath = myXobjBook.Path
    PfncstrGetGrandParentFolderPathOfExcelBook _
        = myXobjFSO.GetParentFolderName(myXstrDirPath)
    Set myXobjFSO = Nothing
ExitPath:
End Function

 '定型Ｆ_指定エクセルブックが既に開いているか確認する
Private Function PfnclonCheckExcelBookOpening(ByVal myXstrFullName As String) As Long
'PfnclonCheckExcelBookOpening = 0 : 指定ブックが存在しない
'PfnclonCheckExcelBookOpening = 1 : 開いていない
'PfnclonCheckExcelBookOpening = 2 : 指定ブックと同一名の別ブックが開いている
'PfnclonCheckExcelBookOpening = 3 : 指定ブックが開いている
    PfnclonCheckExcelBookOpening = Empty
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    If myXobjFSO.FileExists(myXstrFullName) = False Then Exit Function
  Dim myXstrBookName As String
    myXstrBookName = myXobjFSO.GetFileName(myXstrFullName)
    On Error GoTo ExitPath
  Dim myXstrTmp As String: myXstrTmp = Workbooks(myXstrBookName).FullName
    On Error GoTo 0
    If myXstrTmp = myXstrFullName Then
        PfnclonCheckExcelBookOpening = 3
    Else
        PfnclonCheckExcelBookOpening = 2
    End If
    Set myXobjFSO = Nothing
    Exit Function
ExitPath:
    PfnclonCheckExcelBookOpening = 1
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_指定名のエクセルブックが既に開いていればブックオブジェクトを取得する
Private Function PfncobjGetExcelBookIfOpened(ByVal myXstrBookName As String) As Object
    Set PfncobjGetExcelBookIfOpened = Nothing
    On Error GoTo ExitPath
    Set PfncobjGetExcelBookIfOpened = Workbooks(myXstrBookName)
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｐ_エクセルブック開閉時間短縮のために機能を無効化する
Private Sub PfixDisableFunctionBeforeExcelBookOpen( _
            myXbisEnblEvnts As Boolean, _
            myXlonCal As Long, myXbisCalBfrSave As Boolean)
    myXbisEnblEvnts = False: myXlonCal = Empty: myXbisCalBfrSave = False
    With Application
    'イベントプロシジャの設定
        myXbisEnblEvnts = .EnableEvents
        If .EnableEvents = True Then .EnableEvents = False
    '再計算の設定
        myXlonCal = .Calculation
        If .Calculation = xlCalculationAutomatic _
                Or .Calculation = xlCalculationSemiautomatic Then
            .Calculation = xlCalculationManual
        End If
        myXbisCalBfrSave = .CalculateBeforeSave
        If .CalculateBeforeSave = True Then .CalculateBeforeSave = False
    End With
End Sub

 '定型Ｐ_エクセルブック開閉時間短縮のために無効化した機能を元に戻す
Private Sub PfixUndoFunctionAfterExcelBookClose( _
            ByVal myXbisEnblEvnts As Boolean, _
            ByVal myXlonCal As Long, ByVal myXbisCalBfrSave As Boolean)
    With Application
    'イベントプロシジャの設定
        If Not .EnableEvents = myXbisEnblEvnts Then .EnableEvents = myXbisEnblEvnts
    '再計算の設定
        If .Calculation = xlCalculationManual Then
            If Not .CalculateBeforeSave = myXbisCalBfrSave Then _
                .CalculateBeforeSave = myXbisCalBfrSave
        End If
        If Not .Calculation = myXlonCal Then .Calculation = myXlonCal
    End With
End Sub

 '定型Ｐ_エクセルブック開閉時間短縮のために無効化した機能をデフォルトに戻す
Private Sub PfixResetDefaultFunctionForExcelBookOpenClose()
    With Application
    'イベントプロシジャの設定
        If .EnableEvents = False Then .EnableEvents = True
    '再計算の設定
        If .Calculation = xlCalculationManual Then
            If .CalculateBeforeSave = False Then .CalculateBeforeSave = True
        End If
        If Not .Calculation = xlCalculationAutomatic Then _
            .Calculation = xlCalculationAutomatic
    End With
End Sub

 '定型Ｆ_ファイルパスを指定してエクセルブックを開く
Private Function PfncobjOpenExcelBook(ByVal myXstrFullName As String) As Object
    Set PfncobjOpenExcelBook = Nothing
    On Error Resume Next
    Set PfncobjOpenExcelBook = Workbooks.Open(Filename:=myXstrFullName, ReadOnly:=False)
    On Error GoTo 0
End Function

 '定型Ｆ_エクセルブックを開き方を指定して開く
Private Function PfncobjOpenExcelBookByOpeningMethod( _
            ByVal myXstrFullName As String, _
            Optional ByVal coXlonUpdateLinks As Long = 0, _
            Optional ByVal coXbisReadOnly As Boolean = False, _
            Optional ByVal coXlonFormat As Long = 1, _
            Optional ByVal coXstrDelimiter As String = "", _
            Optional ByVal coXbisIgnoreReadOnlyRecommended As Boolean = False) As Object
'UpdateLinks:=0 : リンク更新しない
'UpdateLinks:=3 : リンク更新する
'ReadOnly:=False : 通常
'ReadOnly:=True  : 読取り専用
'Format:=1 : ダブ区切り          : テキスト形式ファイルの開き方
'Format:=2 : コンマ(,)区切り     : テキスト形式ファイルの開き方
'Format:=3 : スペース区切り      : テキスト形式ファイルの開き方
'Format:=4 : セミコロン(;)区切り : テキスト形式ファイルの開き方
'Format:=5 : 区切り指定無し      : テキスト形式ファイルの開き方
'Format:=6 : カスタム文字区切り(引数Delimiterで指定) : テキスト形式ファイルの開き方
'IgnoreReadOnlyRecommended:=False : [読み取り専用を推奨する] メッセージを表示する
'IgnoreReadOnlyRecommended:=True  : [読み取り専用を推奨する] メッセージを表示しない
    Set PfncobjOpenExcelBookByOpeningMethod = Nothing
    On Error Resume Next
    Set PfncobjOpenExcelBookByOpeningMethod _
            = Workbooks.Open(Filename:=myXstrFullName, _
                        UpdateLinks:=coXlonUpdateLinks, _
                        ReadOnly:=coXbisReadOnly, _
                        Format:=coXlonFormat, _
                        Delimiter:=coXstrDelimiter, _
                        IgnoreReadOnlyRecommended:=coXbisIgnoreReadOnlyRecommended)
    On Error GoTo 0
End Function

 '定型Ｆ_TXT(タブ区切り)形式かCSV(カンマ区切り)形式のテキストファイルを読取り専用で開く
Private Function PfncobjOpenTxtCsvFileOnExcelByReadOnly(ByVal myXstrFilePath As String) As Object
    Set PfncobjOpenTxtCsvFileOnExcelByReadOnly = Nothing
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    If myXobjFSO.FileExists(myXstrFilePath) = False Then Exit Function
  Dim myXstrExtsn As String
    myXstrExtsn = myXobjFSO.GetExtensionName(myXstrFilePath)
  Dim myXlonFormat As Long
    Select Case LCase(myXstrExtsn)
        Case "txt": myXlonFormat = 1
        Case "csv": myXlonFormat = 2
        Case Else: myXlonFormat = 5
    End Select
    On Error Resume Next
    Set PfncobjOpenTxtCsvFileOnExcelByReadOnly _
            = Workbooks.Open(Filename:=myXstrFilePath, _
                        UpdateLinks:=0, _
                        ReadOnly:=True, _
                        Format:=myXlonFormat, _
                        IgnoreReadOnlyRecommended:=True)
    On Error GoTo 0
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_エクセルブックを読取り専用で開く
Private Function PfncobjOpenExcelBookByReadOnly(ByVal myXstrFullName As String) As Object
    Set PfncobjOpenExcelBookByReadOnly = Nothing
    On Error Resume Next
    Set PfncobjOpenExcelBookByReadOnly _
            = Workbooks.Open(Filename:=myXstrFullName, _
                                ReadOnly:=True, _
                                IgnoreReadOnlyRecommended:=True)
    On Error GoTo 0
End Function

 '定型Ｆ_開いたエクセルブックが読み取り専用か確認する
Private Function PfncbisCheckExcelBookReadOnly(ByVal myXstrBookName As String) As Boolean
'PfncbisCheckExcelBookReadOnly = True  : 読み取り専用
'PfncbisCheckExcelBookReadOnly = False : 読み取り専用ではない
    PfncbisCheckExcelBookReadOnly = False
    On Error GoTo ExitPath
  Dim myXobjBook  As Object: Set myXobjBook = Workbooks(myXstrBookName)
    On Error GoTo 0
    PfncbisCheckExcelBookReadOnly = myXobjBook.ReadOnly
    Set myXobjBook = Nothing
    Exit Function
ExitPath:
End Function

 '定型Ｐ_エクセルブックを上書き保存する
Private Sub PfixOverwriteSaveExcelBook(myXbisExitFlag As Boolean, ByVal myXstrBookName As String)
    If Application.DisplayAlerts = True Then Application.DisplayAlerts = False
    myXbisExitFlag = False
    On Error GoTo ErrPath
    Workbooks(myXstrBookName).Save
    On Error GoTo 0
    GoTo ExitPath
ErrPath:
    myXbisExitFlag = True
ExitPath:
    If Application.DisplayAlerts = False Then Application.DisplayAlerts = True
End Sub

 '定型Ｐ_エクセルブックを名前を付けて保存する
Private Sub PfixSaveAsExcelBook(myXbisExitFlag As Boolean, _
            ByVal myXstrOrgBkName As String, ByVal myXstrSaveBkName As String)
  Const coXstrXL As String = ".xl"
    If Application.DisplayAlerts = True Then Application.DisplayAlerts = False
    myXbisExitFlag = False
    If myXstrSaveBkName = "" Then GoTo ErrPath
  Dim myXstrTmp1 As String, myXstrTmp2 As String
    myXstrTmp1 = Right(myXstrSaveBkName, Len(coXstrXL) + 1)
    myXstrTmp1 = Left(myXstrTmp1, Len(coXstrXL))
    myXstrTmp2 = Right(myXstrSaveBkName, Len(coXstrXL) + 2)
    myXstrTmp2 = Left(myXstrTmp2, Len(coXstrXL))
    If LCase(myXstrTmp1) <> LCase(coXstrXL) _
        And LCase(myXstrTmp2) <> LCase(coXstrXL) Then GoTo ErrPath
    On Error GoTo ErrPath
    If myXstrSaveBkName = myXstrOrgBkName Then
        Workbooks(myXstrOrgBkName).Save
    Else
      Dim myXstrOrgDirPath As String, myXstrSaveFullName As String
        myXstrOrgDirPath = Workbooks(myXstrOrgBkName).Path
        myXstrSaveFullName = myXstrOrgDirPath & "\" & myXstrSaveBkName
        Workbooks(myXstrOrgBkName).SaveAs Filename:=myXstrSaveFullName
    End If
    On Error GoTo 0
    GoTo ExitPath
ErrPath:
    myXbisExitFlag = True
ExitPath:
    If Application.DisplayAlerts = False Then Application.DisplayAlerts = True
End Sub

 '定型Ｐ_エクセルブックを閉じる
Private Sub PfixCloseExcelBook(myXbisExitFlag As Boolean, ByVal myXstrBookName As String)
    If Application.DisplayAlerts = True Then Application.DisplayAlerts = False
    myXbisExitFlag = False
    On Error GoTo ErrPath
    Workbooks(myXstrBookName).Close SaveChanges:=False
    On Error GoTo 0
    GoTo ExitPath
ErrPath:
    myXbisExitFlag = True
ExitPath:
    If Application.DisplayAlerts = False Then Application.DisplayAlerts = True
End Sub

 '定型Ｐ_エクセルブックのプロパティの個人情報を削除する
Private Sub PfixDeleteIDInExcelBook(myXExitFlag As Boolean, ByVal myXstrFullName As String)
  Const coXstrXL As String = ".xl"
  Const coXstrXLS As String = "Microsoft Excel Worksheet"
  Const coXstrXLSX As String = "Microsoft Excel Worksheet"
  Const coXstrXLSM As String = "Microsoft Excel Macro-Enabled Worksheet"
    If Application.DisplayAlerts = True Then Application.DisplayAlerts = False
    myXExitFlag = False
    If InStr(myXstrFullName, coXstrXL) <= 0 Then GoTo ErrPath
    On Error GoTo ErrPath
  Dim myXobjBook As Object
    Set myXobjBook = Workbooks.Open(Filename:=myXstrFullName, _
                        UpdateLinks:=0, ReadOnly:=False)
    On Error GoTo 0
    myXobjBook.RemovePersonalInformation = True
    myXobjBook.Close SaveChanges:=True
    Set myXobjBook = Nothing
    GoTo ExitPath
ErrPath:
    myXExitFlag = True
ExitPath:
    If Application.DisplayAlerts = False Then Application.DisplayAlerts = True
End Sub

 '定型Ｐ_エクセルブックを自動で閉じる
'Private Sub Workbook_Open()
''→ThisWorkbookモジュールに記述
''→SetDeclaration:モジュールの宣言部分に記述
''  Public myXbisOperated As Boolean
''←EndDeclaration
'    myXbisOperated = False
'  Dim WSH As Object
'    Set WSH = CreateObject("WScript.Shell")
'    WSH.Popup "20秒後に自動的に閉じます", 5, "Title", vbInformation
'    Set WSH = Nothing
'    PfixSetTimer
'End Sub
Public Sub PfixSetTimer()
'→標準モジュールに記述
    Application.OnTime Now + TimeValue("00:00:20"), "PfixCloseMe"
End Sub
Private Sub PfixCloseMe()
'→標準モジュールに記述
  If myXbisOperated = True Then
    myXbisOperated = False
    PfixSetTimer
    Exit Sub
  End If
'//ブックの上書き保存
    ActiveWorkbook.Save
'//保存確認を避けるため保存済みにする
    ThisWorkbook.Saved = True
'//他にブックが開いていなければExcelを終了する
    If Workbooks.Count <= 1 Then Application.Quit
'//ブックをClose
    ThisWorkbook.Close False
End Sub

 '定型Ｐ_指定エクセルブックの状態を確認してオブジェクトを取得する
Private Sub PincGetExcelBookObject( _
            myXbisExitFlag As Boolean, myXobjBook As Object, _
            ByVal myXstrFullName As String)
'Includes PfnclonCheckExcelBookOpening
'Includes PfncobjOpenExcelBook
'Includes PfncobjGetFile
'Includes PfixCloseExcelBook
'Includes PfncobjGetExcelBookIfOpened
    myXbisExitFlag = False: Set myXobjBook = Nothing
  Dim myXlonCheckBookOpening As Long, myXstrBookName As String
'//指定エクセルブックが既に開いているか確認
    myXlonCheckBookOpening = PfnclonCheckExcelBookOpening(myXstrFullName)
    Select Case myXlonCheckBookOpening
        Case 0
            myXbisExitFlag = True
            Exit Sub
        Case 1
        '//ファイルパスを指定してエクセルブックを開く
            Set myXobjBook = PfncobjOpenExcelBook(myXstrFullName)
        Case 2
        '//指定ファイルのオブジェクトを取得
            Set myXobjBook = PfncobjGetFile(myXstrFullName)
            myXstrBookName = myXobjBook.Name
        '//エクセルブックを閉じる
            Call PfixCloseExcelBook(myXbisExitFlag, myXstrBookName)
            If myXbisExitFlag = True Then Exit Sub
        '//ファイルパスを指定してエクセルブックを開く
            Set myXobjBook = PfncobjOpenExcelBook(myXstrFullName)
        Case 3
        '//指定ファイルのオブジェクトを取得
            Set myXobjBook = PfncobjGetFile(myXstrFullName)
            myXstrBookName = myXobjBook.Name
        '//指定名のエクセルブックが既に開いていればブックオブジェクトを取得
            Set myXobjBook = PfncobjGetExcelBookIfOpened(myXstrBookName)
        Case Else
            Exit Sub
    End Select
End Sub

 '定型Ｐ_エクセルブックの全セル範囲をコピーして他のエクセルブックに貼り付ける
Private Sub PfixBookCellsCopyPaste(myXbisExitFlag As Boolean, _
            ByVal myXstrCpyFullName As String, ByVal myXstrCpyShtName As String, _
            ByVal myXstrPstFullName As String, ByVal myXstrPstShtName As String)
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXobjCpyBook As Object, myXobjPstBook As Object
    Set myXobjCpyBook = Workbooks.Open(Filename:=myXstrCpyFullName, ReadOnly:=True)
    Set myXobjPstBook = Workbooks.Open(Filename:=myXstrPstFullName, ReadOnly:=False)
    myXobjCpyBook.Worksheets(myXstrCpyShtName).Cells.Copy
    myXobjPstBook.Worksheets(myXstrPstShtName).Cells(1, 1).PasteSpecial xlPasteAll
    Application.CutCopyMode = False
    On Error GoTo 0
    Application.DisplayAlerts = False
    myXobjPstBook.Save
    myXobjPstBook.Close
    myXobjCpyBook.Close
    Application.DisplayAlerts = True
    Set myXobjCpyBook = Nothing: Set myXobjPstBook = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_エクセルブックのデータ全範囲のデータを読出して他のエクセルブックに書込む
Private Sub PfixBookDataRangeReadWrite(myXbisExitFlag As Boolean, _
            ByVal myXstrCpyFullName As String, ByVal myXstrCpyShtName As String, _
            ByVal myXstrPstFullName As String, ByVal myXstrPstShtName As String)
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXobjCpyBook As Object
    Set myXobjCpyBook = Workbooks.Open(Filename:=myXstrCpyFullName, ReadOnly:=True)
  Dim myZvarShtData As Variant
    With myXobjCpyBook.Worksheets(myXstrCpyShtName)
        myZvarShtData = Range(.Cells(1, 1), .Cells.SpecialCells(xlCellTypeLastCell)).Value
    End With
  Dim myXlonDataRows As Long, myXlonDataCols As Long
    myXlonDataRows = UBound(myZvarShtData, 1) - LBound(myZvarShtData, 1) + 1
    myXlonDataCols = UBound(myZvarShtData, 2) - LBound(myZvarShtData, 2) + 1
    If myXlonDataRows <= 0 Or myXlonDataCols <= 0 Then GoTo ExitPath
  Dim myXobjPstBook As Object
    Set myXobjPstBook = Workbooks.Open(Filename:=myXstrPstFullName, ReadOnly:=False)
    myXobjPstBook.Worksheets(myXstrPstShtName) _
        .Cells(1, 1).Resize(myXlonDataRows, myXlonDataCols) = myZvarShtData
    On Error GoTo 0
    Application.DisplayAlerts = False
    myXobjPstBook.Save
    myXobjPstBook.Close
    myXobjCpyBook.Close
    Application.DisplayAlerts = True
    Set myXobjCpyBook = Nothing: Set myXobjPstBook = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

      <br></pre>
    </body>
  </html>
