<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PfixGetFolderTreeWithComeBack
'Includes PincGetNotHiddenSubFileArray
'Includes PfncbisCheckFileAttribute
'Includes PincSortFileArrayDateLastModified
'Includes PfixGetFolderFileDateInformationAndObject2DArray
'Includes PfixQuickSortOfNumericObject2DArray
'Includes PfixGetFolderFileStringInformationFor1DArray

Option Explicit
Option Base 1

'◆ClassProc名_指定ディレクトリ階層内のファイル一覧を更新日時でソートして取得する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CSubLyrFileLstDateSort"
  
'//モジュール内定数
  Private Const coXlonAtrbt As Long = 2
  Private Const coXlonOrder As Long = 1
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonFileCnt As Long, myZobjFile() As Object, _
            myZstrFileName() As String, myZstrFilePath() As String
  
'//PropertySetLet_入力制御信号
  Private myXbisNotOutFileInfo As Boolean
  
'//PropertySetLet_入力データ
  Private myXstrDirPath As String, myXlonLyrs As Long, myXlonOrder As Long
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getFileCnt() As Long
    getFileCnt = myXlonFileCnt
End Property

Public Property Get getFileAry(ByVal i As Long) As Object
    Set getFileAry = myZobjFile(i)
'myZobjFile(i) : ファイル
End Property

Public Property Get getFileNameAry(ByVal i As Long) As String
    getFileNameAry = myZstrFileName(i)
'myZstrFileName(i) : ファイル名
End Property

Public Property Get getFilePathAry(ByVal i As Long) As String
    getFilePathAry = myZstrFilePath(i)
'myZstrFilePath(i) : ファイルパス
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonFileCnt = Empty: Erase myZobjFile
    Erase myZstrFileName: Erase myZstrFilePath
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letNotOutFileInfo(ByVal RHS As Boolean)
    myXbisNotOutFileInfo = RHS
'myXbisNotOutFileInfo = False : ファイルオブジェクとファイル情報を両方出力する
'myXbisNotOutFileInfo = True  : ファイルオブジェクトのみ出力してファイル情報は出力しない
End Property

Public Property Let letDirPath(ByVal RHS As String)
    myXstrDirPath = RHS
End Property

Public Property Let letLyrs(ByVal RHS As Long)
    myXlonLyrs = RHS
'myXlonLyrs = 0   : 全フォルダ階層取得
'myXlonLyrs = 1～ : 取得したいフォルダ階層数
End Property

Public Property Let letOrder(ByVal RHS As Long)
    myXlonOrder = RHS
'myXlonOrder = -1 : 最新ファイルが先頭(降順)
'myXlonOrder = 1  : 古いファイルが先頭(昇順)
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXstrDirPath = "" Then GoTo ExitPath
    If myXlonLyrs < 0 Then myXlonLyrs = 0
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//S:ディレクトリ階層内のファイル一覧を取得
    Call snsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//P:ファイル情報取得
    Call prsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SnsP_ディレクトリ階層内のファイル一覧を取得
Private Sub snsProc()
    myXbisExitFlag = False
    
'//指定ディレクトリのフォルダ階層一覧を取得
  Dim myXlonFldrTreeCnt As Long, myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long
    Call PfixGetFolderTreeWithComeBack( _
            myXlonFldrTreeCnt, myZobjFldrTree, myZlonFldrLyrNo, _
            myXstrDirPath, myXlonLyrs)
    If myXlonFldrTreeCnt <= 0 Then GoTo ExitPath
    
'//ディレクトリ階層内の隠しファイル以外のファイル一覧を取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim k As Long, n As Long: k = 0: n = L - 1
  Dim myXvarDir As Variant, myXobjDir As Object
  Dim myXlonTmpCnt As Long, myZobjTmpFile() As Object
  Dim myXvarFile As Variant, myZobjBufFile() As Object
    
    For Each myXvarDir In myZobjFldrTree
        k = k + 1
        Set myXobjDir = Nothing
        Set myXobjDir = myXvarDir
 
        Call PincGetNotHiddenSubFileArray(myXlonTmpCnt, myZobjTmpFile, myXobjDir, coXlonAtrbt)
        If myXlonTmpCnt <= 0 Then GoTo NextPath
        
        For Each myXvarFile In myZobjTmpFile
            n = n + 1: ReDim Preserve myZobjBufFile(n) As Object
            Set myZobjBufFile(n) = myXvarFile
        Next myXvarFile
        
NextPath:
    Next myXvarDir
    myXlonFileCnt = n - L + 1
    
'//ファイル一覧を更新日時順にソート
    Call PincSortFileArrayDateLastModified(myXlonFileCnt, myZobjFile, myZobjBufFile, myXlonOrder)
    
    myXlonFldrTreeCnt = Empty: Erase myZobjFldrTree: Erase myZlonFldrLyrNo
    Set myXobjDir = Nothing: Erase myZobjTmpFile: Erase myZobjBufFile
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_ファイル情報取得
Private Sub prsProc()
    myXbisExitFlag = False
    
    If myXbisNotOutFileInfo = True Then Exit Sub
    
'//ファイル一覧のファイル名を取得
  Dim myXlonInfoCnt As Long
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrFileName, _
            myZobjFile, 1)
    If myXlonInfoCnt <> myXlonFileCnt Then GoTo ExitPath
    
'//ファイル一覧のファイルパスを取得
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrFilePath, _
            myZobjFile, 2)
     If myXlonInfoCnt <> myXlonFileCnt Then GoTo ExitPath
   
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｐ_フォルダ階層一覧を取得する
Private Sub PfixGetFolderTreeWithComeBack( _
            myXlonFldrTreeCnt As Long, _
            myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long, _
            ByVal myXstrDirPath As String, _
            Optional ByVal coXlonLyrs As Long = 0)
'myZobjFldrTree(i) : フォルダ階層のオブジェクト一覧
'myZlonFldrLyrNo(i) : フォルダ階層のオブジェクト一覧の階層No
'myXstrDirPath : フォルダ階層を取得したいディレクトリパス
'coXlonLyrs = 0   : 全フォルダ階層取得
'coXlonLyrs = 1～ : 取得したいフォルダ階層数
    myXlonFldrTreeCnt = Empty: Erase myZobjFldrTree: Erase myZlonFldrLyrNo
    If myXstrDirPath = "" Then Exit Sub
'//初期ディレクトリをセット
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXobjDir As Object: Set myXobjDir = myXobjFSO.GetFolder(myXstrDirPath)
    Set myXobjFSO = Nothing
'//再帰プロシージャ開始
  Dim myXlonFldrCnt As Long, myXlonLyrNo As Long
    myXlonFldrCnt = 0: myXlonLyrNo = 0
    Call PsubFolderTreeComeBack( _
            myZobjFldrTree, myZlonFldrLyrNo, myXlonFldrCnt, _
            myXlonLyrNo, coXlonLyrs, myXobjDir)
    Set myXobjDir = Nothing
    myXlonFldrTreeCnt = myXlonFldrCnt
End Sub
Private Sub PsubFolderTreeComeBack( _
            myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long, _
            ByRef myXlonFldrCnt As Long, _
            ByVal myXlonLyrNo As Long, ByVal myXlonLyrs As Long, _
            ByVal myXobjFldr As Object)
'//再起用サブプロシージャ
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
'//フォルダを配列変数に格納
    myXlonFldrCnt = myXlonFldrCnt + 1
    myXlonLyrNo = myXlonLyrNo + 1
  Dim n As Long: n = myXlonFldrCnt + L - 1
    ReDim Preserve myZobjFldrTree(n) As Object
    ReDim Preserve myZlonFldrLyrNo(n) As Long
    Set myZobjFldrTree(n) = myXobjFldr
    myZlonFldrLyrNo(n) = myXlonLyrNo
    If (myXlonLyrs >= 1 And myXlonLyrNo >= myXlonLyrs) Then Exit Sub
'//サブフォルダを確認
  Dim myXlonSubFldrCnt As Long
    myXlonSubFldrCnt = myXobjFldr.SubFolders.Count
    If myXlonSubFldrCnt <= 0 Then Exit Sub
'//再帰を使用してサブフォルダを探索
  Dim myZobjSubFldr As Object
    For Each myZobjSubFldr In myXobjFldr.SubFolders
        Call PsubFolderTreeComeBack( _
                myZobjFldrTree, myZlonFldrLyrNo, myXlonFldrCnt, _
                myXlonLyrNo, myXlonLyrs, myZobjSubFldr)
    Next myZobjSubFldr
    Set myZobjSubFldr = Nothing
End Sub

 '定型Ｐ_指定ディレクトリ内の隠しファイル以外のサブファイルオブジェクト一覧を取得する
Private Sub PincGetNotHiddenSubFileArray( _
            myXlonFileCnt As Long, myZobjNotHdnFile() As Object, _
            ByVal myXobjDir As Object, _
            Optional ByVal coXlonAtrbt As Long = 2)
'Includes PfncbisCheckFileAttribute
'myZobjNotHdnFile(i) : サブファイル
'coXlonAtrbt = 2 : 隠しファイル
    myXlonFileCnt = Empty: Erase myZobjNotHdnFile
    If myXobjDir Is Nothing Then Exit Sub
    If myXobjDir.Files.Count <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXobjSubFile As Object, myXbisAtrChck As Boolean, n As Long: n = L - 1
    For Each myXobjSubFile In myXobjDir.Files
        myXbisAtrChck = PfncbisCheckFileAttribute(myXobjSubFile, coXlonAtrbt, True)
        If myXbisAtrChck = True Then GoTo NextPath
        n = n + 1: ReDim Preserve myZobjNotHdnFile(n) As Object
        Set myZobjNotHdnFile(n) = myXobjSubFile
NextPath:
    Next myXobjSubFile
    myXlonFileCnt = n - L + 1
    Set myXobjSubFile = Nothing
End Sub

 '定型Ｆ_指定ファイルが指定属性であることを確認する(WFDec2Bin使用)
Private Function PfncbisCheckFileAttribute( _
            ByVal myXobjFile As Object, ByVal myXlonAtrbt As Long, _
            Optional ByVal coXbisInStrOptn As Boolean = False) As Boolean
'myXlonAtrbt = 0   : 0000 0000 : 標準ファイル
'myXlonAtrbt = 1   : 0000 0001 : 読み取り専用ファイル
'myXlonAtrbt = 2   : 0000 0010 : 隠しファイル
'myXlonAtrbt = 4   : 0000 0100 : システムファイル
'myXlonAtrbt = 8   : 0000 1000 : ディスクドライブボリュームラベル(取得のみ可能)
'myXlonAtrbt = 16  : 0001 0000 : フォルダまたはディレクトリ(取得のみ可能)
'myXlonAtrbt = 32  : 0010 0000 : アーカイブファイル
'myXlonAtrbt = 64  : 0100 0000 : リンクまたはショートカット(取得のみ可能)
'myXlonAtrbt = 128 : 1000 0000 : 圧縮ファイル(取得のみ可能)
'※複数の属性を持つ場合には数値を足し合わせる
'coXbisInStrOptn = False : 値が一致すれば、myXbisCheck=True
'coXbisInStrOptn = True  : 値が含まれれば、myXbisCheck=True
    PfncbisCheckFileAttribute = False
    If myXobjFile Is Nothing Then Exit Function
    If myXlonAtrbt < 0 Then Exit Function
  Dim myXlonFileAtrbt As Long, myXlonFileAtrbtBin As String
    With myXobjFile
        myXlonFileAtrbt = .Attributes
        myXlonFileAtrbtBin = Application.WorksheetFunction.Dec2Bin(myXlonFileAtrbt, 8)
    End With
  Dim myXstrDsgntAtrbtBin As String
    Select Case coXbisInStrOptn
        Case True
            myXstrDsgntAtrbtBin = Application.WorksheetFunction.Dec2Bin(myXlonAtrbt, 8)
          Dim i As Long
            For i = Len(myXstrDsgntAtrbtBin) To 1 Step -1
                If Mid(myXstrDsgntAtrbtBin, i, 1) = 1 Then _
                    If Mid(myXlonFileAtrbtBin, i, 1) <> 1 Then Exit Function
            Next i
        Case Else
            If myXlonFileAtrbt <> myXlonAtrbt Then Exit Function
    End Select
    PfncbisCheckFileAttribute = True
End Function

 '定型Ｐ_ファイル一覧を更新日時順にソートする
Private Sub PincSortFileArrayDateLastModified( _
            myXlonFileCnt As Long, myZobjFile() As Object, _
            ByRef myZobjOrgFile() As Object, _
            Optional ByVal coXlonOrder As Long = -1)
'Includes PfixGetFolderFileDateInformationAndObject2DArray
'Includes PfixQuickSortOfNumericObject2DArray
'myZobjOrgFile(i) : 元ファイル
'coXlonOrder = 1    : 昇順 (Ascending Order)
'coXlonOrder = -1   : 降順 (Descending Order)
    myXlonFileCnt = Empty: Erase myZobjFile
'//更新日時情報一覧を取得
  Dim myXlonInfoCnt As Long, myZvarDatInfoAndObj() As Variant
    Call PfixGetFolderFileDateInformationAndObject2DArray( _
            myXlonInfoCnt, myZvarDatInfoAndObj, _
            myZobjOrgFile, 3)
    If myXlonInfoCnt <= 0 Then Exit Sub
    myXlonFileCnt = myXlonInfoCnt
'//数値によるクイックソート
  Dim L As Long, U As Long
    L = LBound(myZvarDatInfoAndObj, 1)
    U = UBound(myZvarDatInfoAndObj, 1)
    Call PfixQuickSortOfNumericObject2DArray(myZvarDatInfoAndObj, L, U, coXlonOrder)
'//オブジェクトを配列変数に格納
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long: i = myXlonFileCnt + Lo - 1
    ReDim myZobjFile(i) As Object
    For i = LBound(myZvarDatInfoAndObj, 1) To UBound(myZvarDatInfoAndObj, 1)
        Set myZobjFile(i + Lo - L) = myZvarDatInfoAndObj(i, L + 1)
    Next i
    Erase myZvarDatInfoAndObj
End Sub

 '定型Ｐ_フォルダファイルオブジェクト一覧の日付情報とオブジェクト併記の配列を取得する
Private Sub PfixGetFolderFileDateInformationAndObject2DArray( _
            myXlonInfoCnt As Long, myZvarDatInfoAndObj() As Variant, _
            ByRef myZobjFldrFile() As Object, _
            Optional ByVal coXlonDateOptn As Long = 3)
'myZvarDatInfoAndObj(i, 1) : 日付情報
'myZvarDatInfoAndObj(i, 2) : 元フォルダor元ファイル
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonDateOptn = 1  : 作成日時 (DateCreated)
'coXlonDateOptn = 2  : 最終アクセス日時 (DateLastAccessed)
'coXlonDateOptn = 3  : 最終更新日時 (DateLastModified)
    myXlonInfoCnt = Empty: Erase myZvarDatInfoAndObj
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZdatBuf() As Date, myZobjBuf() As Object
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXdatTmp As Date, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXdatTmp = Empty
        Select Case coXlonDateOptn
            Case 1: myXdatTmp = myZobjFldrFile(i).DateCreated       '作成日時
            Case 2: myXdatTmp = myZobjFldrFile(i).DateLastAccessed  'アクセス日時
            Case 3: myXdatTmp = myZobjFldrFile(i).DateLastModified  '更新日時
        End Select
        n = n + 1
        ReDim Preserve myZdatBuf(n) As Date
        ReDim Preserve myZobjBuf(n) As Object
        myZdatBuf(n) = myXdatTmp
        Set myZobjBuf(n) = myZobjFldrFile(i)
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
    ReDim myZvarDatInfoAndObj(n, Lo + 1) As Variant
    For i = LBound(myZdatBuf) To UBound(myZdatBuf)
        myZvarDatInfoAndObj(i, Lo + 0) = myZdatBuf(i)
        Set myZvarDatInfoAndObj(i, Lo + 1) = myZobjBuf(i)
    Next i
    Erase myZdatBuf: Erase myZobjBuf
ExitPath:
End Sub

 '定型Ｐ_オブジェクト配列の数値によるクイックソート
Private Sub PfixQuickSortOfNumericObject2DArray( _
            myZvarNumAndObj As Variant, _
            ByVal myXlonLbnd As Long, ByVal myXlonUbnd As Long, _
            Optional ByVal coXlonOrder As Long = 1)
'myZvarNumAndObj(i, 1) : 数値情報
'myZvarNumAndObj(i, 2) : オブジェクト
'coXlonOrder = 1    : 昇順 (Ascending Order)
'coXlonOrder = -1   : 降順 (Descending Order)
'→SetProc:定型プロシージャ使用前のコード部分に記述
'    myXlonLbnd = LBound(myZvarNumAndObj, 1)
'    myXlonUbnd = UBound(myZvarNumAndObj, 1)
'    If myXlonUbnd <= myXlonLbnd Then Exit Sub
'←EndProc
  Dim Li As Long, myXlonMed As Long, myXdouMed As Double
    Li = LBound(myZvarNumAndObj, 2)
    myXlonMed = Int((myXlonLbnd + myXlonUbnd) / 2)
    myXdouMed = myZvarNumAndObj(myXlonMed, Li)
  Dim i As Long, j As Long
    i = myXlonLbnd: j = myXlonUbnd
  Dim myXdouBufi As Double, myXdouBufj As Double, myXdouTmp As Double, myXobjTmp As Object
    Do
        myXdouBufi = myZvarNumAndObj(i, Li)
        Do While coXlonOrder * (myXdouBufi - myXdouMed) < 0
            i = i + 1: myXdouBufi = myZvarNumAndObj(i, Li)
        Loop
        myXdouBufj = myZvarNumAndObj(j, Li)
        Do While coXlonOrder * (myXdouBufj - myXdouMed) > 0
            j = j - 1: myXdouBufj = myZvarNumAndObj(j, Li)
        Loop
        If i >= j Then Exit Do
        myXdouTmp = CDbl(myZvarNumAndObj(i, Li + 0))
        myZvarNumAndObj(i, Li + 0) = myZvarNumAndObj(j, Li + 0)
        myZvarNumAndObj(j, Li + 0) = myXdouTmp
        Set myXobjTmp = myZvarNumAndObj(i, Li + 1)
        Set myZvarNumAndObj(i, Li + 1) = myZvarNumAndObj(j, Li + 1)
        Set myZvarNumAndObj(j, Li + 1) = myXobjTmp
        i = i + 1: j = j - 1
        Set myXobjTmp = Nothing
    Loop
    If myXlonLbnd < i - 1 Then _
        Call PfixQuickSortOfNumericObject2DArray( _
                myZvarNumAndObj, myXlonLbnd, i - 1, coXlonOrder)
    If myXlonUbnd > j + 1 Then _
        Call PfixQuickSortOfNumericObject2DArray( _
                myZvarNumAndObj, j + 1, myXlonUbnd, coXlonOrder)
End Sub

 '定型Ｐ_1次元配列のフォルダファイルオブジェクト一覧の文字列情報を取得する
Private Sub PfixGetFolderFileStringInformationFor1DArray( _
                myXlonInfoCnt As Long, myZstrInfo() As String, _
                ByRef myZobjFldrFile() As Object, _
                Optional ByVal coXlonStrOptn As Long = 1)
'myZstrInfo(i) : 抽出フォルダ情報
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZstrInfo
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1: ReDim Preserve myZstrInfo(n) As String
        myZstrInfo(n) = myXstrTmp
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
ExitPath:
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_指定ディレクトリ階層内のファイル一覧を更新日時でソートして取得する
'Private Sub PforNewInstanceOfCSubLyrFileLstDateSort()
'  Dim Lc As Long
'  Dim myXlonFileCnt As Long, myZobjFile() As Object, _
'        myZstrFileName() As String, myZstrFilePath() As String
'    'myZobjFile(i) : ファイル
'    'myZstrFileName(i) : ファイル名
'    'myZstrFilePath(i) : ファイルパス
''//クラス内変数への入力
'  Dim myXbisNotOutFileInfo As Boolean
'    'myXbisNotOutFileInfo = False : ファイルオブジェクとファイル情報を両方出力する
'    'myXbisNotOutFileInfo = True  : ファイルオブジェクトのみ出力してファイル情報は出力しない
'    myXbisNotOutFileInfo = False
'  Dim myXstrDirPath As String
'    myXstrDirPath = "C:\_Cdrive\_VBA\11 プログラムデータベース\04 Classモジュール"
'  Dim myXlonLyrs As Long
'    'myXlonLyrs = 0   : 全フォルダ階層取得
'    'myXlonLyrs = 1～ : 取得したいフォルダ階層数
'    myXlonLyrs = 0
'  Dim myXlonOrder As Long
'    'myXlonOrder = -1 : 最新ファイルが先頭(降順)
'    'myXlonOrder = 1  : 古いファイルが先頭(昇順)
'    myXlonOrder = -1
''//CSubLyrFileLstDateSortクラスモジュールのインスタンス生成
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long
'  Dim myXinsSubLyrFileLst As CSubLyrFileLstDateSort
'    Set myXinsSubLyrFileLst = New CSubLyrFileLstDateSort
'    With myXinsSubLyrFileLst
'    '//クラス内変数への入力
'        .letNotOutFileInfo = myXbisNotOutFileInfo
'        .letDirPath = myXstrDirPath
'        .letLyrs = myXlonLyrs
'        .letOrder = myXlonOrder
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXlonFileCnt = .getFileCnt
'        If myXlonFileCnt <= 0 Then GoTo JumpPath
'        i = myXlonFileCnt + Lo - 1
'        ReDim myZobjFile(i) As Object
'        ReDim myZstrFileName(i) As String
'        ReDim myZstrFilePath(i) As String
'        Lc = .getOptnBase
'        For i = 1 To myXlonFileCnt
'            Set myZobjFile(i + Lo - 1) = .getFileAry(i + Lc - 1)
'            myZstrFileName(i + Lo - 1) = .getFileNameAry(i + Lc - 1)
'            myZstrFilePath(i + Lo - 1) = .getFilePathAry(i + Lc - 1)
'        Next i
'    End With
'JumpPath:
'    Set myXinsSubLyrFileLst = Nothing
'    Call PforDisplayVariablesOfCSubLyrFileLstDateSort(myXlonFileCnt, myZstrFileName)    'Debug.Print
'ExitPath:
'End Sub
'Private Sub PforDisplayVariablesOfCSubLyrFileLstDateSort( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CSubLyrFileLstDateSortクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim k As Long
'    For k = LBound(myZvarField) To UBound(myZvarField)
'        Debug.Print "データ" & k & ": " & myZvarField(k)
'    Next k
'End Sub

      <br></pre>
    </body>
  </html>
