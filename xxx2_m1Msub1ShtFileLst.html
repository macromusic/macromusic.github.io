<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes CSrchShtCmnt
'Includes CSeriesData
'Includes PfixPickUpExistFilePathArray
'Includes PincPickUpExtensionMatchFilePathArray
'Includes PfncbisCheckFileExtension
'Includes PfixGetFileFor1DArray
'Includes PfixGetFolderFileStringInformationFor1DArray

Option Explicit
Option Base 1

'◆ModuleProc名_エクセルシート上に記載されたファイルパス一覧を取得する
'Rev.003
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "m1Msub1ShtFileLst"
  
'//出力データ
  Private myXlonFileCnt As Long, myZobjFile() As Object, _
            myZstrFileName() As String, myZstrFilePath() As String, _
            myXobjFilePstdFrstCell As Object
    'myZobjFile(k) : ファイルオブジェクト
    'myZstrFileName(k) : ファイル名
    'myZstrFilePath(k) : ファイルパス
  Private myXstrDirPath As String, myXobjDirPstdCell As Object, _
            myXstrExtsn As String
  
'//入力データ
  Private myXlonSrchShtNo As Long, myXobjSrchSheet As Object, _
            myXlonShtSrchCnt As Long, myZvarSrchCndtn As Variant, _
            myXbisInStrOptn As Boolean
    'myZvarSrchCndtn(i, 1) : 検索文字列
    'myZvarSrchCndtn(i, 2) : オフセット行数
    'myZvarSrchCndtn(i, 3) : オフセット列数
    'myZvarSrchCndtn(i, 4) : シート上文字列検索[=0]orコメント内文字列検索[=1]
    'myXbisInStrOptn = False : 指定文字列と一致する条件で検索する
    'myXbisInStrOptn = True  : 指定文字列を含む条件で検索する
  
  Private myXbisRowDrctn As Boolean
    'myXbisRowDrctn = True  : 行方向のみを検索
    'myXbisRowDrctn = False : 列方向のみを検索
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonTrgtValCnt As Long, myZstrTrgtVal() As String, myZobjTrgtRng() As Object
'    'myZstrTrgtVal(i) : 取得文字列
'    'myZobjTrgtRng(i) : 行列位置のセル
  
  Private myXlonBgnRow As Long, myXlonBgnCol As Long
  
  Private myXlonSrsDataCnt As Long, myZstrSrsData() As String
    'myZstrSrsData(k) : 取得文字列

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonTrgtValCnt = Empty: Erase myZstrTrgtVal: Erase myZobjTrgtRng
    myXlonBgnRow = Empty: myXlonBgnCol = Empty
    myXlonSrsDataCnt = Empty: Erase myZstrSrsData
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub callProc( _
            myXlonFileCntOUT As Long, myZobjFileOUT() As Object, _
            myZstrFileNameOUT() As String, myZstrFilePathOUT() As String, _
            myXobjFilePstdFrstCellOUT As Object, _
            myXstrDirPathOUT As String, myXobjDirPstdCellOUT As Object, _
            myXstrExtsnOUT As String, _
            ByVal myXlonSrchShtNoIN As Long, ByVal myXobjSrchSheetIN As Object, _
            ByVal myXlonShtSrchCntIN As Long, ByRef myZvarSrchCndtnIN As Variant, _
            ByVal myXbisInStrOptnIN As Boolean, _
            ByVal myXbisRowDrctnIN As Boolean)
    
'//入力変数を初期化
    myXlonSrchShtNo = Empty: Set myXobjSrchSheet = Nothing
    myXlonShtSrchCnt = Empty: myZvarSrchCndtn = Empty
    myXbisInStrOptn = False
    myXbisRowDrctn = False

'//入力変数を取り込み
    myXlonSrchShtNo = myXlonSrchShtNoIN
    Set myXobjSrchSheet = myXobjSrchSheetIN
    myXlonShtSrchCnt = myXlonShtSrchCntIN
    myZvarSrchCndtn = myZvarSrchCndtnIN
    myXbisInStrOptn = myXbisInStrOptnIN
    myXbisRowDrctn = myXbisRowDrctnIN
    
'//出力変数を初期化
    myXlonFileCntOUT = Empty
    Erase myZobjFileOUT: Erase myZstrFileNameOUT: Erase myZstrFilePathOUT
    Set myXobjFilePstdFrstCellOUT = Nothing
    myXstrDirPathOUT = Empty: Set myXobjDirPstdCellOUT = Nothing
    myXstrExtsnOUT = Empty
    
'//処理実行
    Call ctrProc
'    If myXlonFileCnt <= 0 Then Exit Sub
    
'//出力変数に格納
    myXlonFileCntOUT = myXlonFileCnt
    myZobjFileOUT() = myZobjFile()
    myZstrFileNameOUT() = myZstrFileName()
    myZstrFilePathOUT() = myZstrFilePath()
    Set myXobjFilePstdFrstCellOUT = myXobjFilePstdFrstCell
    myXstrDirPathOUT = myXstrDirPath
    Set myXobjDirPstdCellOUT = myXobjDirPstdCell
    myXstrExtsnOUT = myXstrExtsn
    
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//S:シート上の記載データを取得
    Call snsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
ExitPath:
    Call initializeModuleVariables
End Sub

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonFileCnt = Empty: Erase myZobjFile: Erase myZstrFileName: Erase myZstrFilePath
    Set myXobjFilePstdFrstCell = Nothing
    myXstrDirPath = Empty: Set myXobjDirPstdCell = Nothing
    myXstrExtsn = Empty
End Sub

'SnsP_シート上の記載データを取得
Private Sub snsProc()
    myXbisExitFlag = False
    
'//ディレクトリパスを検索して取得
    Call instCSrchShtCmnt
    If myXlonTrgtValCnt <= 0 Then GoTo ExitPath
    If myXlonTrgtValCnt <> myXlonShtSrchCnt Then GoTo ExitPath
    
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim Lc As Long: Lc = LBound(myZstrTrgtVal)
    myXstrDirPath = myZstrTrgtVal(Lc + 0)
    myXstrExtsn = myZstrTrgtVal(Lc + 1)
    Set myXobjDirPstdCell = myZobjTrgtRng(Lc + 0)
    Set myXobjFilePstdFrstCell = myZobjTrgtRng(Lc + 2)
    If myXobjFilePstdFrstCell Is Nothing Then GoTo ExitPath
    
'//ファイルパス一覧を取得
    myXlonBgnRow = myXobjFilePstdFrstCell.Row
    myXlonBgnCol = myXobjFilePstdFrstCell.Column
    Call instCSeriesData
    If myXlonSrsDataCnt <= 0 Then GoTo ExitPath
    
  Dim myZstrFilePathOrg1() As String, myZstrFilePathOrg2() As String
  Dim i As Long
    i = myXlonSrsDataCnt + Lo - 1
    ReDim myZstrFilePathOrg1(i) As String
    ReDim myZstrFilePathOrg2(i) As String
    Lc = LBound(myZstrSrsData)
    For i = 1 To myXlonSrsDataCnt
        myZstrFilePathOrg1(i + Lo - 1) = myXstrDirPath & "\" & myZstrSrsData(i + Lc - 1)
        myZstrFilePathOrg2(i + Lo - 1) = myZstrSrsData(i + Lc - 1)
    Next i
    
'//取得したファイルパス一覧から存在と拡張子で選別
  Dim myXlonExistFileCnt As Long, myZstrExistFilePath() As String
  Dim myXlonExtMtchFileCnt As Long, myZstrExtMtchFilePath() As String
    
    Call PfixPickUpExistFilePathArray( _
            myXlonExistFileCnt, myZstrExistFilePath, _
            myZstrFilePathOrg1)
    Call PincPickUpExtensionMatchFilePathArray( _
            myXlonExtMtchFileCnt, myZstrExtMtchFilePath, _
            myZstrExistFilePath, myXstrExtsn)
    If myXlonExtMtchFileCnt > 0 Then GoTo JumpPath
    
    Call PfixPickUpExistFilePathArray( _
            myXlonExistFileCnt, myZstrExistFilePath, _
            myZstrFilePathOrg2)
    Call PincPickUpExtensionMatchFilePathArray( _
            myXlonExtMtchFileCnt, myZstrExtMtchFilePath, _
            myZstrExistFilePath, myXstrExtsn)
    If myXlonExtMtchFileCnt <= 0 Then GoTo ExitPath
    
JumpPath:
'//ファイルパス一覧からファイルオブジェクト一覧を取得
    Call PfixGetFileFor1DArray(myXlonFileCnt, myZobjFile, myZstrExtMtchFilePath)

'//ファイル一覧のファイル名を取得
  Dim myXlonInfoCnt As Long
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrFileName, _
            myZobjFile, 1)
    If myXlonInfoCnt <= 0 Then GoTo ExitPath

'//ファイル一覧のファイルパスを取得
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrFilePath, _
            myZobjFile, 2)
    If myXlonInfoCnt <= 0 Then GoTo ExitPath
    
    Erase myZstrFilePathOrg1: Erase myZstrFilePathOrg2
    Erase myZstrExistFilePath: Erase myZstrExtMtchFilePath
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

'◆ClassProc名_シート上のデータから文字列を検索してデータと位置情報を取得する
Private Sub instCSrchShtCmnt()
  Dim Lc As Long
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, j As Long
  Dim myXinsSrchShtCmnt As CSrchShtCmnt: Set myXinsSrchShtCmnt = New CSrchShtCmnt
    With myXinsSrchShtCmnt
    '//文字列検索シートと検索条件を設定
        Set .setSrchSheet = myXobjSrchSheet
        .letSrchCndtn = myZvarSrchCndtn
        .letInStrOptn = myXbisInStrOptn
    '//クラス内プロシージャの実行とクラス内変数からの出力
        .exeProc
        myXlonTrgtValCnt = .getValCnt
        If myXlonTrgtValCnt <= 0 Then GoTo JumpPath
        i = myXlonTrgtValCnt + Lo - 1: j = Lo + 1
        ReDim myZstrTrgtVal(i) As String
        ReDim myZobjTrgtRng(i) As Object
        Lc = .getOptnBase
        For i = 1 To myXlonTrgtValCnt
            myZstrTrgtVal(i + Lo - 1) = .getValAry(i + Lc - 1)
            Set myZobjTrgtRng(i + Lo - 1) = .getPstnRngAry(i + Lc - 1)
        Next i
    End With
JumpPath:
    Set myXinsSrchShtCmnt = Nothing
End Sub

'◆ClassProc名_シート上の連続するデータ範囲を取得する
Private Sub instCSeriesData()
  Dim Lc As Long
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim k As Long
  Dim myXinsSeriesData As CSeriesData: Set myXinsSeriesData = New CSeriesData
    With myXinsSeriesData
    '//クラス内変数への入力
        Set .setSrchSheet = myXobjSrchSheet
        .letBgnRowCol(1) = myXlonBgnRow
        .letBgnRowCol(2) = myXlonBgnCol
        .letRowDrctn = myXbisRowDrctn
    '//クラス内プロシージャの実行とクラス内変数からの出力
        .exeProc
        myXlonSrsDataCnt = .getSrsDataCnt
        If myXlonSrsDataCnt <= 0 Then GoTo JumpPath
        k = myXlonSrsDataCnt + Lo - 1
        ReDim myZstrSrsData(k) As String
        Lc = .getOptnBase
        For k = 1 To myXlonSrsDataCnt
            myZstrSrsData(k + Lo - 1) = .getSrsDataAry(k + Lc - 1)
        Next k
    End With
JumpPath:
    Set myXinsSeriesData = Nothing
End Sub

'===============================================================================================

 '定型Ｐ_ファイルパス一覧から存在するファイルパスを抽出する
Private Sub PfixPickUpExistFilePathArray( _
            myXlonExistFileCnt As Long, myZstrExistFilePath() As String, _
            ByRef myZstrOrgFilePath() As String)
'myZstrExistFilePath(i) : 抽出ファイルパス
'myZstrOrgFilePath(i) : 元ファイルパス
    myXlonExistFileCnt = Empty: Erase myZstrExistFilePath
  Dim myXstrTmp As String, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZstrOrgFilePath): myXstrTmp = myZstrOrgFilePath(Li)
    On Error GoTo 0
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXvarPath As Variant, myXbisExistChck As Boolean, n As Long: n = Lo - 1
    For Each myXvarPath In myZstrOrgFilePath
        myXbisExistChck = myXobjFSO.FileExists(myXvarPath)
        If myXbisExistChck = False Then GoTo NextPath
        n = n + 1: ReDim Preserve myZstrExistFilePath(n) As String
        myZstrExistFilePath(n) = CStr(myXvarPath)
NextPath:
    Next myXvarPath
    myXlonExistFileCnt = n - Lo + 1
    Set myXobjFSO = Nothing
ExitPath:
End Sub

 '定型Ｐ_ファイル一覧から指定拡張子と一致するファイルパスを抽出する
Private Sub PincPickUpExtensionMatchFilePathArray( _
            myXlonExtMtchFileCnt As Long, myZstrExtMtchFilePath() As String, _
            ByRef myXstrOrgFilePath() As String, ByVal myXstrExtsn As String)
'Includes PfncbisCheckFileExtension
'myZstrExtMtchFilePath(i) : 抽出ファイルパス
'myXstrOrgFilePath(i) : 元ファイルパス
    myXlonExtMtchFileCnt = Empty: Erase myZstrExtMtchFilePath
  Dim myXstrTmp As String, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myXstrOrgFilePath): myXstrTmp = myXstrOrgFilePath(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXvarFilePath As Variant, myXbisExtChck As Boolean, n As Long: n = Lo - 1
    For Each myXvarFilePath In myXstrOrgFilePath
      Dim myXstrFilePath As String: myXstrFilePath = myXvarFilePath
        myXbisExtChck = PfncbisCheckFileExtension(myXstrFilePath, myXstrExtsn)
        If myXbisExtChck = False Then GoTo NextPath
        n = n + 1: ReDim Preserve myZstrExtMtchFilePath(n) As String
        myZstrExtMtchFilePath(n) = myXvarFilePath
NextPath:
    Next
    myXlonExtMtchFileCnt = n - Lo + 1
    myXvarFilePath = Empty
ExitPath:
End Sub

 '定型Ｆ_指定ファイルが指定拡張子であることを確認する
Private Function PfncbisCheckFileExtension( _
            ByVal myXstrFilePath As String, ByVal myXstrExtsn As String) As Boolean
'myXstrExtsn = "*" : 任意の文字列のワイルドカード
    PfncbisCheckFileExtension = False
    If myXstrFilePath = "" Then Exit Function
    If myXstrExtsn = "" Then GoTo JumpPath
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrOrgExt As String
    With myXobjFSO
        If .FileExists(myXstrFilePath) = False Then Exit Function
        myXstrOrgExt = .GetExtensionName(myXstrFilePath)
    End With
  Dim myXstrDesExt As String: myXstrDesExt = myXstrExtsn
    If Left(myXstrDesExt, 1) = "." Then myXstrDesExt = Mid(myXstrDesExt, 2)
    myXstrOrgExt = LCase(myXstrOrgExt)
    myXstrDesExt = LCase(myXstrDesExt)
    If myXstrOrgExt = myXstrDesExt Then GoTo JumpPath
  Dim myXlonPstn As Long: myXlonPstn = InStr(myXstrDesExt, "*")
    Select Case myXlonPstn
        Case 1
            If Right(myXstrOrgExt, Len(myXstrDesExt) - myXlonPstn) _
                    <> Right(myXstrDesExt, Len(myXstrDesExt) - myXlonPstn) Then _
                Exit Function
        Case Len(myXstrExtsn)
            If Left(myXstrOrgExt, Len(myXstrDesExt) - 1) _
                    <> Left(myXstrDesExt, Len(myXstrDesExt) - 1) Then _
                Exit Function
        Case Else
            If Right(myXstrOrgExt, Len(myXstrDesExt) - myXlonPstn) _
                    <> Right(myXstrDesExt, Len(myXstrDesExt) - myXlonPstn) Then _
                Exit Function
            If Left(myXstrOrgExt, myXlonPstn - 1) _
                    <> Left(myXstrDesExt, myXlonPstn - 1) Then _
                Exit Function
    End Select
    Set myXobjFSO = Nothing
JumpPath:
    PfncbisCheckFileExtension = True
End Function

 '定型Ｐ_1次元配列のファイルパスからファイルオブジェクト一覧を取得する
Private Sub PfixGetFileFor1DArray( _
                myXlonFileCnt As Long, myZobjFile() As Object, _
                ByRef myZstrFilePath() As String)
'myZobjFile(i) : ファイルオブジェクト一覧
'myZstrFilePath(i) : 元ファイルパス一覧
    myXlonFileCnt = Empty: Erase myZobjFile
  Dim myXstrTmp As String, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZstrFilePath): myXstrTmp = myZstrFilePath(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXobjTmp As Object, i As Long, n As Long: n = Lo - 1
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    For i = LBound(myZstrFilePath) To UBound(myZstrFilePath)
        myXstrTmp = Empty
        myXstrTmp = myZstrFilePath(i)
        With myXobjFSO
            If .FileExists(myXstrTmp) = False Then GoTo NextPath
            Set myXobjTmp = .GetFile(myXstrTmp)
        End With
        n = n + 1: ReDim Preserve myZobjFile(n) As Object
        Set myZobjFile(n) = myXobjTmp
NextPath:
    Next i
    myXlonFileCnt = n - Lo + 1
    Set myXobjFSO = Nothing
ExitPath:
End Sub

 '定型Ｐ_1次元配列のフォルダファイルオブジェクト一覧の文字列情報を取得する
Private Sub PfixGetFolderFileStringInformationFor1DArray( _
                myXlonInfoCnt As Long, myZstrInfo() As String, _
                ByRef myZobjFldrFile() As Object, _
                Optional ByVal coXlonStrOptn As Long = 1)
'myZstrInfo(i) : 抽出フォルダ情報
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZstrInfo
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        If myZobjFldrFile(i) Is Nothing Then GoTo NextPath
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1: ReDim Preserve myZstrInfo(n) As String
        myZstrInfo(n) = myXstrTmp
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
ExitPath:
End Sub

'DummyＰ_
Private Sub MsubDummy()
End Sub

      <br></pre>
    </body>
  </html>
