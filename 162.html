<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PfncobjGetChartSeriesCollection
'Includes PfixGetSeriesCollectionFormula
'Includes PfixGetDataRangeAddressInXYScatter
'Includes PfixGetPathBookSheetRangeAddress
'Includes PfixGetRowColumnInRangeAddress
'Includes PfncstrRangeAddressToEndRow
'Includes PfncstrRangeAddressToEndColumn
'Includes PfncstrConvertFormula
'Includes PfncstrSetSeriesFormulaInXYScatter
'Includes PfixSetSeriesCollectionFormulaInXYScatter

Option Explicit
Option Base 1

'◆ClassProc名_散布図グラフ用のセル範囲を最終行/最終列に変更する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CArngChrtRngToEnd"
  
'//PropertyGet_出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//PropertySetLet_入力データ
  Private myXobjChrt As Object, myXlonSrsClctnNo As Long

'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXobjSrsClctn As Object
  Private myXstrOrgNameAdrs As String, myXstrOrgXvalAdrs As String, myXstrOrgYvalAdrs As String
  Private myXstrNewNameAdrs As String, myXstrNewXvalAdrs As String, myXstrNewYvalAdrs As String

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    Set myXobjSrsClctn = Nothing
    myXstrOrgNameAdrs = Empty: myXstrOrgXvalAdrs = Empty: myXstrOrgYvalAdrs = Empty
    myXstrNewNameAdrs = Empty: myXstrNewXvalAdrs = Empty: myXstrNewYvalAdrs = Empty
End Sub

'-----------------------------------------------------------------------------------------------

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Set setChrt(ByVal RHS As Object)
    Set myXobjChrt = RHS
End Property

Public Property Let letSrsClctnNo(ByVal RHS As Long)
    myXlonSrsClctnNo = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
  Dim myXlonSCCnt As Long
    On Error GoTo ExitPath
    myXlonSCCnt = myXobjChrt.SeriesCollection.Count
    On Error GoTo 0
    If myXlonSrsClctnNo <= 0 Or myXlonSrsClctnNo > myXlonSCCnt Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicF_
Public Function fncbisCmpltFlag() As Boolean
    fncbisCmpltFlag = Empty
    Call ctrProc
    fncbisCmpltFlag = myXbisCmpltFlag
End Function

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//S:データ系列のセル範囲アドレスを取得
    Call snsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//P:セル範囲から最終行/最終列までのセル範囲アドレスを取得
    Call prsProc(myXstrNewXvalAdrs, myXstrOrgXvalAdrs)
    If myXbisExitFlag = True Then GoTo ExitPath
    Call prsProc(myXstrNewYvalAdrs, myXstrOrgYvalAdrs)
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
'//Run:データ系列にSERIES関数でデータ参照範囲を設定
    Call runProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SnsP_データ系列のセル範囲アドレスを取得
Private Sub snsProc()
    myXbisExitFlag = False
    
'//指定グラフの系列番号を指定してデータ系列を取得
    Set myXobjSrsClctn = PfncobjGetChartSeriesCollection(myXobjChrt, myXlonSrsClctnNo)
    If myXobjSrsClctn Is Nothing Then GoTo ExitPath
  
'//グラフの指定系列の参照範囲を取得
  Dim myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String
    Call PfixGetSeriesCollectionFormula(myXstrSrsFrmlA1, myXstrSrsFrmlR1C1, myXobjSrsClctn)
    If myXstrSrsFrmlA1 = "" Or myXstrSrsFrmlR1C1 = "" Then GoTo ExitPath
    
'//散布図グラフのデータ系列の参照式からセル範囲アドレスを取得
  Dim myXstrSrsFrml As String
    myXstrSrsFrml = myXstrSrsFrmlA1
  Dim myXlonSCNo As Long
    Call PfixGetDataRangeAddressInXYScatter( _
            myXstrOrgNameAdrs, myXstrOrgXvalAdrs, myXstrOrgYvalAdrs, myXlonSCNo, _
            myXstrSrsFrml)
    If myXlonSCNo <> myXlonSrsClctnNo Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_セル範囲から最終行/最終列までのセル範囲アドレスを取得
Private Sub prsProc(myXstrNewAdrs As String, ByVal myXstrOrgAdrs As String)
    myXbisExitFlag = False

'//セル範囲アドレスからパス[ブック]シート!セル範囲情報を分離
  Dim myXstrPathBkSht As String, myXstrRngAdrs As String
  Dim myXstrDirPath As String, myXstrBookName As String, myXstrShtName As String
    Call PfixGetPathBookSheetRangeAddress( _
            myXstrPathBkSht, myXstrRngAdrs, myXstrDirPath, myXstrBookName, myXstrShtName, _
            myXstrOrgAdrs)
    If myXstrRngAdrs = "" Then GoTo ExitPath
    If myXstrDirPath <> "" Then GoTo ExitPath
    
'//セル範囲アドレスから行列情報を取得
  Dim myXlonBgnRow As Long, myXlonBgnCol As Long, myXlonEndRow As Long, myXlonEndCol As Long
    Call PfixGetRowColumnInRangeAddress( _
            myXlonBgnRow, myXlonBgnCol, myXlonEndRow, myXlonEndCol, _
            myXstrOrgAdrs)
    If myXlonBgnRow <= 0 And myXlonBgnCol <= 0 And _
        myXlonEndRow <= 0 And myXlonEndCol <= 0 Then GoTo ExitPath
    
    If myXlonBgnRow <> myXlonEndRow And myXlonBgnCol = myXlonEndCol Then
    '//グラフ用に指定セル範囲から最終行までのセル範囲アドレスを取得
        myXstrNewAdrs = PfncstrRangeAddressToEndRow(myXstrOrgAdrs)
    
    ElseIf myXlonBgnRow = myXlonEndRow And myXlonBgnCol <> myXlonEndCol Then
    '//グラフ用に指定セル範囲から最終列までのセル範囲アドレスを取得
        myXstrNewAdrs = PfncstrRangeAddressToEndColumn(myXstrOrgAdrs)
    
    Else
        GoTo ExitPath
        
    End If
    If myXstrNewAdrs = "" Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'RunP_データ系列にSERIES関数でデータ参照範囲を設定
Private Sub runProc()
    myXbisExitFlag = False
    
'//グラフ用にセル範囲のアドレスを変換
    myXstrNewNameAdrs = PfncstrConvertFormula(myXstrOrgNameAdrs)
    myXstrNewXvalAdrs = PfncstrConvertFormula(myXstrNewXvalAdrs)
    myXstrNewYvalAdrs = PfncstrConvertFormula(myXstrNewYvalAdrs)
    
'//散布図グラフの指定データ系列設定用のSERIES関数式を設定
  Dim myXstrSrsFrml As String
    myXstrSrsFrml = PfncstrSetSeriesFormulaInXYScatter( _
                    myXlonSrsClctnNo, myXstrNewNameAdrs, myXstrNewXvalAdrs, myXstrNewYvalAdrs)

'//散布図グラフの指定データ系列にSERIES関数でデータ参照範囲を設定
    Call PfixSetSeriesCollectionFormulaInXYScatter(myXbisExitFlag, myXobjSrsClctn, myXstrSrsFrml)
    If myXbisExitFlag = True Then Exit Sub
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｆ_指定グラフの系列番号を指定してデータ系列を取得する
Private Function PfncobjGetChartSeriesCollection( _
            ByVal myXobjChrt As Object, ByVal myXlonSrsClctnNo As Long) As Object
    Set PfncobjGetChartSeriesCollection = Nothing
  Dim myXobjSrsClctn As Object
    On Error GoTo ExitPath
    Set myXobjSrsClctn = myXobjChrt.SeriesCollection(myXlonSrsClctnNo)
    On Error GoTo 0
    Set PfncobjGetChartSeriesCollection = myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
ExitPath:
End Function

 '定型Ｐ_グラフの指定系列の参照範囲を取得する
Private Sub PfixGetSeriesCollectionFormula( _
            myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String, _
            ByVal myXobjSrsClctn As Object)
    myXstrSrsFrmlA1 = Empty: myXstrSrsFrmlR1C1 = Empty
    On Error GoTo ExitPath
    myXstrSrsFrmlA1 = myXobjSrsClctn.Formula
    myXstrSrsFrmlR1C1 = myXobjSrsClctn.FormulaR1C1
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_散布図グラフのデータ系列の参照式からセル範囲アドレスを取得する
Private Sub PfixGetDataRangeAddressInXYScatter( _
            myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
            myXlonSrsClctnNo As Long, _
            ByVal myXstrSrsFrml As String)
'objSrsClctn.Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    myXstrNameAdrs = Empty: myXstrXvalAdrs = Empty: myXstrYvalAdrs = Empty
    If myXstrSrsFrml = "" Then Exit Sub
'//参照式からアドレス部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrSrsFrml, "(") + 1
    myXlonEnd = InStrRev(myXstrSrsFrml, ")") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrSndwchd = Mid(myXstrSrsFrml, myXlonBgn, myXlonLen)
'//項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号を取得
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long
    myZvarSplt = Split(myXstrSndwchd, ",")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
  Dim myXstrSCNo As String
    If myXlonSpltCnt = 4 Then
        myXstrNameAdrs = myZvarSplt(0)
        myXstrXvalAdrs = myZvarSplt(1)
        myXstrYvalAdrs = myZvarSplt(2)
        myXstrSCNo = myZvarSplt(3)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrSCNo) = True Then myXlonSrsClctnNo = CLng(myXstrSCNo)
    myZvarSplt = Empty
End Sub

 '定型Ｐ_セル範囲アドレスからパス[ブック]シート!セル範囲情報を分離する
Private Sub PfixGetPathBookSheetRangeAddress( _
            myXstrPathBkSht As String, myXstrRngAdrs As String, _
            myXstrDirPath As String, myXstrBookName As String, myXstrShtName As String, _
            ByVal myXstrRngAdrsWEx As String)
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    myXstrPathBkSht = Empty: myXstrRngAdrs = Empty
    myXstrDirPath = Empty: myXstrBookName = Empty: myXstrShtName = Empty
    If myXstrRngAdrsWEx = "" Then Exit Sub
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(0)
        myXstrRngAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//パス[ブック]シート部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrTmp, "'") + 1
    myXlonEnd = InStrRev(myXstrTmp, "'") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then
        myXstrPathBkSht = myXstrTmp
    Else
        myXstrPathBkSht = Mid(myXstrTmp, myXlonBgn, myXlonLen)
    End If
'//シート部分を取得
    myZvarSplt = Split(myXstrPathBkSht, "]")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 1 Then
        myXstrShtName = myZvarSplt(0)
    ElseIf myXlonSpltCnt = 2 Then
        myXstrShtName = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//ブック部分を取得
    myXlonBgn = InStr(myXstrPathBkSht, "[") + 1
    myXlonEnd = InStrRev(myXstrPathBkSht, "]") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrBookName = Mid(myXstrPathBkSht, myXlonBgn, myXlonLen)
'//パス部分を取得
    myZvarSplt = Split(myXstrPathBkSht, "[")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrDirPath = myZvarSplt(0)
        If myXstrDirPath <> "" Then _
            myXstrDirPath = Left(myXstrDirPath, Len(myXstrDirPath) - 1)
    Else
        Exit Sub
    End If
End Sub

 '定型Ｐ_セル範囲アドレスから行列情報を取得する
Private Sub PfixGetRowColumnInRangeAddress( _
            myXlonBgnRow As Long, myXlonBgnCol As Long, _
            myXlonEndRow As Long, myXlonEndCol As Long, _
            ByVal myXstrRngAdrsWEx As String)
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    myXlonBgnRow = Empty: myXlonBgnCol = Empty: myXlonEndRow = Empty: myXlonEndCol = Empty
    If myXstrRngAdrsWEx = "" Then Exit Sub
  Dim myXstrRngAdrs As String, myXstrBgnAdrs As String, myXstrEndAdrs As String
  Dim myXstrBgnRow As String, myXstrBgnCol As String, _
        myXstrEndRow As String, myXstrEndCol As String
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(0)
        myXstrRngAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//セル範囲をR1C1形式に変換
    On Error Resume Next
    myXstrRngAdrs = Application.ConvertFormula(Formula:=myXstrRngAdrs, _
                        FromReferenceStyle:=xlA1, ToReferenceStyle:=xlR1C1, ToAbsolute:=True)
    On Error GoTo 0
    If myXstrRngAdrs = "" Then Exit Sub
'//セル範囲の前半部分と後半部分を分離
    myZvarSplt = Split(myXstrRngAdrs, ":")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 1 Then
        myXstrBgnAdrs = myZvarSplt(0)
    ElseIf myXlonSpltCnt = 2 Then
        myXstrBgnAdrs = myZvarSplt(0)
        myXstrEndAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//最初の行と最初の列を取得
    myZvarSplt = Split(myXstrBgnAdrs, "R")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(1)
    Else
        Exit Sub
    End If
    myZvarSplt = Split(myXstrTmp, "C")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrBgnRow = myZvarSplt(0)
        myXstrBgnCol = myZvarSplt(1)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrBgnRow) = True Then myXlonBgnRow = CLng(myXstrBgnRow)
    If IsNumeric(myXstrBgnCol) = True Then myXlonBgnCol = CLng(myXstrBgnCol)
'//最後の行と最後の列を取得
    myZvarSplt = Split(myXstrEndAdrs, "R")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(1)
    Else
        Exit Sub
    End If
    myZvarSplt = Split(myXstrTmp, "C")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrEndRow = myZvarSplt(0)
        myXstrEndCol = myZvarSplt(1)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrEndRow) = True Then myXlonEndRow = CLng(myXstrEndRow)
    If IsNumeric(myXstrEndCol) = True Then myXlonEndCol = CLng(myXstrEndCol)
End Sub

 '定型Ｆ_グラフ用に指定セル範囲から最終行までのセル範囲アドレスを取得する
Private Function PfncstrRangeAddressToEndRow(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrRangeAddressToEndRow = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
  Dim myXobjSheet As Object, myXobjOrgRng As Object
    On Error Resume Next
    myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                    FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    Set myXobjOrgRng = Range(myXstrRngAdrsWEx)
    Set myXobjSheet = myXobjOrgRng.Parent
    On Error GoTo 0
    If myXobjOrgRng Is Nothing Then Exit Function
  Dim myXlonRow1 As Long, myXlonRow2 As Long, myXlonCol1 As Long, myXlonCol2 As Long
    myXlonRow1 = myXobjOrgRng.Row
    myXlonRow2 = myXlonRow1 + myXobjOrgRng.Rows.Count - 1
    myXlonCol1 = myXobjOrgRng.Column
    myXlonCol2 = myXlonCol1 + myXobjOrgRng.Columns.Count - 1
  Dim myXstrNewAdrs As String, myXobjNewRng As Object
  Dim j As Long, myXlonEndRow As Long, myXlonTmpRow As Long
    With myXobjSheet
        For j = myXlonCol1 To myXlonCol2
            myXlonTmpRow = .Cells(.Rows.Count, j).End(xlUp).Row
            If myXlonTmpRow < myXlonRow1 Then myXlonTmpRow = myXlonRow2
            If myXlonEndRow < myXlonTmpRow Then myXlonEndRow = myXlonTmpRow
        Next j
        Set myXobjNewRng = .Range(.Cells(myXlonRow1, myXlonCol1), .Cells(myXlonEndRow, myXlonCol2))
    End With
    myXstrNewAdrs = myXobjNewRng.Address(ReferenceStyle:=Application.ReferenceStyle, external:=True)
    PfncstrRangeAddressToEndRow = myXstrNewAdrs
    Set myXobjOrgRng = Nothing: Set myXobjNewRng = Nothing
ExitPath:
End Function

 '定型Ｆ_グラフ用に指定セル範囲から最終列までのセル範囲アドレスを取得する
Private Function PfncstrRangeAddressToEndColumn(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrRangeAddressToEndColumn = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
  Dim myXobjSheet As Object, myXobjOrgRng As Object
    On Error Resume Next
    myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                    FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    Set myXobjOrgRng = Range(myXstrRngAdrsWEx)
    Set myXobjSheet = myXobjOrgRng.Parent
    On Error GoTo 0
    If myXobjOrgRng Is Nothing Then Exit Function
  Dim myXlonRow1 As Long, myXlonRow2 As Long, myXlonCol1 As Long, myXlonCol2 As Long
    myXlonRow1 = myXobjOrgRng.Row
    myXlonRow2 = myXlonRow1 + myXobjOrgRng.Rows.Count - 1
    myXlonCol1 = myXobjOrgRng.Column
    myXlonCol2 = myXlonCol1 + myXobjOrgRng.Columns.Count - 1
  Dim myXstrNewAdrs As String, myXobjNewRng As Object
  Dim i As Long, myXlonEndCol As Long, myXlonTmpCol As Long
    With myXobjSheet
        For i = myXlonRow1 To myXlonRow2
            myXlonTmpCol = .Cells(i, .Columns.Count).End(xlToLeft).Column
            If myXlonTmpCol < myXlonCol1 Then myXlonTmpCol = myXlonCol2
            If myXlonEndCol < myXlonTmpCol Then myXlonEndCol = myXlonTmpCol
        Next i
        Set myXobjNewRng = .Range(.Cells(myXlonRow1, myXlonCol1), .Cells(myXlonRow2, myXlonEndCol))
    End With
    myXstrNewAdrs = myXobjNewRng.Address(ReferenceStyle:=Application.ReferenceStyle, external:=True)
    PfncstrRangeAddressToEndColumn = myXstrNewAdrs
    Set myXobjOrgRng = Nothing: Set myXobjNewRng = Nothing
ExitPath:
End Function

 '定型Ｆ_グラフ用にセル範囲のアドレスを変換する
Private Function PfncstrConvertFormula(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrConvertFormula = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
    On Error Resume Next
    If Application.ReferenceStyle = xlA1 Then
        myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                        FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    Else
        myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                        FromReferenceStyle:=xlA1, ToReferenceStyle:=xlR1C1, ToAbsolute:=True)
    End If
    On Error GoTo 0
    PfncstrConvertFormula = myXstrRngAdrsWEx
End Function

 '定型Ｆ_散布図グラフの指定データ系列設定用のSERIES関数式を設定する
Private Function PfncstrSetSeriesFormulaInXYScatter( _
            ByVal myXlonSrsClctnNo As Long, _
            ByVal myXstrNameAdrs As String, _
            ByVal myXstrXvalAdrs As String, ByVal myXstrYvalAdrs As String) As String
'Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    PfncstrSetSeriesFormulaInXYScatter = Empty
  Dim myXstrSrsFrml As String
    myXstrSrsFrml = myXstrNameAdrs & "," & myXstrXvalAdrs & "," & _
                    myXstrYvalAdrs & "," & myXlonSrsClctnNo
    myXstrSrsFrml = "=SERIES(" & myXstrSrsFrml & ")"
    PfncstrSetSeriesFormulaInXYScatter = myXstrSrsFrml
ExitPath:
End Function

 '定型Ｐ_散布図グラフの指定データ系列にSERIES関数でデータ参照範囲を設定する
Private Sub PfixSetSeriesCollectionFormulaInXYScatter( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSrsClctn As Object, ByVal myXstrSrsFrml As String)
    myXbisExitFlag = False
    If myXobjSrsClctn Is Nothing Then Exit Sub
    If myXstrSrsFrml = "" Then Exit Sub
    On Error GoTo ExitPath
    If Application.ReferenceStyle = xlA1 Then
        myXobjSrsClctn.Formula = myXstrSrsFrml
    Else
        myXobjSrsClctn.FormulaR1C1 = myXstrSrsFrml
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_散布図グラフ用のセル範囲を最終行/最終列に変更する
'Private Sub instCArngChrtRngToEnd()
'  Dim myXbisCmpltFlag As Boolean
''//クラス内変数への入力
'  Dim myXobjChrt As Object, myXlonSrsClctnNo As Long
'    Set myXobjChrt = ActiveSheet.ChartObjects(1)
'    myXlonSrsClctnNo = 1
''//CArngChrtRngToEndクラスモジュールのインスタンス生成
'  Dim myXinsArngChrtRngToEnd As CArngChrtRngToEnd
'    Set myXinsArngChrtRngToEnd = New CArngChrtRngToEnd
'    With myXinsArngChrtRngToEnd
'    '//クラス内変数への入力
'        Set .setChrt = myXobjChrt
'        .letSrsClctnNo = myXlonSrsClctnNo
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        myXbisCmpltFlag = .fncbisCmpltFlag
'    End With
'    Set myXinsArngChrtRngToEnd = Nothing
'End Sub

      <br></pre>
    </body>
  </html>
