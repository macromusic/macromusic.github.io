<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>


 '定型Ｐ_ワークシート上のデータを並べ替える(～Excel2003)
Private Sub PfixSortByWorksheetSortFor2003( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSortRng As Object, ByVal myXobjSortKey1 As Object, _
            Optional ByVal coXlonOrder1 As Long = xlAscending, _
            Optional ByVal coXlonHeader As Long = xlNo, _
            Optional ByVal coXbisMatchCase As Boolean = False, _
            Optional ByVal coXlonOrientation As Long = xlTopToBottom, _
            Optional ByVal coXlonSortMethod As Long = xlPinYin, _
            Optional ByVal coXlonDataOption1 As Long = xlSortNormal)
'Key1:= 1番目に優先される文字列/Rangeオブジェクトを指定
'Order1:=xlAscending  : 昇順に並び替え
'Order1:=xlDescending : 降順に並び替え
'Key2:= 2番目に優先される文字列/Rangeオブジェクトを指定
'Order2:=xlAscending  : 昇順に並び替え
'Order2:=xlDescending : 降順に並び替え
'Key3:= 3番目に優先される文字列/Rangeオブジェクトを指定
'Order3:=xlAscending  : 昇順に並び替え
'Order3:=xlDescending : 降順に並び替え
'Header:=xlGuess  : 先頭行をタイトル行か自動判定
'Header:=xlNo     : 先頭行をタイトル行と見なさない
'Header:=xlYes    : 先頭行をタイトル行と見なす
'MatchCase:=True  : 大文字・小文字を区別する
'MatchCase:=False : 大文字・小文字を区別しない
'Orientation:=xlTopToBottom = 1 : 行の並び替え
'Orientation:=xlLeftToRight = 2 : 列の並び替え
'SortMethod:=xlPinYin : 日本語のふりがなの順で並べ替え
'SortMethod:=xlStroke : 文字コードの順で並べ替え
'DataOption1:=xlSortNormal        = 0 : 数値データとテキストデータを別々に並べ替え
'DataOption1:=xlSortTextAsNumbers = 1 : テキストを数値データとして並べ替え
    myXbisExitFlag = False
    If myXobjSortRng Is Nothing Then GoTo ExitPath
    If myXobjSortKey1 Is Nothing Then GoTo ExitPath
    On Error GoTo ExitPath
    Call myXobjSortRng.Sort( _
            Key1:=myXobjSortKey1, _
            Order1:=coXlonOrder1, _
            Header:=coXlonHeader, _
            MatchCase:=coXbisMatchCase, _
            Orientation:=coXlonOrientation, _
            SortMethod:=coXlonSortMethod, _
            DataOption1:=coXlonDataOption1)
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_ワークシート上のデータを並べ替える(Excel2007～)
Private Sub PfixSortByWorksheetSortFor2007( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSortRng As Object, ByVal myXobjSortKey As Object, _
            Optional ByVal coXlonSortOn As Long = xlSortOnValues, _
            Optional ByVal coXlonOrder As Long = xlAscending, _
            Optional ByVal coXlonDataOption As Long = xlSortNormal, _
            Optional ByVal coXlonHeader As Long = xlNo, _
            Optional ByVal coXbisMatchCase As Boolean = False, _
            Optional ByVal coXlonOrientation As Long = xlSortColumns, _
            Optional ByVal coXlonSortMethod As Long = xlPinYin)
'Key:= 並べ替えの基準セルを指定
'SortOn:=xlSortOnValues    = 0 : セル内のデータで並べ替え
'SortOn:=xlSortOnCellColor = 1 : セルの背景色で並べ替え
'SortOn:=xlSortOnFontColor = 2 : セルの文字色で並べ替え
'SortOn:=xlSortOnIcon      = 3 : 条件付き書式のアイコンで並べ替え
'Order:=xlAscending        = 1 : 昇順に並び替え
'Order:=xlDescending       = 2 : 降順に並び替え
'DataOption:=xlSortNormal        = 0 : 数値データとテキストデータを別々に並べ替え
'DataOption:=xlSortTextAsNumbers = 1 : テキストを数値データとして並べ替え
'Header:=xlGuess = 0 : 先頭行をタイトル行か自動判定
'Header:=xlYes   = 1 : 先頭行をタイトル行と見なす
'Header:=xlNo    = 2 : 先頭行をタイトル行と見なさない
'MatchCase:=True  : 大文字・小文字を区別する
'MatchCase:=False : 大文字・小文字を区別しない
'Orientation:=xlSortColumns = 1 : 行の並び替え
'Orientation:=xlSortRows    = 2 : 列の並び替え
'SortMethod:=xlPinYin = 1 : 日本語のふりがなの順で並べ替え
'SortMethod:=xlStroke = 2 : 文字コードの順で並べ替え
    myXbisExitFlag = False
    If myXobjSortRng Is Nothing Then GoTo ExitPath
    If myXobjSortKey Is Nothing Then GoTo ExitPath
    On Error GoTo ExitPath
  Dim myXobjSheet As Object
    Set myXobjSheet = myXobjSortRng.Worksheet
    With myXobjSheet.Sort
        .SortFields.Clear
        .SortFields.Add _
            Key:=myXobjSortKey, _
            SortOn:=coXlonSortOn, _
            Order:=coXlonOrder, _
            DataOption:=coXlonDataOption
        .SetRange myXobjSortRng
        .Header = coXlonHeader
        .MatchCase = coXbisMatchCase
        .Orientation = coXlonOrientation
        .SortMethod = coXlonSortMethod
        .Apply
    End With
    On Error GoTo 0
    Set myXobjSheet = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_ワークシート上のデータを並べ替える(Excelバージョンで分岐)
Private Sub PincSortByWorksheetSortWithExcelVersionBranch( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object, ByVal myXobjSortRng As Object, _
            ByVal myXobjSortKey As Object, _
            Optional ByVal coXlonSortOn As Long = xlSortOnValues, _
            Optional ByVal coXlonOrder As Long = xlAscending, _
            Optional ByVal coXlonDataOption As Long = xlSortNormal, _
            Optional ByVal coXlonHeader As Long = xlNo, _
            Optional ByVal coXbisMatchCase As Boolean = False, _
            Optional ByVal coXlonOrientation As Long = xlSortColumns, _
            Optional ByVal coXlonSortMethod As Long = xlPinYin)
'Includes PfixSortByWorksheetSortFor2003
'Includes PfixSortByWorksheetSortFor2007
    Select Case Int(Application.Version)
        Case Is <= 11       'Excel2003以前の場合
            Call PfixSortByWorksheetSortFor2003(myXobjSortRng, myXobjSortKey, _
                    coXlonOrder, coXlonHeader, coXlonOrderCustom, coXbisMatchCase, _
                    coXlonOrientation, coXlonSortMethod, coXlonDataOption)
        Case Is > 11        'Excel2007以降の場合
            Call PfixSortByWorksheetSortFor2007(myXobjSheet, myXobjSortRng, myXobjSortKey, _
                    coXlonSortOn, coXlonOrder, coXstrCustomOrder, coXlonDataOption, _
                    coXlonHeader, coXbisMatchCase, coXlonOrientation, coXlonSortMethod)
    End Select
End Sub

 '定型Ｐ_データの区切り位置を指定して区切る
Private Sub PfixTextToColumns(myXbisExitFlag As Boolean, _
            ByVal myXobjDataRng As Object, _
            Optional ByVal coXbisConsecutiveDelimiter As Boolean = False, _
            Optional ByVal coXbisTab As Boolean = False, _
            Optional ByVal coXbisSemicolon As Boolean = False, _
            Optional ByVal coXbisComma As Boolean = False, _
            Optional ByVal coXbisSpace As Boolean = False, _
            Optional ByVal coXbisOther As Boolean = False, _
            Optional ByVal coXstrOtherChar As String = "")
'coXbisConsecutiveDelimiter = True : 連続した区切り文字を 1 つの区切り文字として認識
'coXstrOtherChar : coXbisOther = True を指定して、その時の区切り文字を指定
    myXbisExitFlag = False
  Dim myXlonFldInfoCnt As Long, myXvarFldInfo As Variant
    myXlonFldInfoCnt = Columns.Count
    ReDim myXvarFldInfo(myXlonFldInfoCnt) As Variant
  Dim i As Long
    For i = 1 To myXlonFldInfoCnt
        myXvarFldInfo(i) = Array(i, 1)      '全フィールドを文字列に指定
    Next
    On Error GoTo ExitPath
    Call myXobjDataRng.TextToColumns( _
            Destination:=myXobjDataRng, _
            DataType:=xlDelimited, _
            TextQualifier:=xlDoubleQuote, _
            ConsecutiveDelimiter:=coXbisConsecutiveDelimiter, _
            Tab:=coXbisTab, _
            Semicolon:=coXbisSemicolon, _
            Comma:=coXbisComma, _
            Space:=coXbisSpace, _
            Other:=coXbisOther, OtherChar:=coXstrOtherChar, _
            FieldInfo:=myXvarFldInfo, _
            TrailingMinusNumbers:=True)
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_エクセルブックを開かないでシートのデータを読み込む
Private Sub PfixReadSheetDataWithoutExcelBookOpening( _
            myXlonDataCnt As Long, myZvarShtData() As Variant, _
            ByVal myXstrFullName As String, _
            ByVal myXstrSheetName As String, ByVal myXlonDesCol As Long, _
            ByVal myXlonBgnRow As Long, ByVal myXlonEndRow As Long, _
            Optional ByVal coXlonBlnkLmt As Long = 100)
  Const coXlonBlnkLmtOrg As Long = 100
    If coXlonBlnkLmt < 1 Then coXlonBlnkLmt = coXlonBlnkLmtOrg
    myXlonDataCnt = Empty: Erase myZvarShtData
    If myXlonDesCol < 1 Then Exit Sub
    If myXlonBgnRow < 1 Then myXlonBgnRow = 1
    If myXlonEndRow < myXlonBgnRow Then myXlonEndRow = ActiveSheet.Rows.Count
'//任意のファイルを選択する(ファイル選択ダイアログを使用)
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    If myXobjFSO.FileExists(myXstrFullName) = False Then GoTo ExitPath
  Dim myXstrBookName As String
    myXstrBookName = myXobjFSO.GetFileName(myXstrFullName)
    Set myXobjFSO = Nothing
'//ファイルパスの表記変更→"C:～\[ブック名.xl*]"
  Dim myXstrTrgt As String
    myXstrTrgt = Replace(myXstrFullName, myXstrBookName, "[" & myXstrBookName & "]")
'//ファイルパス+シート名の表記変更→"'C:～\[ブック名.xl*]シート名'!"
    myXstrTrgt = myXstrTrgt & myXstrSheetName
    myXstrTrgt = Replace(myXstrTrgt, myXstrTrgt, "'" & myXstrTrgt & "'")
    myXstrTrgt = myXstrTrgt & "!"
'//ブックを開かずに読み込み、エラーの発生有無を確認
  Const coXstrRefRngName As String = "R1C1"
  Dim myXvarTmp As Variant
    On Error GoTo ExitPath
    myXvarTmp = ExecuteExcel4Macro(myXstrTrgt & coXstrRefRngName)
    On Error GoTo 0
'//シート上のデータの読み込み
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim i As Long, r As Long, n As Long: r = L - 1: n = 0
  Dim myXvarRet As Variant
    For i = myXlonBgnRow To myXlonEndRow
        myXvarRet = ExecuteExcel4Macro(myXstrTrgt & "R" & i & "C" & myXlonDesCol)
        r = r + 1: ReDim Preserve myZvarShtData(r) As Variant
        myZvarShtData(r) = myXvarRet
        If myXvarRet = 0 Or myXvarRet = "" Then
            n = n + 1: If n > coXlonBlnkLmt Then Exit For
        Else
            n = 0
        End If
    Next i
    myXlonDataCnt = r + (1 - L)
    Exit Sub
ExitPath:
'    MsgBox "対象ブックもしくは対象シートを読み出せません" & vbCrLf & vbCrLf & myXstrTrgt, vbExclamation  'Debug.Print
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ記載全範囲をオブジェクトに取込む
Private Sub PfixGetSheetRangeObject( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myXobjShtRng As Object, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Set myXobjShtRng = Nothing
    If myXobjSheet Is Nothing Then Exit Sub
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ全範囲のデータを配列に取込む
Private Sub PfixGetSheetRangeData( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZstrShtData() As String, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
'myZstrShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZstrShtData
    If myXobjSheet Is Nothing Then Exit Sub
'//シート上の指定範囲をオブジェクト配列に取込む
  Dim myXobjShtRng As Object
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
'//オブジェクト配列からデータを取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZstrShtData(i, j) As String
    myZvarRngData = myXobjShtRng.Value
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZstrShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZstrShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    Set myXobjShtRng = Nothing: myZvarRngData = Empty
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ全範囲のデータをVariant配列に取込む
Private Sub PfixGetSheetRangeDataVariantArray( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData() As Variant, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZvarShtData
    If myXobjSheet Is Nothing Then Exit Sub
'//シート上の指定範囲をオブジェクト配列に取込む
  Dim myXobjShtRng As Object
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
'//オブジェクト配列からデータを取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZvarShtData(i, j) As Variant
    myZvarRngData = myXobjShtRng.Value
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZvarShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZvarShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    Set myXobjShtRng = Nothing: myZvarRngData = Empty
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ全範囲のデータをVariant変数に取込む
Private Sub PfixGetSheetRangeDataVariant( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData As Variant, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: myZvarShtData = Empty
    If myXobjSheet Is Nothing Then Exit Sub
'//シート上の指定範囲をオブジェクト配列に取込む
  Dim myXobjShtRng As Object
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
'//オブジェクト配列からデータを取得
    myZvarShtData = myXobjShtRng.Value
    Set myXobjShtRng = Nothing
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ全範囲の計算式をA1形式でVariant配列に取込む
Private Sub PfixGetSheetRangeFormulaA1VariantArray( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData() As Variant, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZvarShtData
    If myXobjSheet Is Nothing Then Exit Sub
'//シート上の指定範囲をオブジェクト配列に取込む
  Dim myXobjShtRng As Object
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
'//オブジェクト配列からデータを取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZvarShtData(i, j) As Variant
    myZvarRngData = myXobjShtRng.Formula
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZvarShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZvarShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    Set myXobjShtRng = Nothing: myZvarRngData = Empty
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ全範囲の計算式をR1C1形式でVariant配列に取込む
Private Sub PfixGetSheetRangeFormulaR1C1VariantArray( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData() As Variant, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZvarShtData
    If myXobjSheet Is Nothing Then Exit Sub
'//シート上の指定範囲をオブジェクト配列に取込む
  Dim myXobjShtRng As Object
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
'//オブジェクト配列からデータを取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZvarShtData(i, j) As Variant
    myZvarRngData = myXobjShtRng.FormulaR1C1
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZvarShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZvarShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    Set myXobjShtRng = Nothing: myZvarRngData = Empty
End Sub

 '定型Ｐ_シート上の指定範囲またはデータ全範囲のコメントを配列に取込む
Private Sub PfixGetSheetRangeCommentText( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZstrCmntData() As String, _
            ByVal myXobjSheet As Object, _
            ByVal myXobjFrstCell As Object, ByVal myXobjLastCell As Object)
'myZstrCmntData(i, j) : コメントデータ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZstrCmntData
    If myXobjSheet Is Nothing Then Exit Sub
'//シート上の指定範囲をオブジェクト配列に取込む
  Dim myXobjShtRng As Object
    If myXobjFrstCell Is Nothing Then Set myXobjFrstCell = myXobjSheet.Cells(1, 1)
    If myXobjLastCell Is Nothing Then _
        Set myXobjLastCell = myXobjSheet.Cells.SpecialCells(xlCellTypeLastCell)
    Set myXobjShtRng = myXobjSheet.Range(myXobjFrstCell, myXobjLastCell)
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
'//オブジェクト配列からデータを取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXobjTmpCell As Object, myXstrTmpCmnt As String, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZstrCmntData(i, j) As String
    On Error Resume Next
    For j = 1 To myXlonColCnt
        For i = 1 To myXlonRowCnt
            Set myXobjTmpCell = myXobjShtRng.Cells(i, j)
            If TypeName(myXobjTmpCell.Comment) = "Comment" Then
                myXstrTmpCmnt = myXobjTmpCell.Comment.Text
            Else
                myXstrTmpCmnt = ""
            End If
            myZstrCmntData(i + L - 1, j + L - 1) = myXstrTmpCmnt
        Next i
    Next j
    On Error GoTo 0
    Set myXobjShtRng = Nothing: Set myXobjTmpCell = Nothing
End Sub

 '定型Ｐ_シート上の指定範囲のデータを配列に取込む
Private Sub PfixGetSheetPartRangeData( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZstrShtData() As String, _
            ByVal myXobjShtRng As Object)
'myZstrShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZstrShtData
    If myXobjShtRng Is Nothing Then Exit Sub
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZstrShtData(i, j) As String
    myZvarRngData = myXobjShtRng.Value
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZstrShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZstrShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    myZvarRngData = Empty
End Sub

 '定型Ｐ_シート上の指定範囲のデータをVariant配列に取込む
Private Sub PfixGetSheetPartRangeDataVariantArray( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData() As Variant, _
            ByVal myXobjShtRng As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZvarShtData
    If myXobjShtRng Is Nothing Then Exit Sub
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZvarShtData(i, j) As Variant
    myZvarRngData = myXobjShtRng.Value
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZvarShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZvarShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    myZvarRngData = Empty
End Sub

 '定型Ｐ_シート上の指定範囲のデータをVariant変数に取込む
Private Sub PfixGetSheetPartRangeDataVariant( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData As Variant, _
            ByVal myXobjShtRng As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZvarShtData
    If myXobjShtRng Is Nothing Then Exit Sub
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
    myZvarShtData = myXobjShtRng.Value
End Sub

 '定型Ｐ_2次元配列変数のデータ一覧をシートに一括書出しする
Private Sub PfixWriteArrayVariablesToWorkSheet( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjFrstCell As Object, ByRef myZvarOrgData As Variant, _
            Optional ByVal coXbisEachWrtON As Boolean = False)
    myXbisExitFlag = False
    If myXobjFrstCell Is Nothing Then GoTo ExitPath
  Dim Li As Long, myXvarTmp As Variant
    On Error GoTo ExitPath
    Li = LBound(myZvarOrgData, 1): myXvarTmp = myZvarOrgData(Li, Li)
    On Error GoTo 0
'//指定範囲内に配列変数が書出し可能かチェック
  Dim myXlonDataRows As Long, myXlonDataCols As Long
    myXlonDataRows = UBound(myZvarOrgData, 1) - LBound(myZvarOrgData, 1) + 1
    myXlonDataCols = UBound(myZvarOrgData, 2) - LBound(myZvarOrgData, 2) + 1
    If myXlonDataRows <= 0 Or myXlonDataCols <= 0 Then GoTo ExitPath
  Dim myXlonRngRows As Long, myXlonRngCols As Long
    With myXobjFrstCell
        myXlonRngRows = .Parent.Rows.Count - .Row + 1
        myXlonRngCols = .Parent.Columns.Count - .Column + 1
        If myXlonRngRows < myXlonDataRows Then GoTo ExitPath
        If myXlonRngCols < myXlonDataCols Then GoTo ExitPath
    End With
'//格納データをシートに書出し
    On Error GoTo ErrPath
    If coXbisEachWrtON = False Then
        myXobjFrstCell.Resize(myXlonDataRows, myXlonDataCols) = myZvarOrgData
    Else
      Dim i As Long, j As Long
        For i = LBound(myZvarOrgData, 1) To UBound(myZvarOrgData, 1)
            For j = LBound(myZvarOrgData, 2) To UBound(myZvarOrgData, 2)
                myXobjFrstCell.Offset(i - Li, j - Li).Value = myZvarOrgData(i, j)
            Next j
        Next i
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
    Exit Sub
ErrPath:
'//セルの書式を文字列に設定
    myXobjFrstCell.Resize(myXlonDataRows, myXlonDataCols).NumberFormatLocal = "@"
    Resume
End Sub

 '定型Ｐ_オリジナル行をコピーしてデータ数分貼り付けする
Private Sub PfixCopyPasteRow( _
        myXbisExitFlag As Boolean, _
        ByVal myXobjSheet As Object, _
        ByVal myXlonCpyBgnRow As Long, ByVal myXlonCpyRowCnt As Long, _
        ByVal myXlonPstBgnRow As Long, ByVal myXlonPstCnt As Long)
    myXbisExitFlag = False
    If myXobjSheet Is Nothing Then GoTo ExitPath
    myXobjSheet.Activate
    If myXlonCpyBgnRow <= 0 Or myXlonCpyRowCnt <= 0 Then GoTo ExitPath
    If myXlonPstBgnRow <= 0 Or myXlonPstCnt <= 0 Then GoTo ExitPath
  Dim myXlonCpyEndRow As Long, myXstrCpyRowRng As String
    myXlonCpyEndRow = myXlonCpyBgnRow + myXlonCpyRowCnt - 1
    myXstrCpyRowRng = myXlonCpyBgnRow & ":" & myXlonCpyEndRow
  Dim myXobjCpyRow As Object
    On Error GoTo ExitPath
    Set myXobjCpyRow = myXobjSheet.Rows(myXstrCpyRowRng)
    On Error GoTo 0
  Dim myXobjPstRow As Object, myXlonPstRowPstn As Long, i As Long
    For i = 1 To myXlonPstCnt
        Set myXobjCpyRow = myXobjSheet.Rows(myXstrCpyRowRng)
        myXobjCpyRow.Copy
        myXlonPstRowPstn = myXlonPstBgnRow + (i - 1) * myXlonCpyRowCnt
        Set myXobjPstRow = myXobjSheet.Rows(myXlonPstRowPstn)
        myXobjPstRow.PasteSpecial xlAll
        myXobjPstRow.Select: ActiveSheet.Paste
    Next i
    Application.CutCopyMode = False
    Set myXobjCpyRow = Nothing: Set myXobjPstRow = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_オリジナル行をコピーしてデータ数分挿入する
Private Sub PfixCopyInsertRow( _
        myXbisExitFlag As Boolean, _
        ByVal myXobjSheet As Object, _
        ByVal myXlonCpyBgnRow As Long, ByVal myXlonCpyRowCnt As Long, _
        ByVal myXlonInsrtBgnRow As Long, ByVal myXlonInsrtCnt As Long)
    myXbisExitFlag = False
    If myXobjSheet Is Nothing Then GoTo ExitPath
    If myXlonCpyBgnRow <= 0 Or myXlonCpyRowCnt <= 0 Then GoTo ExitPath
    If myXlonInsrtBgnRow <= 0 Or myXlonInsrtCnt <= 0 Then GoTo ExitPath
  Dim myXlonCpyEndRow As Long, myXstrCpyRowRng As String
    myXlonCpyEndRow = myXlonCpyBgnRow + myXlonCpyRowCnt - 1
    myXstrCpyRowRng = myXlonCpyBgnRow & ":" & myXlonCpyEndRow
  Dim myXobjCpyRow As Object
    On Error GoTo ExitPath
    Set myXobjCpyRow = myXobjSheet.Rows(myXstrCpyRowRng)
    On Error GoTo 0
  Dim myXobjInsrtRow As Object, myXlonInsrtRowPstn As Long, i As Long
    For i = 1 To myXlonInsrtCnt
        Set myXobjCpyRow = myXobjSheet.Rows(myXstrCpyRowRng)
        myXobjCpyRow.Copy
        myXlonInsrtRowPstn = myXlonInsrtBgnRow + (i - 1) * myXlonCpyRowCnt
        Set myXobjInsrtRow = myXobjSheet.Rows(myXlonInsrtRowPstn)
        myXobjInsrtRow.Insert Shift:=xlDown
    Next i
    Application.CutCopyMode = False
    Set myXobjCpyRow = Nothing: Set myXobjInsrtRow = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_エクセルシート上の関数を強制的に再計算させる
Public Sub PfixCalculateFull()
    Application.CalculateFull
End Sub

 '定型Ｐ_エクセルシート上の関数を強制的に再計算させる(SendKeys使用)
Public Sub PfixCalculateFullBySendKeys()
'Ctrl+Alt+F9 : ^%{F9}
    Call SendKeys("^%{F9}", True)
End Sub

      <br></pre>
    </body>
  </html>
