<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfncbisIsMatrixNumeric
'Includes PfixArrayPartColumnNumeric
'Includes PfncdouNotchFilter

Option Explicit
Option Base 1

'◆ClassProc名_データ配列をNotchFilter処理する
'Rev.004
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CNtchFltr"
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonStrtRow As Long, myXlonLastRow As Long
  Private myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
    'myZdouCalData(i, 1) : 計算処理後データ配列
  
'//PropertySetLet_入力制御信号
  Private myXlonCalDataRngOptn As Long
  
'//PropertySetLet_入力データ
  Private myZdouOrgData() As Double
    'myZdouOrgData(i, 1) : 元データ配列
  Private myXlonBgnRow As Long, myXlonEndRow As Long
  Private myXdouTs As Double, myXdouFo As Double, myXdouBo As Double
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonInptRowCnt As Long, myXlonInptColCnt As Long, myZdouInptData() As Double
  Private myXlonTmpDataCnt As Long, myZdouTmpData() As Double
  Private myXlonStrtNo As Long, myXlonLastNo As Long
  Private myXlonCalStrtNo As Long, myXlonCalLastNo As Long
  Private myXlonCalTmpCnt As Long, myZdouCalTmp() As Double

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonInptRowCnt = Empty: myXlonInptColCnt = Empty: Erase myZdouInptData
    myXlonTmpDataCnt = Empty: Erase myZdouTmpData
    myXlonStrtNo = Empty: myXlonLastNo = Empty
    myXlonCalStrtNo = Empty: myXlonCalLastNo = Empty
    myXlonCalTmpCnt = Empty: Erase myZdouCalTmp
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getStrtRow() As Long
    getStrtRow = myXlonStrtRow
End Property

Public Property Get getLastRow() As Long
    getLastRow = myXlonLastRow
End Property

Public Property Get getCalRowCnt() As Long
    getCalRowCnt = myXlonCalRowCnt
End Property

Public Property Get getCalColCnt() As Long
    getCalColCnt = myXlonCalColCnt
End Property

Public Property Get getCalData(ByVal i As Long, ByVal j As Long) As Double
    getCalData = myZdouCalData(i, j)
'myZdouCalData(i, j) : 計算処理後データ配列
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonStrtRow = Empty: myXlonLastRow = Empty
    myXlonCalRowCnt = Empty: myXlonCalColCnt = Empty: Erase myZdouCalData
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letCalDataRngOptn(ByVal RHS As Long)
    myXlonCalDataRngOptn = RHS
'myXlonCalDataRngOptn = 1 : 全データ範囲で取得
'myXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'myXlonCalDataRngOptn = 3 : 有効データ範囲で取得
End Property

Public Property Let letOrgData(ByRef RHS() As Double)
    myZdouOrgData() = RHS()
'myZdouOrgData(i, j) : 元データ配列
End Property

Public Property Let letBgnRow(ByVal RHS As Long)
    myXlonBgnRow = RHS
End Property

Public Property Let letEndRow(ByVal RHS As Long)
    myXlonEndRow = RHS
End Property

Public Property Let letTs(ByVal RHS As Double)
    myXdouTs = RHS
'myXdouTs[sec] : 元データ配列のサンプリング時間
End Property

Public Property Let letFo(ByVal RHS As Double)
    myXdouFo = RHS
'myXdouFo[Hz]  : NotchFilterのノッチ周波数
End Property

Public Property Let letBo(ByVal RHS As Double)
    myXdouBo = RHS
'myXdouBo[Hz]  : NotchFilterのノッチの帯域幅
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXlonCalDataRngOptn < 1 Or myXlonCalDataRngOptn > 3 Then myXlonCalDataRngOptn = 1
    
    If PfncbisIsMatrixNumeric(myZdouOrgData) = False Then GoTo ExitPath
    
  Dim L As Long, i As Long, j As Long
    L = LBound(myZdouOrgData, 1)
    myXlonInptRowCnt = UBound(myZdouOrgData, 1) - L + 1
    myXlonInptColCnt = UBound(myZdouOrgData, 2) - L + 1
    ReDim myZdouInptData(myXlonInptRowCnt, myXlonInptColCnt) As Double
    For j = LBound(myZdouOrgData, 2) To UBound(myZdouOrgData, 2)
        For i = LBound(myZdouOrgData, 1) To UBound(myZdouOrgData, 1)
            myZdouInptData(i - L + 1, j - L + 1) = myZdouOrgData(i, j)
        Next i
    Next j
    
    If myXlonBgnRow < LBound(myZdouInptData, 1) Then myXlonBgnRow = LBound(myZdouInptData, 1)
    If myXlonEndRow < myXlonBgnRow Or myXlonEndRow > UBound(myZdouInptData, 1) Then _
        myXlonEndRow = UBound(myZdouInptData, 1)
    
    If myXdouFo * myXdouBo * myXdouTs <= 0 Then GoTo ExitPath
    If myXdouBo > myXdouFo Then GoTo ExitPath
    If myXdouFo * myXdouTs = 0.25 Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//C:データ用変数を設定
    Call setDataVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//C:制御用変数を設定
    Call setControlVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag

'//P:計算実行:NotchFilter
    Call prsCal
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
'//C:出力データを格納
    Call setOutputVariables
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SetP_データ用変数を設定する
Private Sub setDataVariables()
    myXbisExitFlag = False
    
  Dim myXlonBgnNo As Long, myXlonEndNo As Long
    myXlonBgnNo = LBound(myZdouInptData, 1)
    myXlonEndNo = UBound(myZdouInptData, 1)
    
'//2次元の数値配列の指定列から一部を1次元配列で取得
  Const coXlonCalCol As Long = 1
  Const coXlonCalStep As Long = 1
    Call PfixArrayPartColumnNumeric( _
            myXlonTmpDataCnt, myZdouTmpData, _
            myZdouInptData, coXlonCalCol, myXlonBgnNo, myXlonEndNo, coXlonCalStep)
    If myXlonTmpDataCnt <= 0 Then GoTo ExitPath
    
    myXlonStrtNo = myXlonBgnNo
    myXlonLastNo = myXlonEndNo
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables()
    myXbisExitFlag = False
    
'//計算が有効な最初と最後の位置を設定
    myXlonStrtNo = myXlonStrtNo + 1
    myXlonLastNo = myXlonLastNo
    If myXlonLastNo < myXlonStrtNo Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:NotchFilter
Private Sub prsCal()
    myXbisExitFlag = False
    
'//計算処理:NotchFilter
  Dim i As Long, n As Long: n = 0
  Dim myXlonRetCnt As Long, myZdouRet() As Double
  Dim myXdouYnm1 As Double, myXdouYnm2 As Double, _
        myXdouXn As Double, myXdouXnm1 As Double, myXdouXnm2 As Double

    myXlonRetCnt = UBound(myZdouTmpData) - LBound(myZdouTmpData) + 1
    ReDim myZdouRet(myXlonRetCnt) As Double
    
    For i = LBound(myZdouTmpData) To UBound(myZdouTmpData)
        n = n + 1
        If i = LBound(myZdouTmpData) Then
            myXdouYnm1 = myZdouTmpData(i)
            myXdouYnm2 = myZdouTmpData(i)
            myXdouXnm1 = myZdouTmpData(i)
            myXdouXnm2 = myZdouTmpData(i)
        ElseIf i = LBound(myZdouTmpData) + 1 Then
            myXdouYnm1 = myZdouRet(n - 1)
            myXdouYnm2 = myZdouTmpData(i - 1)
            myXdouXnm1 = myZdouTmpData(i - 1)
            myXdouXnm2 = myZdouTmpData(i - 1)
        Else
            myXdouYnm1 = myZdouRet(n - 1)
            myXdouYnm2 = myZdouRet(n - 2)
            myXdouXnm1 = myZdouTmpData(i - 1)
            myXdouXnm2 = myZdouTmpData(i - 2)
        End If
        myXdouXn = myZdouTmpData(i)
        myZdouRet(n) = PfncdouNotchFilter( _
                        myXdouYnm1, myXdouYnm2, myXdouXn, myXdouXnm1, myXdouXnm2, _
                        myXdouTs, myXdouFo, myXdouBo)
    Next i
    If n <> myXlonRetCnt Then GoTo ExitPath
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Select Case myXlonCalDataRngOptn
        Case 1
            myXlonCalStrtNo = LBound(myZdouTmpData)
            myXlonCalLastNo = UBound(myZdouTmpData)
        Case 2
            myXlonCalStrtNo = myXlonBgnRow
            myXlonCalLastNo = myXlonEndRow
        Case Else
            myXlonCalStrtNo = myXlonStrtNo
            myXlonCalLastNo = myXlonLastNo
    End Select
    
'//データ抽出
    myXlonCalTmpCnt = myXlonCalLastNo - myXlonCalStrtNo + 1
    ReDim myZdouCalTmp(myXlonCalTmpCnt) As Double
    
    n = 0
    For i = myXlonCalStrtNo To myXlonCalLastNo
        n = n + 1
        myZdouCalTmp(n) = myZdouRet(i)
    Next i
    If n <> myXlonCalTmpCnt Then GoTo ExitPath
    
    Erase myZdouRet
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_出力データを格納する
Private Sub setOutputVariables()
    myXbisExitFlag = False
    
    myXlonStrtRow = myXlonCalStrtNo
    myXlonLastRow = myXlonCalLastNo
    
  Dim i As Long
    myXlonCalRowCnt = myXlonCalTmpCnt
    myXlonCalColCnt = 1
    ReDim myZdouCalData(myXlonCalRowCnt, myXlonCalColCnt) As Double
    For i = LBound(myZdouCalTmp) To UBound(myZdouCalTmp)
        myZdouCalData(i, 1) = myZdouCalTmp(i)
    Next i
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｆ_指定行列が数値行列かどうかをチェックする
Private Function PfncbisIsMatrixNumeric(ByRef myZvarMtrx As Variant) As Boolean
    PfncbisIsMatrixNumeric = False
  Dim myXvarTmp As Variant, myXdouTmp As Double, L1 As Long, L2 As Long
    On Error GoTo ExitPath
    L1 = LBound(myZvarMtrx, 1): L2 = LBound(myZvarMtrx, 2)
    myXdouTmp = myZvarMtrx(L1, L2)
    For Each myXvarTmp In myZvarMtrx
        If IsNumeric(myXvarTmp) = False Then GoTo ExitPath
    Next myXvarTmp
    On Error GoTo 0
    PfncbisIsMatrixNumeric = True
ExitPath:
End Function

 '定型Ｐ_2次元の数値配列の指定列から一部を1次元配列で取得する
Private Sub PfixArrayPartColumnNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, ByVal myXlonExtrctCol As Long, _
            ByVal myXlonBgnRow As Long, ByVal myXlonEndRow As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 抽出データ配列
'myZdouOrgData(i, j) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgnRow < LBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow > UBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow < myXlonBgnRow Then Exit Sub
    If myXlonExtrctCol < LBound(myZdouOrgData, 2) _
        Or myXlonExtrctCol > UBound(myZdouOrgData, 2) Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData, 1) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgnRow, myXlonExtrctCol)
    Li = LBound(myZdouOrgData, 1)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgnRow To myXlonEndRow Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i, myXlonExtrctCol)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｆ_配列計算-NotchFilter
Private Function PfncdouNotchFilter( _
            ByVal myXdouYnm1 As Double, ByVal myXdouYnm2 As Double, _
            ByVal myXdouXn As Double, ByVal myXdouXnm1 As Double, ByVal myXdouXnm2 As Double, _
            ByVal myXdouTs As Double, _
            ByVal myXdouFo As Double, ByVal myXdouBo As Double) As Double
'//計算式
    'Y[n]=a1*Y[n-1]+a2*Y[n-2]+co*(X[n]+b1*X[n-1]+X[n-2])
    'a1=2*exp{(-1)*π*Bo*Ts}*cos(2*π*Fo*Ts)
    'a2=(-1)*exp{(-2)*π*Bo*Ts}
    'b1=(-2)*cos(2*π*Fo*Ts)
    'co=(1-a1-a2)/(2+b1)
'myXdouYnm1 : 計算値の前回値
'myXdouYnm2 : 計算値の前々回値
'myXdouXn   : 元データ今回値
'myXdouXnm1 : 元データ前回値
'myXdouXnm2 : 元データ前々回値
'myXdouTs[sec] : 元データ配列のサンプリング時間
'myXdouFo[Hz]  : NotchFilterのノッチ周波数
'myXdouBo[Hz]  : NotchFilterのノッチの帯域幅
  Const myXdouPI As Double = 3.14159265358979
'  Dim myXdouPI As Double: myXdouPI = Application.WorksheetFunction.Pi()
    PfncdouNotchFilter = Empty
    If myXdouBo > myXdouFo Then Exit Function
  Dim myXdouTmpBo As Double, myXdouTmpFo As Double
    myXdouTmpBo = myXdouBo * myXdouTs
    myXdouTmpFo = myXdouFo * myXdouTs
    If myXdouTmpBo * myXdouTmpFo <= 0 Then Exit Function
    If myXdouTmpFo = 0.25 Then Exit Function
  Dim myXdouA1 As Double, myXdouCal1 As Double, myXdouCal2 As Double
    myXdouCal1 = (-1) * myXdouPI * myXdouTmpBo
    myXdouCal1 = Exp(myXdouCal1)
    myXdouCal2 = 2 * myXdouPI * myXdouTmpFo
    myXdouCal2 = Cos(myXdouCal2)
    myXdouA1 = 2 * myXdouCal1 * myXdouCal2
  Dim myXdouA2 As Double
    myXdouA2 = (-2) * myXdouPI * myXdouTmpBo
    myXdouA2 = (-1) * Exp(myXdouA2)
  Dim myXdouB1 As Double
    myXdouB1 = (-2) * myXdouCal2
  Dim myXdouCo As Double
    myXdouCo = (1 - myXdouA1 - myXdouA2) / (2 + myXdouB1)
  Dim myXdouBuf1 As Double, myXdouBuf2 As Double, myXdouBuf3 As Double
    myXdouBuf1 = myXdouA1 * myXdouYnm1
    myXdouBuf2 = myXdouA2 * myXdouYnm2
    myXdouBuf3 = myXdouCo * (myXdouXn + myXdouB1 * myXdouXnm1 + myXdouXnm2)
  Dim myXdouRet As Double
    myXdouRet = myXdouBuf1 + myXdouBuf2 + myXdouBuf3
    PfncdouNotchFilter = myXdouRet
End Function

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_データ配列をNotchFilter処理する
'Private Sub instCNtchFltr()
'  Const coXlonCalDataRngOptn As Long = 1
'    'coXlonCalDataRngOptn = 1 : 全データ範囲で取得
'    'coXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'    'coXlonCalDataRngOptn = 3 : 有効データ範囲で取得
'  Const coXlonBgnRow As Long = 1
'  Const coXlonEndRow As Long = 0
'  Const coXdouTs As Double = 0.00001
'    'coXdouTs[sec] : 元データ配列のサンプリング時間
'  Const coXdouFo As Double = 500
'    'coXdouFo[Hz]  : NotchFilterのノッチ周波数
'  Const coXdouBo As Double = coXdouFo / 2
'    'coXdouBo[Hz]  : NotchFilterのノッチの帯域幅
''  Dim myXlonOrgRowCnt As Long, myXlonOrgColCnt As Long, myZdouOrgData() As Double
''    'myZdouOrgData(i, 1) : 元データ配列
''    myXlonOrgRowCnt = UBound(myZdouOrgData, 1)
''    myXlonOrgColCnt = 1
'  Dim Lc As Long
''  Dim myXlonStrtRow As Long, myXlonLastRow As Long
''  Dim myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
''    'myZdouCalData(i, 1) : 計算処理後データ配列
''//CNtchFltrクラスモジュールのインスタンス生成
'  Dim myXinsNtchFltr As CNtchFltr: Set myXinsNtchFltr = New CNtchFltr
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long, j As Long
'    With myXinsNtchFltr
'    '//クラス内変数への入力
'        .letCalDataRngOptn = coXlonCalDataRngOptn
'        .letOrgData = myZdouOrgData
'        .letBgnRow = coXlonBgnRow
'        .letEndRow = coXlonEndRow
'        .letTs = coXdouTs
'        .letFo = coXdouFo
'        .letBo = coXdouBo
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXlonStrtRow = .getStrtRow
'        myXlonLastRow = .getLastRow
'        myXlonCalRowCnt = .getCalRowCnt
'        myXlonCalColCnt = .getCalColCnt
'        If myXlonCalRowCnt <= 0 Or myXlonCalColCnt <= 0 Then GoTo JumpPath
'        i = myXlonCalRowCnt + Lo - 1: j = myXlonCalColCnt + Lo - 1
'        ReDim myZdouCalData(i, j) As Double
'        Lc = .getOptnBase
'        For i = 1 To myXlonCalRowCnt
'            myZdouCalData(i + Lo - 1, Lo) = .getCalData(i + Lc - 1, Lc)
'        Next i
'    End With
'JumpPath:
'    Set myXinsNtchFltr = Nothing
'    Call variablesOfCNtchFltr(myXlonCalRowCnt, myZdouCalData)  'Debug.Print
'End Sub
'Private Sub variablesOfCNtchFltr( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CNtchFltrクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim i As Long, j As Long
'    For i = LBound(myZvarField, 1) To UBound(myZvarField, 1)
'        For j = LBound(myZvarField, 2) To UBound(myZvarField, 2)
'            Debug.Print "データ" & i & "," & j & ": " & myZvarField(i, j)
'        Next j
'    Next i
'End Sub

      <br></pre>
    </body>
  </html>
