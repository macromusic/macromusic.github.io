<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfixCopyPasteRow
'Includes PincChangeChartFormulaR1C1
'Includes PfixChangeObjectFormulaR1C1
'Includes PfixChangeModuleConstValue
'Includes x

Option Explicit
Option Base 1

'◆ModuleProc名_行とグラフをコピーしてデータ数分貼り付けてオブジェクトの参照式をR1C1形式で変更する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "xRefCpyPstRowChrtChngFrml"
  Private Const meMlonExeNum As Long = 0
  Private Const meMvarField As Variant = Empty
  
'//モジュール内定数
  Private Const coXlonCpyBgnRow As Long = 3
  Private Const coXlonCpyRowCnt As Long = 1
  Private Const coXlonPstBgnRow As Long = coXlonCpyBgnRow + coXlonCpyRowCnt
  Private Const coXlonXvalRow As Long = 3
  Private Const coXlonYvalRow As Long = 3
  Private Const coXlonXttlRow As Long = 3
  Private Const coXlonYttlRow As Long = 3

  Private Const coXlonDataCnt As Long = 4
  
'//出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXobjSheet As Object
  Private myXobjChrt As Object, myXobjChrtAxsTtl As Object

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    Set myXobjSheet = Nothing
    Set myXobjChrt = Nothing: Set myXobjChrtAxsTtl = Nothing
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_モジュールメモリのリセット
Public Sub resetConstant()
  Dim myXstrMdlName As String: myXstrMdlName = meMstrMdlName
  Dim myZvarM(1, 2) As Variant
    myZvarM(1, 1) = "meMlonExeNum": myZvarM(1, 2) = 0
    Call PfixChangeModuleConstValue(myXbisExitFlag, myXstrMdlName, myZvarM)
End Sub

'PublicP_
Public Sub exeProc()
    
'//処理実行
    Call ctrProc
    
'//処理結果表示
    Select Case myXbisCmpltFlag
        Case True: MsgBox "実行完了"
        Case Else: MsgBox "異常あり", vbExclamation
    End Select
    
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call remProc: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
    If coXlonCpyBgnRow <= 0 Then GoTo ExitPath
    If coXlonCpyRowCnt <= 0 Then GoTo ExitPath
    If coXlonPstBgnRow <= 0 Then GoTo ExitPath
    If coXlonPstBgnRow <= coXlonCpyBgnRow + coXlonCpyRowCnt - 1 Then GoTo ExitPath
    If coXlonXvalRow <= 0 Then GoTo ExitPath
    If coXlonYvalRow <= 0 Then GoTo ExitPath
    If coXlonXttlRow <= 0 Then GoTo ExitPath
    If coXlonYttlRow <= 0 Then GoTo ExitPath
    
    If coXlonDataCnt <= 0 Then GoTo ExitPath
    
  Dim myXlonChrtObjCntOrg As Long, myXlonChrtObjCnt As Long
  Dim myXstrOrgAdrsR1C1 As String, myXstrNewAdrsR1C1 As String
  Dim myXlonPstRow As Long, myXlonOfstRow As Long, i As Long
    
    Set myXobjSheet = ActiveSheet
    myXlonChrtObjCntOrg = myXobjSheet.ChartObjects.Count
        
    myXlonOfstRow = coXlonPstBgnRow - coXlonCpyBgnRow - coXlonCpyRowCnt
    
    For i = 1 To coXlonDataCnt
    
    '//オリジナル行をコピーして貼り付ける
        myXlonPstRow = coXlonPstBgnRow + (i - 1) * coXlonCpyRowCnt
        Call PfixCopyPasteRow( _
                myXbisExitFlag, _
                myXobjSheet, coXlonCpyBgnRow, coXlonCpyRowCnt, myXlonPstRow)
        If myXbisExitFlag = True Then GoTo ExitPath
    '    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
        
        If myXlonChrtObjCntOrg <= 0 Then GoTo NextPath
        
        myXlonChrtObjCnt = myXobjSheet.ChartObjects.Count
        If myXlonChrtObjCnt = myXlonChrtObjCntOrg Then GoTo NextPath
        Set myXobjChrt = myXobjSheet.ChartObjects(myXlonChrtObjCnt).Chart
        
    '//グラフのX軸データの参照式を変更
        myXstrOrgAdrsR1C1 = "R" & coXlonXvalRow
        myXstrNewAdrsR1C1 = "R" & coXlonXvalRow + myXlonOfstRow + i * coXlonCpyRowCnt
        Call PincChangeChartFormulaR1C1( _
                myXbisExitFlag, _
                myXobjChrt, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
    '    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
    '//グラフのY軸データの参照式を変更
        myXstrOrgAdrsR1C1 = "R" & coXlonYvalRow
        myXstrNewAdrsR1C1 = "R" & coXlonYvalRow + myXlonOfstRow + i * coXlonCpyRowCnt
        If coXlonYvalRow <> coXlonXvalRow Then _
            Call PincChangeChartFormulaR1C1( _
                    myXbisExitFlag, _
                    myXobjChrt, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
    '    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
        
    '//グラフのX軸主軸のタイトルの参照式を変更
        Set myXobjChrtAxsTtl = myXobjChrt.Axes(xlCategory, xlPrimary).AxisTitle
        myXstrOrgAdrsR1C1 = "R" & coXlonXttlRow
        myXstrNewAdrsR1C1 = "R" & coXlonXttlRow + myXlonOfstRow + i * coXlonCpyRowCnt
        Call PfixChangeObjectFormulaR1C1( _
                myXbisExitFlag, _
                myXobjChrtAxsTtl, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
    '    Debug.Print "PassFlag: " & meMstrMdlName & "5"     'PassFlag
    
    '//グラフのY軸主軸のタイトルの参照式を変更
        Set myXobjChrtAxsTtl = myXobjChrt.Axes(xlValue, xlPrimary).AxisTitle
        myXstrOrgAdrsR1C1 = "R" & coXlonYttlRow
        myXstrNewAdrsR1C1 = "R" & coXlonYttlRow + myXlonOfstRow + i * coXlonCpyRowCnt
        Call PfixChangeObjectFormulaR1C1( _
                myXbisExitFlag, _
                myXobjChrtAxsTtl, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
    '    Debug.Print "PassFlag: " & meMstrMdlName & "6"     'PassFlag
    
NextPath:
    Next i
    
    myXbisCmpltFlag = True
ExitPath:
    If coXbisTestMode = False Then Call recProc
    Call initializeModuleVariables
End Sub

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
End Sub

'RemP_モジュールメモリに保存した変数を取り出す
Private Sub remProc()
    myXbisExitFlag = False
    On Error GoTo ExitPath
    
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'RecP_使用した変数をモジュールメモリに保存する
Private Sub recProc()
    myXbisExitFlag = False
    On Error GoTo ExitPath
    
  Dim myZvarM(1, 2) As Variant
    myZvarM(1, 1) = "meMlonExeNum"
    myZvarM(1, 2) = meMlonExeNum + 1

  Dim myXstrMdlName As String: myXstrMdlName = meMstrMdlName
    Call PfixChangeModuleConstValue(myXbisExitFlag, myXstrMdlName, myZvarM)
    
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｐ_オリジナル行をコピーして貼り付ける
Private Sub PfixCopyPasteRow( _
        myXbisExitFlag As Boolean, _
        ByVal myXobjSheet As Object, _
        ByVal myXlonCpyBgnRow As Long, ByVal myXlonCpyRowCnt As Long, _
        ByVal myXlonPstBgnRow As Long)
    myXbisExitFlag = False
    If Not myXobjSheet Is ActiveSheet Then GoTo ExitPath
    If myXlonCpyBgnRow <= 0 Then GoTo ExitPath
    If myXlonCpyRowCnt <= 0 Then GoTo ExitPath
    If myXlonPstBgnRow <= 0 Then GoTo ExitPath
    If myXlonPstBgnRow <= myXlonCpyBgnRow + myXlonCpyRowCnt - 1 Then GoTo ExitPath
  Dim myXlonCpyEndRow As Long, myXstrCpyRowAdrs As String
    myXlonCpyEndRow = myXlonCpyBgnRow + myXlonCpyRowCnt - 1
    myXstrCpyRowAdrs = myXlonCpyBgnRow & ":" & myXlonCpyEndRow
  Dim myXobjCpyRows As Object, myXobjPstRows As Object
    On Error GoTo ExitPath
    Set myXobjCpyRows = myXobjSheet.Rows(myXstrCpyRowAdrs)
    myXobjCpyRows.Copy
    Set myXobjPstRows = myXobjSheet.Rows(myXlonPstBgnRow)
    myXobjPstRows.PasteSpecial xlAll
    myXobjPstRows.Select: ActiveSheet.Paste
    On Error GoTo 0
    Application.CutCopyMode = False
    Set myXobjCpyRows = Nothing: Set myXobjPstRows = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定グラフに設定されている参照式をR1C1形式で変更する
Private Sub PincChangeChartFormulaR1C1( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            ByVal myXstrOrgAdrsR1C1 As String, ByVal myXstrNewAdrsR1C1 As String)
'Includes PfixChangeObjectFormulaR1C1
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then Exit Sub
    If myXstrOrgAdrsR1C1 = "" Or myXstrNewAdrsR1C1 = "" Then GoTo ExitPath
  Dim myXobjSrsClctn As Object
    For Each myXobjSrsClctn In myXobjChrt.SeriesCollection
        Call PfixChangeObjectFormulaR1C1( _
                myXbisExitFlag, _
                myXobjSrsClctn, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
    Next myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定オブジェクトに設定されている参照式をR1C1形式で変更する
Private Sub PfixChangeObjectFormulaR1C1( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjOrg As Object, _
            ByVal myXstrOrgAdrsR1C1 As String, ByVal myXstrNewAdrsR1C1 As String)
    myXbisExitFlag = False
    If myXobjOrg Is Nothing Then Exit Sub
    If myXstrOrgAdrsR1C1 = "" Or myXstrNewAdrsR1C1 = "" Then GoTo ExitPath
  Dim myXstrOrgFrml As String, myXstrNewFrml As String
    On Error GoTo ExitPath
    myXstrOrgFrml = myXobjOrg.FormulaR1C1
'    Debug.Print "系列参照式: " & myXstrOrgFrml
    If InStr(myXstrOrgFrml, myXstrOrgAdrsR1C1) <= 0 Then GoTo ExitPath
    myXstrNewFrml = Replace(myXstrOrgFrml, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
'    Debug.Print "系列参照式: " & myXstrNewFrml
    If Application.ReferenceStyle = xlA1 Then
        Application.ReferenceStyle = xlR1C1
        myXobjOrg.Formula = myXstrNewFrml
        Application.ReferenceStyle = xlA1
    Else
        myXobjOrg.FormulaR1C1 = myXstrNewFrml
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_モジュール内定数の値を変更する
Private Sub PfixChangeModuleConstValue(myXbisExitFlag As Boolean, _
            ByVal myXstrMdlName As String, ByRef myZvarM() As Variant)
    myXbisExitFlag = False
    If myXstrMdlName = "" Then GoTo ExitPath
  Dim L As Long, myXvarTmp As Variant
    On Error GoTo ExitPath
    L = LBound(myZvarM, 1): myXvarTmp = myZvarM(L, L)
    On Error GoTo 0
  Dim myXlonDclrLines As Long, myXobjCdMdl As Object
    Set myXobjCdMdl = ThisWorkbook.VBProject.VBComponents(myXstrMdlName).CodeModule
    myXlonDclrLines = myXobjCdMdl.CountOfDeclarationLines
    If myXlonDclrLines <= 0 Then GoTo ExitPath
  Dim i As Long, n As Long
  Dim myXstrTmp As String, myXstrSrch As String, myXstrOrg As String, myXstrRplc As String
Application.DisplayAlerts = False
    For i = 1 To myXlonDclrLines
        myXstrTmp = myXobjCdMdl.Lines(i, 1)
        For n = LBound(myZvarM, 1) To UBound(myZvarM, 1)
            myXstrSrch = "Const" & Space(1) & myZvarM(n, L) & Space(1) & "As" & Space(1)
            If InStr(myXstrTmp, myXstrSrch) > 0 Then
                myXstrOrg = Left(myXstrTmp, InStr(myXstrTmp, "=" & Space(1)) + 1)
                myXstrRplc = myXstrOrg & myZvarM(n, L + 1)
                Call myXobjCdMdl.ReplaceLine(i, myXstrRplc)
            End If
        Next n
    Next i
Application.DisplayAlerts = True
    Set myXobjCdMdl = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'DummyＰ_
Private Sub MsubDummy()
End Sub

'===============================================================================================
'
'  Public Const coXbisTestMode As Boolean = True
'  Public Const coXbisTestMode As Boolean = False
'
Private Sub resetConstantInxRefCpyPstRowChrtChngFrml()
'//xRefCpyPstRowChrtChngFrmlモジュールのモジュールメモリのリセット処理
    Call xRefCpyPstRowChrtChngFrml.resetConstant
End Sub

      <br></pre>
    </body>
  </html>
