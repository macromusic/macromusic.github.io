<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes m1MexeFileLstup
'Includes m1MexeFileLstupSub
'Includes CSrchSht
'Includes CXlsOpen
'Includes CXlsClose
'Includes CRngSlct
'Includes CVrblToSht
'Includes PfncstrFileNameByFSO
'Includes PfixGetSheetPartRangeDataVariantArray

Option Explicit
Option Base 1

'◆ModuleProc名_1個の元ファイル  → 複数のファイルへコピペ
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "bMsub2OneToMulti"
  
'//出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  Private myXbisEndFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonOrgFileCnt As Long, myZstrOrgFilePath() As String
    'myZstrOrgFilePath(i) : 元データファイルパス

  Private myXlonSubFileCnt As Long, myZstrSubFilePath() As String
    'myZstrSubFilePath(i) : データ保存ファイルパス

  Private myXlonSrchShtNo As Long, myXobjSrchSheet As Object, _
            myXlonShtSrchCnt As Long, myZvarSrchCndtn As Variant, _
            myXbisInStrOptn As Boolean
    'myZvarSrchCndtn(i, 1) : 検索文字列
    'myZvarSrchCndtn(i, 2) : オフセット行数
    'myZvarSrchCndtn(i, 3) : オフセット列数
    'myXbisInStrOptn = False : 指定文字列と一致する条件で検索する
    'myXbisInStrOptn = True  : 指定文字列を含む条件で検索する
  Private myXlonValCnt As Long, myZstrVal() As String, myZobjPstnRng() As Object
  Private myZobjOrgPstnRng() As Object, myZobjSubPstnRng() As Object
    'myZstrVal(i) : 取得文字列
    'myZobjPstnRng(i) : 行列位置のセル

  Private myXlonOrgFileNo As Long, myXstrOrgFileName As String, myXstrOrgFilePath As String
  Private myXlonSubFileNo As Long, myXstrSubFileName As String, myXstrSubFilePath As String

  Private myXstrOpnFullName As String
  Private myXbisOpnRdOnly As Boolean
    'myXbisOpnRdOnly = False : 指定エクセルブックを読み取り専用にせずに開く
    'myXbisOpnRdOnly = True  : 指定エクセルブックを読み取り専用で開く
  Private myXobjOpndBook As Object
  Private myXobjOpndOrgBook As Object, myXobjOpndSubBook As Object
  
  Private myXstrCloseFullName As String
  Private myXbisSaveON As Boolean
    'myXbisSaveON = False : 閉じる前に保存しない
    'myXbisSaveON = True  : 閉じる前に保存する
  Private myXstrSaveBkName As String
  
  Private myXlonOrgRngSlctCnt As Long, myXlonSubRngSlctCnt As Long
  
  Private myXobjWrkSht As Object, myXobjFrstCell As Object, myXobjLastCell As Object
  
  Private myXstrInptBxPrmpt As String, myXstrInptBxTtl As String
  Private myXobjSlctRng As Object
  Private myXobjBook As Object, myXstrShtName As String, myXlonShtNo As Long
  Private myXlonBgnRow As Long, myXlonEndRow As Long, _
            myXlonBgnCol As Long, myXlonEndCol As Long, _
            myXlonRows As Long, myXlonCols As Long
  
  Private myXlonOrgShtNo As Long, _
            myXlonOrgBgnRow As Long, myXlonOrgEndRow As Long, _
            myXlonOrgBgnCol As Long, myXlonOrgEndCol As Long, _
            myXlonOrgShtNoOfst As Long, _
            myXlonOrgRowOfst As Long, myXlonOrgColOfst As Long
  
  Private myXobjDataRng As Object
  Private myXlonShtDataRowCnt As Long, myXlonShtDataColCnt As Long, myZvarShtData() As Variant
  
  Private myXlonSubShtNo As Long, _
            myXlonSubBgnRow As Long, myXlonSubBgnCol As Long, _
            myXlonSubShtNoOfst As Long, _
            myXlonSubRowOfst As Long, myXlonSubColOfst As Long
  
  Private myZvarPstVrbl As Variant, myXobjPstFrstCell As Object
  
'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    myXbisEndFlag = False
    
    myXlonOrgFileCnt = Empty: Erase myZstrOrgFilePath
    myXlonSubFileCnt = Empty: Erase myZstrSubFilePath
    
    myXlonSrchShtNo = Empty: Set myXobjSrchSheet = Nothing
    myXlonShtSrchCnt = Empty: myZvarSrchCndtn = Empty
    myXbisInStrOptn = False
    myXlonValCnt = Empty: Erase myZstrVal: Erase myZobjPstnRng
    Erase myZobjOrgPstnRng: Erase myZobjSubPstnRng
    
    myXlonOrgFileNo = Empty: myXstrOrgFileName = Empty: myXstrOrgFilePath = Empty
    myXlonSubFileNo = Empty: myXstrSubFileName = Empty: myXstrSubFilePath = Empty
    
    myXstrOpnFullName = Empty: myXbisOpnRdOnly = False: Set myXobjOpndBook = Nothing
    Set myXobjOpndOrgBook = Nothing: Set myXobjOpndSubBook = Nothing
    myXstrCloseFullName = Empty: myXbisSaveON = False: myXstrSaveBkName = Empty
    
    myXlonOrgRngSlctCnt = Empty: myXlonSubRngSlctCnt = Empty
    
    Set myXobjWrkSht = Nothing: Set myXobjFrstCell = Nothing: Set myXobjLastCell = Nothing
    
    myXstrInptBxPrmpt = Empty: myXstrInptBxTtl = Empty
    Set myXobjSlctRng = Nothing
    Set myXobjBook = Nothing: myXstrShtName = Empty: myXlonShtNo = Empty
    myXlonBgnRow = Empty: myXlonEndRow = Empty
    myXlonBgnCol = Empty: myXlonEndCol = Empty
    myXlonRows = Empty: myXlonCols = Empty
    
    myXlonOrgShtNo = Empty
    myXlonOrgBgnRow = Empty: myXlonOrgEndRow = Empty
    myXlonOrgBgnCol = Empty: myXlonOrgEndCol = Empty
    myXlonOrgShtNoOfst = Empty
    myXlonOrgRowOfst = Empty: myXlonOrgColOfst = Empty
    
    Set myXobjDataRng = Nothing
    myXlonShtDataRowCnt = Empty: myXlonShtDataColCnt = Empty: Erase myZvarShtData
    
    myXlonSubShtNo = Empty
    myXlonSubBgnRow = Empty: myXlonSubBgnCol = Empty
    myXlonSubShtNoOfst = Empty
    myXlonSubRowOfst = Empty: myXlonSubColOfst = Empty
    
    myZvarPstVrbl = Empty: Set myXobjPstFrstCell = Nothing
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub callProc(myXbisCmpltFlagOUT As Boolean)
    
'//出力変数を初期化
    myXbisCmpltFlagOUT = False
    
'//処理実行
    Call ctrProc
    If myXbisCmpltFlag = False Then Exit Sub
    
'//出力変数に格納
    myXbisCmpltFlagOUT = myXbisCmpltFlag
    
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"    'PassFlag
    
'//S:Loop前の情報取得処理
    Call snsProcBeforeLoop
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"    'PassFlag
    
'//C:ファイルリストを順次実行
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim n As Long: n = Lo - 1
  Dim k As Long
    For k = LBound(myZstrSubFilePath) To UBound(myZstrSubFilePath)
        myXstrSubFilePath = Empty: myXstrSubFileName = Empty
        myXlonSubFileNo = k
        myXstrSubFilePath = myZstrSubFilePath(k)
        myXstrSubFileName = PfncstrFileNameByFSO(myXstrSubFilePath)
        If myXstrSubFileName = "" Then GoTo NextPath
        
    '//S:各ファイルのデータ取得処理
        Call snsProcForLoop
        If myXbisExitFlag = True Then GoTo NextPath
        If myXbisEndFlag = True Then GoTo ExitPath
'        Debug.Print "PassFlag: " & meMstrMdlName & "3-" & k   'PassFlag
 
    '//P:各ファイルのデータ加工処理
        Call prsProcForLoop
        If myXbisExitFlag = True Then GoTo NextPath
'        Debug.Print "PassFlag: " & meMstrMdlName & "4-" & k   'PassFlag
        
    '//Run:各ファイルのデータ出力処理
        Call runProcForLoop
        If myXbisExitFlag = True Then GoTo NextPath
        If myXbisEndFlag = True Then GoTo ExitPath
'        Debug.Print "PassFlag: " & meMstrMdlName & "5-" & k   'PassFlag
        
        If myXlonOrgRngSlctCnt = 2 Then Application.ScreenUpdating = False
        
NextPath:
        If myXbisExitFlag = True Then
            n = n + 1
            Debug.Print "エラーデータ" & n & ": " & myXstrSubFileName
        End If
    Next k
'    Debug.Print "PassFlag: " & meMstrMdlName & "6"    'PassFlag

'//Run:ファイナライズ処理
    Call runFinalize
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "7"   'PassFlag
    
    Application.ScreenUpdating = True
    
    If n > Lo - 1 Then GoTo ExitPath
    myXbisCmpltFlag = True
ExitPath:
    Call initializeModuleVariables
End Sub

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables1()
    
    Set myXobjSrchSheet = ActiveSheet
  
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    myXlonShtSrchCnt = 8
    ReDim myZvarSrchCndtn(myXlonShtSrchCnt + L - 1, L + 2) As Variant
    'myZvarSrchCndtn(i, 1) : 検索文字列
    'myZvarSrchCndtn(i, 2) : オフセット行数
    'myZvarSrchCndtn(i, 3) : オフセット列数
  Dim i As Long: i = L - 1
    i = i + 1   'i = 1
    myZvarSrchCndtn(i, L + 0) = "元データ取得シートNo. ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 2
    myZvarSrchCndtn(i, L + 0) = "元データ取得範囲_開始行 ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 3
    myZvarSrchCndtn(i, L + 0) = "元データ取得範囲_終了行 ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 4
    myZvarSrchCndtn(i, L + 0) = "元データ取得範囲_開始列 ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 5
    myZvarSrchCndtn(i, L + 0) = "元データ取得範囲_終了列 ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 6
    myZvarSrchCndtn(i, L + 0) = "元データ取得シートオフセット ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 7
    myZvarSrchCndtn(i, L + 0) = "元データ取得範囲_行オフセット ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 8
    myZvarSrchCndtn(i, L + 0) = "元データ取得範囲_列オフセット ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
  
    myXbisInStrOptn = False
    'myXbisInStrOptn = False : 指定文字列と一致する条件で検索する
    'myXbisInStrOptn = True  : 指定文字列を含む条件で検索する

End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables2()
    
    Set myXobjSrchSheet = ActiveSheet

  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    myXlonShtSrchCnt = 6
    ReDim myZvarSrchCndtn(myXlonShtSrchCnt + L - 1, L + 2) As Variant
    'myZvarSrchCndtn(i, 1) : 検索文字列
    'myZvarSrchCndtn(i, 2) : オフセット行数
    'myZvarSrchCndtn(i, 3) : オフセット列数
  Dim i As Long: i = L - 1
    i = i + 1   'i = 1
    myZvarSrchCndtn(i, L + 0) = "データ貼り付けシートNo. ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 2
    myZvarSrchCndtn(i, L + 0) = "データ貼り付け位置_開始行 ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 3
    myZvarSrchCndtn(i, L + 0) = "データ貼り付け位置_開始列 ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 4
    myZvarSrchCndtn(i, L + 0) = "データ貼り付けシートオフセット ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 5
    myZvarSrchCndtn(i, L + 0) = "データ貼り付け位置_行オフセット ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
    i = i + 1   'i = 6
    myZvarSrchCndtn(i, L + 0) = "データ貼り付け位置_列オフセット ："
    myZvarSrchCndtn(i, L + 1) = 0
    myZvarSrchCndtn(i, L + 2) = 1
  
    myXbisInStrOptn = False
    'myXbisInStrOptn = False : 指定文字列と一致する条件で検索する
    'myXbisInStrOptn = True  : 指定文字列を含む条件で検索する

End Sub

'SnsP_Loop前の情報取得処理
Private Sub snsProcBeforeLoop()
    myXbisExitFlag = False
    
'//元データファイルリストを取得
    Call m1MexeFileLstup.callProc(myXlonOrgFileCnt, myZstrOrgFilePath)
    If myXlonOrgFileCnt <> 1 Then GoTo ExitPath
    myXstrOrgFilePath = myZstrOrgFilePath(LBound(myZstrOrgFilePath))
    
'//データ保存ファイルリストを取得
    Call m1MexeFileLstupSub.callProc(myXlonSubFileCnt, myZstrSubFilePath)
    If myXlonSubFileCnt <= 0 Then GoTo ExitPath
    
'//シート上のデータから文字列を検索してデータと位置情報を取得
    Call setControlVariables1
    Call instCSrchSht
    If myXlonValCnt <= 0 Then GoTo ExitPath
  Dim L As Long: L = LBound(myZstrVal)
    myXlonOrgShtNo = CLng(myZstrVal(L + 0))
    myXlonOrgBgnRow = CLng(myZstrVal(L + 1))
    myXlonOrgEndRow = CLng(myZstrVal(L + 2))
    myXlonOrgBgnCol = CLng(myZstrVal(L + 3))
    myXlonOrgEndCol = CLng(myZstrVal(L + 4))
    myXlonOrgShtNoOfst = CLng(myZstrVal(L + 5))
    myXlonOrgRowOfst = CLng(myZstrVal(L + 6))
    myXlonOrgColOfst = CLng(myZstrVal(L + 7))
    myZobjOrgPstnRng = myZobjPstnRng
    
'//シート上のデータから文字列を検索してデータと位置情報を取得
    Call setControlVariables2
    Call instCSrchSht
    If myXlonValCnt <= 0 Then GoTo ExitPath
    L = LBound(myZstrVal)
    myXlonSubShtNo = CLng(myZstrVal(L + 0))
    myXlonSubBgnRow = CLng(myZstrVal(L + 1))
    myXlonSubBgnCol = CLng(myZstrVal(L + 2))
    myXlonSubShtNoOfst = CLng(myZstrVal(L + 3))
    myXlonSubRowOfst = CLng(myZstrVal(L + 4))
    myXlonSubColOfst = CLng(myZstrVal(L + 5))
    myZobjSubPstnRng = myZobjPstnRng
    
'//元データファイルを開く
    myXstrOpnFullName = myXstrOrgFilePath
    myXbisOpnRdOnly = True
    Call instCXlsOpen
    If myXobjOpndBook Is Nothing Then GoTo ExitPath
    Set myXobjOpndOrgBook = myXobjOpndBook
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SnsP_各ファイルのデータ取得処理
Private Sub snsProcForLoop()
    myXbisExitFlag = False
    
'//データ取得範囲を指定
  Dim L As Long: L = LBound(myZobjOrgPstnRng)
    
    myXlonOrgRngSlctCnt = myXlonOrgRngSlctCnt + 1
    If myXlonOrgRngSlctCnt = 1 Then
        Set myXobjWrkSht = myXobjOpndOrgBook.Worksheets(myXlonOrgShtNo)
        Set myXobjFrstCell = myXobjWrkSht.Cells(myXlonOrgBgnRow, myXlonOrgBgnCol)
        Set myXobjLastCell = myXobjWrkSht.Cells(myXlonOrgEndRow, myXlonOrgEndCol)
        Set myXobjDataRng = Range(myXobjFrstCell, myXobjLastCell)
        myXobjWrkSht.Activate
        myXobjDataRng.Select
        
        myXstrInptBxTtl = "1個目の元データの取得範囲の指定"
        myXstrInptBxPrmpt = "1個目の元データの取得範囲を指定して下さい"
        Call instCRngSlct
        If myXobjSlctRng Is Nothing Then GoTo EndPath
        
    '//データ取得範囲を取得
        myXlonOrgShtNo = myXlonShtNo
        myXlonOrgBgnRow = myXlonBgnRow
        myXlonOrgEndRow = myXlonEndRow
        myXlonOrgBgnCol = myXlonBgnCol
        myXlonOrgEndCol = myXlonEndCol
        myXlonOrgShtNoOfst = 0
        myXlonOrgRowOfst = 0
        myXlonOrgColOfst = 0
        
    '//データ取得範囲をシートに記入
        myZobjOrgPstnRng(L + 0).Value = myXlonOrgShtNo
        myZobjOrgPstnRng(L + 1).Value = myXlonOrgBgnRow
        myZobjOrgPstnRng(L + 2).Value = myXlonOrgEndRow
        myZobjOrgPstnRng(L + 3).Value = myXlonOrgBgnCol
        myZobjOrgPstnRng(L + 4).Value = myXlonOrgEndCol
        myZobjOrgPstnRng(L + 5).Value = myXlonOrgShtNoOfst
        myZobjOrgPstnRng(L + 6).Value = myXlonOrgRowOfst
        myZobjOrgPstnRng(L + 7).Value = myXlonOrgColOfst
        
    ElseIf myXlonOrgRngSlctCnt = 2 Then
        Set myXobjWrkSht = myXobjOpndOrgBook.Worksheets(myXlonOrgShtNo)
        Set myXobjFrstCell = myXobjWrkSht.Cells(myXlonOrgBgnRow, myXlonOrgBgnCol)
        Set myXobjLastCell = myXobjWrkSht.Cells(myXlonOrgEndRow, myXlonOrgEndCol)
        Set myXobjDataRng = Range(myXobjFrstCell, myXobjLastCell)
        myXobjWrkSht.Activate
        myXobjDataRng.Select
        
        myXstrInptBxTtl = "2個目の元データの取得範囲の指定"
        myXstrInptBxPrmpt = "2個目の元データの取得範囲を指定して下さい" & vbCrLf & vbCrLf & _
                            "2個目以降の元データの取得範囲は、" & vbCrLf & _
                            "1個目と2個目の取得範囲の関係から" & vbCrLf & _
                            "自動設定して繰り返します" & vbCrLf
        Call instCRngSlct
        If myXobjSlctRng Is Nothing Then GoTo EndPath
        
    '//データ取得範囲を計算
        myXlonOrgShtNoOfst = myXlonShtNo - myXlonOrgShtNo
        myXlonOrgRowOfst = myXlonBgnRow - myXlonOrgBgnRow
        myXlonOrgColOfst = myXlonBgnCol - myXlonOrgBgnCol
        
    '//データ取得範囲をシートに記入
        myZobjOrgPstnRng(L + 5).Value = myXlonOrgShtNoOfst
        myZobjOrgPstnRng(L + 6).Value = myXlonOrgRowOfst
        myZobjOrgPstnRng(L + 7).Value = myXlonOrgColOfst
        
    End If
    
    myXlonOrgShtNo = myXlonOrgShtNo + myXlonOrgShtNoOfst
    myXlonOrgBgnRow = myXlonOrgBgnRow + myXlonOrgRowOfst
    myXlonOrgEndRow = myXlonOrgEndRow + myXlonOrgRowOfst
    myXlonOrgBgnCol = myXlonOrgBgnCol + myXlonOrgColOfst
    myXlonOrgEndCol = myXlonOrgEndCol + myXlonOrgColOfst
    
    Set myXobjWrkSht = myXobjOpndOrgBook.Worksheets(myXlonOrgShtNo)
    Set myXobjFrstCell = myXobjWrkSht.Cells(myXlonOrgBgnRow, myXlonOrgBgnCol)
    Set myXobjLastCell = myXobjWrkSht.Cells(myXlonOrgEndRow, myXlonOrgEndCol)
    Set myXobjDataRng = Range(myXobjFrstCell, myXobjLastCell)
    
'//データ取得
    Call PfixGetSheetPartRangeDataVariantArray( _
            myXlonShtDataRowCnt, myXlonShtDataColCnt, myZvarShtData, _
            myXobjDataRng)
    If myXlonShtDataRowCnt <= 0 Or myXlonShtDataColCnt <= 0 Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
    Exit Sub
EndPath:
    myXbisEndFlag = True
End Sub

'PrcsP_各ファイルのデータ加工処理
Private Sub prsProcForLoop()
    myXbisExitFlag = False
    
    myZvarPstVrbl = myZvarShtData
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'RunP_各ファイルのデータ出力処理
Private Sub runProcForLoop()
    myXbisExitFlag = False
    
'//データ保存ファイルを開く
    myXstrOpnFullName = myXstrSubFilePath
    myXbisOpnRdOnly = False
    Call instCXlsOpen
    If myXobjOpndBook Is Nothing Then GoTo ExitPath
    Set myXobjOpndSubBook = myXobjOpndBook
    
'//データ保存ファイルの貼り付け位置を指定
  Dim L As Long: L = LBound(myZobjSubPstnRng)
    
    myXlonSubRngSlctCnt = myXlonSubRngSlctCnt + 1
    If myXlonSubRngSlctCnt = 1 Then
        Set myXobjWrkSht = myXobjOpndSubBook.Worksheets(myXlonSubShtNo)
        Set myXobjPstFrstCell = myXobjWrkSht.Cells(myXlonSubBgnRow, myXlonSubBgnCol)
        myXobjWrkSht.Activate
        myXobjPstFrstCell.Select
        
        myXstrInptBxTtl = "データ貼り付け位置の指定"
        myXstrInptBxPrmpt = "データ貼り付け位置を指定して下さい"
        Call instCRngSlct
        If myXobjSlctRng Is Nothing Then GoTo EndPath
        
    '//データ貼り付け位置を取得
        myXlonSubShtNo = myXlonShtNo
        myXlonSubBgnRow = myXlonBgnRow
        myXlonSubBgnCol = myXlonBgnCol
        myXlonSubShtNoOfst = 0
        myXlonSubRowOfst = 0
        myXlonSubColOfst = 0
        
    '//データ貼り付け位置をシートに記入
        myZobjSubPstnRng(L + 0).Value = myXlonSubShtNo
        myZobjSubPstnRng(L + 1).Value = myXlonSubBgnRow
        myZobjSubPstnRng(L + 2).Value = myXlonSubBgnCol
        myZobjSubPstnRng(L + 3).Value = myXlonSubShtNoOfst
        myZobjSubPstnRng(L + 4).Value = myXlonSubRowOfst
        myZobjSubPstnRng(L + 5).Value = myXlonSubColOfst
        
    End If
    
    Set myXobjWrkSht = myXobjOpndSubBook.Worksheets(myXlonSubShtNo)
    Set myXobjPstFrstCell = myXobjWrkSht.Cells(myXlonSubBgnRow, myXlonSubBgnCol)
    
'//データ保存ファイルに貼り付け
    Call instCVrblToSht
    If myXbisExitFlag = True Then GoTo ExitPath
    
'//データ保存ファイルを保存
'//データ保存ファイルを閉じる
    myXstrCloseFullName = myXstrSubFilePath
    myXbisSaveON = True
    myXstrSaveBkName = ""
    Call instCXlsClose
    If myXbisExitFlag = True Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
    Exit Sub
EndPath:
    myXbisEndFlag = True
End Sub

'RunP_ファイナライズ処理
Private Sub runFinalize()
    myXbisExitFlag = False
    
'//元データファイルを閉じる
    myXstrCloseFullName = myXstrOrgFilePath
    myXbisSaveON = False
    myXstrSaveBkName = ""
    Call instCXlsClose
    If myXbisExitFlag = True Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

'◆ClassProc名_シート上のデータから文字列を検索してデータと位置情報を取得する
Private Sub instCSrchSht()
  Dim Lc As Long
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, j As Long
  Dim myXinsSrchSht As CSrchSht: Set myXinsSrchSht = New CSrchSht
    With myXinsSrchSht
    '//文字列検索シートと検索条件を設定
        Set .setSrchSheet = myXobjSrchSheet
        .letSrchCndtn = myZvarSrchCndtn
        .letInStrOptn = myXbisInStrOptn
    '//クラス内プロシージャの実行とクラス内変数からの出力
        .exeProc
        myXlonValCnt = .getValCnt
        If myXlonValCnt <= 0 Then GoTo JumpPath
        i = myXlonValCnt + Lo - 1: j = Lo + 1
        ReDim myZstrVal(i) As String
        ReDim myZobjPstnRng(i) As Object
        Lc = .getOptnBase
        For i = 1 To myXlonValCnt
            myZstrVal(i + Lo - 1) = .getValAry(i + Lc - 1)
            Set myZobjPstnRng(i + Lo - 1) = .getPstnRngAry(i + Lc - 1)
        Next i
    End With
JumpPath:
    Set myXinsSrchSht = Nothing
End Sub

'◆ClassProc名_エクセルブックを開く
Private Sub instCXlsOpen()
  Dim myXinsXlsOpen As CXlsOpen: Set myXinsXlsOpen = New CXlsOpen
    With myXinsXlsOpen
    '//クラス内変数への入力
        .letOpnRdOnly = myXbisOpnRdOnly
        .letOpnFullName = myXstrOpnFullName
    '//クラス内プロシージャの実行とクラス内変数からの出力
        Set myXobjOpndBook = .fncobjOpenedBook
    End With
    Set myXinsXlsOpen = Nothing
End Sub

'◆ClassProc名_エクセルブックを閉じる
Private Sub instCXlsClose()
  Dim myXinsXlsClose As CXlsClose: Set myXinsXlsClose = New CXlsClose
    With myXinsXlsClose
    '//クラス内変数への入力
        .letCloseFullName = myXstrCloseFullName
        .letSaveON = myXbisSaveON
        .letSaveBkName = myXstrSaveBkName
    '//クラス内プロシージャの実行とクラス内変数からの出力
        .exeProc
        myXbisExitFlag = Not .getCmpltFlag
    End With
    Set myXinsXlsClose = Nothing
End Sub

'◆ClassProc名_シート上のセル範囲を取得する
Private Sub instCRngSlct()
  Dim myXlonRngOptn As Long: myXlonRngOptn = 0
    'myXlonRngOptn = 0  : 選択範囲
    'myXlonRngOptn = 1  : 選択位置から最終行までの範囲
    'myXlonRngOptn = 2  : 選択位置から最終列までの範囲
    'myXlonRngOptn = 3  : 全データ範囲
  Dim myXinsRngSlct As CRngSlct: Set myXinsRngSlct = New CRngSlct
    With myXinsRngSlct
    '//クラス内変数への入力
        .letRngOptn = 0
        .letInptBoxPrmptTtl(1) = myXstrInptBxPrmpt
        .letInptBoxPrmptTtl(2) = myXstrInptBxTtl
    '//クラス内プロシージャの実行とクラス内変数からの出力
        .exeProc
        Set myXobjSlctRng = .getSlctRng
        If myXobjSlctRng Is Nothing Then GoTo JumpPath
        Set myXobjBook = .getBook
        myXstrShtName = .getShtName
        myXlonShtNo = .getShtNo
        myXlonBgnRow = .getBgnEndRowCol(1, 1)
        myXlonEndRow = .getBgnEndRowCol(2, 1)
        myXlonBgnCol = .getBgnEndRowCol(1, 2)
        myXlonEndCol = .getBgnEndRowCol(2, 2)
        myXlonRows = .getBgnEndRowCol(1, 0)
        myXlonCols = .getBgnEndRowCol(0, 1)
    End With
JumpPath:
    Set myXinsRngSlct = Nothing
End Sub

'◆ClassProc名_変数情報をエクセルシートに書き出す
Private Sub instCVrblToSht()
    'myXbisInptBxOFF = False : 指定位置が無い場合にInputBoxで範囲指定する
    'myXbisInptBxOFF = True  : 指定位置が無い場合にInputBoxで範囲指定しない
    'myXbisEachWrtON = False : 配列変数内データを一度に書き出しする
    'myXbisEachWrtON = True  : 配列変数内データを1データづつ書き出しする
  Dim myXinsVrblToSht As CVrblToSht: Set myXinsVrblToSht = New CVrblToSht
    With myXinsVrblToSht
    '//クラス内変数への入力
        .letVrbl = myZvarPstVrbl
        Set .setPstFrstCell = myXobjPstFrstCell
        .letInptBxOFF = True
        .letEachWrtON = False
    '//クラス内プロシージャの実行とクラス内変数からの出力
        myXbisExitFlag = Not .fncbisCmpltFlag
    End With
    Set myXinsVrblToSht = Nothing
End Sub

'===============================================================================================

 '定型Ｆ_指定ファイルのファイル名を取得する(FileSystemObject使用)
Private Function PfncstrFileNameByFSO(ByVal myXstrOrgFilePath As String) As String
    PfncstrFileNameByFSO = Empty
    If myXstrOrgFilePath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXbisFileExist As Boolean: myXbisFileExist = myXobjFSO.FileExists(myXstrOrgFilePath)
    If myXbisFileExist = False Then Exit Function
    PfncstrFileNameByFSO = myXobjFSO.GetFileName(myXstrOrgFilePath)
    Set myXobjFSO = Nothing
End Function

 '定型Ｐ_シート上の指定範囲のデータをVariant配列に取込む
Private Sub PfixGetSheetPartRangeDataVariantArray( _
            myXlonRowCnt As Long, myXlonColCnt As Long, myZvarShtData() As Variant, _
            ByVal myXobjShtRng As Object)
'myZvarShtData(i, j) : データ
    myXlonRowCnt = Empty: myXlonColCnt = Empty: Erase myZvarShtData
    If myXobjShtRng Is Nothing Then Exit Sub
    myXlonRowCnt = myXobjShtRng.Rows.Count
    myXlonColCnt = myXobjShtRng.Columns.Count
    If myXlonRowCnt <= 0 Or myXlonColCnt <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myZvarRngData As Variant, i As Long, j As Long
    i = myXlonRowCnt + L - 1: j = myXlonColCnt + L - 1
    ReDim myZvarShtData(i, j) As Variant
    myZvarRngData = myXobjShtRng.Value
    On Error Resume Next
    If myXlonRowCnt * myXlonColCnt = 1 Then
        myZvarShtData(L, L) = myZvarRngData
    Else
        For j = LBound(myZvarRngData, 2) To UBound(myZvarRngData, 2)
            For i = LBound(myZvarRngData, 1) To UBound(myZvarRngData, 1)
                myZvarShtData(i, j) = myZvarRngData(i, j)
            Next i
        Next j
    End If
    On Error GoTo 0
    myZvarRngData = Empty
End Sub

'DummyＰ_
Private Sub MsubDummy()
End Sub

      <br></pre>
    </body>
  </html>
