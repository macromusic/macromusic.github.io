<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>


 '定型Ｆ_指定名のShapeの存在を確認する
Private Function PfncbisCheckShapeExist( _
            ByVal myXobjSheet As Object, ByVal myXstrShpName As String) As Boolean
    PfncbisCheckShapeExist = False
    If myXstrShpName = "" Then Exit Function
    On Error GoTo ExitPath
  Dim myXlonShpCnt As Long: myXlonShpCnt = myXobjSheet.Shapes.Count
    On Error GoTo 0
    If myXlonShpCnt <= 0 Then Exit Function
  Dim myXobjShp As Object
    For Each myXobjShp In myXobjSheet.Shapes
        If myXobjShp.Name = myXstrShpName Then
            PfncbisCheckShapeExist = True: Exit For
        End If
    Next
    Set myXobjShp = Nothing
ExitPath:
End Function

 '定型Ｆ_指定名のShapeを検索してShapeを取得する
Private Function PfncobjSearchShape( _
            ByVal myXobjSheet As Object, ByVal myXstrShpName As String) As Object
    Set PfncobjSearchShape = Nothing
    If myXstrShpName = "" Then Exit Function
    On Error GoTo ExitPath
  Dim myXlonShpCnt As Long: myXlonShpCnt = myXobjSheet.Shapes.Count
    On Error GoTo 0
    If myXlonShpCnt <= 0 Then Exit Function
  Dim myXobjShp As Object
    For Each myXobjShp In myXobjSheet.Shapes
        If myXobjShp.Name = myXstrShpName Then
            Set PfncobjSearchShape = myXobjShp: Exit For
        End If
    Next
    Set myXobjShp = Nothing
ExitPath:
End Function

 '定型Ｆ_指定名の画像の存在を確認する
Private Function PfncbisCheckPictureExist( _
            ByVal myXobjSheet As Object, ByVal myXstrPicName As String) As Boolean
    PfncbisCheckPictureExist = False
    If myXstrPicName = "" Then Exit Function
    On Error GoTo ExitPath
  Dim myXlonPicCnt As Long: myXlonPicCnt = myXobjSheet.Pictures.Count
    On Error GoTo 0
    If myXlonPicCnt <= 0 Then Exit Function
  Dim myXobjPic As Object
    For Each myXobjPic In myXobjSheet.Pictures
        If myXobjPic.Name = myXstrPicName Then
            PfncbisCheckPictureExist = True: Exit For
        End If
    Next
    Set myXobjPic = Nothing
ExitPath:
End Function

 '定型Ｆ_指定名の画像を検索して画像を取得する
Private Function PfncobjSearchPicture( _
            ByVal myXobjSheet As Object, ByVal myXstrPicName As String) As Object
    Set PfncobjSearchPicture = Nothing
    If myXstrPicName = "" Then Exit Function
    On Error GoTo ExitPath
  Dim myXlonPicCnt As Long: myXlonPicCnt = myXobjSheet.Pictures.Count
    On Error GoTo 0
    If myXlonPicCnt <= 0 Then Exit Function
  Dim myXobjPic As Object
    For Each myXobjPic In myXobjSheet.Pictures
        If myXobjPic.Name = myXstrPicName Then
            Set PfncobjSearchPicture = myXobjPic: Exit For
        End If
    Next
    Set myXobjPic = Nothing
ExitPath:
End Function

 '定型Ｆ_画像ShapeRangeを取得する
Private Function PfncobjGetPictureShapeRange(ByVal myXobjPicShp As Object) As Object
    Set PfncobjGetPictureShapeRange = Nothing
    On Error GoTo ExitPath
    Set PfncobjGetPictureShapeRange = myXobjPicShp.ShapeRange
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｆ_シート上に新規図形を追加する
Private Function PfncobjAddNewShape( _
            ByVal myXobjSheet As Object, ByVal myXlonShpType As Long, _
            ByVal myXdouLft As Double, ByVal myXdouTop As Double, _
            ByVal myXdouWdth As Double, ByVal myXdouHght As Double) As Object
    Set PfncobjAddNewShape = Nothing
    If myXobjSheet Is Nothing Then Exit Function
    If myXlonShpType <= 0 Then Exit Function
    If myXdouLft < 0 Or myXdouTop < 0 _
        Or myXdouWdth <= 0 Or myXdouHght <= 0 Then Exit Function
    On Error GoTo ExitPath
    Set PfncobjAddNewShape = myXobjSheet.Shapes _
            .AddShape(myXlonShpType, myXdouLft, myXdouTop, myXdouWdth, myXdouHght)
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｆ_シート上に画像を挿入する
Private Function PfncobjInsertPicture( _
            ByVal myXobjSheet As Object, ByVal myXstrFilePath As String) As Object
    Set PfncobjInsertPicture = Nothing
    If myXobjSheet Is Nothing Then Exit Function
    If myXstrFilePath = "" Then Exit Function
    On Error GoTo ExitPath
    Set PfncobjInsertPicture = myXobjSheet.Pictures.Insert(myXstrFilePath)
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｆ_シート上の画像を切り取って形式を指定して貼り付ける
Private Function PfncobjCutPasteSpecialPicture( _
            ByVal myXobjPicShp As Object, _
            Optional ByVal coXlonPstFrmt As Long = 1) As Object
'coXlonPstFrmt = 1 : "図 (PNG)"
'coXlonPstFrmt = 2 : "図 (JPEG)"
'coXlonPstFrmt = 3 : "図 (GIF)"
'coXlonPstFrmt = 4 : "図 (拡張メタファイル)"
'coXlonPstFrmt = 5 : "ビットマップ"
'coXlonPstFrmt = 6 : "Microsoft Office 描画オブジェクト"
'coXlonPstFrmt = ? : 形式変更しない
    Set PfncobjCutPasteSpecialPicture = Nothing
    If myXobjPicShp Is Nothing Then Exit Function
  Dim myXstrPstFrmt As String
    If coXlonPstFrmt = 1 Then
        myXstrPstFrmt = "図 (PNG)"
    ElseIf coXlonPstFrmt = 2 Then
        myXstrPstFrmt = "図 (JPEG)"
    ElseIf coXlonPstFrmt = 3 Then
        myXstrPstFrmt = "図 (GIF)"
    ElseIf coXlonPstFrmt = 4 Then
        myXstrPstFrmt = "図 (拡張メタファイル)"
    ElseIf coXlonPstFrmt = 5 Then
        myXstrPstFrmt = "ビットマップ"
    ElseIf coXlonPstFrmt = 6 Then
        myXstrPstFrmt = "Microsoft Office 描画オブジェクト"
    Else
        Set PfncobjCutPasteSpecialPicture = myXobjPicShp
        Exit Function
    End If
    On Error GoTo ExitPath
  Dim myXobjPstSht As Object
    Set myXobjPstSht = myXobjPicShp.Parent
    myXobjPstSht.Activate
    DoEvents
    myXobjPicShp.Cut
    DoEvents
    Call myXobjPstSht.PasteSpecial(Format:=myXstrPstFrmt)
    Set PfncobjCutPasteSpecialPicture = myXobjPstSht.Pictures(myXobjPstSht.Pictures.Count)
    On Error GoTo 0
    Set myXobjPstSht = Nothing
ExitPath:
End Function

 '定型Ｆ_シートにクリップボードの画像を貼り付ける
Private Function PfncobjPastePicture(ByVal myXobjPstRng As Object) As Object
    Set PfncobjPastePicture = Nothing
    If myXobjPstRng Is Nothing Then Exit Function
  Dim myXobjPstSht As Object
    On Error GoTo ExitPath
    Set myXobjPstSht = myXobjPstRng.Worksheet
    Call myXobjPstSht.Paste(Destination:=myXobjPstRng)
    Set PfncobjPastePicture = myXobjPstSht.Pictures(myXobjPstSht.Pictures.Count)
    On Error GoTo 0
    Set myXobjPstSht = Nothing
ExitPath:
End Function

 '定型Ｐ_Shapeの縦横比の固定を設定する
Private Sub PfixSetShapeLockAspectRatio(myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            Optional ByVal coXbisUnlckAspctRto As Boolean = True)
'coXbisUnlckAspctRto = False : 縦横比を固定する
'coXbisUnlckAspctRto = True  : 縦横比を固定しない
    myXbisExitFlag = False
    If myXobjShape Is Nothing Then GoTo ExitPath
  Dim myXlonLckAspctRto As Long
    If coXbisUnlckAspctRto = True Then
        myXlonLckAspctRto = msoFalse
    Else
        myXlonLckAspctRto = msoTrue
    End If
    On Error GoTo ExitPath
    myXobjShape.LockAspectRatio = myXlonLckAspctRto
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの位置サイズ変更のプロパティを設定する
Private Sub PfixSetShapePlacement(myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            Optional ByVal coXlonPlcmnt As Long = xlMove)
'coXlonPlcmnt = xlMoveAndSize = 1  : セルに合わせて移動やサイズ変更をする
'coXlonPlcmnt = xlMove = 2         : セルに合わせて移動するがサイズ変更はしない
'coXlonPlcmnt = xlFreeFloating = 3 : セルに合わせて移動やサイズ変更をしない
    myXbisExitFlag = False
    If myXobjShape Is Nothing Then GoTo ExitPath
    On Error GoTo ExitPath
    myXobjShape.Placement = coXlonPlcmnt
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の縦横比の固定を設定する
Private Sub PfixSetPictureLockAspectRatio(myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            Optional ByVal coXbisUnlckAspctRto As Boolean = True)
'coXbisUnlckAspctRto = False : 縦横比を固定する
'coXbisUnlckAspctRto = True  : 縦横比を固定しない
    myXbisExitFlag = False
    If myXobjPicShpRng Is Nothing Then GoTo ExitPath
  Dim myXlonLckAspctRto As Long
    If coXbisUnlckAspctRto = True Then
        myXlonLckAspctRto = msoFalse
    Else
        myXlonLckAspctRto = msoTrue
    End If
    On Error GoTo ExitPath
    myXobjPicShpRng.LockAspectRatio = myXlonLckAspctRto
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の位置サイズ変更のプロパティを設定する
Private Sub PfixSetPicturePlacement(myXbisExitFlag As Boolean, _
            ByVal myXobjPicShp As Object, _
            Optional ByVal coXlonPlcmnt As Long = xlMove)
'coXlonPlcmnt = xlMoveAndSize = 1  : セルに合わせて移動やサイズ変更をする
'coXlonPlcmnt = xlMove = 2         : セルに合わせて移動するがサイズ変更はしない
'coXlonPlcmnt = xlFreeFloating = 3 : セルに合わせて移動やサイズ変更をしない
    myXbisExitFlag = False
    If myXobjPicShp Is Nothing Then GoTo ExitPath
    On Error GoTo ExitPath
    myXobjPicShp.Placement = coXlonPlcmnt
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像のトリミング値を取得する
Private Sub PfixGetPictureCrop( _
            myXdouTrmTop As Double, myXdouTrmLft As Double, _
            myXdouTrmBttm As Double, myXdouTrmRght As Double, _
            ByVal myXobjPicShpRng As Object)
'myXdouTrmTop  : x [cm]
'myXdouTrmLft  : x [cm]
'myXdouTrmBttm : x [cm]
'myXdouTrmRght : x [cm]
  Const coXdouCmPerPoint As Double = 2.54 / 72
    myXdouTrmTop = Empty: myXdouTrmLft = Empty
    myXdouTrmBttm = Empty: myXdouTrmRght = Empty
    If myXobjPicShpRng Is Nothing Then Exit Sub
    With myXobjPicShpRng.PictureFormat
        myXdouTrmTop = .CropTop
        myXdouTrmLft = .CropLeft
        myXdouTrmBttm = .CropBottom
        myXdouTrmRght = .CropRight
    End With
    myXdouTrmTop = coXdouCmPerPoint * myXdouTrmTop
    myXdouTrmLft = coXdouCmPerPoint * myXdouTrmLft
    myXdouTrmBttm = coXdouCmPerPoint * myXdouTrmBttm
    myXdouTrmRght = coXdouCmPerPoint * myXdouTrmRght
End Sub

 '定型Ｐ_画像をトリミングする
Private Sub PfixCropPicture(myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            ByVal myXdouTrmTop As Double, ByVal myXdouTrmLft As Double, _
            ByVal myXdouTrmBttm As Double, ByVal myXdouTrmRght As Double)
'myXdouTrmTop  : x [cm]
'myXdouTrmLft  : x [cm]
'myXdouTrmBttm : x [cm]
'myXdouTrmRght : x [cm]
  Const coXdouPointPerCm As Double = 72 / 2.54
    myXbisExitFlag = False
    If myXobjPicShpRng Is Nothing Then GoTo ExitPath
  Dim myXdouPicHght As Double, myXdouPicWdth As Double
    On Error GoTo ExitPath
    myXdouPicHght = myXobjPicShpRng.Height
    myXdouPicWdth = myXobjPicShpRng.Width
    On Error GoTo 0
    If myXdouTrmTop + myXdouTrmBttm > myXdouPicHght Then GoTo ExitPath
    If myXdouTrmLft + myXdouTrmRght > myXdouPicWdth Then GoTo ExitPath
    With myXobjPicShpRng.PictureFormat
        .CropTop = coXdouPointPerCm * myXdouTrmTop
        .CropLeft = coXdouPointPerCm * myXdouTrmLft
        .CropBottom = coXdouPointPerCm * myXdouTrmBttm
        .CropRight = coXdouPointPerCm * myXdouTrmRght
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの位置とサイズを取得する
Private Sub PfixGetShapePlaceSize( _
            myXdouShpTop As Double, myXdouShpLft As Double, _
            myXdouShpBttm As Double, myXdouShpRght As Double, _
            myXdouShpHght As Double, myXdouShpWdth As Double, _
            ByVal myXobjShape As Object)
    myXdouShpTop = Empty: myXdouShpLft = Empty
    myXdouShpBttm = Empty: myXdouShpRght = Empty
    myXdouShpHght = Empty: myXdouShpWdth = Empty
    On Error GoTo ExitPath
    With myXobjShape
        myXdouShpTop = .Top
        myXdouShpLft = .Left
        myXdouShpHght = .Height
        myXdouShpWdth = .Width
    End With
    On Error GoTo 0
    myXdouShpBttm = myXdouShpTop + myXdouShpHght
    myXdouShpRght = myXdouShpLft + myXdouShpWdth
ExitPath:
End Sub

 '定型Ｐ_Shapeの位置とサイズをセル範囲で取得する
Private Sub PfixGetShapePlaceSizeRange( _
            myXlonShpTopRow As Long, myXlonShpLftCol As Long, _
            myXlonShpBttmRow As Long, myXlonShpRghtCol As Long, _
            myXlonShpHghtRows As Long, myXlonShpWdthCols As Long, _
            ByVal myXobjShape As Object)
    myXlonShpTopRow = Empty: myXlonShpLftCol = Empty
    myXlonShpBttmRow = Empty: myXlonShpRghtCol = Empty
    myXlonShpHghtRows = Empty: myXlonShpWdthCols = Empty
    On Error GoTo ExitPath
    With myXobjShape
        myXlonShpTopRow = .TopLeftCell.Row
        myXlonShpLftCol = .TopLeftCell.Column
        myXlonShpBttmRow = .BottomRightCell.Row
        myXlonShpRghtCol = .BottomRightCell.Column
    End With
    On Error GoTo 0
    myXlonShpHghtRows = myXlonShpBttmRow - myXlonShpTopRow + 1
    myXlonShpWdthCols = myXlonShpRghtCol - myXlonShpLftCol + 1
ExitPath:
End Sub

 '定型Ｐ_画像の位置とサイズを取得する
Private Sub PfixGePicturePlaceSize( _
            myXdouPicTop As Double, myXdouPicLft As Double, _
            myXdouPicBttm As Double, myXdouPicRght As Double, _
            myXdouPicHght As Double, myXdouPicWdth As Double, _
            ByVal myXobjPicture As Object)
    myXdouPicTop = Empty: myXdouPicLft = Empty
    myXdouPicBttm = Empty: myXdouPicRght = Empty
    myXdouPicHght = Empty: myXdouPicWdth = Empty
    On Error GoTo ExitPath
    With myXobjPicture
        myXdouPicTop = .Top
        myXdouPicLft = .Left
        myXdouPicHght = .Height
        myXdouPicWdth = .Width
    End With
    On Error GoTo 0
    myXdouPicBttm = myXdouPicTop + myXdouPicHght
    myXdouPicRght = myXdouPicLft + myXdouPicWdth
ExitPath:
End Sub

 '定型Ｐ_画像の原型サイズと比較した時の倍率を取得する
Private Sub PfixGetPictureScale( _
        myXdouSclHght As Double, myXdouSclWdth As Double, _
        ByVal myXobjPicShpRng As Object)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
    myXdouSclHght = Empty: myXdouSclWdth = Empty
    If myXobjPicShpRng Is Nothing Then Exit Sub
  Dim myXlonLckAspctRto As Long
  Dim myXdouCrpTop As Double, myXdouCrpLft As Double, _
        myXdouCrpBttm As Double, myXdouCrpRght As Double
  Dim myXdouHght As Double, myXdouWdth As Double
  Dim myXdouOrgHght As Double, myXdouOrgWdth As Double
    With myXobjPicShpRng
    '//画像の現状を取得して原型状態に変更
        With .PictureFormat
            myXdouCrpTop = .CropTop
            myXdouCrpLft = .CropLeft
            myXdouCrpBttm = .CropBottom
            myXdouCrpRght = .CropRight
            .CropTop = 0
            .CropLeft = 0
            .CropRight = 0
            .CropBottom = 0
        End With
        myXdouHght = .Height
        myXdouWdth = .Width
        myXlonLckAspctRto = .LockAspectRatio
        .LockAspectRatio = msoFalse
        Call .ScaleHeight(1, msoTrue)
        Call .ScaleWidth(1, msoTrue)
    '//原型サイズと比較した時の倍率を取得
        myXdouOrgHght = .Height
        myXdouOrgWdth = .Width
        myXdouSclHght = myXdouHght / myXdouOrgHght * 100
        myXdouSclWdth = myXdouWdth / myXdouOrgWdth * 100
    '//画像を最初の状態に戻す
        .Height = myXdouHght
        .Width = myXdouWdth
        .LockAspectRatio = myXlonLckAspctRto
        With .PictureFormat
            .CropTop = myXdouCrpTop
            .CropLeft = myXdouCrpLft
            .CropBottom = myXdouCrpBttm
            .CropRight = myXdouCrpRght
        End With
    End With
End Sub

 '定型Ｐ_画像の位置とサイズをセル範囲で取得する
Private Sub PfixGetPicturePlaceSizeRange( _
            myXlonPicTopRow As Long, myXlonPicLftCol As Long, _
            myXlonPicBttmRow As Long, myXlonPicRghtCol As Long, _
            myXlonPicHghtRows As Long, myXlonPicWdthCols As Long, _
            ByVal myXobjPicture As Object)
    myXlonPicTopRow = Empty: myXlonPicLftCol = Empty
    myXlonPicBttmRow = Empty: myXlonPicRghtCol = Empty
    myXlonPicHghtRows = Empty: myXlonPicWdthCols = Empty
    On Error GoTo ExitPath
    With myXobjPicture
        myXlonPicTopRow = .TopLeftCell.Row
        myXlonPicLftCol = .TopLeftCell.Column
        myXlonPicBttmRow = .BottomRightCell.Row
        myXlonPicRghtCol = .BottomRightCell.Column
    End With
    On Error GoTo 0
    myXlonPicHghtRows = myXlonPicBttmRow - myXlonPicTopRow + 1
    myXlonPicWdthCols = myXlonPicRghtCol - myXlonPicLftCol + 1
ExitPath:
End Sub

 '定型Ｐ_Shapeの位置を設定する
Private Sub PfixSetShapePlace( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            ByVal myXdouShpTop As Double, ByVal myXdouShpLft As Double)
'myXdouShpTop : x [point]
'myXdouShpLft : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpTop As Double: myXdouTmpTop = myXobjShape.Top
    On Error GoTo 0
    If myXdouShpTop < 0 Or myXdouShpLft < 0 Then GoTo ExitPath
    myXobjShape.Top = myXdouShpTop
    myXobjShape.Left = myXdouShpLft
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの縦横比を固定せずにサイズを設定する
Private Sub PfixSetShapeSizeWithoutLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            ByVal myXdouShpHght As Double, ByVal myXdouShpWdth As Double)
'myXdouShpHght : x [point]
'myXdouShpWdth : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpHght As Double: myXdouTmpHght = myXobjShape.Height
    On Error GoTo 0
    If myXdouShpHght < 0 Or myXdouShpWdth < 0 Then GoTo ExitPath
    myXobjShape.LockAspectRatio = msoFalse
    myXobjShape.Height = myXdouShpHght
    myXobjShape.Width = myXdouShpWdth
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの縦横比を固定してサイズを設定する
Private Sub PfixSetShapeSizeWithLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            ByVal myXdouShpHght As Double, ByVal myXdouShpWdth As Double)
'myXdouShpHght : x [point]
'myXdouShpWdth : x [point]
    myXbisExitFlag = False
  Dim myXdouOrgHght As Double, myXdouOrgWdth As Double
    On Error GoTo ExitPath
    myXdouOrgHght = myXobjShape.Height
    myXdouOrgWdth = myXobjShape.Width
    On Error GoTo 0
    If myXdouShpHght < 0 Or myXdouShpWdth < 0 Then GoTo ExitPath
  Dim myXdouHghtRatio As Double, myXdouWdthRatio As Double, myXdouRatio As Double
    myXdouHghtRatio = myXdouShpHght / myXdouOrgHght
    myXdouWdthRatio = myXdouShpWdth / myXdouOrgWdth
    myXobjShape.LockAspectRatio = msoTrue
    If myXdouHghtRatio <= myXdouWdthRatio Then
        myXdouRatio = myXdouHghtRatio
    Else
        myXdouRatio = myXdouWdthRatio
    End If
    myXobjShape.Height = myXdouOrgHght * myXdouRatio
'    myXobjShape.Width = myXdouOrgWdth * myXdouRatio
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの縦横比を固定せずに倍率でサイズを設定する
Private Sub PfixSetShapeScaleWithoutLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double, _
            Optional ByVal coXbisRltvToOrgSize As Boolean = False, _
            Optional ByVal coXlonScaleFrom As Long = msoScaleFromTopLeft)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
'coXbisRltvToOrgSize = False : 元のサイズを基準にしない
'coXbisRltvToOrgSize = True  : 元のサイズを基準にする
'coXlonScaleFrom = msoScaleFromTopLeft     : 0 : 図形の左上隅の位置が保持
'coXlonScaleFrom = msoScaleFromMiddle      : 1 : 図形の中心の位置が保持
'coXlonScaleFrom = msoScaleFromBottomRight : 2 : 図形の右下隅の位置が保持
    myXbisExitFlag = False
  Dim myXdouShpType As Double
    On Error GoTo ExitPath
    myXdouShpType = myXobjShape.Type
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    myXdouSclHght = myXdouSclHght / 100
    myXdouSclWdth = myXdouSclWdth / 100
    If coXbisRltvToOrgSize = True Then
        If myXdouShpType = msoEmbeddedOLEObject Or myXdouShpType = msoLinkedOLEObject Or _
                myXdouShpType = msoOLEControlObject Or _
                myXdouShpType = msoLinkedPicture Or myXdouShpType = msoPicture Then
            coXbisRltvToOrgSize = msoTrue
        Else
            coXbisRltvToOrgSize = msoFalse
        End If
    Else
        coXbisRltvToOrgSize = msoFalse
    End If
    myXobjShape.LockAspectRatio = msoFalse
    Call myXobjShape.ScaleHeight(myXdouSclHght, coXbisRltvToOrgSize, coXlonScaleFrom)
    Call myXobjShape.ScaleWidth(myXdouSclWdth, coXbisRltvToOrgSize, coXlonScaleFrom)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの縦横比を固定して倍率でサイズを設定する
Private Sub PfixSetShapeScaleWithLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double, _
            Optional ByVal coXbisRltvToOrgSize As Boolean = False, _
            Optional ByVal coXlonScaleFrom As Long = msoScaleFromTopLeft)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
'coXbisRltvToOrgSize = False : 元のサイズを基準にしない
'coXbisRltvToOrgSize = True  : 元のサイズを基準にする
'coXlonScaleFrom = msoScaleFromTopLeft     : 0 : 図形の左上隅の位置が保持
'coXlonScaleFrom = msoScaleFromMiddle      : 1 : 図形の中心の位置が保持
'coXlonScaleFrom = msoScaleFromBottomRight : 2 : 図形の右下隅の位置が保持
    myXbisExitFlag = False
  Dim myXdouShpType As Double
    On Error GoTo ExitPath
    myXdouShpType = myXobjShape.Type
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    If coXbisRltvToOrgSize = True Then
        If myXdouShpType = msoEmbeddedOLEObject Or myXdouShpType = msoLinkedOLEObject Or _
                myXdouShpType = msoOLEControlObject Or _
                myXdouShpType = msoLinkedPicture Or myXdouShpType = msoPicture Then
            coXbisRltvToOrgSize = msoTrue
        Else
            coXbisRltvToOrgSize = msoFalse
        End If
    Else
        coXbisRltvToOrgSize = msoFalse
    End If
  Dim myXdouScale As Double
    If myXdouSclHght <= myXdouSclWdth Then
        myXdouScale = myXdouSclHght / 100
    Else
        myXdouScale = myXdouSclWdth / 100
    End If
    myXobjShape.LockAspectRatio = msoTrue
    Call myXobjShape.ScaleHeight(myXdouScale, coXbisRltvToOrgSize, coXlonScaleFrom)
'    Call myXobjShape.ScaleWidth(myXdouScale, coXbisRltvToOrgSize, coXlonScaleFrom)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeの位置をエクセルシートの行列位置で設定する
Private Sub PincSetShapePlaceInSheetRange( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, ByVal myXobjSheet As Object, _
            ByVal myXlonTopRow As Long, ByVal myXlonLftCol As Long, _
            ByVal myXdouTopOfst As Double, ByVal myXdouLftOfst As Double)
'Includes PfixGetRangePlaceSize
'Includes PfixSetShapePlace
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
'//エクセルシートの指定セル範囲の位置情報とサイズ情報を取得
  Dim myXdouRngTop As Double, myXdouRngLft As Double, _
        myXdouRngHght As Double, myXdouRngWdth As Double
    Call PfixGetRangePlaceSize( _
            myXdouRngTop, myXdouRngLft, myXdouRngHght, myXdouRngWdth, _
            myXobjSheet, myXlonTopRow, myXlonLftCol, myXlonTopRow, myXlonLftCol)
    If myXdouRngHght <= 0 Or myXdouRngWdth <= 0 Then GoTo ExitPath
'//Shapeの位置を設定
  Dim myXdouShpTop As Double, myXdouShpLft As Double
    myXdouShpTop = myXdouRngTop + myXdouTopOfst
    myXdouShpLft = myXdouRngLft + myXdouLftOfst
    Call PfixSetShapePlace(myXbisExitFlag, myXobjShape, myXdouShpTop, myXdouShpLft)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeのサイズをエクセルシートの行列位置で設定する
Private Sub PincSetShapeSizeInSheetRange( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, ByVal myXobjSheet As Object, _
            ByVal myXlonTopRow As Long, ByVal myXlonLftCol As Long, _
            ByVal myXlonBttmRow As Long, ByVal myXlonRghtCol As Long, _
            ByVal myXdouTopOfst As Double, ByVal myXdouLftOfst As Double, _
            ByVal myXdouBttmOfst As Double, ByVal myXdouRghtOfst As Double, _
            Optional ByVal coXbisShpWthOtAspctRto As Boolean = False)
'Includes PfixGetRangePlaceSize
'Includes PfixSetShapeSizeWithLockAspectRatio
'Includes PfixSetShapeSizeWithoutLockAspectRatio
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
'myXdouBttmOfst : x [point]
'myXdouRghtOfst : x [point]
'coXbisShpWthOtAspctRto = False : Shapeの縦横比を固定してサイズを設定
'coXbisShpWthOtAspctRto = True  : Shapeの縦横比を固定せずにサイズを設定
'//エクセルシートの指定セル範囲の位置情報とサイズ情報を取得
  Dim myXdouRngTop As Double, myXdouRngLft As Double, _
        myXdouRngHght As Double, myXdouRngWdth As Double
    Call PfixGetRangePlaceSize( _
            myXdouRngTop, myXdouRngLft, myXdouRngHght, myXdouRngWdth, _
            myXobjSheet, myXlonTopRow, myXlonLftCol, myXlonBttmRow, myXlonRghtCol)
    If myXdouRngHght <= 0 Or myXdouRngWdth <= 0 Then GoTo ExitPath
  Dim myXdouShpHght As Double, myXdouShpWdth As Double
    myXdouShpHght = myXdouRngHght - myXdouTopOfst + myXdouBttmOfst
    myXdouShpWdth = myXdouRngWdth - myXdouLftOfst + myXdouRghtOfst
    If myXdouShpHght < 0 Or myXdouShpWdth < 0 Then GoTo ExitPath
    If coXbisShpWthOtAspctRto = False Then
    '//Shapeの縦横比を固定してサイズを設定
        Call PfixSetShapeSizeWithLockAspectRatio( _
                myXbisExitFlag, _
                myXobjShape, myXdouShpHght, myXdouShpWdth)
    Else
    '//Shapeの縦横比を固定せずにサイズを設定
        Call PfixSetShapeSizeWithoutLockAspectRatio( _
                myXbisExitFlag, _
                myXobjShape, myXdouShpHght, myXdouShpWdth)
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の位置を設定する
Private Sub PfixSetPicturePlace( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            ByVal myXdouShpTop As Double, ByVal myXdouShpLft As Double)
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpTop As Double: myXdouTmpTop = myXobjPicShpRng.Top
    On Error GoTo 0
    If myXdouShpTop < 0 Or myXdouShpLft < 0 Then GoTo ExitPath
    myXobjPicShpRng.Top = myXdouShpTop
    myXobjPicShpRng.Left = myXdouShpLft
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の縦横比を固定せずにサイズを設定する
Private Sub PfixSetPictureSizeWithoutLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            ByVal myXdouPicHght As Double, ByVal myXdouPicWdth As Double)
'myXdouPicHght : x [point]
'myXdouPicWdth : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpHght As Double: myXdouTmpHght = myXobjPicShpRng.Height
    On Error GoTo 0
    If myXdouPicHght < 0 Or myXdouPicWdth < 0 Then GoTo ExitPath
    myXobjPicShpRng.LockAspectRatio = msoFalse
    myXobjPicShpRng.Height = myXdouPicHght
    myXobjPicShpRng.Width = myXdouPicWdth
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の縦横比を固定してサイズを設定する
Private Sub PfixSetPictureSizeWithLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            ByVal myXdouPicHght As Double, ByVal myXdouPicWdth As Double)
'myXdouPicHght : x [point]
'myXdouPicWdth : x [point]
    myXbisExitFlag = False
  Dim myXdouOrgHght As Double, myXdouOrgWdth As Double
    On Error GoTo ExitPath
    myXdouOrgHght = myXobjPicShpRng.Height
    myXdouOrgWdth = myXobjPicShpRng.Width
    On Error GoTo 0
    If myXdouPicHght < 0 Or myXdouPicWdth < 0 Then GoTo ExitPath
  Dim myXdouHghtRatio As Double, myXdouWdthRatio As Double, myXdouRatio As Double
    myXdouHghtRatio = myXdouPicHght / myXdouOrgHght
    myXdouWdthRatio = myXdouPicWdth / myXdouOrgWdth
    myXobjPicShpRng.LockAspectRatio = msoTrue
    If myXdouHghtRatio <= myXdouWdthRatio Then
        myXdouRatio = myXdouHghtRatio
    Else
        myXdouRatio = myXdouWdthRatio
    End If
    myXobjPicShpRng.Height = myXdouOrgHght * myXdouRatio
'    myXobjPicShpRng.Width = myXdouOrgWdth * myXdouRatio
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の縦横比を固定せずに倍率でサイズを設定する
Private Sub PfixSetPictureScaleWithoutLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double, _
            Optional ByVal coXbisRltvToOrgSize As Boolean = True, _
            Optional ByVal coXlonScaleFrom As Long = msoScaleFromTopLeft)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
'coXbisRltvToOrgSize = True  : 元のサイズを基準にする
'coXbisRltvToOrgSize = False : 元のサイズを基準にしない
'coXlonScaleFrom = msoScaleFromTopLeft     : 0 : 図形の左上隅の位置が保持
'coXlonScaleFrom = msoScaleFromMiddle      : 1 : 図形の中心の位置が保持
'coXlonScaleFrom = msoScaleFromBottomRight : 2 : 図形の右下隅の位置が保持
    myXbisExitFlag = False
  Dim myXdouPicType As Double
    On Error GoTo ExitPath
    myXdouPicType = myXobjPicShpRng.Type
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    myXdouSclHght = myXdouSclHght / 100
    myXdouSclWdth = myXdouSclWdth / 100
    If coXbisRltvToOrgSize = True Then
        If myXdouPicType = msoEmbeddedOLEObject Or myXdouPicType = msoLinkedOLEObject Or _
                myXdouPicType = msoOLEControlObject Or _
                myXdouPicType = msoLinkedPicture Or myXdouPicType = msoPicture Then
            coXbisRltvToOrgSize = msoTrue
        Else
            coXbisRltvToOrgSize = msoFalse
        End If
    Else
        coXbisRltvToOrgSize = msoFalse
    End If
    myXobjPicShpRng.LockAspectRatio = msoFalse
    Call myXobjPicShpRng.ScaleHeight(myXdouSclHght, coXbisRltvToOrgSize, coXlonScaleFrom)
    Call myXobjPicShpRng.ScaleWidth(myXdouSclWdth, coXbisRltvToOrgSize, coXlonScaleFrom)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の縦横比を固定して倍率でサイズを設定する
Private Sub PfixSetPictureScaleWithLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double, _
            Optional ByVal coXbisRltvToOrgSize As Boolean = True, _
            Optional ByVal coXlonScaleFrom As Long = msoScaleFromTopLeft)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
'coXbisRltvToOrgSize = True  : 元のサイズを基準にする
'coXbisRltvToOrgSize = False : 元のサイズを基準にしない
'coXlonScaleFrom = msoScaleFromTopLeft     : 0 : 図形の左上隅の位置が保持
'coXlonScaleFrom = msoScaleFromMiddle      : 1 : 図形の中心の位置が保持
'coXlonScaleFrom = msoScaleFromBottomRight : 2 : 図形の右下隅の位置が保持
    myXbisExitFlag = False
  Dim myXdouPicType As Double
    On Error GoTo ExitPath
    myXdouPicType = myXobjPicShpRng.Type
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    If coXbisRltvToOrgSize = True Then
        If myXdouPicType = msoEmbeddedOLEObject Or myXdouPicType = msoLinkedOLEObject Or _
                myXdouPicType = msoOLEControlObject Or _
                myXdouPicType = msoLinkedPicture Or myXdouPicType = msoPicture Then
            coXbisRltvToOrgSize = msoTrue
        Else
            coXbisRltvToOrgSize = msoFalse
        End If
    Else
        coXbisRltvToOrgSize = msoFalse
    End If
  Dim myXdouScale As Double
    If myXdouSclHght <= myXdouSclWdth Then
        myXdouScale = myXdouSclHght / 100
    Else
        myXdouScale = myXdouSclWdth / 100
    End If
    myXobjPicShpRng.LockAspectRatio = msoTrue
    Call myXobjPicShpRng.ScaleHeight(myXdouScale, coXbisRltvToOrgSize, coXlonScaleFrom)
'    Call myXobjPicShpRng.ScaleWidth(myXdouScale, coXbisRltvToOrgSize, coXlonScaleFrom)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像の位置をエクセルシートの行列位置で設定する
Private Sub PincSetPicturePlaceInSheetRange( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, ByVal myXobjSheet As Object, _
            ByVal myXlonTopRow As Long, ByVal myXlonLftCol As Long, _
            ByVal myXdouTopOfst As Double, ByVal myXdouLftOfst As Double)
'Includes PfixGetRangePlaceSize
'Includes PfixSetPicturePlace
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
'//エクセルシートの指定セル範囲の位置情報とサイズ情報を取得
  Dim myXdouRngTop As Double, myXdouRngLft As Double, _
        myXdouRngHght As Double, myXdouRngWdth As Double
    Call PfixGetRangePlaceSize( _
            myXdouRngTop, myXdouRngLft, myXdouRngHght, myXdouRngWdth, _
            myXobjSheet, myXlonTopRow, myXlonLftCol, myXlonTopRow, myXlonLftCol)
    If myXdouRngHght <= 0 Or myXdouRngWdth <= 0 Then GoTo ExitPath
'//画像の位置を設定
  Dim myXdouPicTop As Double, myXdouPicLft As Double
    myXdouPicTop = myXdouRngTop + myXdouTopOfst
    myXdouPicLft = myXdouRngLft + myXdouLftOfst
    Call PfixSetPicturePlace(myXbisExitFlag, myXobjPicShpRng, myXdouPicTop, myXdouPicLft)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_画像のサイズをエクセルシートの行列位置で設定する
Private Sub PincSetPictureSizeInSheetRange( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjPicShpRng As Object, ByVal myXobjSheet As Object, _
            ByVal myXlonTopRow As Long, ByVal myXlonLftCol As Long, _
            ByVal myXlonBttmRow As Long, ByVal myXlonRghtCol As Long, _
            ByVal myXdouTopOfst As Double, ByVal myXdouLftOfst As Double, _
            ByVal myXdouBttmOfst As Double, ByVal myXdouRghtOfst As Double, _
            Optional ByVal coXbisPicWthOtAspctRto As Boolean = False)
'Includes PfixGetRangePlaceSize
'Includes PfixSetPictureSizeWithLockAspectRatio
'Includes PfixSetPictureSizeWithoutLockAspectRatio
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
'myXdouBttmOfst : x [point]
'myXdouRghtOfst : x [point]
'coXbisPicWthOtAspctRto = False : 画像の縦横比を固定してサイズを設定
'coXbisPicWthOtAspctRto = True  : 画像の縦横比を固定せずにサイズを設定
'//エクセルシートの指定セル範囲の位置情報とサイズ情報を取得
  Dim myXdouRngTop As Double, myXdouRngLft As Double, _
        myXdouRngHght As Double, myXdouRngWdth As Double
    Call PfixGetRangePlaceSize( _
            myXdouRngTop, myXdouRngLft, myXdouRngHght, myXdouRngWdth, _
            myXobjSheet, myXlonTopRow, myXlonLftCol, myXlonBttmRow, myXlonRghtCol)
    If myXdouRngHght <= 0 Or myXdouRngWdth <= 0 Then GoTo ExitPath
  Dim myXdouPicHght As Double, myXdouPicWdth As Double
    myXdouPicHght = myXdouRngHght - myXdouTopOfst + myXdouBttmOfst
    myXdouPicWdth = myXdouRngWdth - myXdouLftOfst + myXdouRghtOfst
    If myXdouPicHght < 0 Or myXdouPicWdth < 0 Then GoTo ExitPath
    If coXbisPicWthOtAspctRto = False Then
    '//画像の縦横比を固定してサイズを設定
        Call PfixSetPictureSizeWithLockAspectRatio( _
                myXbisExitFlag, _
                myXobjPicShpRng, myXdouPicHght, myXdouPicWdth)
    Else
    '//画像の縦横比を固定せずにサイズを設定
        Call PfixSetPictureSizeWithoutLockAspectRatio( _
                myXbisExitFlag, _
                myXobjPicShpRng, myXdouPicHght, myXdouPicWdth)
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeを順番に等間隔に配置する場合の位置情報を取得する
Private Sub PfixGetPlaceForShapeArrange( _
            myXdouTop As Double, myXdouLft As Double, _
            myXdouBttm As Double, myXdouRght As Double, _
            ByVal k As Long, _
            ByVal myXdouStrtTop As Double, ByVal myXdouStrtLft As Double, _
            ByVal myXdouShpHght As Double, ByVal myXdouShpWdth As Double, _
            ByVal myXlonNforDrctn As Long, _
            ByVal myXdouVIntrvl As Double, ByVal myXdouHIntrvl As Double, _
            Optional ByVal coXstrPstDrctn As String = "LftToRght")
'myXdouX : x [point]
'myXdouStrtTop : Shapeを配置する最初のTop位置
'myXdouStrtLft : Shapeを配置する最初のLeft位置
'myXdouShpHght : Shapeサイズ調整する高さ
'myXdouShpWdth : Shapeサイズ調整する幅
'myXlonNforDrctn : 配置の方向に配置するShape数
'myXdouVIntrvl : ShapeとShapeの間の縦方向の間隔
'myXdouHIntrvl : ShapeとShapeの間の横方向の間隔
'coXstrPstDrctn = "LftToRght" : 配置の方向_左から右_Zタイプ
'coXstrPstDrctn = "TopToBttm" : 配置の方向_上から下_Nタイプ
    myXdouTop = Empty: myXdouLft = Empty: myXdouBttm = Empty: myXdouRght = Empty
    If k <= 0 Then Exit Sub
    If myXdouStrtTop < 0 Or myXdouStrtLft < 0 Then Exit Sub
    If myXdouShpHght <= 0 Or myXdouShpWdth <= 0 Then Exit Sub
    If myXlonNforDrctn <= 0 Then Exit Sub
    If myXdouVIntrvl < 0 Or myXdouHIntrvl < 0 Then Exit Sub
    If coXstrPstDrctn <> "LftToRght" And coXstrPstDrctn <> "TopToBttm" Then Exit Sub
  Dim myXdouSctnHght As Double, myXdouSctnWdth As Double
    myXdouSctnHght = myXdouShpHght + myXdouVIntrvl
    myXdouSctnWdth = myXdouShpWdth + myXdouHIntrvl
    If myXdouSctnHght <= 0 Or myXdouSctnWdth <= 0 Then Exit Sub
  Dim i As Long, j As Long
    If coXstrPstDrctn = "LftToRght" Then
        i = Int((k - 0.1) / myXlonNforDrctn) * myXdouSctnHght
        myXdouTop = myXdouStrtTop + i
        myXdouBttm = myXdouTop + myXdouShpHght
        j = (k - 1 - Int((k - 0.1) / myXlonNforDrctn) * myXlonNforDrctn) * myXdouSctnWdth
        myXdouLft = myXdouStrtLft + j
        myXdouRght = myXdouLft + myXdouShpWdth
    ElseIf coXstrPstDrctn = "TopToBttm" Then
        j = Int((k - 0.1) / myXlonNforDrctn) * myXdouSctnWdth
        myXdouLft = myXdouStrtLft + j
        myXdouRght = myXdouLft + myXdouShpWdth
        i = (k - 1 - Int((k - 0.1) / myXlonNforDrctn) * myXlonNforDrctn) * myXdouSctnHght
        myXdouTop = myXdouStrtTop + i
        myXdouBttm = myXdouTop + myXdouShpHght
    End If
End Sub

 '定型Ｐ_Shapeを順番に等間隔に配置する場合のセル範囲の位置情報を取得する
Private Sub PfixGetRangePlaceForShapeArrange( _
            myXlonTopRow As Long, myXlonLftCol As Long, _
            myXlonBttmRow As Long, myXlonRghtCol As Long, _
            myXlonShpNameRow As Long, myXlonShpNameCol As Long, _
            ByVal k As Long, _
            ByVal myXlonStrtRow As Long, ByVal myXlonStrtCol As Long, _
            ByVal myXlonShpHghtRows As Long, ByVal myXlonShpWdthCols As Long, _
            ByVal myXlonNforDrctn As Long, _
            ByVal myXlonRowIntrvl As Long, ByVal myXlonColIntrvl As Long, _
            Optional ByVal coXstrPstDrctn As String = "LftToRght", _
            Optional ByVal coXlonShpNameOn As Long = 0)
'myXlonStrtRow : Shapeを配置する最初の行(画像名表示行の1行分も含めた行位置)
'myXlonStrtCol : Shapeを配置する最初の列
'myXlonShpHghtRows : Shapeサイズ調整する行範囲(画像名表示行の1行分も含めた行数)
'myXlonShpWdthCols : Shapeサイズ調整する列範囲
'myXlonNforDrctn : 配置の方向に配置するShape数
'myXlonRowIntrvl : ShapeとShapeの間の行間隔
'myXlonColIntrvl : ShapeとShapeの間の列間隔
'coXstrPstDrctn = "LftToRght" : 配置の方向_左から右_Zタイプ
'coXstrPstDrctn = "TopToBttm" : 配置の方向_上から下_Nタイプ
'coXlonShpNameOn = 0 : Shape名をセルに表示しない
'coXlonShpNameOn = 1 : Shape名をセルに表示する
    myXlonTopRow = Empty: myXlonLftCol = Empty
    myXlonBttmRow = Empty: myXlonRghtCol = Empty
    myXlonShpNameRow = Empty: myXlonShpNameCol = Empty
    If k <= 0 Then Exit Sub
    If myXlonStrtRow <= 0 Or myXlonStrtCol <= 0 Then Exit Sub
    If myXlonShpHghtRows <= 0 Or myXlonShpWdthCols <= 0 Then Exit Sub
    If myXlonNforDrctn <= 0 Then Exit Sub
    If myXlonRowIntrvl < 0 Or myXlonColIntrvl < 0 Then Exit Sub
    If coXstrPstDrctn <> "LftToRght" And coXstrPstDrctn <> "TopToBttm" Then Exit Sub
    If coXlonShpNameOn <> 0 Then coXlonShpNameOn = 1
  Dim myXlonShpNameOn As Long
    If myXlonShpHghtRows = 1 Then
        myXlonShpNameOn = 0
    Else
        myXlonShpNameOn = coXlonShpNameOn
    End If
  Dim myXlonSctnRngHght As Long, myXlonSctnRngWdth As Long
    myXlonSctnRngHght = myXlonShpHghtRows + myXlonRowIntrvl
    myXlonSctnRngWdth = myXlonShpWdthCols + myXlonColIntrvl
    If myXlonSctnRngHght <= 0 Or myXlonSctnRngWdth <= 0 Then Exit Sub
  Dim i As Long, j As Long
    If coXstrPstDrctn = "LftToRght" Then
        i = Int((k - 0.1) / myXlonNforDrctn) * myXlonSctnRngHght
        myXlonTopRow = myXlonStrtRow + i + myXlonShpNameOn
        myXlonBttmRow = myXlonTopRow + myXlonShpHghtRows - myXlonShpNameOn - 1
        j = (k - 1 - Int((k - 0.1) / myXlonNforDrctn) * myXlonNforDrctn) * myXlonSctnRngWdth
        myXlonLftCol = myXlonStrtCol + j
        myXlonRghtCol = myXlonLftCol + myXlonShpWdthCols - 1
    ElseIf coXstrPstDrctn = "TopToBttm" Then
        j = Int((k - 0.1) / myXlonNforDrctn) * myXlonSctnRngWdth
        myXlonLftCol = myXlonStrtCol + j
        myXlonRghtCol = myXlonLftCol + myXlonShpWdthCols - 1
        i = (k - 1 - Int((k - 0.1) / myXlonNforDrctn) * myXlonNforDrctn) * myXlonSctnRngHght
        myXlonTopRow = myXlonStrtRow + i + myXlonShpNameOn
        myXlonBttmRow = myXlonTopRow + myXlonShpHghtRows - myXlonShpNameOn - 1
    End If
    If coXlonShpNameOn = 1 Then
        myXlonShpNameRow = myXlonTopRow - myXlonShpNameOn
        myXlonShpNameCol = myXlonLftCol
    End If
End Sub

 '定型Ｆ_Shapeの名前を取得する
Private Function PfncstrGetShapeName(ByVal myXobjShape As Object) As String
    PfncstrGetShapeName = Empty
    On Error GoTo ExitPath
    PfncstrGetShapeName = myXobjShape.Name
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｐ_Shapeに名前を設定する
Private Sub PfixSetShapeName(myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, ByVal myXstrShpName As String)
    myXbisExitFlag = False
    If myXstrShpName = "" Then Exit Sub
    On Error GoTo ExitPath
    myXobjShape.Name = myXstrShpName
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_Shapeのテキストを取得する
Private Function PfncstrGetShapeText(ByVal myXobjShape As Object) As String
    PfncstrGetShapeText = Empty
    On Error GoTo ExitPath
    PfncstrGetShapeText = myXobjShape.TextFrame2.TextRange.Characters.Text
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｐ_Shapeのテキストを編集する
Private Sub PfixSetShapeText(myXbisExitFlag As Boolean, _
            ByVal myXobjShape As Object, ByVal myXstrTxt As String)
    myXbisExitFlag = False
    If myXstrTxt = "" Then Exit Sub
    On Error GoTo ExitPath
    myXobjShape.TextFrame2.TextRange.Characters.Text = myXstrTxt
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_シート上の全Shapeを削除する
Private Sub PfixDeleteShapes(myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object)
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXlonShpCnt As Long: myXlonShpCnt = myXobjSheet.Shapes.Count
    On Error GoTo 0
    If myXlonShpCnt <= 0 Then GoTo ExitPath
  Dim myXobjShp As Object
    For Each myXobjShp In myXobjSheet.Shapes
        myXobjShp.Delete
    Next myXobjShp
    Set myXobjShp = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定名のShapeを削除する
Private Sub PfixDeleteDesignateShape(myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object, ByVal myXstrShpName As String)
    myXbisExitFlag = False
    If myXstrShpName = "" Then GoTo ExitPath
    On Error GoTo ExitPath
  Dim myXlonShpCnt As Long: myXlonShpCnt = myXobjSheet.Shapes.Count
    On Error GoTo 0
    If myXlonShpCnt <= 0 Then GoTo ExitPath
  Dim myXobjShp As Object
    For Each myXobjShp In myXobjSheet.Shapes
        If myXobjShp.Name = myXstrShpName Then
            myXobjShp.Delete: Exit For
        End If
    Next myXobjShp
    Set myXobjShp = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定名のShape以外の全Shapeを削除する
Private Sub PfixDeleteNoDesignateShapes(myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object, ByVal myXstrShpName As String)
    myXbisExitFlag = False
    If myXstrShpName = "" Then GoTo ExitPath
    On Error GoTo ExitPath
  Dim myXlonShpCnt As Long: myXlonShpCnt = myXobjSheet.Shapes.Count
    On Error GoTo 0
    If myXlonShpCnt <= 0 Then GoTo ExitPath
  Dim myXobjShp As Object
    For Each myXobjShp In myXobjSheet.Shapes
        If myXobjShp.Name <> myXstrShpName Then
            myXobjShp.Delete
        End If
    Next myXobjShp
    Set myXobjShp = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

      <br></pre>
    </body>
  </html>
