<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfncbisIsMatrixNumeric
'Includes PfixArrayPartColumnNumeric
'Includes PincdouDistributionAtRandomVariable
'Includes PincdouRandomVariableAtDistribution
'Includes PfixQuickSortOfNumeric1DArray

Option Explicit
Option Base 1

'◆ClassProc名_分布特性データの累積確率と特性(確率変数)位置を取得する
'Rev.002
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CDstrbtnPstn"
  
'//PropertyGet_入出力データ
  Private myXdouDist As Double, myXdouRndVrbl As Double
  
'//PropertySetLet_入力制御信号
  Private myXlonCalOption As Long
    'myXlonCalOption = 1 : 分布特性データの指定特性(確率変数)位置での累積確率を取得
    'myXlonCalOption = 2 : 分布特性データの指定累積確率となる特性(確率変数)位置を取得
  Private myXlonSrtOrdr As Long
    'myXlonSrtOrdr = 1  : 昇順 (Ascending Order)
    'myXlonSrtOrdr = -1 : 降順 (Descending Order)
  
'//PropertySetLet_入力データ
  Private myZdouOrgData() As Double
    'myZdouOrgData(i, 1) : 元分布特性データ配列
  Private myXlonBgnRow As Long, myXlonEndRow As Long
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonInptRowCnt As Long, myXlonInptColCnt As Long, myZdouInptData() As Double
  Private myXlonDstrbtDataCnt As Long, myZdouDstrbtData() As Double

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonInptRowCnt = Empty: myXlonInptColCnt = Empty: Erase myZdouInptData
    myXlonDstrbtDataCnt = Empty: Erase myZdouDstrbtData
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getDist() As Double
    getDist = myXdouDist
'myXdouDist : 累積確率[%]
End Property

Public Property Get getRndVrbl() As Double
    getRndVrbl = myXdouRndVrbl
'myXdouRndVrbl : 特性(確率変数)
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    Select Case myXlonCalOption
        Case 1: myXdouDist = Empty
        Case 2: myXdouRndVrbl = Empty
    End Select
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letCalOption(ByVal RHS As Long)
    myXlonCalOption = RHS
'myXlonCalOption = 1 : 分布特性データの指定特性(確率変数)位置での累積確率を取得
'myXlonCalOption = 2 : 分布特性データの指定累積確率となる特性(確率変数)位置を取得
End Property

Public Property Let letSrtOrdr(ByVal RHS As Long)
    myXlonSrtOrdr = RHS
'myXlonSrtOrdr = 1  : 昇順 (Ascending Order)
'myXlonSrtOrdr = -1 : 降順 (Descending Order)
End Property

Public Property Let letOrgDataAry(ByRef RHS() As Double)
'myZdouOrgData(i, 1) : 元分布特性データ配列
    myZdouOrgData = RHS
End Property

Public Property Let letBgnRow(ByVal RHS As Long)
    myXlonBgnRow = RHS
End Property

Public Property Let letEndRow(ByVal RHS As Long)
    myXlonEndRow = RHS
End Property

Public Property Let letRndVrbl(ByVal RHS As Double)
    myXdouRndVrbl = RHS
'myXdouRndVrbl : 検索する特性(確率変数)
End Property

Public Property Let letDist(ByVal RHS As Double)
    myXdouDist = RHS
'myXdouDist : 検索する累積確率[%]
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXlonCalOption < 1 Or myXlonCalOption > 2 Then GoTo ExitPath
    If myXlonSrtOrdr <> 1 And myXlonSrtOrdr <> -1 Then GoTo ExitPath
    
    If PfncbisIsMatrixNumeric(myZdouOrgData) = False Then GoTo ExitPath
    
  Dim L As Long, i As Long, j As Long
    L = LBound(myZdouOrgData, 1)
    myXlonInptRowCnt = UBound(myZdouOrgData, 1) - L + 1
    myXlonInptColCnt = UBound(myZdouOrgData, 2) - L + 1
    ReDim myZdouInptData(myXlonInptRowCnt, myXlonInptColCnt) As Double
    For j = LBound(myZdouOrgData, 2) To UBound(myZdouOrgData, 2)
        For i = LBound(myZdouOrgData, 1) To UBound(myZdouOrgData, 1)
            myZdouInptData(i - L + 1, j - L + 1) = myZdouOrgData(i, j)
        Next i
    Next j
    
    If myXlonBgnRow < LBound(myZdouInptData, 1) Then myXlonBgnRow = LBound(myZdouInptData, 1)
    If myXlonEndRow < myXlonBgnRow Or myXlonEndRow > UBound(myZdouInptData, 1) Then _
        myXlonEndRow = UBound(myZdouInptData, 1)
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicF_分布特性データの指定特性(確率変数)位置での累積確率を取得する
Public Function fncdouDist() As Double
    fncdouDist = Empty
    Call ctrProc
    fncdouDist = myXdouDist
End Function

'PublicF_分布特性データの指定累積確率となる特性(確率変数)位置を取得する
Public Function fncdouRndVrbl() As Double
    fncdouRndVrbl = Empty
    Call ctrProc
    fncdouRndVrbl = myXdouRndVrbl
End Function

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
  Const coXlonCalCol As Long = 1
  Const coXlonCalStep As Long = 1
    Call PfixArrayPartColumnNumeric( _
            myXlonDstrbtDataCnt, myZdouDstrbtData, _
            myZdouInptData, coXlonCalCol, myXlonBgnRow, myXlonEndRow, coXlonCalStep)
    If myXlonDstrbtDataCnt <= 0 Then GoTo ExitPath
    
'//C:分岐
    If myXlonCalOption = 1 Then
    '//分布特性データの指定特性(確率変数)位置での累積確率を取得
        myXdouDist = PincdouDistributionAtRandomVariable( _
                        myZdouDstrbtData, myXdouRndVrbl, myXlonSrtOrdr)
        
    ElseIf myXlonCalOption = 2 Then
    '//分布特性データの指定累積確率となる特性(確率変数)位置を取得
        myXdouRndVrbl = PincdouRandomVariableAtDistribution( _
                            myZdouDstrbtData, myXdouDist, myXlonSrtOrdr)
        
    Else
        GoTo ExitPath
    End If
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables()
End Sub

'===============================================================================================

 '定型Ｆ_指定行列が数値行列かどうかをチェックする
Private Function PfncbisIsMatrixNumeric(ByRef myZvarMtrx As Variant) As Boolean
    PfncbisIsMatrixNumeric = False
  Dim myXvarTmp As Variant, myXdouTmp As Double, L1 As Long, L2 As Long
    On Error GoTo ExitPath
    L1 = LBound(myZvarMtrx, 1): L2 = LBound(myZvarMtrx, 2)
    myXdouTmp = myZvarMtrx(L1, L2)
    For Each myXvarTmp In myZvarMtrx
        If IsNumeric(myXvarTmp) = False Then GoTo ExitPath
    Next myXvarTmp
    On Error GoTo 0
    PfncbisIsMatrixNumeric = True
ExitPath:
End Function

 '定型Ｐ_2次元の数値配列の指定列から一部を1次元配列で取得する
Private Sub PfixArrayPartColumnNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, ByVal myXlonExtrctCol As Long, _
            ByVal myXlonBgnRow As Long, ByVal myXlonEndRow As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 抽出データ配列
'myZdouOrgData(i, j) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgnRow < LBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow > UBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow < myXlonBgnRow Then Exit Sub
    If myXlonExtrctCol < LBound(myZdouOrgData, 2) _
        Or myXlonExtrctCol > UBound(myZdouOrgData, 2) Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData, 1) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgnRow, myXlonExtrctCol)
    Li = LBound(myZdouOrgData, 1)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgnRow To myXlonEndRow Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i, myXlonExtrctCol)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｆ_分布特性データの指定特性(確率変数)位置での累積確率を取得する
Private Function PincdouDistributionAtRandomVariable( _
            ByRef myZdouDstrbtData() As Double, _
            ByVal myXdouRndVrbl As Double, _
            Optional ByVal coXlonOrder As Long = 1) As Double
'Includes PfixQuickSortOfNumeric1DArray
'PincdouDistributionAtRandomVariable:  累積確率 [%]
'myXdouRndVrbl : 検索する特性(確率変数)
'myZdouDstrbtData(i) : 元分布特性データ配列
'coXlonOrder = 1  : 昇順 (Ascending Order)
'coXlonOrder = -1 : 降順 (Descending Order)
    PincdouDistributionAtRandomVariable = Empty
    If Abs(coXlonOrder) <> 1 Then Exit Function
  Dim myXdouTmp As Double, L As Long
    On Error GoTo ExitPath
    L = LBound(myZdouDstrbtData): myXdouTmp = myZdouDstrbtData(L)
    On Error GoTo 0
'//データ配列のクイックソート
  Dim myXlonLbnd As Long, myXlonUbnd As Long
    myXlonLbnd = LBound(myZdouDstrbtData)
    myXlonUbnd = UBound(myZdouDstrbtData)
    Call PfixQuickSortOfNumeric1DArray( _
            myZdouDstrbtData, myXlonLbnd, myXlonUbnd, coXlonOrder)
'//指定特性(確率変数)位置での累積確率を取得
  Dim myXlonDstrbtCnt As Long
    myXlonDstrbtCnt = UBound(myZdouDstrbtData) - LBound(myZdouDstrbtData) + 1
  Dim myXdouDist As Double, i As Long, n As Long: n = 0
    If coXlonOrder = 1 Then
        For i = LBound(myZdouDstrbtData) To UBound(myZdouDstrbtData)
            myXdouTmp = myZdouDstrbtData(i)
            If myXdouTmp > myXdouRndVrbl Then
                n = i - L
                myXdouDist = n / myXlonDstrbtCnt * 100
                Exit For
            End If
        Next i
    Else
        For i = LBound(myZdouDstrbtData) To UBound(myZdouDstrbtData)
            myXdouTmp = myZdouDstrbtData(i)
            If myXdouTmp < myXdouRndVrbl Then
                n = i - L
                myXdouDist = (1 - n / myXlonDstrbtCnt) * 100
                Exit For
            End If
        Next i
    End If
    PincdouDistributionAtRandomVariable = myXdouDist
ExitPath:
End Function

 '定型Ｆ_分布特性データの指定累積確率となる特性(確率変数)位置を取得する
Private Function PincdouRandomVariableAtDistribution( _
            ByRef myZdouDstrbtData() As Double, _
            ByVal myXdouDist As Double, _
            Optional ByVal coXlonOrder As Long = 1) As Double
'Includes PfixQuickSortOfNumeric1DArray
'PincdouRandomVariableAtDistribution:  特性 (確率変数)
'myXdouDist : 検索する累積確率[%]
'myZdouDstrbtData(i) : 元分布特性データ配列
'coXlonOrder = 1  : 昇順 (Ascending Order)
'coXlonOrder = -1 : 降順 (Descending Order)
    PincdouRandomVariableAtDistribution = Empty
    If Abs(coXlonOrder) <> 1 Then Exit Function
  Dim myXdouTmp As Double, L As Long
    On Error GoTo ExitPath
    L = LBound(myZdouDstrbtData): myXdouTmp = myZdouDstrbtData(L)
    On Error GoTo 0
'//データ配列のクイックソート
  Dim myXlonLbnd As Long, myXlonUbnd As Long
    myXlonLbnd = LBound(myZdouDstrbtData)
    myXlonUbnd = UBound(myZdouDstrbtData)
    Call PfixQuickSortOfNumeric1DArray( _
            myZdouDstrbtData, myXlonLbnd, myXlonUbnd, coXlonOrder)
'//指定特性(確率変数)位置での累積確率を取得
  Dim myXlonDstrbtCnt As Long
    myXlonDstrbtCnt = UBound(myZdouDstrbtData) - LBound(myZdouDstrbtData) + 1
  Dim myXdouRndVrbl As Double, i As Long, n As Long: n = 0
    If coXlonOrder = 1 Then
        For i = LBound(myZdouDstrbtData) To UBound(myZdouDstrbtData)
            n = i + (1 - L)
            myXdouTmp = n / myXlonDstrbtCnt * 100
            If myXdouTmp > myXdouDist Then
                If i = LBound(myZdouDstrbtData) Then Exit Function
                myXdouRndVrbl = myZdouDstrbtData(i - 1)
                Exit For
            End If
        Next i
    Else
        For i = LBound(myZdouDstrbtData) To UBound(myZdouDstrbtData)
            n = i + (1 - L)
            myXdouTmp = (1 - n / myXlonDstrbtCnt) * 100
            If myXdouTmp < myXdouDist Then
                If i = LBound(myZdouDstrbtData) Then Exit Function
                myXdouRndVrbl = myZdouDstrbtData(i - 1)
                Exit For
            End If
        Next i
    End If
    PincdouRandomVariableAtDistribution = myXdouRndVrbl
ExitPath:
End Function

 '定型Ｐ_1次元の数値配列のクイックソート
Private Sub PfixQuickSortOfNumeric1DArray( _
            myZdouSrtData() As Double, _
            ByVal myXlonLbnd As Long, ByVal myXlonUbnd As Long, _
            Optional ByVal coXlonOrder As Long = 1)
'myZdouSrtData(i) : 数値情報
'coXlonOrder = 1    : 昇順 (Ascending Order)
'coXlonOrder = -1   : 降順 (Descending Order)
'→SetProc:定型プロシージャ使用前のコード部分に記述
'    myXlonLbnd = LBound(myZdouSrtData)
'    myXlonUbnd = UBound(myZdouSrtData)
'    If myXlonUbnd <= myXlonLbnd Then Exit Sub
'←EndProc
  Dim myXlonMed As Long, myXdouMed As Double
    myXlonMed = Int((myXlonLbnd + myXlonUbnd) / 2)
    myXdouMed = myZdouSrtData(myXlonMed)
  Dim i As Long, j As Long
    i = myXlonLbnd: j = myXlonUbnd
  Dim myXdouBufi As Double, myXdouBufj As Double, myXdouTmp As Double
    Do
        myXdouBufi = myZdouSrtData(i)
        Do While coXlonOrder * (myXdouBufi - myXdouMed) < 0
            i = i + 1: myXdouBufi = myZdouSrtData(i)
        Loop
        myXdouBufj = myZdouSrtData(j)
        Do While coXlonOrder * (myXdouBufj - myXdouMed) > 0
            j = j - 1: myXdouBufj = myZdouSrtData(j)
        Loop
        If i >= j Then Exit Do
        myXdouTmp = myZdouSrtData(i)
        myZdouSrtData(i) = myZdouSrtData(j)
        myZdouSrtData(j) = myXdouTmp
        i = i + 1: j = j - 1
    Loop
    If myXlonLbnd < i - 1 Then _
        Call PfixQuickSortOfNumeric1DArray(myZdouSrtData, myXlonLbnd, i - 1, coXlonOrder)
    If myXlonUbnd > j + 1 Then _
        Call PfixQuickSortOfNumeric1DArray(myZdouSrtData, j + 1, myXlonUbnd, coXlonOrder)
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_分布特性データの累積確率と特性(確率変数)位置を取得する
'Private Sub instCDstrbtnPstn()
'  Dim myXdouDist As Double
'    'myXdouDist : 累積確率[%]
'  Dim myXdouRndVrbl As Double
'    'myXdouRndVrbl : 特性(確率変数)
''//クラス内変数への入力
'  Dim myXlonCalOption As Long
'    'myXlonCalOption = 1 : 分布特性データの指定特性(確率変数)位置での累積確率を取得
'    'myXlonCalOption = 2 : 分布特性データの指定累積確率となる特性(確率変数)位置を取得
'    myXlonCalOption = 2
'  Dim myXlonSrtOrdr As Long
'    'myXlonSrtOrdr = 1  : 昇順 (Ascending Order)
'    'myXlonSrtOrdr = -1 : 降順 (Descending Order)
'    myXlonSrtOrdr = 1
'  Dim myXlonBgnRow As Long, myXlonEndRow As Long
'    myXlonBgnRow = 1
'    myXlonEndRow = 0
'  Dim myXlonOrgDataCnt As Long, myZdouOrgData() As Double
'    myXlonOrgDataCnt = 3
'    ReDim myZdouOrgData(myXlonOrgDataCnt, 1) As Double
'    myZdouOrgData(1, 1) = 0.5
'    myZdouOrgData(2, 1) = 1
'    myZdouOrgData(3, 1) = 1.5
'    myXdouRndVrbl = 1
'    myXdouDist = 50
''//CDstrbtnPstnクラスモジュールのインスタンス生成
'  Dim i As Long
'  Dim myXinsDstrbtnPstn As CDstrbtnPstn: Set myXinsDstrbtnPstn = New CDstrbtnPstn
'    With myXinsDstrbtnPstn
'    '//クラス内変数への入力
'        .letCalOption = myXlonCalOption
'        .letSrtOrdr = myXlonSrtOrdr
'        .letOrgDataAry = myZdouOrgData
'        .letBgnRow = myXlonBgnRow
'        .letEndRow = myXlonEndRow
'        Select Case myXlonCalOption
'            Case 1: .letRndVrbl = myXdouRndVrbl
'            Case 2: .letDist = myXdouDist
'            Case Else
'        End Select
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        Select Case myXlonCalOption
'            Case 1: myXdouDist = .fncdouDist
'            Case 2: myXdouRndVrbl = .fncdouRndVrbl
'            Case Else
'        End Select
'    End With
'    Set myXinsDstrbtnPstn = Nothing
'    Select Case myXlonCalOption
'        Case 1: Debug.Print "データ: " & myXdouDist
'        Case 2: Debug.Print "データ: " & myXdouRndVrbl
'        Case Else
'    End Select
'ExitPath:
'End Sub

      <br></pre>
    </body>
  </html>
