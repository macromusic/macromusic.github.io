<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfixGetChartAreaPlaceSize
'Includes PfixGetPlotAreaPlaceSize
'Includes PfixSetPlotAreaPlace
'Includes PfixSetPlotAreaSize
'Includes PfixSetPlotAreaScaleRelative
'Includes PfnclonCountSeriesCollectionSetAxisGroup

Option Explicit
Option Base 1

'◆ClassProc名_グラフのプロットエリアの外形サイズを設定する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CChrtPlaceSizePltArea"
  
'//モジュール内定数
  Private Const coXdouSclHght As Double = 90
  Private Const coXdouSclWdth1 As Double = 92
  Private Const coXdouSclWdth2 As Double = 85
  
'//PropertyGet_出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//PropertySetLet_入力データ
  Dim myXobjChrtObjct As Object
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXdouChrtAreaTop As Double, myXdouChrtAreaLft As Double, _
            myXdouChrtAreaBttm As Double, myXdouChrtAreaRght As Double, _
            myXdouChrtAreaHght As Double, myXdouChrtAreaWdth As Double

  Private myXdouPltAreaTop As Double, myXdouPltAreaLft As Double, _
            myXdouPltAreaBttm As Double, myXdouPltAreaRght As Double, _
            myXdouPltAreaHght As Double, myXdouPltAreaWdth As Double

  Private myXdouSclHght As Double, myXdouSclWdth As Double

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXdouChrtAreaTop = Empty: myXdouChrtAreaLft = Empty
    myXdouChrtAreaBttm = Empty: myXdouChrtAreaRght = Empty
    myXdouChrtAreaHght = Empty: myXdouChrtAreaWdth = Empty

    myXdouPltAreaTop = Empty: myXdouPltAreaLft = Empty
    myXdouPltAreaBttm = Empty: myXdouPltAreaRght = Empty
    myXdouPltAreaHght = Empty: myXdouPltAreaWdth = Empty
    
    myXdouSclHght = Empty: myXdouSclWdth = Empty
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getCmpltFlag() As Boolean
    getCmpltFlag = myXbisCmpltFlag
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Set setChrtObjct(ByVal RHS As Object)
    Set myXobjChrtObjct = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXobjChrtObjct Is Nothing Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicF_
Public Function fncbisCmpltFlag() As Boolean
    fncbisCmpltFlag = Empty
    Call ctrProc
    fncbisCmpltFlag = myXbisCmpltFlag
End Function

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//グラフオブジェクトのチャートエリアの位置とサイズを取得
    Call PfixGetChartAreaPlaceSize( _
            myXdouChrtAreaTop, myXdouChrtAreaLft, _
            myXdouChrtAreaBttm, myXdouChrtAreaRght, _
            myXdouChrtAreaHght, myXdouChrtAreaWdth, _
            myXobjChrtObjct)
    If myXdouChrtAreaTop < 0 Or myXdouChrtAreaLft < 0 Or _
        myXdouChrtAreaHght <= 0 Or myXdouChrtAreaWdth <= 0 Then GoTo ExitPath
    
'//グラフオブジェクトのプロットエリアの位置を設定
    myXdouPltAreaTop = 0
    myXdouPltAreaLft = 0
    Call PfixSetPlotAreaPlace(myXbisExitFlag, _
            myXobjChrtObjct, myXdouPltAreaTop, myXdouPltAreaLft)
    If myXbisExitFlag = True Then GoTo ExitPath
    
'//グラフオブジェクトのプロットエリアのサイズを設定
    myXdouPltAreaHght = myXdouChrtAreaHght
    myXdouPltAreaWdth = myXdouChrtAreaWdth
    Call PfixSetPlotAreaSize(myXbisExitFlag, _
            myXobjChrtObjct, myXdouPltAreaHght, myXdouPltAreaWdth)
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//グラフオブジェクトのプロットエリアの位置とサイズを取得
    Call PfixGetPlotAreaPlaceSize( _
            myXdouPltAreaTop, myXdouPltAreaLft, _
            myXdouPltAreaBttm, myXdouPltAreaRght, _
            myXdouPltAreaHght, myXdouPltAreaWdth, _
            myXobjChrtObjct)
    If myXdouPltAreaTop < 0 Or myXdouPltAreaLft < 0 Or _
        myXdouPltAreaHght <= 0 Or myXdouPltAreaWdth <= 0 Then GoTo ExitPath
    
'//グラフ内で第1軸と第2軸に設定されている系列数を確認
  Dim myXobjChrt As Object
    Set myXobjChrt = myXobjChrtObjct.Chart
  Dim myXlonAxsGrp1Cnt As Long, myXlonAxsGrp2Cnt As Long
    Call PfnclonCountSeriesCollectionSetAxisGroup( _
            myXlonAxsGrp1Cnt, myXlonAxsGrp2Cnt, _
            myXobjChrt)
    
'//グラフオブジェクトのプロットエリアのサイズを現サイズに対する倍率指定で設定
    If myXlonAxsGrp2Cnt <= 0 Then
        myXdouSclHght = coXdouSclHght
        myXdouSclWdth = coXdouSclWdth1
    Else
        myXdouSclHght = coXdouSclHght
        myXdouSclWdth = coXdouSclWdth2
    End If
    Call PfixSetPlotAreaScaleRelative(myXbisExitFlag, _
            myXobjChrtObjct, myXdouSclHght, myXdouSclWdth)
    If myXbisExitFlag = True Then GoTo ExitPath
    
'//グラフオブジェクトのプロットエリアの位置を設定
    If myXlonAxsGrp2Cnt <= 0 Then
        myXdouPltAreaTop = myXdouPltAreaTop
        myXdouPltAreaLft = (100 - myXdouSclWdth) / 100 * myXdouPltAreaWdth * 1
    Else
        myXdouPltAreaTop = myXdouPltAreaTop
        myXdouPltAreaLft = (100 - myXdouSclWdth) / 100 * myXdouPltAreaWdth * 0.5
    End If
    Call PfixSetPlotAreaPlace(myXbisExitFlag, _
            myXobjChrtObjct, myXdouPltAreaTop, myXdouPltAreaLft)
    If myXbisExitFlag = True Then GoTo ExitPath
    
    Set myXobjChrt = Nothing
    myXbisCmpltFlag = True
ExitPath:
    Call initializeModuleVariables
End Sub

'===============================================================================================

 '定型Ｐ_グラフオブジェクトのチャートエリアの位置とサイズを取得する
Private Sub PfixGetChartAreaPlaceSize( _
            myXdouChrtAreaTop As Double, myXdouChrtAreaLft As Double, _
            myXdouChrtAreaBttm As Double, myXdouChrtAreaRght As Double, _
            myXdouChrtAreaHght As Double, myXdouChrtAreaWdth As Double, _
            ByVal myXobjChrtObjct As Object)
    myXdouChrtAreaTop = Empty: myXdouChrtAreaLft = Empty
    myXdouChrtAreaBttm = Empty: myXdouChrtAreaRght = Empty
    myXdouChrtAreaHght = Empty: myXdouChrtAreaWdth = Empty
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.ChartArea
        myXdouChrtAreaTop = .Top
        myXdouChrtAreaLft = .Left
        myXdouChrtAreaHght = .Height
        myXdouChrtAreaWdth = .Width
    End With
    On Error GoTo 0
    myXdouChrtAreaBttm = myXdouChrtAreaTop + myXdouChrtAreaHght
    myXdouChrtAreaRght = myXdouChrtAreaLft + myXdouChrtAreaWdth
ExitPath:
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアの位置とサイズを取得する
Private Sub PfixGetPlotAreaPlaceSize( _
            myXdouPltAreaTop As Double, myXdouPltAreaLft As Double, _
            myXdouPltAreaBttm As Double, myXdouPltAreaRght As Double, _
            myXdouPltAreaHght As Double, myXdouPltAreaWdth As Double, _
            ByVal myXobjChrtObjct As Object)
    myXdouPltAreaTop = Empty: myXdouPltAreaLft = Empty
    myXdouPltAreaBttm = Empty: myXdouPltAreaRght = Empty
    myXdouPltAreaHght = Empty: myXdouPltAreaWdth = Empty
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        myXdouPltAreaTop = .Top
        myXdouPltAreaLft = .Left
        myXdouPltAreaHght = .Height
        myXdouPltAreaWdth = .Width
    End With
    On Error GoTo 0
    myXdouPltAreaBttm = myXdouPltAreaTop + myXdouPltAreaHght
    myXdouPltAreaRght = myXdouPltAreaLft + myXdouPltAreaWdth
ExitPath:
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアの位置を設定する
Private Sub PfixSetPlotAreaPlace( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouPltAreaTop As Double, ByVal myXdouPltAreaLft As Double)
'myXdouPltAreaTop : x [point]
'myXdouPltAreaLft : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpTop As Double: myXdouTmpTop = myXobjChrtObjct.Chart.PlotArea.Top
    On Error GoTo 0
    If myXdouPltAreaTop < 0 Or myXdouPltAreaLft < 0 Then GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        .Top = myXdouPltAreaTop
        .Left = myXdouPltAreaLft
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアのサイズを設定する
Private Sub PfixSetPlotAreaSize( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouPltAreaHght As Double, ByVal myXdouPltAreaWdth As Double)
'myXdouPltAreaHght : x [point]
'myXdouPltAreaWdth : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpHght As Double: myXdouTmpHght = myXobjChrtObjct.Chart.PlotArea.Height
    On Error GoTo 0
    If myXdouPltAreaHght < 0 Or myXdouPltAreaWdth < 0 Then GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        .Height = myXdouPltAreaHght
        .Width = myXdouPltAreaWdth
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアのサイズを現サイズに対する倍率指定で設定する
Private Sub PfixSetPlotAreaScaleRelative( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
    myXbisExitFlag = False
  Dim myXdouTmpHght As Double, myXdouTmpWdth As Double
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        myXdouTmpHght = .Height
        myXdouTmpWdth = .Width
    End With
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    myXdouSclHght = myXdouSclHght / 100
    myXdouSclWdth = myXdouSclWdth / 100
    With myXobjChrtObjct.Chart.PlotArea
        .Height = myXdouTmpHght * myXdouSclHght
        .Width = myXdouTmpWdth * myXdouSclWdth
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフ内で第1軸と第2軸に設定されている系列数を確認する
Private Sub PfnclonCountSeriesCollectionSetAxisGroup( _
            myXlonAxsGrp1Cnt As Long, myXlonAxsGrp2Cnt As Long, _
            ByVal myXobjChrt As Object)
    myXlonAxsGrp1Cnt = Empty: myXlonAxsGrp2Cnt = Empty
    On Error GoTo ExitPath
  Dim myXlonSCCnt As Long: myXlonSCCnt = myXobjChrt.SeriesCollection.Count
    On Error GoTo 0
    If myXlonSCCnt <= 0 Then GoTo ExitPath
  Dim myXobjSrsClctn As Object
    For Each myXobjSrsClctn In myXobjChrt.SeriesCollection
        Select Case myXobjSrsClctn.AxisGroup
            Case xlPrimary: myXlonAxsGrp1Cnt = myXlonAxsGrp1Cnt + 1
            Case xlSecondary: myXlonAxsGrp2Cnt = myXlonAxsGrp2Cnt + 1
        End Select
    Next myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
ExitPath:
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_グラフのプロットエリアの外形サイズを設定する
'Private Sub instCChrtPlaceSizePltArea()
'  Dim myXbisCmpltFlag As Boolean
''//クラス内変数への入力
'  Dim myXobjChrtObjct As Object
'    Set myXobjChrtObjct = ActiveSheet.ChartObjects(1)
''//CChrtPlaceSizePltAreaクラスモジュールのインスタンス生成
'  Dim myXinsChrtPlaceSizePltArea As CChrtPlaceSizePltArea
'    Set myXinsChrtPlaceSizePltArea = New CChrtPlaceSizePltArea
'    With myXinsChrtPlaceSizePltArea
'    '//クラス内変数への入力
'        Set .setChrtObjct = myXobjChrtObjct
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        myXbisCmpltFlag = .fncbisCmpltFlag
'    End With
'    Set myXinsChrtPlaceSizePltArea = Nothing
'ExitPath:
'End Sub

      <br></pre>
    </body>
  </html>
