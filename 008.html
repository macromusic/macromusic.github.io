<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>


 '定型Ｆ_MsgBox関数でOK／キャンセルを確認する
Private Function PfnclonMsgBoxOKCancel( _
            ByVal myXstrTitle As String, ByVal myXstrPrompt As String) As Long
'vbOK=1 / vbCancel=2
    PfnclonMsgBoxOKCancel = Empty
    If myXstrTitle = "" Then myXstrTitle = "OK/キャンセルの選択"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    PfnclonMsgBoxOKCancel = MsgBox(myXstrPrompt, vbOKCancel, myXstrTitle)
End Function

 '定型Ｆ_MsgBox関数ではい／いいえを確認する
Private Function PfnclonMsgBoxYesNo( _
            ByVal myXstrTitle As String, ByVal myXstrPrompt As String) As Long
'vbYes=6 / vbNo=7
    PfnclonMsgBoxYesNo = Empty
    If myXstrTitle = "" Then myXstrTitle = "はい/いいえの選択"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    PfnclonMsgBoxYesNo = MsgBox(myXstrPrompt, vbYesNo, myXstrTitle)
End Function

 '定型Ｆ_MsgBox関数ではい／いいえ／キャンセルを確認する
Private Function PfnclonMsgBoxYesNoCancel( _
            ByVal myXstrTitle As String, ByVal myXstrPrompt As String) As Long
'vbYes=6 / vbNo=7 / vbCancel=2
    PfnclonMsgBoxYesNoCancel = Empty
    If myXstrTitle = "" Then myXstrTitle = "はい/いいえ/キャンセルの選択"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    PfnclonMsgBoxYesNoCancel = MsgBox(myXstrPrompt, vbYesNoCancel, myXstrTitle)
End Function

 '定型Ｆ_InputBoxメソッドで整数値の入力を促す
Private Function PfnclonInputBoxLong( _
            ByVal myXstrPrompt As String, ByVal myXstrTitle As String, _
            ByVal myXlonDefault As Long) As Long
    PfnclonInputBoxLong = Empty
    If myXstrTitle = "" Then myXstrTitle = "数値の入力"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    On Error Resume Next
    PfnclonInputBoxLong = Application.InputBox( _
                            prompt:=myXstrPrompt, Title:=myXstrTitle, _
                            Default:=myXlonDefault, Type:=1)
    On Error GoTo 0
End Function

 '定型Ｆ_InputBoxメソッドで小数値の入力を促す
Private Function PfnclonInputBoxDouble( _
            ByVal myXstrPrompt As String, ByVal myXstrTitle As String, _
            ByVal myXdouDefault As Double) As Double
    PfnclonInputBoxDouble = Empty
    If myXstrTitle = "" Then myXstrTitle = "数値の入力"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    On Error Resume Next
    PfnclonInputBoxDouble = Application.InputBox( _
                            prompt:=myXstrPrompt, Title:=myXstrTitle, _
                            Default:=myXdouDefault, Type:=1)
    On Error GoTo 0
End Function

 '定型Ｆ_InputBoxメソッドで文字列の入力を促す
Private Function PfncvarInputBoxString( _
            ByVal myXstrPrompt As String, ByVal myXstrTitle As String, _
            ByVal myXstrDefault As String) As Variant
    PfncvarInputBoxString = Empty
    If myXstrTitle = "" Then myXstrTitle = "文字列の入力"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    On Error Resume Next
    PfncvarInputBoxString = Application.InputBox( _
                            prompt:=myXstrPrompt, Title:=myXstrTitle, _
                            Default:=myXstrDefault, Type:=2)
    On Error GoTo 0
End Function

 '定型Ｆ_文字列が確定するまでInputBoxメソッド入力を繰り返す
Private Function PfncvarRepeatInputBoxString( _
            ByVal myXvarOrgInptBx As Variant, _
            ByVal myXstrPrompt As String, ByVal myXstrTitle As String, _
            ByVal myXstrDefault As String) As Variant
'myXvarOrgInptBx : 入力データ
'myXstrDefault   : InputBoxのデフォルト値
    PfncvarRepeatInputBoxString = Empty
    If myXstrTitle = "" Then myXstrTitle = "文字列の入力"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
  Dim myXvarInptBx As Variant: myXvarInptBx = myXvarOrgInptBx
  Dim myXbisExitLpFlag As Boolean: myXbisExitLpFlag = False
    Do While myXbisExitLpFlag = False
        myXvarInptBx = Application.InputBox( _
                        prompt:=myXstrPrompt, Title:=myXstrTitle, _
                        Default:=myXstrDefault, Type:=2)
        If myXvarInptBx <> "" Then myXbisExitLpFlag = True
    Loop
    If myXvarInptBx = False Then myXvarInptBx = Empty
    PfncvarRepeatInputBoxString = myXvarInptBx
End Function

 '定型Ｆ_InputBoxメソッドでセル範囲の入力を促す
Private Function PfncobjInputBoxRange( _
                    ByVal myXstrPrompt As String, ByVal myXstrTitle As String, _
                    ByVal myXstrDefault As String) As Object
    Set PfncobjInputBoxRange = Nothing
    If myXstrTitle = "" Then myXstrTitle = "セル範囲の選択"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
  Dim myXobjRng As Object
    On Error Resume Next
    Set myXobjRng = Application.InputBox(prompt:=myXstrPrompt, Title:=myXstrTitle, _
                                            Default:=myXstrDefault, Type:=8)
    On Error GoTo 0
'    Debug.Print "入力値: " & myXobjRng.Address(ReferenceStyle:=Application.ReferenceStyle, _
'                                RowAbsolute:=True, ColumnAbsolute:=True, external:=True)    'Debug.Print
    Set PfncobjInputBoxRange = myXobjRng
    Set myXobjRng = Nothing
End Function

 '定型Ｆ_InputBox用のデフォルト範囲を設定する
Private Function PfncstrDefaultAddressInputBox(ByVal myXobjDfltRng As Object) As String
    PfncstrDefaultAddressInputBox = Empty
  Dim myXstrDfltAdrs As String
    On Error GoTo ExitPath
    If myXobjDfltRng Is Nothing Then Set myXobjDfltRng = Selection
    myXstrDfltAdrs = myXobjDfltRng.Address(ReferenceStyle:=Application.ReferenceStyle, _
                        RowAbsolute:=True, ColumnAbsolute:=True, external:=True)
    On Error GoTo 0
    PfncstrDefaultAddressInputBox = myXstrDfltAdrs
ExitPath:
End Function

 '定型Ｆ_指定セル範囲を最終行または最終列まで広げる
Private Function PfncobjExtendRangeToEnd(myXobjOrgRng As Object) As Object
    Set PfncobjExtendRangeToEnd = Nothing
    If myXobjOrgRng Is Nothing Then Exit Function
  Dim myXobjBook As Object, myXobjSheet As Object, myXstrShtName As String
    On Error GoTo ExitPath
    Set myXobjBook = myXobjOrgRng.Parent.Parent
    Set myXobjSheet = myXobjOrgRng.Parent
    myXobjSheet.Activate
    myXobjOrgRng.Select
    On Error GoTo 0
  Dim myXlonMsgBox As Long, myXstrPrompt As String, myXstrTitle As String
    myXstrTitle = "データ範囲の変更"
    myXstrPrompt = "データ範囲を最終行まで広げますか？" & vbCrLf & vbCrLf _
            & "　・「はい」　：最終行まで広げる場合に選択" & vbCrLf & vbCrLf _
            & "　・「いいえ」：最終列まで広げる場合に選択" & vbCrLf & vbCrLf _
            & "　・「キャンセル」：広げない場合に選択"
    myXlonMsgBox = MsgBox(myXstrPrompt, vbYesNoCancel, myXstrTitle)
  Dim myXlonRow1 As Long, myXlonRow2 As Long, myXlonCol1 As Long, myXlonCol2 As Long
    myXlonRow1 = myXobjOrgRng.Row
    myXlonRow2 = myXlonRow1 + myXobjOrgRng.Rows.Count - 1
    myXlonCol1 = myXobjOrgRng.Column
    myXlonCol2 = myXlonCol1 + myXobjOrgRng.Columns.Count - 1
  Dim myXobjFrstCell As Object, myXobjLastCell As Object, myXobjExtndRng As Object
  Dim myXlonEndRow As Long, myXlonEndCol As Long, myXlonTmp As Long
  Dim i As Long, j As Long
    With myXobjOrgRng
        If myXlonMsgBox = vbYes Then
            With .Worksheet
                For j = myXlonCol1 To myXlonCol2
                    myXlonTmp = .Cells(.Rows.Count, j).End(xlUp).Row
                    If myXlonEndRow < myXlonTmp Then myXlonEndRow = myXlonTmp
                    If myXlonEndRow < myXlonRow1 Then myXlonEndRow = myXlonRow2
                Next j
                Set myXobjFrstCell = .Cells(myXlonRow1, myXlonCol1)
                Set myXobjLastCell = .Cells(myXlonEndRow, myXlonCol2)
                Set myXobjExtndRng = .Range(myXobjFrstCell, myXobjLastCell)
            End With
        ElseIf myXlonMsgBox = vbNo Then
            With .Worksheet
                For i = myXlonRow1 To myXlonRow2
                    myXlonTmp = .Cells(i, .Columns.Count).End(xlToLeft).Column
                    If myXlonEndCol < myXlonTmp Then myXlonEndCol = myXlonTmp
                    If myXlonEndCol < myXlonCol1 Then myXlonEndCol = myXlonCol2
                Next i
                Set myXobjFrstCell = .Cells(myXlonRow1, myXlonCol1)
                Set myXobjLastCell = .Cells(myXlonRow2, myXlonEndCol)
                Set myXobjExtndRng = .Range(myXobjFrstCell, myXobjLastCell)
            End With
        ElseIf myXlonMsgBox = vbCancel Then
            Set myXobjExtndRng = myXobjOrgRng
        Else
            Set myXobjExtndRng = myXobjOrgRng
        End If
    End With
    Set PfncobjExtendRangeToEnd = myXobjExtndRng
    Set myXobjBook = Nothing: Set myXobjSheet = Nothing
    Set myXobjFrstCell = Nothing: Set myXobjLastCell = Nothing
    Set myXobjExtndRng = Nothing
ExitPath:
End Function

 '定型Ｆ_デフォルトフォルダパスを設定する
Private Function PfncstrSetDefaultFolderPath( _
            Optional ByVal coXstrDflt As String = "") As String
'coXstrDflt = ""  : デフォルトパス指定無し
'coXstrDflt = "C" : Cドライブをデフォルトパスに指定
'coXstrDflt = "1" : このブックの親フォルダをデフォルトパスに指定
'coXstrDflt = "2" : アクティブブックの親フォルダをデフォルトパスに指定
'coXstrDflt = "*" : デフォルトパスを指定
    PfncstrSetDefaultFolderPath = Empty
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrDfltPath As String, myXbisDfltExist As Boolean
    With myXobjFSO
        Select Case coXstrDflt
            Case "C": myXstrDfltPath = "C:\"
            Case "1": myXstrDfltPath = .GetParentFolderName(ThisWorkbook.Path)
            Case "2": myXstrDfltPath = .GetParentFolderName(ActiveWorkbook.Path)
            Case Else: myXstrDfltPath = coXstrDflt
        End Select
        myXbisDfltExist = .FolderExists(myXstrDfltPath)
    End With
    If myXbisDfltExist = True Then PfncstrSetDefaultFolderPath = myXstrDfltPath
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_デフォルトファイルパスを設定する
Private Function PfncstrSetDefaultFilePath( _
            Optional ByVal coXstrDflt As String = "") As String
'coXstrDflt = ""  : デフォルトパス指定無し
'coXstrDflt = "1" : このブックをデフォルトパスに指定
'coXstrDflt = "2" : アクティブブックをデフォルトパスに指定
'coXstrDflt = "*" : デフォルトパスを指定
    PfncstrSetDefaultFilePath = Empty
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrDfltPath As String, myXbisDfltExist As Boolean
    Select Case coXstrDflt
        Case "1": myXstrDfltPath = ThisWorkbook.FullName
        Case "2": myXstrDfltPath = ActiveWorkbook.FullName
        Case Else: myXstrDfltPath = coXstrDflt
    End Select
    myXbisDfltExist = myXobjFSO.FileExists(myXstrDfltPath)
    If myXbisDfltExist = True Then PfncstrSetDefaultFilePath = myXstrDfltPath
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_フォルダ選択用のFileDialogオブジェクトを設定する
Private Function PfncobjSetFileDialogForFolder() As Object
    Set PfncobjSetFileDialogForFolder = Nothing
  Dim myXobjFileDialog As Object
    On Error GoTo ExitPath
    Set myXobjFileDialog = Application.FileDialog(msoFileDialogFolderPicker)
    myXobjFileDialog.Title = "フォルダの選択"
    myXobjFileDialog.ButtonName = "選択"
    On Error GoTo 0
    Set PfncobjSetFileDialogForFolder = myXobjFileDialog
    Set myXobjFileDialog = Nothing
ExitPath:
End Function

 '定型Ｐ_フォルダ選択用のFileDialogオブジェクトの初期状態を設定する
Private Sub PfixSetFileDialogForFolderInitial( _
            myXobjFileDialog As Object, _
            Optional ByVal coXlonIniView As Long = msoFileDialogViewList, _
            Optional ByVal coXstrDfltPath As String = "")
'coXlonIniView = msoFileDialogViewDetails    : ファイルを詳細情報と共に一覧表示
'coXlonIniView = msoFileDialogViewLargeIcons : ファイルを大きいアイコンで表示
'coXlonIniView = msoFileDialogViewList       : ファイルを詳細情報なしで一覧表示
'coXlonIniView = msoFileDialogViewPreview    : ファイルの一覧を表示し、選択したファイルをプレビュー ウィンドウ枠に表示
'coXlonIniView = msoFileDialogViewProperties : ファイルの一覧を表示し、選択したファイルのプロパティをウィンドウ枠に表示
'coXlonIniView = msoFileDialogViewSmallIcons : ファイルを小さいアイコンで表示
'coXlonIniView = msoFileDialogViewThumbnail  : ファイルを縮小表示
'coXlonIniView = msoFileDialogViewTiles      : ファイルをアイコンで並べて表示
'coXlonIniView = msoFileDialogViewWebView    : ファイルを Web 表示
'coXstrDfltPath : ファイルパス指定→ダイアログボックスにファイル名を表示
'coXstrDfltPath : フォルダパス指定→デフォルトフォルダを表示
    If myXobjFileDialog Is Nothing Then Exit Sub
    On Error Resume Next
    With myXobjFileDialog
        .InitialView = coXlonIniView
        .InitialFileName = coXstrDfltPath
    End With
    On Error GoTo 0
End Sub

 '定型Ｆ_フォルダ選択用のFileDialogオブジェクトを使用してフォルダを選択する
Private Function PfncstrSelectFolderByFileDialog(ByVal myXobjFileDialog As Object) As String
    PfncstrSelectFolderByFileDialog = Empty
    If myXobjFileDialog Is Nothing Then Exit Function
  Dim myXstrFldrPath As String, myXvarTmpPath As Variant
    If myXobjFileDialog.Show = False Then Exit Function
    For Each myXvarTmpPath In myXobjFileDialog.SelectedItems
        myXstrFldrPath = CStr(myXvarTmpPath)
    Next
    PfncstrSelectFolderByFileDialog = myXstrFldrPath
End Function

 '定型Ｆ_ファイル選択用のFileDialogオブジェクトを設定する
Private Function PfncobjSetFileDialogForFile( _
            Optional ByVal coXlonFDType As Long = msoFileDialogFilePicker) As Object
'coXlonFDType = msoFileDialogFilePicker : ファイルを選択
'coXlonFDType = msoFileDialogOpen       : ファイルを開く
'coXlonFDType = msoFileDialogSaveAs     : ファイルを保存
    Set PfncobjSetFileDialogForFile = Nothing
  Dim myXobjFileDialog As Object
    On Error GoTo ExitPath
    Set myXobjFileDialog = Application.FileDialog(coXlonFDType)
    myXobjFileDialog.Filters.Clear
    Select Case coXlonFDType
        Case msoFileDialogFilePicker
            myXobjFileDialog.Title = "ファイルの選択"
            myXobjFileDialog.ButtonName = "選択"
        Case msoFileDialogOpen
            myXobjFileDialog.Title = "ファイルを開く"
            myXobjFileDialog.ButtonName = "開く"
        Case msoFileDialogSaveAs
            myXobjFileDialog.Title = "ファイルを保存"
            myXobjFileDialog.ButtonName = "保存"
        Case Else: Exit Function
    End Select
    On Error GoTo 0
    Set PfncobjSetFileDialogForFile = myXobjFileDialog
    Set myXobjFileDialog = Nothing
ExitPath:
End Function

 '定型Ｐ_ファイル選択用のFileDialogオブジェクトのフィルタ固定値を設定する
Private Sub PfixSetFileDialogFixedFilter( _
            myXobjFileDialog As Object, _
            Optional ByVal coXlonFltrIndx As Long = 1)
'coXlonFltrIndx = 1～ : ファイルフィルタの初期選択
  Const coXlonFDType As Long = msoFileDialogFilePicker  'ファイルを選択
    If myXobjFileDialog Is Nothing Then Exit Sub
  Dim i As Long: i = 0
    With myXobjFileDialog
        .Filters.Clear
        i = i + 1: Call .Filters.Add("全てのファイル", "*.*", i)
        i = i + 1: Call .Filters.Add("Excel File", "*.xl*", i)
        i = i + 1: Call .Filters.Add("Text File", "*.txt", i)
        i = i + 1: Call .Filters.Add("CSV File", "*.csv", i)
        i = i + 1: Call .Filters.Add("画像ファイル", "*.jpg", i)
        i = i + 1: Call .Filters.Add("画像ファイル", "*.bmp", i)
        i = i + 1: Call .Filters.Add("画像ファイル", "*.png", i)
        If coXlonFltrIndx < 1 Or coXlonFltrIndx > i Then coXlonFltrIndx = 1
        .FilterIndex = coXlonFltrIndx
    End With
End Sub

 '定型Ｐ_ファイル選択用のFileDialogオブジェクトのフィルタ可変値を設定する
Private Sub PfixSetFileDialogVariableFilter( _
            myXobjFileDialog As Object, _
            ByRef myZstrAddFltr() As String, _
            Optional ByVal coXbisFltrClr As Boolean = False, _
            Optional ByVal coXlonFltrIndx As Long = 1)
'myZstrAddFltr(i, 1) : ファイルの候補を指定する文字列(ファイル説明)
'myZstrAddFltr(i, 2) : ファイルの候補を指定する文字列(フィルタ拡張子)
'coXbisFltrClr = False : ファイルフィルタを初期化せずに追加する
'coXbisFltrClr = True  : ファイルフィルタを初期化する
'coXlonFltrIndx = 1～ : ファイルフィルタの初期選択
    If myXobjFileDialog Is Nothing Then Exit Sub
  Dim myXstrTmp As String, L As Long, U As Long
    On Error GoTo ExitPath
    L = LBound(myZstrAddFltr, 1): U = UBound(myZstrAddFltr, 1)
    myXstrTmp = myZstrAddFltr(L, L)
    On Error GoTo 0
    If UBound(myZstrAddFltr, 2) <> L + 1 Then Exit Sub
  Dim i As Long, myXstrFltrFile As String, myXstrFltrExtsn As String, n As Long
    If coXbisFltrClr = False Then
        n = myXobjFileDialog.Filters.Count
    Else
        myXobjFileDialog.Filters.Clear: n = 0
    End If
    On Error Resume Next
    For i = L To U
        myXstrFltrFile = Empty: myXstrFltrExtsn = Empty
        myXstrFltrFile = myZstrAddFltr(i, L + 0)
        myXstrFltrExtsn = myZstrAddFltr(i, L + 1)
        If myXstrFltrExtsn <> "" Then
            n = n + 1
            Call myXobjFileDialog.Filters.Add(myXstrFltrFile, myXstrFltrExtsn, n)
        End If
    Next i
    On Error GoTo 0
    If coXlonFltrIndx < 1 Or coXlonFltrIndx > n Then coXlonFltrIndx = 1
    myXobjFileDialog.FilterIndex = coXlonFltrIndx
ExitPath:
End Sub

 '定型Ｐ_ファイル選択用のFileDialogオブジェクトの初期状態を設定する
Private Sub PfixSetFileDialogForFileInitial( _
            myXobjFileDialog As Object, _
            Optional ByVal coXlonIniView As Long = msoFileDialogViewList, _
            Optional ByVal coXstrDfltPath As String = "", _
            Optional ByVal coXbisMultSlct As Boolean = False)
'coXlonIniView = msoFileDialogViewDetails    : ファイルを詳細情報と共に一覧表示
'coXlonIniView = msoFileDialogViewLargeIcons : ファイルを大きいアイコンで表示
'coXlonIniView = msoFileDialogViewList       : ファイルを詳細情報なしで一覧表示
'coXlonIniView = msoFileDialogViewPreview    : ファイルの一覧を表示し、選択したファイルをプレビュー ウィンドウ枠に表示
'coXlonIniView = msoFileDialogViewProperties : ファイルの一覧を表示し、選択したファイルのプロパティをウィンドウ枠に表示
'coXlonIniView = msoFileDialogViewSmallIcons : ファイルを小さいアイコンで表示
'coXlonIniView = msoFileDialogViewThumbnail  : ファイルを縮小表示
'coXlonIniView = msoFileDialogViewTiles      : ファイルをアイコンで並べて表示
'coXlonIniView = msoFileDialogViewWebView    : ファイルを Web 表示
'coXstrDfltPath : ファイルパス指定→ダイアログボックスにファイル名を表示
'coXstrDfltPath : フォルダパス指定→デフォルトフォルダを表示
'coXbisMultSlct = False : 複数のファイルを選択不可能
'coXbisMultSlct = True  : 複数のファイルを選択可能
    If myXobjFileDialog Is Nothing Then Exit Sub
    On Error Resume Next
    With myXobjFileDialog
        .InitialView = coXlonIniView
        .InitialFileName = coXstrDfltPath
        .AllowMultiSelect = coXbisMultSlct
    End With
    On Error GoTo 0
End Sub

 '定型Ｐ_ファイル選択用のFileDialogオブジェクトを使用してファイルを選択する
Private Sub PfixSelectFileByFileDialog( _
            myXlonFileCnt As Long, myZstrFilePath() As String, _
            ByVal myXobjFileDialog As Object)
'myZstrFilePath(i) : ファイルパス
    myXlonFileCnt = Empty: Erase myZstrFilePath
    If myXobjFileDialog Is Nothing Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXvarTmpPath As Variant, n As Long: n = L - 1
  Dim myXlonFDType As Long
    With myXobjFileDialog
        myXlonFDType = .DialogType
        If .Show = False Then Exit Sub
        myXlonFileCnt = .SelectedItems.Count
        ReDim myZstrFilePath(myXlonFileCnt) As String
        For Each myXvarTmpPath In .SelectedItems
            n = n + 1: myZstrFilePath(n) = CStr(myXvarTmpPath)
            If myXlonFDType = msoFileDialogOpen Or _
                myXlonFDType = msoFileDialogSaveAs Then .Execute
        Next
    End With
End Sub

 '定型Ｆ_フォルダ参照ダイアログを使用してフォルダを選択する
Private Function PfncstrSelectFolderByBrowseForFolder(ByVal myXstrDfltPath As String) As String
  Const coXstrTtl As String = "フォルダ選択"
    PfncstrSelectFolderByBrowseForFolder = Empty
'//デフォルトフォルダを設定
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXbisFldrExist As Boolean, myXobjDfltFldr As Object
    myXbisFldrExist = myXobjFSO.FolderExists(myXstrDfltPath)
    If myXbisFldrExist = True Then Set myXobjDfltFldr = myXobjFSO.GetFolder(myXstrDfltPath)
'//フォルダ参照ダイアログを使用してフォルダを選択
  Dim myXobjShl As Object: Set myXobjShl = CreateObject("Shell.Application")
  Dim myXobjFldr As Object
    Set myXobjFldr = myXobjShl.BrowseForFolder(&O0, coXstrTtl, &H1 + &H10, myXobjDfltFldr.Path)
    If myXobjFldr Is Nothing Then Exit Function
'//選択したフォルダのフォルダパスを取得
  Dim myXstrFldrPath As String
'    myXstrFldrPath = myXobjFldr.self.Path
    myXstrFldrPath = myXobjFldr.Items.Item.Path
    PfncstrSelectFolderByBrowseForFolder = myXstrFldrPath
    Set myXobjFSO = Nothing: Set myXobjShl = Nothing: Set myXobjFldr = Nothing
End Function

 '定型Ｆ_ファイル選択ダイアログのフィルタを設定する
Private Function PfnncstrSetFixedFilterForGetFilename() As String
    PfnncstrSetFixedFilterForGetFilename = Empty
  Dim myXstrFileFltr As String
    myXstrFileFltr = "全てのファイル, *.*"
    myXstrFileFltr = myXstrFileFltr & "," & "Excel File, *.xl*"
    myXstrFileFltr = myXstrFileFltr & "," & "Text File, *.txt"
    myXstrFileFltr = myXstrFileFltr & "," & "CSV File, *.csv"
    myXstrFileFltr = myXstrFileFltr & "," & "画像ファイル, *.jpg"
    myXstrFileFltr = myXstrFileFltr & "," & "画像ファイル, *.bmp"
    myXstrFileFltr = myXstrFileFltr & "," & "画像ファイル, *.png"
    PfnncstrSetFixedFilterForGetFilename = myXstrFileFltr
End Function

 '定型Ｆ_ファイル選択ダイアログのフィルタ可変値を設定する
Private Function PfnncstrSetVariableFilterForGetFilename( _
            ByVal myXstrFltrOrg As String, ByRef myZstrAddFltr() As String, _
            Optional ByVal coXbisFltrClr As Boolean = False) As String
'myZstrAddFltr(i) : ファイルの候補を指定する文字列("ファイル説明, フィルタ拡張子")
'coXbisFltrClr = False : ファイルフィルタを初期化せずに追加する
'coXbisFltrClr = True  : ファイルフィルタを初期化する
    PfnncstrSetVariableFilterForGetFilename = Empty
  Dim myXstrTmp As String, L As Long, U As Long
    On Error GoTo ExitPath
    L = LBound(myZstrAddFltr): U = UBound(myZstrAddFltr): myXstrTmp = myZstrAddFltr(L)
    On Error GoTo 0
  Dim myXstrFileFltr As String, i As Long
    If coXbisFltrClr = False Then myXstrFileFltr = myXstrFltrOrg
    For i = L To U
        myXstrTmp = Empty
        myXstrTmp = myZstrAddFltr(i)
        If myXstrTmp <> "" Then
            If myXstrFileFltr = "" Then
                myXstrFileFltr = myXstrTmp
            Else
                myXstrFileFltr = myXstrFileFltr & "," & myXstrTmp
            End If
        End If
    Next i
    PfnncstrSetVariableFilterForGetFilename = myXstrFileFltr
    Exit Function
ExitPath:
    PfnncstrSetVariableFilterForGetFilename = myXstrFltrOrg
End Function

 '定型Ｐ_ファイル選択ダイアログを使用してファイルを選択する(ファイル開く用)
Private Sub PfixSelectFileByGetOpenFilename( _
            myXlonFileCnt As Long, myZstrFilePath() As String, _
            Optional ByVal coXstrFileFltr As String = "全てのファイル, *.*", _
            Optional ByVal coXlonFltrIndx As Long = 1, _
            Optional ByVal coXbisMultSlct As Boolean = False)
'coXstrFileFltr : ファイルの候補を指定する文字列("ファイル説明, フィルタ拡張子")
'coXlonFltrIndx = 1～ : ファイルフィルタの初期選択
'coXbisMultSlct = False : 複数のファイルを選択不可能
'coXbisMultSlct = True  : 複数のファイルを選択可能
  Const coXstrTtl As String = "ファイルを選択"
  Const coXstrBtnName As String = "選択"
    myXlonFileCnt = Empty: Erase myZstrFilePath
  Dim myXvarFilePath As Variant
    On Error GoTo ExitPath
    myXvarFilePath = Application.GetOpenFilename( _
                        FileFilter:=coXstrFileFltr, _
                        FilterIndex:=coXlonFltrIndx, _
                        Title:=coXstrTtl, _
                        ButtonText:=coXstrBtnName, _
                        MultiSelect:=coXbisMultSlct)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXvarTmpPath As Variant, n As Long: n = L - 1
    If IsArray(myXvarFilePath) = False Then
        If myXvarFilePath = False Then Exit Sub
        n = n + 1: ReDim myZstrFilePath(n) As String
        myZstrFilePath(n) = CStr(myXvarFilePath)
    Else
        For Each myXvarTmpPath In myXvarFilePath
            n = n + 1: ReDim Preserve myZstrFilePath(n) As String
            myZstrFilePath(n) = CStr(myXvarTmpPath)
        Next
    End If
    myXlonFileCnt = n + (1 - L)
ExitPath:
End Sub

 '定型Ｆ_ファイル選択ダイアログを使用してファイルを選択する(ファイル保存用)
Private Function PfncstrSelectFileByGetSaveAsFilename( _
            Optional ByVal coXstrIniFileName As String = "", _
            Optional ByVal coXstrFileFltr As String = "", _
            Optional ByVal coXlonFltrIndx As Long = 1) As String
'coXstrIniFileName : ファイルパス指定→ダイアログボックスにファイル名を表示
'coXstrIniFileName : フォルダパス指定→デフォルトフォルダを表示
'coXstrFileFltr : ファイルの候補を指定する文字列("ファイル説明, フィルタ拡張子")
'coXlonFltrIndx = 1～ : ファイルフィルタの初期選択
  Const coXstrTtl As String = "ファイルを選択"
    PfncstrSelectFileByGetSaveAsFilename = Empty
  Dim myXvarFilePath As Variant
    On Error GoTo ExitPath
    myXvarFilePath = Application.GetSaveAsFilename( _
                        InitialFileName:=coXstrIniFileName, _
                        FileFilter:=coXstrFileFltr, _
                        FilterIndex:=coXlonFltrIndx, _
                        Title:=coXstrTtl)
    On Error GoTo 0
    If myXvarFilePath = False Then Exit Function
    PfncstrSelectFileByGetSaveAsFilename = CStr(myXvarFilePath)
ExitPath:
End Function

 '定型Ｆ_WindowsScriptHostでメッセージボックスを表示する
Private Function PfnclonMsgBoxWScript( _
            ByVal myXstrTitle As String, ByVal myXstrText As String, _
            ByVal myXdouScndsToWait As Double, ByVal myXlonType As Long) As Long
'戻り値 = 1 (vbOK) : [OK] ボタン
'戻り値 = 2 (vbCancel) : [キャンセル] ボタン
'戻り値 = 3 (vbAbort) : [中止] ボタン
'戻り値 = 4 (vbRetry) : [再試行] ボタン
'戻り値 = 5 (vbIgnore) : [無視] ボタン
'戻り値 = 6 (vbYes) : [はい] ボタン
'戻り値 = 7 (vbNo) : [いいえ] ボタン
'戻り値 = -1 : 自動的に閉じた場合
'myXstrTitle : メッセージボックスのタイトル
'myXstrText : メッセージボックスに表示する文字列
'myXdouScndsToWait : メッセージボックスを閉じるまでの時間
'myXlonType : メッセージボックスのアイコンやボタンの種類
'myXlonType = vbOKOnly = 0 : [OK] ボタンを表示
'myXlonType = vbOKCancel = 1 : [OK]ボタンと[キャンセル]ボタンを表示
'myXlonType = vbAbortRetryIgnore = 2 : [中止]ボタン、[再試行]ボタン、[無視]ボタンを表示
'myXlonType = vbYesNoCancel = 3 : [はい]ボタン、[いいえ]ボタン、[キャンセル]ボタンを表示
'myXlonType = vbYesNo = 4 : [はい]ボタンと[いいえ]ボタンを表示
'myXlonType = vbRetryCancel = 5 : [再試行]ボタンと[キャンセル]ボタンを表示
'myXlonType = vbCritical = 16
'myXlonType = vbQuestion = 32
'myXlonType = vbExclamation =48
'myXlonType = vbInformation = 64
    If myXstrText = "" Then Exit Function
  Dim myXlonRet As Long
  Dim WSH As Object: Set WSH = CreateObject("WScript.Shell")
    myXlonRet = WSH.Popup(myXstrText, myXdouScndsToWait, myXstrTitle, myXlonType)
    PfnclonMsgBoxWScript = myXlonRet
    Set WSH = Nothing
End Function

      <br></pre>
    </body>
  </html>
