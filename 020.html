<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>


 '定型Ｆ_指定フォルダの存在を確認する
Private Function PfncbisCheckFolderExist(ByVal myXstrDirPath As String) As Boolean
    PfncbisCheckFolderExist = False
    If myXstrDirPath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfncbisCheckFolderExist = myXobjFSO.FolderExists(myXstrDirPath)
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_指定フォルダのオブジェクトを取得する
Private Function PfncobjGetFolder(ByVal myXstrDirPath As String) As Object
    Set PfncobjGetFolder = Nothing
    If myXstrDirPath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    With myXobjFSO
        If .FolderExists(myXstrDirPath) = False Then Exit Function
        Set PfncobjGetFolder = .GetFolder(myXstrDirPath)
    End With
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_指定フォルダの階層位置を取得する
Private Function PfnclonCountFolderLayer(ByVal myXstrDirPath As String) As Long
    PfnclonCountFolderLayer = Empty
    If myXstrDirPath = "" Then Exit Function
    If Right(myXstrDirPath, 1) = "\" Then Exit Function
  Dim myXstrTmpPath As String: myXstrTmpPath = myXstrDirPath
    If Left(myXstrTmpPath, 2) = "\\" Then myXstrTmpPath = Mid(myXstrTmpPath, 3)
  Dim i As Long, n As Long: n = 0
    For i = Len(myXstrTmpPath) To 1 Step -1
        If Mid(myXstrTmpPath, i, 1) = "\" Then n = n + 1
    Next i
    PfnclonCountFolderLayer = n
End Function

 '定型Ｐ_指定フォルダのフォルダ名情報を取得する
Private Sub PfixGetFolderNameInformation( _
            myXstrPrntPath As String, myXstrDirName As String, _
            ByVal myXstrDirPath As String)
    myXstrPrntPath = Empty: myXstrDirName = Empty
    If myXstrDirPath = "" Then Exit Sub
  Dim i As Long, myXlonInStrRev As Long: myXlonInStrRev = 0
    For i = Len(myXstrDirPath) To 1 Step -1
        If Mid(myXstrDirPath, i, 1) = "\" Then
            myXlonInStrRev = i: Exit For
        End If
    Next i
    If myXlonInStrRev <= 0 Or myXlonInStrRev >= Len(myXstrDirPath) Then Exit Sub
    myXstrPrntPath = Left(myXstrDirPath, myXlonInStrRev - 1)
    myXstrDirName = Mid(myXstrDirPath, myXlonInStrRev + 1)
End Sub

 '定型Ｐ_指定フォルダのフォルダ名情報を取得する(FileSystemObject使用)
Private Sub PfixGetFolderNameInformationByFSO( _
            myXstrPrntPath As String, myXstrDirName As String, _
            ByVal myXstrDirPath As String)
    myXstrPrntPath = Empty: myXstrDirName = Empty
    If myXstrDirPath = "" Then Exit Sub
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    With myXobjFSO
        myXstrPrntPath = .GetParentFolderName(myXstrDirPath)    '親フォルダパス
        myXstrDirName = .GetFolder(myXstrDirPath).Name          'フォルダ名
    End With
    Set myXobjFSO = Nothing
End Sub

 '定型Ｆ_全ドライブ数を取得する
Private Function PfnclonCountDrive() As Long
    PfnclonCountDrive = Empty
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfnclonCountDrive = myXobjFSO.Drives.Count
    Set myXobjFSO = Nothing
End Function

 '定型Ｐ_全ドライブのパス一覧を取得する
Private Sub PfixGetDrivePathArray( _
            myXlonDrvCnt As Long, myZstrDrvPath() As String)
'myZstrDrvPath(i) : ドライブパス
    myXlonDrvCnt = Empty: Erase myZstrDrvPath
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    myXlonDrvCnt = myXobjFSO.Drives.Count
    If myXlonDrvCnt <= 0 Then Exit Sub
  Dim n As Long: n = myXlonDrvCnt + L - 1
    ReDim myZstrDrvPath(n) As String
  Dim myXobjDrv As Object: n = L - 1
    For Each myXobjDrv In myXobjFSO.Drives
        n = n + 1: myZstrDrvPath(n) = myXobjDrv.Path
    Next myXobjDrv
    Set myXobjFSO = Nothing: Set myXobjDrv = Nothing
End Sub

 '定型Ｆ_指定ディレクトリ内のサブフォルダ数を取得する
Private Function PfnclonCountSubFolder(ByVal myXobjDir As Object) As Long
    PfnclonCountSubFolder = Empty
    If myXobjDir Is Nothing Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfnclonCountSubFolder = myXobjDir.SubFolders.Count
    Set myXobjFSO = Nothing
End Function

 '定型Ｐ_指定ディレクトリ内のサブフォルダオブジェクト一覧を取得する
Private Sub PfixGetSubFolderArray( _
            myXlonFldrCnt As Long, myZobjFldr() As Object, _
            ByVal myXobjDir As Object)
'myZobjFldr(i) : サブフォルダ
    myXlonFldrCnt = Empty: Erase myZobjFldr
    If myXobjDir Is Nothing Then Exit Sub
    myXlonFldrCnt = myXobjDir.SubFolders.Count
    If myXlonFldrCnt <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim n As Long: n = myXlonFldrCnt + L - 1
    ReDim myZobjFldr(n) As Object
  Dim myXobjSubFldr As Object: n = L - 1
    For Each myXobjSubFldr In myXobjDir.SubFolders
        n = n + 1: Set myZobjFldr(n) = myXobjSubFldr
    Next myXobjSubFldr
    Set myXobjSubFldr = Nothing
End Sub

 '定型Ｐ_指定ディレクトリ内のサブフォルダのパス一覧と名前一覧を取得する
Private Sub PfixGetSubFolderPathName( _
            myXlonFldrCnt As Long, _
            myZstrFldrName() As String, myZstrFldrPath() As String, _
            ByVal myXstrDirPath As String)
    myXlonFldrCnt = Empty: Erase myZstrFldrName: Erase myZstrFldrPath
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    If myXobjFSO.FolderExists(myXstrDirPath) = True Then Exit Sub
  Dim myXobjDir As Object: Set myXobjDir = myXobjFSO.GetFolder(myXstrDirPath)
    myXlonFldrCnt = myXobjDir.SubFolders.Count
    If myXlonFldrCnt <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim n As Long: n = myXlonFldrCnt + L - 1
    ReDim myZstrFldrName(n) As String
    ReDim myZstrFldrPath(n) As String
  Dim myXobjSubFldr As Object: n = L - 1
    For Each myXobjSubFldr In myXobjFldr.SubFolders
        n = n + 1
        myZstrFldrName(n) = myXobjSubFldr.Name
        myZstrFldrPath(n) = myXobjSubFldr.Path
    Next
    Set myXobjFSO = Nothing
    Set myXobjDir = Nothing: Set myXobjSubFldr = Nothing
End Sub

 '定型Ｐ_フォルダパス一覧から存在するフォルダパスを抽出する
Private Sub PfixPickUpExistFolderArray( _
            myXlonExistFldrCnt As Long, myZstrExistFldrPath() As String, _
            ByRef myZstrOrgFldrPath() As String)
'myZstrExistFldrPath(i) : 抽出フォルダパス
'myZstrOrgFldrPath(i) : 元フォルダパス
    myXlonExistFldrCnt = Empty: Erase myZstrExistFldrPath
  Dim myXstrTmp As String, L As Long
    On Error GoTo ExitPath
    L = LBound(myZstrOrgFldrPath): myXstrTmp = myZstrOrgFldrPath(L)
    On Error GoTo 0
  Dim i As Long, myXstrPath As String, n As Long: n = L - 1
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    For i = LBound(myZstrOrgFldrPath) To UBound(myZstrOrgFldrPath)
        myXstrPath = myZstrOrgFldrPath(i)
        If myXobjFSO.FolderExists(myXstrPath) = False Then GoTo NextPath
        n = n + 1: ReDim Preserve myZstrExistFldrPath(n) As String
        myZstrExistFldrPath(n) = myXstrPath
NextPath:
    Next i
    myXlonExistFldrCnt = n + L - 1
    Set myXobjFSO = Nothing
ExitPath:
End Sub

 '定型Ｐ_1次元配列のフォルダパス一覧からフォルダオブジェクト一覧を取得する
Private Sub PfixGetFolderFor1DArray( _
                myXlonFldrCnt As Long, myZobjFldr() As Object, _
                ByRef myZstrFldrPath() As String)
'myZobjFldr(i) : フォルダオブジェクト一覧
'myZstrFldrPath(i) : 元フォルダパス一覧
    myXlonFldrCnt = Empty: Erase myZobjFldr
  Dim myXstrTmp As String, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZstrFldrPath): myXstrTmp = myZstrFldrPath(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXobjTmp As Object, i As Long, n As Long: n = Lo - 1
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    For i = LBound(myZstrFldrPath) To UBound(myZstrFldrPath)
        myXstrTmp = Empty
        myXstrTmp = myZstrFldrPath(i)
        With myXobjFSO
            If .FolderExists(myXstrTmp) = False Then GoTo NextPath
            Set myXobjTmp = .GetFolder(myXstrTmp)
        End With
        n = n + 1: ReDim Preserve myZobjFldr(n) As Object
        Set myZobjFldr(n) = myXobjTmp
NextPath:
    Next i
    myXlonFldrCnt = n - Lo + 1
    Set myXobjFSO = Nothing
ExitPath:
End Sub

 '定型Ｐ_1次元配列のフォルダファイルオブジェクト一覧の文字列情報を取得する
Private Sub PfixGetFolderFileStringInformationFor1DArray( _
                myXlonInfoCnt As Long, myZstrInfo() As String, _
                ByRef myZobjFldrFile() As Object, _
                Optional ByVal coXlonStrOptn As Long = 1)
'myZstrInfo(i) : 抽出フォルダ情報
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZstrInfo
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        If myZobjFldrFile(i) Is Nothing Then GoTo NextPath
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1: ReDim Preserve myZstrInfo(n) As String
        myZstrInfo(n) = myXstrTmp
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_2次元配列のフォルダファイルオブジェクト一覧の文字列情報を取得する
Private Sub PfixGetFolderFileStringInformationFor2DArray( _
                myXlonInfoRowCnt As Long, myXlonInfoColCnt As Long, _
                myXlonInfoCnt As Long, myZstrInfo() As String, _
                ByRef myZobjFldrFile() As Object, _
                Optional ByVal coXlonStrOptn As Long = 1)
'myZstrInfo(i, j) : 抽出フォルダ情報
'myZobjFldrFile(i, j) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoRowCnt = Empty: myXlonInfoColCnt = Empty
    myXlonInfoCnt = Empty: Erase myZstrInfo
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile, 1): Set myXobjTmp = myZobjFldrFile(Li, Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, j As Long, k As Long, m As Long, n As Long
    m = UBound(myZobjFldrFile, 1) - LBound(myZobjFldrFile, 1) + 1
    n = UBound(myZobjFldrFile, 2) - LBound(myZobjFldrFile, 2) + 1
    i = m + Lo - 1: j = n + Lo - 1
    ReDim myZstrInfo(i, j) As String
    n = Lo - 1: k = 0
    For j = LBound(myZobjFldrFile, 2) To UBound(myZobjFldrFile, 2)
        m = Lo - 1: n = n + 1
        For i = LBound(myZobjFldrFile, 1) To UBound(myZobjFldrFile, 1)
            m = m + 1
            myXstrTmp = Empty
            If myZobjFldrFile(i, j) Is Nothing Then GoTo NextPath
            Select Case coXlonStrOptn
                Case 1: myXstrTmp = myZobjFldrFile(i, j).Name
                Case 2: myXstrTmp = myZobjFldrFile(i, j).Path
                Case 3: myXstrTmp = myZobjFldrFile(i, j).ParentFolder
                Case 4: myXstrTmp = myZobjFldrFile(i, j).Attributes
                Case 5: myXstrTmp = myZobjFldrFile(i, j).Type
            End Select
            k = k + 1: myZstrInfo(m, n) = myXstrTmp
NextPath:
        Next i
    Next j
    myXlonInfoRowCnt = m - Lo + 1: myXlonInfoColCnt = n - Lo + 1: myXlonInfoCnt = k
ExitPath:
End Sub

 '定型Ｐ_フォルダファイルオブジェクト一覧の文字列情報とオブジェクト併記の配列を取得する
Private Sub PfixGetFolderFileStringInformationAndObject2DArray( _
            myXlonInfoCnt As Long, myZvarStrInfoAndObj() As Variant, _
            ByRef myZobjFldrFile() As Object, _
            Optional ByVal coXlonStrOptn As Long = 1)
'myZvarStrInfoAndObj(i, 1) : 文字列情報
'myZvarStrInfoAndObj(i, 2) : 元フォルダor元ファイル
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZvarStrInfoAndObj
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZstrBuf() As String, myZobjBuf() As Object
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1
        ReDim Preserve myZstrBuf(n) As String
        ReDim Preserve myZobjBuf(n) As Object
        myZstrBuf(n) = myXstrTmp
        Set myZobjBuf(n) = myZobjFldrFile(i)
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
    ReDim myZvarStrInfoAndObj(n, Lo + 1) As Variant
    For i = LBound(myZstrBuf) To UBound(myZstrBuf)
        myZvarStrInfoAndObj(i, Lo + 0) = myZstrBuf(i)
        Set myZvarStrInfoAndObj(i, Lo + 1) = myZobjBuf(i)
    Next i
    Erase myZstrBuf: Erase myZobjBuf
ExitPath:
End Sub

 '定型Ｐ_指定ディレクトリ内のサブフォルダ一覧を取得してソートする
Private Sub PincGetAndSortSubFolderArray( _
            myXlonFldrCnt As Long, myZobjFldr() As Object, _
            ByVal myXobjDir As Object)
'Includes PfixGetSubFolderArray
'Includes PfixGetFolderFileStringInformationAndObject2DArray
'Includes PfixQuickSortOfStringObject2DArray
'myZobjFldr(i) : サブフォルダ
'//サブフォルダオブジェクトを取得
  Dim myZobjSubFldr() As Object
    Call PfixGetSubFolderArray(myXlonFldrCnt, myZobjSubFldr, myXobjDir)
    If myXlonFldrCnt <= 0 Then Exit Sub
'//文字列情報一覧を取得
  Dim myXlonInfoCnt As Long, myZvarStrAndObj() As Variant
    Call PfixGetFolderFileStringInformationAndObject2DArray( _
            myXlonInfoCnt, myZvarStrAndObj, _
            myZobjSubFldr, 1)
    If myXlonInfoCnt <> myXlonFldrCnt Then myXlonFldrCnt = 0
    If myXlonFldrCnt <= 0 Then Exit Sub
'//文字列によるクイックソート
  Dim L As Long, U As Long
    L = LBound(myZvarStrAndObj, 1)
    U = UBound(myZvarStrAndObj, 1)
    Call PfixQuickSortOfStringObject2DArray(myZvarStrAndObj, L, U, 1, 1)
'//オブジェクトを配列変数に格納
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long: i = myXlonFldrCnt + Lo - 1
    ReDim myZobjFldr(i) As Object
    For i = LBound(myZvarStrAndObj, 1) To UBound(myZvarStrAndObj, 1)
        Set myZobjFldr(i + Lo - L) = myZvarStrAndObj(i, L + 1)
    Next i
    Erase myZobjSubFldr: Erase myZvarStrAndObj
End Sub

 '定型Ｐ_指定フォルダを開く
Private Sub PfixOpenDirectoryWithShell(myXlonhWnd As Long, _
            ByVal myXstrDirPath As String)
    myXlonhWnd = Empty
    On Error GoTo ExitPath
    myXlonhWnd = Shell("Explorer.exe " & myXstrDirPath, vbNormalFocus)
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_指定フォルダを開き指定ファイルを選択する
Private Sub PfixOpenDirectoryAndSelectFileWithShell(myXlonhWnd As Long, _
            ByVal myXstrDirPath As String, ByVal myXstrFileName As String)
    myXlonhWnd = Empty
    If myXstrDirPath = "" Then Exit Sub
  Dim myXstrFilePath As String
    myXstrFilePath = myXstrDirPath & "\" & myXstrFileName
    On Error GoTo ExitPath
    myXlonhWnd = Shell("Explorer.exe /select,""" & myXstrFilePath & """", vbNormalFocus)
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_フォルダを新規作成する
Private Sub PfixCreateFolder(myXbisExitFlag As Boolean, _
            ByVal myXstrDirPath As String)
    myXbisExitFlag = False
    If myXstrDirPath = "" Then GoTo ExitPath
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    With myXobjFSO
        If .FolderExists(myXstrDirPath) = True Then GoTo ExitPath
        On Error GoTo ExitPath
        Call .CreateFolder(myXstrDirPath)
        On Error GoTo 0
    End With
    Set myXobjFSO = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_フォルダを複製または移動する
Private Sub PfixCopyOrMoveFolder(myXbisExitFlag As Boolean, _
            ByVal myXstrOrgDirPath As String, ByVal myXstrNewDirPath As String, _
            Optional ByVal coXbisMove As Boolean = False)
'coXbisMove = False  : ファイルを複製
'coXbisMove = True   : ファイルを移動
    myXbisExitFlag = False
    If myXstrOrgDirPath = "" Or myXstrNewDirPath = "" Then GoTo ExitPath
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    With myXobjFSO
        If .FolderExists(myXstrOrgDirPath) = False Then GoTo ExitPath
        If .FolderExists(myXstrNewDirPath) = True Then GoTo ExitPath
        On Error GoTo ExitPath
        Select Case coXbisMove
            Case True: Call .MoveFolder(myXstrOrgDirPath, myXstrNewDirPath)
            Case Else: Call .CopyFolder(myXstrOrgDirPath, myXstrNewDirPath)
        End Select
        On Error GoTo 0
    End With
    Set myXobjFSO = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_フォルダを削除する
Private Sub PfixDeleteFolder(myXbisExitFlag As Boolean, _
            ByVal myXstrDirPath As String, _
            Optional ByVal coXbisCheckOptn As Boolean = True)
'coXbisCheckOptn = True   : 削除直前に再確認する
'coXbisCheckOptn = False  : 削除確認無し
    myXbisExitFlag = False
    If myXstrDirPath = "" Then Exit Sub
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    If myXobjFSO.FolderExists(myXstrDirPath) = False Then GoTo ExitPath
    If coXbisCheckOptn = True Then
      Dim myXstrTitle As String, myXstrPrompt As String
        myXstrTitle = "フォルダ削除の確認"
        myXstrPrompt = "本当にフォルダを削除しますか？" _
                & vbCrLf & myXstrDirPath _
                & vbCrLf _
                & vbCrLf & "「OK」：削除" _
                & vbCrLf & "「キャンセル」：中止"
      Dim myXlonMsgBox As Long
        myXlonMsgBox = MsgBox(myXstrPrompt, vbOKCancel, myXstrTitle)
        If myXlonMsgBox = vbCancel Then GoTo ExitPath
    End If
    Call myXobjFSO.DeleteFolder(myXstrDirPath)
    Set myXobjFSO = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｆ_フォルダパス内のディレクトリパスorベース名を置換する
Private Function PfncstrFolderPathReplaceParentBase( _
            ByVal myXstrOrgFldrPath As String, _
            ByVal myXstrOrgPrnt As String, ByVal myXstrOrgBase As String, _
            ByVal myXstrNewPrnt As String, ByVal myXstrNewBase As String) As String
    PfncstrFolderPathReplaceParentBase = Empty
    If myXstrOrgFldrPath = "" Then Exit Function
    If myXstrNewPrnt = "" And myXstrNewBase = "" Then Exit Function
  Dim myXstrNewFilePath As String
    If InStr(myXstrOrgFldrPath, myXstrOrgPrnt) > 0 And myXstrNewPrnt <> "" Then _
        myXstrNewFilePath = Replace(myXstrOrgFldrPath, myXstrOrgPrnt, myXstrNewPrnt)
    If InStr(myXstrOrgFldrPath, myXstrOrgBase) > 0 And myXstrNewBase <> "" Then _
        myXstrNewFilePath = Replace(myXstrOrgFldrPath, myXstrOrgBase, myXstrNewBase)
    PfncstrFolderPathReplaceParentBase = myXstrNewFilePath
End Function

 '定型Ｆ_フォルダ名を自動生成する
Private Function PfncstrFolderAutoNaming(ByVal myXstrDirPath As String) As String
    PfncstrFolderAutoNaming = Empty
    If myXstrDirPath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXbisDirExistFlag As Boolean
    myXbisDirExistFlag = myXobjFSO.FolderExists(myXstrDirPath)
    Do While myXbisDirExistFlag = True
        myXstrDirPath = myXstrDirPath & "_" & Format(Now, "YYYYMMDDHHmmSS")
        myXbisDirExistFlag = myXobjFSO.FolderExists(myXstrDirPath)
    Loop
    PfncstrFolderAutoNaming = myXstrDirPath
    Set myXobjFSO = Nothing
End Function

 '定型Ｐ_フォルダ階層をチェックしてフォルダを新規作成する
Private Sub PfixCreateFolderWithHierarchyCheck(myXbisExitFlag As Boolean, _
            ByVal myXstrBaseDirPath As String, ByVal myXstrRltvDirPath As String)
    myXbisExitFlag = False
    If myXstrRltvDirPath = "" Then GoTo ExitPath
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
'//基点フォルダが存在しなければ中止
    If myXobjFSO.FolderExists(myXstrBaseDirPath) = False Then GoTo ExitPath
'//新規フォルダと同一名フォルダが存在すれば中止
  Dim myXstrTmpPath As String
    myXstrTmpPath = myXstrBaseDirPath & "\" & myXstrRltvDirPath
    If myXobjFSO.FolderExists(myXstrTmpPath) = True Then Exit Sub
'//フォルダパスをフォルダ名に分割
  Dim myZvarSplitVrbl As Variant
    myZvarSplitVrbl = Split(myXstrRltvDirPath, "\")
'//フォルダの有無を階層で順次チェックして、フォルダ無しの場合フォルダを新規作成
    myXstrTmpPath = myXstrBaseDirPath
  Dim i As Long
    For i = LBound(myZvarSplitVrbl) To UBound(myZvarSplitVrbl)
        myXstrTmpPath = myXstrTmpPath & "\" & myZvarSplitVrbl(i)
        With myXobjFSO
            If .FolderExists(myXstrTmpPath) = True Then GoTo NextPath
            Call .CreateFolder(myXstrTmpPath)
        End With
NextPath:
    Next i
    Set myXobjFSO = Nothing: myZvarSplitVrbl = Empty
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_フォルダ名自動生成＆階層チェック
Private Sub PincFolderAutoNamingAndHierarchyCheck( _
            myXbisDirExistFlag As Boolean, myXstrDirPathOUT As String, _
            ByVal myXstrDirPathIN As String, _
            Optional ByVal coXlonNGLayer As Long = 1, _
            Optional ByVal coXbisAutoNaming As Boolean = False)
'Includes PfnclonCountFolderLayer
'Includes PfncbisCheckFolderExist
'Includes PfncstrFolderAutoNaming
'Includes PfixGetFolderNameInformation
'Includes PfixCreateFolderWithHierarchyCheck
'coXlonNGLayer >= 1  : フォルダの必要階層数
'coXbisAutoNaming = False  : 同一名フォルダがあれば終了
'coXbisAutoNaming = True   : 同一名フォルダがあればフォルダ名を自動生成
    myXbisDirExistFlag = False: myXstrDirPathOUT = Empty
'//対象フォルダが指定階層より下層でなければ中止
    If coXlonNGLayer <= 0 Then GoTo ExitPath
    If PfnclonCountFolderLayer(myXstrDirPathIN) <= coXlonNGLayer Then GoTo ExitPath
'//対象フォルダと同一名フォルダが存在するか確認
    myXbisDirExistFlag = PfncbisCheckFolderExist(myXstrDirPathIN)
    If myXbisDirExistFlag = True Then
    '//同一名フォルダが存在する場合
    '//フォルダ名を自動生成
        Select Case coXbisAutoNaming
            Case True: myXstrDirPathOUT = PfncstrFolderAutoNaming(myXstrDirPathIN)
            Case Else: myXstrDirPathOUT = myXstrDirPathIN: Exit Sub
        End Select
    Else
    '//同一名フォルダが存在しない場合
    '//フォルダ階層をチェック
      Dim myXstrPrntPath As String, myXstrDirName As String
        Call PfixGetFolderNameInformation(myXstrPrntPath, myXstrDirName, myXstrDirPathIN)
      Dim myXstrBaseDirPath As String, myXstrRltvDirPath As String
        If Left(myXstrPrntPath, 2) = "\\" Then
          Dim myXstrTmpPath As String: myXstrTmpPath = Mid(myXstrPrntPath, 3)
            myXstrBaseDirPath = Left(myXstrPrntPath, InStr(myXstrTmpPath, "\") + 2 - 1)
        Else
            myXstrBaseDirPath = Left(myXstrPrntPath, InStr(myXstrPrntPath, "\") - 1)
        End If
        myXstrRltvDirPath = Mid(myXstrPrntPath, Len(myXstrBaseDirPath) + 2)
      Dim myXbisExitFlag As Boolean
        Call PfixCreateFolderWithHierarchyCheck(myXbisExitFlag, _
                myXstrBaseDirPath, myXstrRltvDirPath)
        If myXbisExitFlag = True Then GoTo ExitPath
        myXstrDirPathOUT = myXstrDirPathIN
    End If
    Exit Sub
ExitPath:
    myXstrDirPathOUT = ""
End Sub

 '定型Ｐ_フォルダを新規作成する(フォルダ名自動生成＆階層チェック機能付き)
Private Sub PincCreateFolderWithAutoNamingAndHierarchyCheck( _
            myXstrDirPathOUT As String, _
            ByVal myXstrDirPathIN As String, _
            Optional ByVal coXlonNGLayer As Long = 1, _
            Optional ByVal coXbisAutoNaming As Boolean = False)
'Includes PincFolderAutoNamingAndHierarchyCheck
'Includes PfixCreateFolder
'coXlonNGLayer >= 1  : フォルダの必要階層数
'coXbisAutoNaming = False  : 同一名フォルダがあれば終了
'coXbisAutoNaming = True   : 同一名フォルダがあればフォルダ名を自動生成
    myXstrDirPathOUT = Empty
'//新規フォルダのフォルダ名自動生成＆階層チェック
  Dim myXbisDirExistFlag As Boolean
    Call PincFolderAutoNamingAndHierarchyCheck( _
            myXbisDirExistFlag, myXstrDirPathOUT, _
            myXstrDirPathIN, coXlonNGLayer, coXbisAutoNaming)
    If myXstrDirPathOUT = "" Then GoTo ExitPath
    If myXbisDirExistFlag = True Then _
        If myXstrDirPathOUT = myXstrDirPathIN Then Exit Sub
'//フォルダを新規作成
  Dim myXbisExitFlag As Boolean
    Call PfixCreateFolder(myXbisExitFlag, myXstrDirPathOUT)
    If myXbisExitFlag = True Then GoTo ExitPath
    Exit Sub
ExitPath:
    myXstrDirPathOUT = ""
End Sub

 '定型Ｐ_フォルダを複製または移動する(フォルダ名自動生成＆階層チェック機能付き)
Private Sub PincCopyOrMoveFolderWithAutoNamingAndHierarchyCheck( _
            myXstrDirPathOUT As String, _
            ByVal myXstrOrgDirPath As String, ByVal myXstrNewDirPath As String, _
            Optional ByVal coXbisMove As Boolean = False, _
            Optional ByVal coXlonNGLayer As Long = 1, _
            Optional ByVal coXbisAutoNaming As Boolean = False)
'Includes PfncbisCheckFolderExist
'Includes PincFolderAutoNamingAndHierarchyCheck
'Includes PfixCopyOrMoveFolder
'coXbisMove = False  : フォルダを複製
'coXbisMove = True   : フォルダを移動
'coXlonNGLayer >= 1  : フォルダの必要階層数
'coXbisAutoNaming = False  : 同一名フォルダがあれば終了
'coXbisAutoNaming = True   : 同一名フォルダがあればフォルダ名を自動生成
    myXstrDirPathOUT = Empty
'//元フォルダが存在しなければ中止
    If PfncbisCheckFolderExist(myXstrOrgDirPath) = False Then GoTo ExitPath
'//複製or移動フォルダのフォルダ名自動生成＆階層チェック
  Dim myXbisDirExistFlag As Boolean
    Call PincFolderAutoNamingAndHierarchyCheck( _
            myXbisDirExistFlag, myXstrDirPathOUT, _
            myXstrNewDirPath, coXlonNGLayer, coXbisAutoNaming)
    If myXstrDirPathOUT = "" Then GoTo ExitPath
    If myXbisDirExistFlag = True Then _
        If myXstrDirPathOUT = myXstrNewDirPath Then GoTo ExitPath
'//フォルダを複製または移動
  Dim myXbisExitFlag As Boolean
    Call PfixCopyOrMoveFolder(myXbisExitFlag, _
            myXstrOrgDirPath, myXstrDirPathOUT, coXbisMove)
    If myXbisExitFlag = True Then GoTo ExitPath
    Exit Sub
ExitPath:
    myXstrDirPathOUT = ""
End Sub

 '定型Ｐ_指定フォルダファイルの容量情報を取得する
Private Sub PfncdouGetFolderFileSize( _
            myXdouSize As Double, myXdouOrgSize As Double, _
            ByVal myXobjFldrFile As Object, _
            Optional ByVal coXstrUnit As String = "Byte", _
            Optional ByVal coXlonDigit As Long = 0)
'coXstrUnit = "Byte", "KByte", "MByte", "GByte", "TByte", ...
'coXlonDigit : 桁数
    myXdouSize = Empty: myXdouOrgSize = Empty
    If myXobjFldrFile Is Nothing Then Exit Sub
    If coXlonDigit < 0 Then Exit Sub
    myXdouOrgSize = myXobjFldrFile.Size
    Select Case coXstrUnit
        Case "Byte"
            myXdouSize = myXdouOrgSize
        Case "KByte"
            myXdouSize = myXdouOrgSize / 1024
        Case "MByte"
            myXdouSize = myXdouOrgSize / 1024 / 1024
        Case "GByte"
            myXdouSize = myXdouOrgSize / 1024 / 1024 / 1024
        Case "TByte"
            myXdouSize = myXdouOrgSize / 1024 / 1024 / 1024 / 1024
        Case Else
            myXdouSize = myXdouOrgSize
    End Select
    myXdouSize = Round(myXdouSize, coXlonDigit)
End Sub

 '定型Ｐ_指定フォルダファイルパスからフォルダファイルの容量情報を取得する
Private Sub PfncdouGetFolderFileSizeFromPath( _
            myXdouSize As Double, myXdouOrgSize As Double, _
            ByVal myXstrPath As String, _
            Optional ByVal coXstrUnit As String = "Byte", _
            Optional ByVal coXlonDigit As Long = 0)
'coXstrUnit = "Byte", "KByte", "MByte", "GByte", "TByte", ...
'coXlonDigit : 桁数
    myXdouSize = Empty: myXdouOrgSize = Empty
    If myXstrPath = "" Then Exit Sub
    If coXlonDigit < 0 Then Exit Sub
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXobjFldrFile As Object
    On Error Resume Next
    Set myXobjFldrFile = myXobjFSO.GetFolder(myXstrPath)
    Set myXobjFldrFile = myXobjFSO.GetFile(myXstrPath)
    On Error GoTo 0
    If myXobjFldrFile Is Nothing Then Exit Sub
    myXdouOrgSize = myXobjFldrFile.Size
    Select Case coXstrUnit
        Case "Byte"
            myXdouSize = myXdouOrgSize
        Case "KByte"
            myXdouSize = myXdouOrgSize / 1024
        Case "MByte"
            myXdouSize = myXdouOrgSize / 1024 / 1024
        Case "GByte"
            myXdouSize = myXdouOrgSize / 1024 / 1024 / 1024
        Case "TByte"
            myXdouSize = myXdouOrgSize / 1024 / 1024 / 1024 / 1024
        Case Else
            myXdouSize = myXdouOrgSize
    End Select
    myXdouSize = Round(myXdouSize, coXlonDigit)
    Set myXobjFSO = Nothing: Set myXobjFldrFile = Nothing
End Sub

      <br></pre>
    </body>
  </html>
