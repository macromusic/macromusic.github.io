<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>


 '定型Ｆ_指定グラフの存在を確認して無ければアクティブグラフに設定する
Private Function PfncobjCheckChart(ByVal myXobjChrt As Object) As Object
    Set PfncobjCheckChart = Nothing
    On Error Resume Next
    If myXobjChrt Is Nothing Then
        Set PfncobjCheckChart = ActiveChart
    Else
        Set PfncobjCheckChart = myXobjChrt
    End If
    On Error GoTo 0
End Function

 '定型Ｆ_新規グラフオブジェクトを追加する
Private Function PfncobjAddChartObject(ByVal myXobjSheet As Object) As Object
    Set PfncobjAddChartObject = Nothing
  Dim myXobjShape As Object, myXlonChrtObjCnt As Long, myXobjChrtObjct As Object
    On Error GoTo ExitPath
    With myXobjSheet
        .Activate
        Set myXobjShape = .Shapes.AddChart
        myXlonChrtObjCnt = .ChartObjects.Count
        Set myXobjChrtObjct = .ChartObjects(myXlonChrtObjCnt)
    End With
    On Error GoTo 0
    Set PfncobjAddChartObject = myXobjChrtObjct
    Set myXobjShape = Nothing: Set myXobjChrtObjct = Nothing
ExitPath:
End Function

 '定型Ｆ_新規グラフオブジェクトを追加する2
Private Function PfncobjAddChartObject2(ByVal myXobjSheet As Object, _
            Optional ByVal coXlonChrtType As Long = xlXYScatterLines) As Object
'coXlonChrtType = xlXYScatterLines = 75 : 折れ線付き散布図(データ マーカーなし)
    Set PfncobjAddChartObject2 = Nothing
  Dim myXobjShape As Object, myXlonChrtObjCnt As Long, myXobjChrtObjct As Object
    On Error GoTo ExitPath
    With myXobjSheet
        .Activate
        Set myXobjShape = .Shapes.AddChart2(-1, coXlonChrtType)
        myXlonChrtObjCnt = .ChartObjects.Count
        Set myXobjChrtObjct = .ChartObjects(myXlonChrtObjCnt)
    End With
    On Error GoTo 0
    Set PfncobjAddChartObject2 = myXobjChrtObjct
    Set myXobjShape = Nothing: Set myXobjChrtObjct = Nothing
ExitPath:
End Function

 '定型Ｆ_指定グラフオブジェクトをShapeで取得する
Private Function PfncobjGetShapeOfChartObject(ByVal myXobjChrtObjct As Object) As Object
    Set PfncobjGetShapeOfChartObject = Nothing
  Dim myXstrChrtObjName As String, myXobjSheet As Object, myXobjShape As Object
    On Error GoTo ExitPath
    myXstrChrtObjName = myXobjChrtObjct.Name
    Set myXobjSheet = myXobjChrtObjct.Parent
    Set myXobjShape = myXobjSheet.Shapes(myXstrChrtObjName)
    On Error GoTo 0
    Set PfncobjGetShapeOfChartObject = myXobjShape
    Set myXobjSheet = Nothing: Set myXobjShape = Nothing
ExitPath:
End Function

 '定型Ｆ_指定グラフオブジェクトを複製する
Private Function PfncobjDuplicateChartObject(ByVal myXobjChrtObjct As Object) As Object
    Set PfncobjDuplicateChartObject = Nothing
  Dim myXstrChrtObjName As String, myXobjSheet As Object, _
        myXobjShape As Object, myXobjDupShp As Object, _
        myXlonChrtObjCnt As Long, myXobjDupChrtObjct As Object
    On Error GoTo ExitPath
    myXstrChrtObjName = myXobjChrtObjct.Name
    Set myXobjSheet = myXobjChrtObjct.Parent
    Set myXobjShape = myXobjSheet.Shapes(myXstrChrtObjName)
    Set myXobjDupShp = myXobjShape.Duplicate
    myXlonChrtObjCnt = myXobjSheet.ChartObjects.Count
    Set myXobjDupChrtObjct = myXobjSheet.ChartObjects(myXlonChrtObjCnt)
    On Error GoTo 0
    Set PfncobjDuplicateChartObject = myXobjDupChrtObjct
    Set myXobjSheet = Nothing: Set myXobjShape = Nothing
    Set myXobjDupShp = Nothing: Set myXobjDupChrtObjct = Nothing
ExitPath:
End Function

 '定型Ｆ_指定グラフオブジェクトを選択する
Private Function PfncbisSelectChartObject(ByVal myXobjChrtObjct As Object) As Boolean
    PfncbisSelectChartObject = False
    On Error GoTo ExitPath
    myXobjChrtObjct.Select
    On Error GoTo 0
    PfncbisSelectChartObject = True
ExitPath:
End Function

 '定型Ｆ_指定グラフのチャートエリアを選択する
Private Function PfncbisSelectChartArea(ByVal myXobjChrt As Object) As Boolean
    PfncbisSelectChartArea = False
    On Error GoTo ExitPath
    myXobjChrt.ChartArea.Select
    On Error GoTo 0
    PfncbisSelectChartArea = True
ExitPath:
End Function

 '定型Ｆ_指定グラフのグラフオブジェクトを取得する
Private Function PfncobjGetChartObjectOfChart(ByVal myXobjChrt As Object) As Object
    Set PfncobjGetChartObjectOfChart = Nothing
    If myXobjChrt Is Nothing Then Exit Function
    On Error Resume Next
    Set PfncobjGetChartObjectOfChart = myXobjChrt.Parent
    On Error GoTo 0
End Function

 '定型Ｆ_指定グラフがあるシートを取得する
Private Function PfncobjGetSheetOfChart(ByVal myXobjChrt As Object) As Object
    Set PfncobjGetSheetOfChart = Nothing
    If myXobjChrt Is Nothing Then Exit Function
    On Error Resume Next
    Set PfncobjGetSheetOfChart = myXobjChrt.Parent.Parent
    On Error GoTo 0
End Function

 '定型Ｆ_指定グラフがあるブックを取得する
Private Function PfncobjGetBookOfChart(ByVal myXobjChrt As Object) As Object
    Set PfncobjGetBookOfChart = Nothing
    If myXobjChrt Is Nothing Then Exit Function
    On Error Resume Next
    Set PfncobjGetBookOfChart = myXobjChrt.Parent.Parent.Parent
    On Error GoTo 0
End Function

 '定型Ｆ_指定名のグラフオブジェクトの存在を確認する
Private Function PfncbisCheckChartExist( _
            ByVal myXobjSheet As Object, ByVal myXstrChrtObjName As String) As Boolean
    PfncbisCheckChartExist = False
    If myXobjSheet Is Nothing Then Exit Function
    If myXstrChrtObjName = "" Then Exit Function
  Dim myXobjChrt As Object, myXbisExistFlag As Boolean
    For Each myXobjChrt In myXobjSheet.ChartObjects
        If myXobjChrt.Name = myXstrChrtObjName Then
            myXbisExistFlag = True
            Exit For
        End If
    Next
    PfncbisCheckChartExist = myXbisExistFlag
    Set myXobjChrt = Nothing
End Function

 '定型Ｆ_指定名のグラフオブジェクトを検索してグラフオブジェクトを取得する
Private Function PfncobjSearchChartObject( _
            ByVal myXobjSheet As Object, ByVal myXstrChrtObjName As String) As Object
    Set PfncobjSearchChartObject = Nothing
    If myXobjSheet Is Nothing Then Exit Function
    If myXstrChrtObjName = "" Then Exit Function
  Dim myXobjChrtObjct As Object
    For Each myXobjChrtObjct In myXobjSheet.ChartObjects
        If myXobjChrtObjct.Name = myXstrChrtObjName Then Exit For
    Next
    Set PfncobjSearchChartObject = myXobjChrtObjct
    Set myXobjChrtObjct = Nothing
End Function

 '定型Ｆ_指定シート上のグラフオブジェクト数を取得する
Private Function PfnclonCountChartObjects(ByVal myXobjSheet As Object) As Long
    PfnclonCountChartObjects = Empty
    If myXobjSheet Is Nothing Then Exit Function
  Dim myXlonChrtCnt As Long
    myXlonChrtCnt = myXobjSheet.ChartObjects.Count
    PfnclonCountChartObjects = myXlonChrtCnt
End Function

 '定型Ｆ_指定シート上のグラフ番号を指定してグラフオブジェクトを取得する
Private Function PfncobjGetChartObject( _
            ByVal myXobjSheet As Object, ByVal myXlonChrtObjNo As Long) As Object
    Set PfncobjGetChartObject = Nothing
  Dim myXobjChrtObjct As Object
    On Error GoTo ExitPath
    Set myXobjChrtObjct = myXobjSheet.ChartObjects(myXlonChrtObjNo)
    On Error GoTo 0
    Set PfncobjGetChartObject = myXobjChrtObjct
    Set myXobjChrtObjct = Nothing
ExitPath:
End Function

 '定型Ｆ_指定シート上の最後のグラフ番号のグラフオブジェクトを取得する
Private Function PfncobjGetLastChartObject(ByVal myXobjSheet As Object) As Object
    Set PfncobjGetLastChartObject = Nothing
  Dim myXlonChrtObjCnt As Long, myXobjChrtObjct As Object
    On Error GoTo ExitPath
    myXlonChrtObjCnt = myXobjSheet.ChartObjects.Count
    Set myXobjChrtObjct = myXobjSheet.ChartObjects(myXlonChrtObjCnt)
    On Error GoTo 0
    Set PfncobjGetLastChartObject = myXobjChrtObjct
    Set myXobjChrtObjct = Nothing
ExitPath:
End Function

 '定型Ｆ_グラフオブジェクト名を自動生成する
Private Function PfncstrChartObjectAutoNaming( _
        ByVal myXobjSheet As Object, ByVal myXstrChrtObjName As String, _
        Optional ByVal coXstrBaseName As String = "Graph") As String
    PfncstrChartObjectAutoNaming = Empty
    If myXobjSheet Is Nothing Then Exit Function
  Dim myXstrTmpName As String, n As Long: n = 0
    If myXstrChrtObjName = "" Then
        n = n + 1: myXstrTmpName = coXstrBaseName & n
    Else
        myXstrTmpName = myXstrChrtObjName
    End If
  Dim myXbisExistFlag As Boolean: myXbisExistFlag = False
  Dim myXstrChck As String
    On Error Resume Next
    With myXobjSheet
        If .ChartObjects.Count = 0 Then Exit Function
        myXstrChck = .ChartObjects(myXstrTmpName).Name
        If myXstrChck = myXstrTmpName Then myXbisExistFlag = True
        Do While myXbisExistFlag = True
            n = n + 1: myXstrTmpName = coXstrBaseName & n
            myXstrChck = .ChartObjects(myXstrTmpName).Name
        If myXstrChck <> myXstrTmpName Then myXbisExistFlag = False
        Loop
    End With
    On Error GoTo 0
    PfncstrChartObjectAutoNaming = myXstrTmpName
End Function

 '定型Ｆ_グラフオブジェクトの名前を取得する
Private Function PfncstrGetChartObjectName(ByVal myXobjChrtObjct As Object) As String
    PfncstrGetChartObjectName = Empty
    If myXobjChrtObjct Is Nothing Then Exit Function
    On Error Resume Next
    PfncstrGetChartObjectName = myXobjChrtObjct.Name
    On Error GoTo 0
End Function

 '定型Ｐ_グラフの名前を取得する
Private Sub PfncstrGetChartName( _
            myXstrChrtObjName As String, myXstrChrtName As String, _
            ByVal myXobjChrtObjct As Object)
    myXstrChrtObjName = Empty: myXstrChrtName = Empty
    If myXobjChrtObjct Is Nothing Then Exit Sub
    On Error Resume Next
    myXstrChrtObjName = myXobjChrtObjct.Name
    myXstrChrtName = myXobjChrtObjct.Chart.Name
    On Error GoTo 0
End Sub

 '定型Ｐ_グラフオブジェクトに名前を設定する
Private Sub PfixSetChartObjectName(myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, ByVal myXstrChrtObjName As String)
    myXbisExitFlag = False
    If myXstrChrtObjName = "" Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrtObjct.Name = myXstrChrtObjName
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフに名前を設定する
Private Sub PfixSetChartName(myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, ByVal myXstrChrtName As String)
    myXbisExitFlag = False
    If myXstrChrtName = "" Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrtObjct.Chart.Name = myXstrChrtName
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフの種類を設定する
Private Sub PfixSetChartType(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXlonChrtType As Long)
    myXbisExitFlag = False
    On Error GoTo ExitPath
    myXobjChrt.ChartType = myXlonChrtType
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフの指定データ系列だけグラフの種類を変更する
Private Sub PfixSetSeriesCollectionChartType( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSrsClctn As Object, ByVal myXlonChrtType As Long)
    myXbisExitFlag = False
    On Error GoTo ExitPath
    myXobjSrsClctn.ChartType = myXlonChrtType
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_シート上の全グラフオブジェクトを削除する
Private Sub PfixDeleteChartObjects(myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object)
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXlonChrtObjCnt As Long: myXlonChrtObjCnt = myXobjSheet.ChartObjects.Count
    On Error GoTo 0
    If myXlonChrtObjCnt <= 0 Then GoTo ExitPath
  Dim myXobjChrtObjct As Object
    For Each myXobjChrtObjct In myXobjSheet.ChartObjects
        myXobjChrtObjct.Delete
    Next myXobjChrtObjct
    Set myXobjChrtObjct = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定名のグラフオブジェクトを削除する
Private Sub PfixDeleteDesignateChartObject(myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object, ByVal myXstrChrtObjName As String)
    myXbisExitFlag = False
    If myXstrChrtObjName = "" Then GoTo ExitPath
    On Error GoTo ExitPath
  Dim myXlonChrtObjCnt As Long: myXlonChrtObjCnt = myXobjSheet.ChartObjects.Count
    On Error GoTo 0
    If myXlonChrtObjCnt <= 0 Then GoTo ExitPath
  Dim myXobjChrtObjct As Object
    For Each myXobjChrtObjct In myXobjSheet.ChartObjects
        If myXobjChrtObjct.Name = myXstrChrtObjName Then
            myXobjChrtObjct.Delete: Exit For
        End If
    Next myXobjChrtObjct
    Set myXobjChrtObjct = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定名のグラフオブジェクト以外の全グラフオブジェクトを削除する
Private Sub PfixDeleteNoDesignateChartObjects(myXbisExitFlag As Boolean, _
            ByVal myXobjSheet As Object, ByVal myXstrChrtObjName As String)
    myXbisExitFlag = False
    If myXstrChrtObjName = "" Then GoTo ExitPath
    On Error GoTo ExitPath
  Dim myXlonChrtObjCnt As Long: myXlonChrtObjCnt = myXobjSheet.ChartObjects.Count
    On Error GoTo 0
    If myXlonChrtObjCnt <= 0 Then GoTo ExitPath
  Dim myXobjChrtObjct As Object
    For Each myXobjChrtObjct In myXobjSheet.ChartObjects
        If myXobjChrtObjct.Name <> myXstrChrtObjName Then
            myXobjChrtObjct.Delete
        End If
    Next myXobjChrtObjct
    Set myXobjChrtObjct = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｆ_グラフオブジェクトをコピーしてシートに図として形式を指定して貼り付ける
Private Function PfncobjCopyPasteSpecialChartAsPicture( _
            ByVal myXobjChrtObjct As Object, _
            Optional ByVal coXlonPstFrmt As Long = 4) As Object
'coXlonPstFrmt = 1 : "図 (PNG)"
'coXlonPstFrmt = 2 : "図 (JPEG)"
'coXlonPstFrmt = 3 : "図 (GIF)"
'coXlonPstFrmt = 4 : "図 (拡張メタファイル)"
'coXlonPstFrmt = 5 : "ビットマップ"
'coXlonPstFrmt = 6 : "Microsoft Office 描画オブジェクト"
'coXlonPstFrmt = ? : 形式変更しない
    Set PfncobjCopyPasteSpecialChartAsPicture = Nothing
    If myXobjChrtObjct Is Nothing Then Exit Function
    On Error GoTo ExitPath
  Dim myXobjSheet As Object: Set myXobjSheet = myXobjChrtObjct.Parent
    myXobjSheet.Activate
  Dim myXstrPstFrmt As String
    If coXlonPstFrmt = 1 Then
        myXstrPstFrmt = "図 (PNG)"
    ElseIf coXlonPstFrmt = 2 Then
        myXstrPstFrmt = "図 (JPEG)"
    ElseIf coXlonPstFrmt = 3 Then
        myXstrPstFrmt = "図 (GIF)"
    ElseIf coXlonPstFrmt = 4 Then
        myXstrPstFrmt = "図 (拡張メタファイル)"
    ElseIf coXlonPstFrmt = 5 Then
        myXstrPstFrmt = "ビットマップ"
    ElseIf coXlonPstFrmt = 6 Then
        myXstrPstFrmt = "Microsoft Office 描画オブジェクト"
    Else
        Exit Function
    End If
    myXobjChrtObjct.Chart.ChartArea.Copy
  Dim myXobjTopLftCell As Object
    Set myXobjTopLftCell = myXobjChrtObjct.TopLeftCell
    myXobjTopLftCell.Select
    Call myXobjSheet.PasteSpecial(Format:=myXstrPstFrmt)
  Dim myXlonPicCnt As Long, myXobjPic As Object
    myXlonPicCnt = myXobjSheet.Pictures.Count
    Set myXobjPic = myXobjSheet.Pictures(myXlonPicCnt)
    On Error GoTo 0
    Set PfncobjCopyPasteSpecialChartAsPicture = myXobjPic
    Set myXobjTopLftCell = Nothing: Set myXobjPic = Nothing
ExitPath:
End Function

 '定型Ｐ_グラフオブジェクトの縦横比の固定を設定する
Private Sub PfixSetChartObjectLockAspectRatio(myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            Optional ByVal coXbisUnlckAspctRto As Boolean = True)
'coXbisUnlckAspctRto = False : 縦横比を固定する
'coXbisUnlckAspctRto = True  : 縦横比を固定しない
    myXbisExitFlag = False
    If myXobjChrtObjct Is Nothing Then GoTo ExitPath
  Dim myXlonLckAspctRto As Long
    If coXbisUnlckAspctRto = True Then
        myXlonLckAspctRto = msoFalse
    Else
        myXlonLckAspctRto = msoTrue
    End If
    On Error GoTo ExitPath
    myXobjChrtObjct.ShapeRange.LockAspectRatio = myXlonLckAspctRto
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの位置サイズ変更のプロパティを設定する
Private Sub PfixSetChartObjectPlacement(myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            Optional ByVal coXlonPlcmnt As Long = xlMove)
'coXlonPlcmnt = xlMoveAndSize = 1  : セルに合わせて移動やサイズ変更をする
'coXlonPlcmnt = xlMove = 2         : セルに合わせて移動するがサイズ変更はしない
'coXlonPlcmnt = xlFreeFloating = 3 : セルに合わせて移動やサイズ変更をしない
    myXbisExitFlag = False
    If myXobjChrtObjct Is Nothing Then GoTo ExitPath
    On Error GoTo ExitPath
    myXobjChrtObjct.Placement = coXlonPlcmnt
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの位置とサイズを取得する
Private Sub PfixGetChartObjectPlaceSize( _
            myXdouChrtObjTop As Double, myXdouChrtObjLft As Double, _
            myXdouChrtObjBttm As Double, myXdouChrtObjRght As Double, _
            myXdouChrtObjHght As Double, myXdouChrtObjWdth As Double, _
            ByVal myXobjChrtObjct As Object)
    myXdouChrtObjTop = Empty: myXdouChrtObjLft = Empty
    myXdouChrtObjBttm = Empty: myXdouChrtObjRght = Empty
    myXdouChrtObjHght = Empty: myXdouChrtObjWdth = Empty
    On Error GoTo ExitPath
    With myXobjChrtObjct
        myXdouChrtObjTop = .Top
        myXdouChrtObjLft = .Left
        myXdouChrtObjHght = .Height
        myXdouChrtObjWdth = .Width
    End With
    On Error GoTo 0
    myXdouChrtObjBttm = myXdouChrtObjTop + myXdouChrtObjHght
    myXdouChrtObjRght = myXdouChrtObjLft + myXdouChrtObjWdth
ExitPath:
End Sub

 '定型Ｐ_グラフオブジェクトの位置とサイズをセル範囲で取得する
Private Sub PfixGetChartObjectPlaceSizeRange( _
            myXlonChrtObjTopRow As Long, myXlonChrtObjLftCol As Long, _
            myXlonChrtObjBttmRow As Long, myXlonChrtObjRghtCol As Long, _
            myXlonChrtObjHghtRows As Long, myXlonChrtObjWdthCols As Long, _
            ByVal myXobjChrtObjct As Object)
    myXlonChrtObjTopRow = Empty: myXlonChrtObjLftCol = Empty
    myXlonChrtObjBttmRow = Empty: myXlonChrtObjRghtCol = Empty
    myXlonChrtObjHghtRows = Empty: myXlonChrtObjWdthCols = Empty
    On Error GoTo ExitPath
    With myXobjChrtObjct
        myXlonChrtObjTopRow = .TopLeftCell.Row
        myXlonChrtObjLftCol = .TopLeftCell.Column
        myXlonChrtObjBttmRow = .BottomRightCell.Row
        myXlonChrtObjRghtCol = .BottomRightCell.Column
    End With
    On Error GoTo 0
    myXlonChrtObjHghtRows = myXlonChrtObjBttmRow - myXlonChrtObjTopRow + 1
    myXlonChrtObjWdthCols = myXlonChrtObjRghtCol - myXlonChrtObjLftCol + 1
ExitPath:
End Sub

 '定型Ｐ_グラフオブジェクトの位置を設定する
Private Sub PfixSetChartObjectPlace( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouChrtObjTop As Double, ByVal myXdouChrtObjLft As Double)
'myXdouChrtObjTop : x [point]
'myXdouChrtObjLft : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpTop As Double: myXdouTmpTop = myXobjChrtObjct.Top
    On Error GoTo 0
    If myXdouChrtObjTop < 0 Or myXdouChrtObjLft < 0 Then GoTo ExitPath
    myXobjChrtObjct.Top = myXdouChrtObjTop
    myXobjChrtObjct.Left = myXdouChrtObjLft
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの縦横比を固定せずにサイズを設定する
Private Sub PfixSetChartObjectSizeWithoutLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouChrtObjHght As Double, ByVal myXdouChrtObjWdth As Double)
'myXdouChrtObjHght : x [point]
'myXdouChrtObjWdth : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpHght As Double: myXdouTmpHght = myXobjChrtObjct.Height
    On Error GoTo 0
    If myXdouChrtObjHght < 0 Or myXdouChrtObjWdth < 0 Then GoTo ExitPath
    myXobjChrtObjct.ShapeRange.LockAspectRatio = msoFalse
    myXobjChrtObjct.Height = myXdouChrtObjHght
    myXobjChrtObjct.Width = myXdouChrtObjWdth
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの縦横比を固定してサイズを設定する
Private Sub PfixSetChartObjectSizeWithLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouChrtObjHght As Double, ByVal myXdouChrtObjWdth As Double)
'myXdouChrtObjHght : x [point]
'myXdouChrtObjWdth : x [point]
    myXbisExitFlag = False
  Dim myXdouOrgHght As Double, myXdouOrgWdth As Double
    On Error GoTo ExitPath
    myXdouOrgHght = myXobjChrtObjct.Height
    myXdouOrgWdth = myXobjChrtObjct.Width
    On Error GoTo 0
    If myXdouChrtObjHght < 0 Or myXdouChrtObjWdth < 0 Then GoTo ExitPath
  Dim myXdouHghtRatio As Double, myXdouWdthRatio As Double, myXdouRatio As Double
    myXdouHghtRatio = myXdouChrtObjHght / myXdouOrgHght
    myXdouWdthRatio = myXdouChrtObjWdth / myXdouOrgWdth
    myXobjChrtObjct.ShapeRange.LockAspectRatio = msoTrue
    If myXdouHghtRatio <= myXdouWdthRatio Then
        myXdouRatio = myXdouHghtRatio
    Else
        myXdouRatio = myXdouWdthRatio
    End If
    myXobjChrtObjct.Height = myXdouOrgHght * myXdouRatio
'    myXobjChrtObjct.Width = myXdouOrgWdth * myXdouRatio
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの縦横比を固定せずに倍率でサイズを設定する
Private Sub PfixSetChartObjectScaleWithoutLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double, _
            Optional ByVal coXlonScaleFrom As Long = msoScaleFromTopLeft)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
'coXlonScaleFrom = msoScaleFromTopLeft     : 0 : 図形の左上隅の位置が保持
'coXlonScaleFrom = msoScaleFromMiddle      : 1 : 図形の中心の位置が保持
'coXlonScaleFrom = msoScaleFromBottomRight : 2 : 図形の右下隅の位置が保持
    myXbisExitFlag = False
  Dim myXdouChrtObjType As Double
    On Error GoTo ExitPath
    myXdouChrtObjType = myXobjChrtObjct.ShapeRange.Type
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    myXdouSclHght = myXdouSclHght / 100
    myXdouSclWdth = myXdouSclWdth / 100
    With myXobjChrtObjct.ShapeRange
        .LockAspectRatio = msoFalse
        Call .ScaleHeight(myXdouSclHght, msoFalse, coXlonScaleFrom)
        Call .ScaleWidth(myXdouSclWdth, msoFalse, coXlonScaleFrom)
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの縦横比を固定して倍率でサイズを設定する
Private Sub PfixSetChartObjectScaleWithLockAspectRatio( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double, _
            Optional ByVal coXlonScaleFrom As Long = msoScaleFromTopLeft)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
'coXlonScaleFrom = msoScaleFromTopLeft     : 0 : 図形の左上隅の位置が保持
'coXlonScaleFrom = msoScaleFromMiddle      : 1 : 図形の中心の位置が保持
'coXlonScaleFrom = msoScaleFromBottomRight : 2 : 図形の右下隅の位置が保持
    myXbisExitFlag = False
  Dim myXdouChrtObjType As Double
    On Error GoTo ExitPath
    myXdouChrtObjType = myXobjChrtObjct.ShapeRange.Type
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
  Dim myXdouScale As Double
    If myXdouSclHght <= myXdouSclWdth Then
        myXdouScale = myXdouSclHght / 100
    Else
        myXdouScale = myXdouSclWdth / 100
    End If
    With myXobjChrtObjct.ShapeRange
        .LockAspectRatio = msoTrue
        Call .ScaleHeight(myXdouScale, msoFalse, coXlonScaleFrom)
'        Call .ScaleWidth(myXdouScale, msoFalse, coXlonScaleFrom)
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトの位置をエクセルシートの行列位置で設定する
Private Sub PincSetChartObjectPlaceInSheetRange( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, ByVal myXobjSheet As Object, _
            ByVal myXlonTopRow As Long, ByVal myXlonLftCol As Long, _
            ByVal myXdouTopOfst As Double, ByVal myXdouLftOfst As Double)
'Includes PfixGetRangePlaceSize
'Includes PfixSetChartObjectPlace
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
'//エクセルシートの指定セル範囲の位置情報とサイズ情報を取得
  Dim myXdouRngTop As Double, myXdouRngLft As Double, _
        myXdouRngHght As Double, myXdouRngWdth As Double
    Call PfixGetRangePlaceSize( _
            myXdouRngTop, myXdouRngLft, myXdouRngHght, myXdouRngWdth, _
            myXobjSheet, myXlonTopRow, myXlonLftCol, myXlonTopRow, myXlonLftCol)
    If myXdouRngHght <= 0 Or myXdouRngWdth <= 0 Then GoTo ExitPath
'//グラフオブジェクトの位置を設定
  Dim myXdouChrtObjTop As Double, myXdouChrtObjLft As Double
    myXdouChrtObjTop = myXdouRngTop + myXdouTopOfst
    myXdouChrtObjLft = myXdouRngLft + myXdouLftOfst
    Call PfixSetChartObjectPlace( _
            myXbisExitFlag, myXobjChrtObjct, myXdouChrtObjTop, myXdouChrtObjLft)
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのサイズをエクセルシートの行列位置で設定する
Private Sub PincSetChartObjectSizeInSheetRange( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, ByVal myXobjSheet As Object, _
            ByVal myXlonTopRow As Long, ByVal myXlonLftCol As Long, _
            ByVal myXlonBttmRow As Long, ByVal myXlonRghtCol As Long, _
            ByVal myXdouTopOfst As Double, ByVal myXdouLftOfst As Double, _
            ByVal myXdouBttmOfst As Double, ByVal myXdouRghtOfst As Double, _
            Optional ByVal coXbisChrtObjWthOtAspctRto As Boolean = False)
'Includes PfixGetRangePlaceSize
'Includes PfixSetChartObjectSizeWithLockAspectRatio
'Includes PfixSetChartObjectSizeWithoutLockAspectRatio
'myXdouTopOfst : x [point]
'myXdouLftOfst : x [point]
'myXdouBttmOfst : x [point]
'myXdouRghtOfst : x [point]
'coXbisChrtObjWthOtAspctRto = False : 図形の縦横比を固定してサイズを設定
'coXbisChrtObjWthOtAspctRto = True  : 図形の縦横比を固定せずにサイズを設定
'//エクセルシートの指定セル範囲の位置情報とサイズ情報を取得
  Dim myXdouRngTop As Double, myXdouRngLft As Double, _
        myXdouRngHght As Double, myXdouRngWdth As Double
    Call PfixGetRangePlaceSize( _
            myXdouRngTop, myXdouRngLft, myXdouRngHght, myXdouRngWdth, _
            myXobjSheet, myXlonTopRow, myXlonLftCol, myXlonBttmRow, myXlonRghtCol)
    If myXdouRngHght <= 0 Or myXdouRngWdth <= 0 Then GoTo ExitPath
  Dim myXdouChrtObjHght As Double, myXdouChrtObjWdth As Double
    myXdouChrtObjHght = myXdouRngHght - myXdouTopOfst + myXdouBttmOfst
    myXdouChrtObjWdth = myXdouRngWdth - myXdouLftOfst + myXdouRghtOfst
    If myXdouChrtObjHght < 0 Or myXdouChrtObjWdth < 0 Then GoTo ExitPath
    If coXbisChrtObjWthOtAspctRto = False Then
    '//図形の縦横比を固定してサイズを設定
        Call PfixSetChartObjectSizeWithLockAspectRatio( _
                myXbisExitFlag, _
                myXobjChrtObjct, myXdouChrtObjHght, myXdouChrtObjWdth)
    Else
    '//図形の縦横比を固定せずにサイズを設定
        Call PfixSetChartObjectSizeWithoutLockAspectRatio( _
                myXbisExitFlag, _
                myXobjChrtObjct, myXdouChrtObjHght, myXdouChrtObjWdth)
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのチャートエリアの位置とサイズを取得する
Private Sub PfixGetChartAreaPlaceSize( _
            myXdouChrtAreaTop As Double, myXdouChrtAreaLft As Double, _
            myXdouChrtAreaBttm As Double, myXdouChrtAreaRght As Double, _
            myXdouChrtAreaHght As Double, myXdouChrtAreaWdth As Double, _
            ByVal myXobjChrtObjct As Object)
    myXdouChrtAreaTop = Empty: myXdouChrtAreaLft = Empty
    myXdouChrtAreaBttm = Empty: myXdouChrtAreaRght = Empty
    myXdouChrtAreaHght = Empty: myXdouChrtAreaWdth = Empty
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.ChartArea
        myXdouChrtAreaTop = .Top
        myXdouChrtAreaLft = .Left
        myXdouChrtAreaHght = .Height
        myXdouChrtAreaWdth = .Width
    End With
    On Error GoTo 0
    myXdouChrtAreaBttm = myXdouChrtAreaTop + myXdouChrtAreaHght
    myXdouChrtAreaRght = myXdouChrtAreaLft + myXdouChrtAreaWdth
ExitPath:
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアの位置とサイズを取得する
Private Sub PfixGetPlotAreaPlaceSize( _
            myXdouPltAreaTop As Double, myXdouPltAreaLft As Double, _
            myXdouPltAreaBttm As Double, myXdouPltAreaRght As Double, _
            myXdouPltAreaHght As Double, myXdouPltAreaWdth As Double, _
            ByVal myXobjChrtObjct As Object)
    myXdouPltAreaTop = Empty: myXdouPltAreaLft = Empty
    myXdouPltAreaBttm = Empty: myXdouPltAreaRght = Empty
    myXdouPltAreaHght = Empty: myXdouPltAreaWdth = Empty
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        myXdouPltAreaTop = .Top
        myXdouPltAreaLft = .Left
        myXdouPltAreaHght = .Height
        myXdouPltAreaWdth = .Width
    End With
    On Error GoTo 0
    myXdouPltAreaBttm = myXdouPltAreaTop + myXdouPltAreaHght
    myXdouPltAreaRght = myXdouPltAreaLft + myXdouPltAreaWdth
ExitPath:
End Sub

 '定型Ｐ_グラフオブジェクトのチャートエリアの位置を設定する
Private Sub PfixSetChartAreaPlace( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouChrtAreaTop As Double, ByVal myXdouChrtAreaLft As Double)
'myXdouChrtAreaTop : x [point]
'myXdouChrtAreaLft : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpTop As Double: myXdouTmpTop = myXobjChrtObjct.Chart.ChartArea.Top
    On Error GoTo 0
    If myXdouChrtAreaTop < 0 Or myXdouChrtAreaLft < 0 Then GoTo ExitPath
    With myXobjChrtObjct.Chart.ChartArea
        .Top = myXdouChrtAreaTop
        .Left = myXdouChrtAreaLft
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのチャートエリアのサイズを設定する
Private Sub PfixSetChartAreaSize( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouChrtAreaHght As Double, ByVal myXdouChrtAreaWdth As Double)
'myXdouChrtAreaHght : x [point]
'myXdouChrtAreaWdth : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpHght As Double: myXdouTmpHght = myXobjChrtObjct.Chart.ChartArea.Height
    On Error GoTo 0
    If myXdouChrtAreaHght < 0 Or myXdouChrtAreaWdth < 0 Then GoTo ExitPath
    With myXobjChrtObjct.Chart.ChartArea
        .Height = myXdouChrtAreaHght
        .Width = myXdouChrtAreaWdth
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのチャートエリアのサイズを現サイズに対する倍率指定で設定する
Private Sub PfixSetChartAreaScaleRelative( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
    myXbisExitFlag = False
  Dim myXdouTmpHght As Double, myXdouTmpWdth As Double
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.ChartArea
        myXdouTmpHght = .Height
        myXdouTmpWdth = .Width
    End With
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    myXdouSclHght = myXdouSclHght / 100
    myXdouSclWdth = myXdouSclWdth / 100
    With myXobjChrtObjct.Chart.ChartArea
        .Height = myXdouTmpHght * myXdouSclHght
        .Width = myXdouTmpWdth * myXdouSclWdth
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアの位置を設定する
Private Sub PfixSetPlotAreaPlace( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouPltAreaTop As Double, ByVal myXdouPltAreaLft As Double)
'myXdouPltAreaTop : x [point]
'myXdouPltAreaLft : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpTop As Double: myXdouTmpTop = myXobjChrtObjct.Chart.PlotArea.Top
    On Error GoTo 0
    If myXdouPltAreaTop < 0 Or myXdouPltAreaLft < 0 Then GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        .Top = myXdouPltAreaTop
        .Left = myXdouPltAreaLft
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアのサイズを設定する
Private Sub PfixSetPlotAreaSize( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouPltAreaHght As Double, ByVal myXdouPltAreaWdth As Double)
'myXdouPltAreaHght : x [point]
'myXdouPltAreaWdth : x [point]
    myXbisExitFlag = False
    On Error GoTo ExitPath
  Dim myXdouTmpHght As Double: myXdouTmpHght = myXobjChrtObjct.Chart.PlotArea.Height
    On Error GoTo 0
    If myXdouPltAreaHght < 0 Or myXdouPltAreaWdth < 0 Then GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        .Height = myXdouPltAreaHght
        .Width = myXdouPltAreaWdth
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフオブジェクトのプロットエリアのサイズを現サイズに対する倍率指定で設定する
Private Sub PfixSetPlotAreaScaleRelative( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtObjct As Object, _
            ByVal myXdouSclHght As Double, ByVal myXdouSclWdth As Double)
'myXdouSclHght : x [%]
'myXdouSclWdth : x [%]
    myXbisExitFlag = False
  Dim myXdouTmpHght As Double, myXdouTmpWdth As Double
    On Error GoTo ExitPath
    With myXobjChrtObjct.Chart.PlotArea
        myXdouTmpHght = .Height
        myXdouTmpWdth = .Width
    End With
    On Error GoTo 0
    If myXdouSclHght < 0 Or myXdouSclWdth < 0 Then GoTo ExitPath
    myXdouSclHght = myXdouSclHght / 100
    myXdouSclWdth = myXdouSclWdth / 100
    With myXobjChrtObjct.Chart.PlotArea
        .Height = myXdouTmpHght * myXdouSclHght
        .Width = myXdouTmpWdth * myXdouSclWdth
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｆ_指定グラフの系列番号を指定してデータ系列を取得する
Private Function PfncobjGetChartSeriesCollection( _
            ByVal myXobjChrt As Object, ByVal myXlonSrsClctnNo As Long) As Object
    Set PfncobjGetChartSeriesCollection = Nothing
  Dim myXobjSrsClctn As Object
    On Error GoTo ExitPath
    Set myXobjSrsClctn = myXobjChrt.SeriesCollection(myXlonSrsClctnNo)
    On Error GoTo 0
    Set PfncobjGetChartSeriesCollection = myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
ExitPath:
End Function

 '定型Ｆ_指定グラフの最後の系列番号のデータ系列を取得する
Private Function PfncobjGetChartLastSeriesCollection(ByVal myXobjChrt As Object) As Object
    Set PfncobjGetChartLastSeriesCollection = Nothing
  Dim myXlonSrsClctnCnt As Long, myXobjSrsClctn As Object
    On Error GoTo ExitPath
    myXlonSrsClctnCnt = myXobjChrt.SeriesCollection.Count
    Set myXobjSrsClctn = myXobjChrt.SeriesCollection(myXlonSrsClctnCnt)
    On Error GoTo 0
    Set PfncobjGetChartLastSeriesCollection = myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
ExitPath:
End Function

 '定型Ｐ_グラフの指定系列の参照範囲を取得する
Private Sub PfixGetSeriesCollectionFormula( _
            myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String, _
            ByVal myXobjSrsClctn As Object)
    myXstrSrsFrmlA1 = Empty: myXstrSrsFrmlR1C1 = Empty
    On Error GoTo ExitPath
    myXstrSrsFrmlA1 = myXobjSrsClctn.Formula
    myXstrSrsFrmlR1C1 = myXobjSrsClctn.FormulaR1C1
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｆ_グラフの指定データ系列の系列番号を取得する
Private Function PfnclonGetChartSeriesCollectionOrder( _
            ByVal myXobjSrsClctn As Object) As Long
'objSrsClctn.Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
  Const coXstrPreChr As String = "("
  Const coXstrPstChr As String = ")"
    PfnclonGetChartSeriesCollectionOrder = Empty
    If myXobjSrsClctn Is Nothing Then Exit Function
  Dim myZstrSrsFrml As String, myZvarSplt As Variant, myXlonSpltCnt As Long
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    On Error GoTo ExitPath
    myZstrSrsFrml = myXobjSrsClctn.Formula
    On Error GoTo 0
    myXlonBgn = InStr(myZstrSrsFrml, coXstrPreChr) + 1
    myXlonEnd = InStrRev(myZstrSrsFrml, coXstrPstChr) - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    myXstrSndwchd = Mid(myZstrSrsFrml, myXlonBgn, myXlonLen)
    myZvarSplt = Split(myXstrSndwchd, ",")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt <= 0 Then Exit Function
  Dim myXvarSCNo As Variant, myXlonSrsClctnNo As Long
    myXvarSCNo = myZvarSplt(UBound(myZvarSplt))
    If IsNumeric(myXvarSCNo) = True Then myXlonSrsClctnNo = CLng(myXvarSCNo)
    PfnclonGetChartSeriesCollectionOrder = myXlonSrsClctnNo
ExitPath:
End Function

 '定型Ｐ_グラフの指定データ系列の設定軸とプロット番号を取得する
Private Sub PfixGetChartSeriesCollectionOrderInformation( _
            myXlonAxsGrp As Long, myXlonPltOrdr As Long, _
            ByVal myXobjSrsClctn As Object)
    myXlonAxsGrp = Empty: myXlonPltOrdr = Empty
    If myXobjSrsClctn Is Nothing Then Exit Sub
'//設定軸を取得
    myXlonAxsGrp = myXobjSrsClctn.AxisGroup
'//プロット番号を取得
    myXlonPltOrdr = myXobjSrsClctn.PlotOrder
End Sub

 '定型Ｐ_散布図グラフのデータ系列の参照式からセル範囲アドレスを取得する
Private Sub PfixGetDataRangeAddressInXYScatter( _
            myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
            myXlonSrsClctnNo As Long, _
            ByVal myXstrSrsFrml As String)
'objSrsClctn.Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    myXstrNameAdrs = Empty: myXstrXvalAdrs = Empty: myXstrYvalAdrs = Empty
    If myXstrSrsFrml = "" Then Exit Sub
'//参照式からアドレス部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrSrsFrml, "(") + 1
    myXlonEnd = InStrRev(myXstrSrsFrml, ")") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrSndwchd = Mid(myXstrSrsFrml, myXlonBgn, myXlonLen)
'//項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号を取得
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long
    myZvarSplt = Split(myXstrSndwchd, ",")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
  Dim myXstrSCNo As String
    If myXlonSpltCnt = 4 Then
        myXstrNameAdrs = myZvarSplt(0)
        myXstrXvalAdrs = myZvarSplt(1)
        myXstrYvalAdrs = myZvarSplt(2)
        myXstrSCNo = myZvarSplt(3)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrSCNo) = True Then myXlonSrsClctnNo = CLng(myXstrSCNo)
    myZvarSplt = Empty
End Sub

 '定型Ｐ_セル範囲アドレスからパス[ブック]シート!セル範囲情報を分離する
Private Sub PfixGetPathBookSheetRangeAddress( _
            myXstrPathBkSht As String, myXstrRngAdrs As String, _
            myXstrDirPath As String, myXstrBookName As String, myXstrShtName As String, _
            ByVal myXstrRngAdrsWEx As String)
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    myXstrPathBkSht = Empty: myXstrRngAdrs = Empty
    myXstrDirPath = Empty: myXstrBookName = Empty: myXstrShtName = Empty
    If myXstrRngAdrsWEx = "" Then Exit Sub
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(0)
        myXstrRngAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//パス[ブック]シート部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrTmp, "'") + 1
    myXlonEnd = InStrRev(myXstrTmp, "'") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then
        myXstrPathBkSht = myXstrTmp
    Else
        myXstrPathBkSht = Mid(myXstrTmp, myXlonBgn, myXlonLen)
    End If
'//シート部分を取得
    myZvarSplt = Split(myXstrPathBkSht, "]")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 1 Then
        myXstrShtName = myZvarSplt(0)
    ElseIf myXlonSpltCnt = 2 Then
        myXstrShtName = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//ブック部分を取得
    myXlonBgn = InStr(myXstrPathBkSht, "[") + 1
    myXlonEnd = InStrRev(myXstrPathBkSht, "]") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrBookName = Mid(myXstrPathBkSht, myXlonBgn, myXlonLen)
'//パス部分を取得
    myZvarSplt = Split(myXstrPathBkSht, "[")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrDirPath = myZvarSplt(0)
        If myXstrDirPath <> "" Then _
            myXstrDirPath = Left(myXstrDirPath, Len(myXstrDirPath) - 1)
    Else
        Exit Sub
    End If
End Sub

 '定型Ｐ_セル範囲アドレスから行列情報を取得する
Private Sub PfixGetRowColumnInRangeAddress( _
            myXlonBgnRow As Long, myXlonBgnCol As Long, _
            myXlonEndRow As Long, myXlonEndCol As Long, _
            ByVal myXstrRngAdrsWEx As String)
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    myXlonBgnRow = Empty: myXlonBgnCol = Empty: myXlonEndRow = Empty: myXlonEndCol = Empty
    If myXstrRngAdrsWEx = "" Then Exit Sub
  Dim myXstrRngAdrs As String, myXstrBgnAdrs As String, myXstrEndAdrs As String
  Dim myXstrBgnRow As String, myXstrBgnCol As String, _
        myXstrEndRow As String, myXstrEndCol As String
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(0)
        myXstrRngAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//セル範囲をR1C1形式に変換
    On Error Resume Next
    myXstrRngAdrs = Application.ConvertFormula(Formula:=myXstrRngAdrs, _
                        FromReferenceStyle:=xlA1, ToReferenceStyle:=xlR1C1, ToAbsolute:=True)
    On Error GoTo 0
    If myXstrRngAdrs = "" Then Exit Sub
'//セル範囲の前半部分と後半部分を分離
    myZvarSplt = Split(myXstrRngAdrs, ":")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 1 Then
        myXstrBgnAdrs = myZvarSplt(0)
    ElseIf myXlonSpltCnt = 2 Then
        myXstrBgnAdrs = myZvarSplt(0)
        myXstrEndAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//最初の行と最初の列を取得
    myZvarSplt = Split(myXstrBgnAdrs, "R")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(1)
    Else
        Exit Sub
    End If
    myZvarSplt = Split(myXstrTmp, "C")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrBgnRow = myZvarSplt(0)
        myXstrBgnCol = myZvarSplt(1)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrBgnRow) = True Then myXlonBgnRow = CLng(myXstrBgnRow)
    If IsNumeric(myXstrBgnCol) = True Then myXlonBgnCol = CLng(myXstrBgnCol)
'//最後の行と最後の列を取得
    myZvarSplt = Split(myXstrEndAdrs, "R")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(1)
    Else
        Exit Sub
    End If
    myZvarSplt = Split(myXstrTmp, "C")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrEndRow = myZvarSplt(0)
        myXstrEndCol = myZvarSplt(1)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrEndRow) = True Then myXlonEndRow = CLng(myXstrEndRow)
    If IsNumeric(myXstrEndCol) = True Then myXlonEndCol = CLng(myXstrEndCol)
End Sub

 '定型Ｆ_セル範囲アドレスのパス[ブック]シート情報を指定シート名に置換する
Private Function PfncstrReplacePathBookSheetToSheet( _
            ByVal myXstrRngAdrsWEx As String, ByVal myXstrShtName As String) As String
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    PfncstrReplacePathBookSheetToSheet = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
    If myXstrShtName = "" Then Exit Function
  Dim myXstrPathBkSht As String, myXstrNewAdrs As String
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrPathBkSht = myZvarSplt(0)
    Else
        Exit Function
    End If
'//パス[ブック]シート部分を置換
    myXstrNewAdrs = Replace(myXstrRngAdrsWEx, myXstrPathBkSht, myXstrShtName)
    PfncstrReplacePathBookSheetToSheet = myXstrNewAdrs
End Function

 '定型Ｆ_グラフ系列用のセル範囲アドレスを設定する
Private Function PfncstrSetRangeAddress( _
            ByVal myXlonBgnRow As Long, ByVal myXlonBgnCol As Long, _
            ByVal myXlonEndRow As Long, ByVal myXlonEndCol As Long) As String
    PfncstrSetRangeAddress = Empty
'//セル範囲の表記→"R1C1"
  Dim myXstrRng1 As String, myXstrRng2 As String, myXstrRngAdrs As String
    If myXlonBgnRow >= 1 And myXlonBgnCol >= 1 Then
        myXstrRng1 = "R" & myXlonBgnRow & "C" & myXlonBgnCol
    Else
        Exit Function
    End If
'//セル範囲の表記→"R2C2"
    If myXlonEndRow >= 1 And myXlonEndCol >= 1 Then
        myXstrRng2 = "R" & myXlonEndRow & "C" & myXlonEndCol
    End If
'//セル範囲の表記→"R1C1:R2C2"
    myXstrRngAdrs = myXstrRng1 & ":" & myXstrRng2
'//セル範囲をA1形式に変換
    On Error Resume Next
    If Application.ReferenceStyle = xlA1 Then _
        myXstrRngAdrs = Application.ConvertFormula(Formula:=myXstrRngAdrs, _
                        FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    On Error GoTo 0
    PfncstrSetRangeAddress = myXstrRngAdrs
End Function

 '定型Ｆ_グラフ系列用の外部参照を含めたセル範囲アドレスを設定する
Private Function PfncstrSetRangeAddressWithExternal( _
            ByVal myXlonBgnRow As Long, ByVal myXlonBgnCol As Long, _
            ByVal myXlonEndRow As Long, ByVal myXlonEndCol As Long, _
            ByVal myXstrShtName As String, _
            ByVal myXstrBkName As String, ByVal myXstrDirPath As String) As String
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    PfncstrSetRangeAddressWithExternal = Empty
'//セル範囲の表記→"R1C1"
  Dim myXstrRng1 As String, myXstrRng2 As String, myXstrRngAdrs As String
    If myXlonBgnRow >= 1 And myXlonBgnCol >= 1 Then
        myXstrRng1 = "R" & myXlonBgnRow & "C" & myXlonBgnCol
    Else
        Exit Function
    End If
'//セル範囲の表記→"R2C2"
    If myXlonEndRow >= 1 And myXlonEndCol >= 1 Then
        myXstrRng2 = "R" & myXlonEndRow & "C" & myXlonEndCol
    End If
'//セル範囲の表記→"R1C1:R2C2"
    myXstrRngAdrs = myXstrRng1 & ":" & myXstrRng2
'//セル範囲をA1形式に変換
    On Error Resume Next
    If Application.ReferenceStyle = xlA1 Then _
        myXstrRngAdrs = Application.ConvertFormula(Formula:=myXstrRngAdrs, _
                        FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    On Error GoTo 0
'//セル範囲アドレスの表記
  Dim myXstrPathBkSht As String
    If myXstrDirPath = "" And myXstrBkName = "" And myXstrShtName = "" Then
        myXstrPathBkSht = ""
    ElseIf myXstrDirPath = "" And myXstrBkName = "" And myXstrShtName <> "" Then
        myXstrPathBkSht = myXstrShtName & "!"
    ElseIf myXstrDirPath = "" And myXstrBkName <> "" And myXstrShtName <> "" Then
        myXstrPathBkSht = "[" & myXstrBkName & "]" & myXstrShtName & "!"
    ElseIf myXstrDirPath <> "" And myXstrBkName <> "" And myXstrShtName <> "" Then
        myXstrPathBkSht = "[" & myXstrBkName & "]" & myXstrShtName
        myXstrPathBkSht = myXstrDirPath & "\" & myXstrPathBkSht
        myXstrPathBkSht = "'" & myXstrPathBkSht & "'" & "!"
    End If
    myXstrRngAdrs = myXstrPathBkSht & myXstrRngAdrs
    PfncstrSetRangeAddressWithExternal = myXstrRngAdrs
End Function

 '定型Ｆ_グラフ用に指定セル範囲から最終行までのセル範囲アドレスを取得する
Private Function PfncstrRangeAddressToEndRow(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrRangeAddressToEndRow = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
  Dim myXobjSheet As Object, myXobjOrgRng As Object
    On Error Resume Next
    myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                    FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    Set myXobjOrgRng = Range(myXstrRngAdrsWEx)
    Set myXobjSheet = myXobjOrgRng.Parent
    On Error GoTo 0
    If myXobjOrgRng Is Nothing Then Exit Function
  Dim myXlonRow1 As Long, myXlonRow2 As Long, myXlonCol1 As Long, myXlonCol2 As Long
    myXlonRow1 = myXobjOrgRng.Row
    myXlonRow2 = myXlonRow1 + myXobjOrgRng.Rows.Count - 1
    myXlonCol1 = myXobjOrgRng.Column
    myXlonCol2 = myXlonCol1 + myXobjOrgRng.Columns.Count - 1
  Dim myXstrNewAdrs As String, myXobjNewRng As Object
  Dim j As Long, myXlonEndRow As Long, myXlonTmpRow As Long
    With myXobjSheet
        For j = myXlonCol1 To myXlonCol2
            myXlonTmpRow = .Cells(.Rows.Count, j).End(xlUp).Row
            If myXlonTmpRow < myXlonRow1 Then myXlonTmpRow = myXlonRow2
            If myXlonEndRow < myXlonTmpRow Then myXlonEndRow = myXlonTmpRow
        Next j
        Set myXobjNewRng = .Range(.Cells(myXlonRow1, myXlonCol1), .Cells(myXlonEndRow, myXlonCol2))
    End With
    myXstrNewAdrs = myXobjNewRng.Address(ReferenceStyle:=Application.ReferenceStyle, external:=True)
    PfncstrRangeAddressToEndRow = myXstrNewAdrs
    Set myXobjOrgRng = Nothing: Set myXobjNewRng = Nothing
ExitPath:
End Function

 '定型Ｆ_グラフ用に指定セル範囲から最終列までのセル範囲アドレスを取得する
Private Function PfncstrRangeAddressToEndColumn(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrRangeAddressToEndColumn = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
  Dim myXobjSheet As Object, myXobjOrgRng As Object
    On Error Resume Next
    myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                    FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    Set myXobjOrgRng = Range(myXstrRngAdrsWEx)
    Set myXobjSheet = myXobjOrgRng.Parent
    On Error GoTo 0
    If myXobjOrgRng Is Nothing Then Exit Function
  Dim myXlonRow1 As Long, myXlonRow2 As Long, myXlonCol1 As Long, myXlonCol2 As Long
    myXlonRow1 = myXobjOrgRng.Row
    myXlonRow2 = myXlonRow1 + myXobjOrgRng.Rows.Count - 1
    myXlonCol1 = myXobjOrgRng.Column
    myXlonCol2 = myXlonCol1 + myXobjOrgRng.Columns.Count - 1
  Dim myXstrNewAdrs As String, myXobjNewRng As Object
  Dim i As Long, myXlonEndCol As Long, myXlonTmpCol As Long
    With myXobjSheet
        For i = myXlonRow1 To myXlonRow2
            myXlonTmpCol = .Cells(i, .Columns.Count).End(xlToLeft).Column
            If myXlonTmpCol < myXlonCol1 Then myXlonTmpCol = myXlonCol2
            If myXlonEndCol < myXlonTmpCol Then myXlonEndCol = myXlonTmpCol
        Next i
        Set myXobjNewRng = .Range(.Cells(myXlonRow1, myXlonCol1), .Cells(myXlonRow2, myXlonEndCol))
    End With
    myXstrNewAdrs = myXobjNewRng.Address(ReferenceStyle:=Application.ReferenceStyle, external:=True)
    PfncstrRangeAddressToEndColumn = myXstrNewAdrs
    Set myXobjOrgRng = Nothing: Set myXobjNewRng = Nothing
ExitPath:
End Function

 '定型Ｆ_グラフ用にセル範囲のアドレスをA1からR1C1に変換する
Private Function PfncstrConvertFormulaA1ToR1C1(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrConvertFormulaA1ToR1C1 = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
    On Error Resume Next
    myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                    FromReferenceStyle:=xlA1, ToReferenceStyle:=xlR1C1, ToAbsolute:=xlAbsolute)
    On Error GoTo 0
    PfncstrConvertFormulaA1ToR1C1 = myXstrRngAdrsWEx
End Function

 '定型Ｆ_グラフ用にセル範囲のアドレスをR1C1からA1に変換する
Private Function PfncstrConvertFormulaR1C1ToA1(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrConvertFormulaR1C1ToA1 = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
    On Error Resume Next
    myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                    FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=xlAbsolute)
    On Error GoTo 0
    PfncstrConvertFormulaR1C1ToA1 = myXstrRngAdrsWEx
End Function

 '定型Ｆ_グラフ用にセル範囲のアドレスを変換する
Private Function PfncstrConvertFormula(ByVal myXstrRngAdrsWEx As String) As String
    PfncstrConvertFormula = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
    On Error Resume Next
    If Application.ReferenceStyle = xlA1 Then
        myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                        FromReferenceStyle:=xlR1C1, ToReferenceStyle:=xlA1, ToAbsolute:=True)
    Else
        myXstrRngAdrsWEx = Application.ConvertFormula(Formula:=myXstrRngAdrsWEx, _
                        FromReferenceStyle:=xlA1, ToReferenceStyle:=xlR1C1, ToAbsolute:=True)
    End If
    On Error GoTo 0
    PfncstrConvertFormula = myXstrRngAdrsWEx
End Function

 '定型Ｐ_グラフにソースデータを設定する
Private Sub PfixSetGraphSourceData(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrRngAdrsWEx As String)
    myXbisExitFlag = False
    On Error GoTo ExitPath
    myXobjChrt.SetSourceData Source:=Range(myXstrRngAdrsWEx)
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_InputBoxメソッドを使用してグラフにソースデータを設定する
Private Sub PfixSetGraphSourceDataByInputBox(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrPrompt As String, ByVal myXstrTitle As String)
    myXbisExitFlag = False
    If myXstrTitle = "" Then myXstrTitle = "セル範囲の選択"
    If myXstrPrompt = "" Then myXstrPrompt = "グラフのソースデータを選択"
  Dim myXobjDfltRng As Object, myXstrDfltAdrs As String, myXobjInptRng As Object
    On Error GoTo ExitPath
    Set myXobjDfltRng = Selection
    myXstrDfltAdrs = myXobjDfltRng.Address(ReferenceStyle:=Application.ReferenceStyle, _
                        RowAbsolute:=True, ColumnAbsolute:=True, external:=True)
    Set myXobjInptRng = Application.InputBox(prompt:=myXstrPrompt, Title:=myXstrTitle, _
                                                Default:=myXstrDfltAdrs, Type:=8)
    myXobjChrt.SetSourceData Source:=myXobjInptRng
    On Error GoTo 0
    Set myXobjDfltRng = Nothing: Set myXobjInptRng = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフに新規データ系列を追加する
Private Sub PfixAddNewSeries(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    On Error GoTo ExitPath
    myXobjChrt.SeriesCollection.NewSeries
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_散布図グラフ用に項目名/項目軸/データのセル範囲からセル範囲アドレスを設定する
Private Sub PfixSetRangeAddressForXYScatter( _
            myXbisExitFlag As Boolean, _
            myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
            ByVal myXobjNameRng As Object, _
            ByVal myXobjXvalRng As Object, ByVal myXobjYvalRng As Object)
    myXbisExitFlag = False
    myXstrNameAdrs = Empty: myXstrXvalAdrs = Empty: myXstrYvalAdrs = Empty
    On Error GoTo ExitPath
    With Application
        myXstrNameAdrs = myXobjNameRng.Address(ReferenceStyle:=.ReferenceStyle, external:=True)
        myXstrXvalAdrs = myXobjXvalRng.Address(ReferenceStyle:=.ReferenceStyle, external:=True)
        myXstrYvalAdrs = myXobjYvalRng.Address(ReferenceStyle:=.ReferenceStyle, external:=True)
    End With
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_散布図グラフの指定データ系列の項目名/項目軸/データを設定する
Private Sub PfixSetSeriesCollectionNameValueInXYScatter( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSrsClctn As Object, _
            ByVal myXstrNameAdrs As String, _
            ByVal myXstrXvalAdrs As String, ByVal myXstrYvalAdrs As String)
    myXbisExitFlag = False
    If myXobjSrsClctn Is Nothing Then Exit Sub
    On Error GoTo ExitPath
    With myXobjSrsClctn
        .Name = "=" & myXstrNameAdrs
        .XValues = "=" & myXstrXvalAdrs
        .Values = "=" & myXstrYvalAdrs
    End With
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｆ_散布図グラフの指定データ系列設定用のSERIES関数式を設定する
Private Function PfncstrSetSeriesFormulaInXYScatter( _
            ByVal myXlonSrsClctnNo As Long, _
            ByVal myXstrNameAdrs As String, _
            ByVal myXstrXvalAdrs As String, ByVal myXstrYvalAdrs As String) As String
'Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    PfncstrSetSeriesFormulaInXYScatter = Empty
  Dim myXstrSrsFrml As String
    myXstrSrsFrml = myXstrNameAdrs & "," & myXstrXvalAdrs & "," & _
                    myXstrYvalAdrs & "," & myXlonSrsClctnNo
    myXstrSrsFrml = "=SERIES(" & myXstrSrsFrml & ")"
    PfncstrSetSeriesFormulaInXYScatter = myXstrSrsFrml
ExitPath:
End Function

 '定型Ｐ_散布図グラフの指定データ系列にSERIES関数でデータ参照範囲を設定する
Private Sub PfixSetSeriesCollectionFormulaInXYScatter( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSrsClctn As Object, ByVal myXstrSrsFrml As String)
    myXbisExitFlag = False
    If myXobjSrsClctn Is Nothing Then Exit Sub
    If myXstrSrsFrml = "" Then Exit Sub
    On Error GoTo ExitPath
    If Application.ReferenceStyle = xlA1 Then
        myXobjSrsClctn.Formula = myXstrSrsFrml
    Else
        myXobjSrsClctn.FormulaR1C1 = myXstrSrsFrml
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定オブジェクトに設定されている参照式を取得する
Private Sub PfixGetObjectFormula( _
            myXstrObjFrmlA1 As String, myXstrObjFrmlR1C1 As String, _
            ByVal myXobjOrg As Object)
    myXstrObjFrmlA1 = Empty: myXstrObjFrmlR1C1 = Empty
    On Error GoTo ExitPath
    myXstrObjFrmlA1 = myXobjOrg.Formula
    myXstrObjFrmlR1C1 = myXobjOrg.FormulaR1C1
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_指定オブジェクトに設定されている参照式をR1C1形式で変更する
Private Sub PfixChangeObjectFormulaR1C1( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjOrg As Object, _
            ByVal myXstrOrgAdrsR1C1 As String, ByVal myXstrNewAdrsR1C1 As String)
    myXbisExitFlag = False
    If myXobjOrg Is Nothing Then Exit Sub
    If myXstrOrgAdrsR1C1 = "" Or myXstrNewAdrsR1C1 = "" Then GoTo ExitPath
  Dim myXstrOrgFrml As String, myXstrNewFrml As String
    On Error GoTo ExitPath
    myXstrOrgFrml = myXobjOrg.FormulaR1C1
'    Debug.Print "系列参照式: " & myXstrOrgFrml
    If InStr(myXstrOrgFrml, myXstrOrgAdrsR1C1) <= 0 Then GoTo ExitPath
    myXstrNewFrml = Replace(myXstrOrgFrml, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
'    Debug.Print "系列参照式: " & myXstrNewFrml
    If Application.ReferenceStyle = xlA1 Then
        Application.ReferenceStyle = xlR1C1
        myXobjOrg.Formula = myXstrNewFrml
        Application.ReferenceStyle = xlA1
    Else
        myXobjOrg.FormulaR1C1 = myXstrNewFrml
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定オブジェクトに設定されている参照式をA1形式で変更する
Private Sub PfixChangeObjectFormulaA1( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjOrg As Object, _
            ByVal myXstrOrgAdrsA1 As String, ByVal myXstrNewAdrsA1 As String)
    myXbisExitFlag = False
    If myXobjOrg Is Nothing Then Exit Sub
    If myXstrOrgAdrsA1 = "" Or myXstrNewAdrsA1 = "" Then GoTo ExitPath
  Dim myXstrOrgFrml As String, myXstrNewFrml As String
    On Error GoTo ExitPath
    myXstrOrgFrml = myXobjOrg.Formula
'    Debug.Print "系列参照式: " & myXstrOrgFrml
    If InStr(myXstrOrgFrml, myXstrOrgAdrsA1) <= 0 Then GoTo ExitPath
    myXstrNewFrml = Replace(myXstrOrgFrml, myXstrOrgAdrsA1, myXstrNewAdrsA1)
'    Debug.Print "系列参照式: " & myXstrNewFrml
    If Application.ReferenceStyle = xlA1 Then
        myXobjOrg.Formula = myXstrNewFrml
    Else
        Application.ReferenceStyle = xlA1
        myXobjOrg.FormulaR1C1 = myXstrNewFrml
        Application.ReferenceStyle = xlR1C1
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定グラフに設定されている参照式をR1C1形式で変更する
Private Sub PincChangeChartFormulaR1C1( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            ByVal myXstrOrgAdrsR1C1 As String, ByVal myXstrNewAdrsR1C1 As String)
'Includes PfixChangeObjectFormulaR1C1
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then Exit Sub
    If myXstrOrgAdrsR1C1 = "" Or myXstrNewAdrsR1C1 = "" Then GoTo ExitPath
  Dim myXobjSrsClctn As Object
    For Each myXobjSrsClctn In myXobjChrt.SeriesCollection
        Call PfixChangeObjectFormulaR1C1( _
                myXbisExitFlag, _
                myXobjSrsClctn, myXstrOrgAdrsR1C1, myXstrNewAdrsR1C1)
    Next myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定グラフに設定されている参照式をA1形式で変更する
Private Sub PincChangeChartFormulaA1( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            ByVal myXstrOrgAdrsA1 As String, ByVal myXstrNewAdrsA1 As String)
'Includes PfixChangeObjectFormulaA1
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then Exit Sub
    If myXstrOrgAdrsA1 = "" Or myXstrNewAdrsA1 = "" Then GoTo ExitPath
  Dim myXobjSrsClctn As Object
    For Each myXobjSrsClctn In myXobjChrt.SeriesCollection
        Call PfixChangeObjectFormulaA1( _
                myXbisExitFlag, _
                myXobjSrsClctn, myXstrOrgAdrsA1, myXstrNewAdrsA1)
    Next myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフの凡例を設定する
Private Sub PfixSetChartLegend(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisLgndSet As Boolean = True, _
            Optional ByVal coXbisLgndVsbl As Boolean = True, _
            Optional ByVal coXlonLgndPstn As Long = xlLegendPositionRight, _
            Optional ByVal coXbisLgndInLayout As Boolean = False, _
            Optional ByVal coXlonLgndFillColor As Long = msoThemeColorBackground1, _
            Optional ByVal coXlonLgndLineColor As Long = msoThemeColorText1, _
            Optional ByVal coXdouLgndLineWeight As Double = 0.75)
'coXbisLgndSet = False : 設定処理しない
'coXbisLgndVsbl = True  : 凡例表示
'coXbisLgndVsbl = False : 凡例非表示
'coXlonLgndPstn = xlLegendPositionCorner = 2     : グラフの輪郭線の右上隅
'coXlonLgndPstn = xlLegendPositionBottom = -4107 : グラフの下
'coXlonLgndPstn = xlLegendPositionCustom = -4161 : 任意の位置
'coXlonLgndPstn = xlLegendPositionLeft   = -4131 : グラフの左
'coXlonLgndPstn = xlLegendPositionRight  = -4152 : グラフの右
'coXlonLgndPstn = xlLegendPositionTop    = -4160 : グラフの上
'coXbisLgndInLayout = False : 凡例をグラフに重ねて表示
'coXbisLgndInLayout = True  : 凡例をグラフに重ねずに表示
'coXlonLgndFillColor : 凡例の塗りつぶしの色
'coXlonLgndLineColor : 凡例の枠線の色
'coXdouLgndLineWeight : 凡例の枠線の幅
    myXbisExitFlag = False
    If coXbisLgndSet = False Then Exit Sub
    If myXobjChrt Is Nothing Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrt.HasLegend = coXbisLgndVsbl
    On Error GoTo 0
    If coXbisLgndVsbl = False Then Exit Sub
    With myXobjChrt.Legend
    '//凡例の位置設定
        .Position = coXlonLgndPstn
        .IncludeInLayout = coXbisLgndInLayout
    '//凡例の塗りつぶし
        .Format.Fill.Visible = msoTrue
        .Format.Fill.Solid
        .Format.Fill.ForeColor.ObjectThemeColor = coXlonLgndFillColor
    '//凡例の枠線
        .Format.Line.Visible = msoTrue
        .Format.Line.ForeColor.ObjectThemeColor = coXlonLgndLineColor
        .Format.Line.Weight = coXdouLgndLineWeight
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフ内で第1軸と第2軸に設定されている系列数を確認する
Private Sub PfnclonCountSeriesCollectionSetAxisGroup( _
            myXlonAxsGrp1Cnt As Long, myXlonAxsGrp2Cnt As Long, _
            ByVal myXobjChrt As Object)
    myXlonAxsGrp1Cnt = Empty: myXlonAxsGrp2Cnt = Empty
    On Error GoTo ExitPath
  Dim myXlonSCCnt As Long: myXlonSCCnt = myXobjChrt.SeriesCollection.Count
    On Error GoTo 0
    If myXlonSCCnt <= 0 Then GoTo ExitPath
  Dim myXobjSrsClctn As Object
    For Each myXobjSrsClctn In myXobjChrt.SeriesCollection
        Select Case myXobjSrsClctn.AxisGroup
            Case xlPrimary: myXlonAxsGrp1Cnt = myXlonAxsGrp1Cnt + 1
            Case xlSecondary: myXlonAxsGrp2Cnt = myXlonAxsGrp2Cnt + 1
        End Select
    Next myXobjSrsClctn
    Set myXobjSrsClctn = Nothing
ExitPath:
End Sub

 '定型Ｐ_グラフのX軸主軸の数値軸を設定する
Private Sub PincSetChartPrimaryCategoryAxesScale( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            ByVal myXvarMinScl As Variant, ByVal myXvarMaxScl As Variant, _
            ByVal myXvarMjrUnt As Variant, ByVal myXvarMnrUnt As Variant)
'Includes PfixSetChartAxesScale
'myXvarX = ""  : 設定処理しない
'myXvarX = "X" : 自動に設定
'myXvarX = X   : 数値を設定
    myXbisExitFlag = False
  Dim myXobjChrtAxs As Object
    On Error GoTo ExitPath
    Set myXobjChrtAxs = myXobjChrt.Axes(xlCategory, xlPrimary)
    On Error GoTo 0
    Call PfixSetChartAxesScale(myXbisExitFlag, _
            myXobjChrtAxs, myXvarMinScl, myXvarMaxScl, myXvarMjrUnt, myXvarMnrUnt)
    Set myXobjChrtAxs = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのY軸主軸の数値軸を設定する
Private Sub PincSetChartPrimaryValueAxesScale( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            ByVal myXvarMinScl As Variant, ByVal myXvarMaxScl As Variant, _
            ByVal myXvarMjrUnt As Variant, ByVal myXvarMnrUnt As Variant)
'Includes PfixSetChartAxesScale
'myXvarX = ""  : 設定処理しない
'myXvarX = "X" : 自動に設定
'myXvarX = X   : 数値を設定
    myXbisExitFlag = False
  Dim myXobjChrtAxs As Object
    On Error GoTo ExitPath
    Set myXobjChrtAxs = myXobjChrt.Axes(xlValue, xlPrimary)
    On Error GoTo 0
    Call PfixSetChartAxesScale(myXbisExitFlag, _
            myXobjChrtAxs, myXvarMinScl, myXvarMaxScl, myXvarMjrUnt, myXvarMnrUnt)
    Set myXobjChrtAxs = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのY軸第2軸の数値軸を設定する
Private Sub PincSetChartSecondaryValueAxesScale( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            ByVal myXvarMinScl As Variant, ByVal myXvarMaxScl As Variant, _
            ByVal myXvarMjrUnt As Variant, ByVal myXvarMnrUnt As Variant)
'Includes PfnclonCountSeriesCollectionSetAxisGroup
'Includes PfixSetChartAxesScale
'myXvarX = ""  : 設定処理しない
'myXvarX = "X" : 自動に設定
'myXvarX = X   : 数値を設定
    myXbisExitFlag = False
  Dim myXlonAxsGrp1Cnt As Long, myXlonAxsGrp2Cnt As Long
    Call PfnclonCountSeriesCollectionSetAxisGroup(myXlonAxsGrp1Cnt, myXlonAxsGrp2Cnt, myXobjChrt)
    If myXlonAxsGrp2Cnt <= 0 Then Exit Sub
  Dim myXobjChrtAxs As Object
    On Error GoTo ExitPath
    Set myXobjChrtAxs = myXobjChrt.Axes(xlValue, xlSecondary)
    On Error GoTo 0
    Call PfixSetChartAxesScale(myXbisExitFlag, _
            myXobjChrtAxs, myXvarMinScl, myXvarMaxScl, myXvarMjrUnt, myXvarMnrUnt)
    Set myXobjChrtAxs = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX/Y軸の数値軸を設定する
Private Sub PfixSetChartAxesScale( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtAxs As Object, _
            ByVal myXvarMinScl As Variant, ByVal myXvarMaxScl As Variant, _
            ByVal myXvarMjrUnt As Variant, ByVal myXvarMnrUnt As Variant)
'myXvarX = ""  : 設定処理しない
'myXvarX = "X" : 自動に設定
'myXvarX = X   : 数値を設定
    myXbisExitFlag = False
    If myXobjChrtAxs Is Nothing Then Exit Sub
    On Error GoTo ExitPath
    With myXobjChrtAxs
        If CStr(myXvarMinScl) <> "" Then
            If IsNumeric(myXvarMinScl) = True Then
                .MinimumScale = CDbl(myXvarMinScl)
            Else
                .MinimumScaleIsAuto = True
            End If
        End If
        If CStr(myXvarMaxScl) <> "" Then
            If IsNumeric(myXvarMaxScl) = True Then
                .MaximumScale = CDbl(myXvarMaxScl)
            Else
                .MaximumScaleIsAuto = True
            End If
        End If
        If CStr(myXvarMjrUnt) <> "" Then
            If IsNumeric(myXvarMjrUnt) = True Then
                .MajorUnit = CDbl(myXvarMjrUnt)
            Else
                .MajorUnitIsAuto = True
            End If
        End If
        If CStr(myXvarMnrUnt) <> "" Then
            If IsNumeric(myXvarMnrUnt) = True Then
                .MinorUnit = CDbl(myXvarMnrUnt)
            Else
                .MinorUnitIsAuto = True
            End If
        End If
    End With
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルを設定する
Private Sub PincSetChartTitle( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrChrtTtlTxt As String, _
            Optional ByVal coXbisTtlSet As Boolean = True, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'Includes PfixSetChartTitleVisibleFormula
'Includes PfixSetChartTitleVisibleText
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrChrtTtlTxt = "X" : 文字列を設定
    myXbisExitFlag = False
    If Left(myXstrChrtTtlTxt, 1) = "=" Then
        Call PfixSetChartTitleVisibleFormula(myXbisExitFlag, _
                myXobjChrt, myXstrChrtTtlTxt, coXbisTtlSet, coXbisTtlVsbl)
    Else
        Call PfixSetChartTitleVisibleText(myXbisExitFlag, _
                myXobjChrt, myXstrChrtTtlTxt, coXbisTtlSet, coXbisTtlVsbl)
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX軸主軸のタイトルを設定する
Private Sub PincSetChartPrimaryCategoryAxisTitle( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrAxsTtlX1 As String, _
            Optional ByVal coXbisTtlSetX1 As Boolean = True, _
            Optional ByVal coXbisTtlVsblX1 As Boolean = True)
'Includes PfixSetChartAxisTitleVisibleFormula
'Includes PfixSetChartAxisTitleVisibleText
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrAxsTtl = "X" : 文字列を設定
    myXbisExitFlag = False
  Dim myXobjChrtAxsX1 As Object
    On Error GoTo ExitPath
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    On Error GoTo 0
    If Left(myXstrAxsTtlX1, 1) = "=" Then
        Call PfixSetChartAxisTitleVisibleFormula(myXbisExitFlag, _
                myXobjChrtAxsX1, myXstrAxsTtlX1, coXbisTtlSetX1, coXbisTtlVsblX1)
    Else
        Call PfixSetChartAxisTitleVisibleText(myXbisExitFlag, _
                myXobjChrtAxsX1, myXstrAxsTtlX1, coXbisTtlSetX1, coXbisTtlVsblX1)
    End If
    Set myXobjChrtAxsX1 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのY軸主軸のタイトルを設定する
Private Sub PincSetChartPrimaryValueAxisTitle( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrAxsTtlY1 As String, _
            Optional ByVal coXbisTtlSetY1 As Boolean = True, _
            Optional ByVal coXbisTtlVsblY1 As Boolean = True)
'Includes PfixSetChartAxisTitleVisibleFormula
'Includes PfixSetChartAxisTitleVisibleText
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrAxsTtl = "X" : 文字列を設定
    myXbisExitFlag = False
  Dim myXobjChrtAxsY1 As Object
    On Error GoTo ExitPath
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    On Error GoTo 0
    If Left(myXstrAxsTtlY1, 1) = "=" Then
        Call PfixSetChartAxisTitleVisibleFormula(myXbisExitFlag, _
                myXobjChrtAxsY1, myXstrAxsTtlY1, coXbisTtlSetY1, coXbisTtlVsblY1)
    Else
        Call PfixSetChartAxisTitleVisibleText(myXbisExitFlag, _
                myXobjChrtAxsY1, myXstrAxsTtlY1, coXbisTtlSetY1, coXbisTtlVsblY1)
    End If
    Set myXobjChrtAxsY1 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのY軸第2軸のタイトルを設定する
Private Sub PincSetChartSecondaryValueAxisTitle( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrAxsTtlY2 As String, _
            Optional ByVal coXbisTtlSetY2 As Boolean = True, _
            Optional ByVal coXbisTtlVsblY2 As Boolean = True)
'Includes PfnclonCountSeriesCollectionSetAxisGroup
'Includes PfixSetChartAxisTitleVisibleFormula
'Includes PfixSetChartAxisTitleVisibleText
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrAxsTtl = "X" : 文字列を設定
    myXbisExitFlag = False
  Dim myXlonAxsGrp1Cnt As Long, myXlonAxsGrp2Cnt As Long
    Call PfnclonCountSeriesCollectionSetAxisGroup(myXlonAxsGrp1Cnt, myXlonAxsGrp2Cnt, myXobjChrt)
    If myXlonAxsGrp2Cnt <= 0 Then Exit Sub
  Dim myXobjChrtAxsY2 As Object
    On Error GoTo ExitPath
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    On Error GoTo 0
    If Left(myXstrAxsTtlY2, 1) = "=" Then
        Call PfixSetChartAxisTitleVisibleFormula(myXbisExitFlag, _
                myXobjChrtAxsY2, myXstrAxsTtlY2, coXbisTtlSetY2, coXbisTtlVsblY2)
    Else
        Call PfixSetChartAxisTitleVisibleText(myXbisExitFlag, _
                myXobjChrtAxsY2, myXstrAxsTtlY2, coXbisTtlSetY2, coXbisTtlVsblY2)
    End If
    Set myXobjChrtAxsY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルの文字列を設定する
Private Sub PfixSetChartTitleVisibleText( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrChrtTtl As String, _
            Optional ByVal coXbisTtlSet As Boolean = True, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrChrtTtlTxt = "X" : 文字列を設定
    myXbisExitFlag = False
    If coXbisTtlSet = False Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrt.HasTitle = coXbisTtlVsbl
    On Error GoTo 0
    If coXbisTtlVsbl = False Then Exit Sub
    myXobjChrt.ChartTitle.Text = myXstrChrtTtl
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルの参照式を設定する
Private Sub PfixSetChartTitleVisibleFormula( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, ByVal myXstrChrtTtl As String, _
            Optional ByVal coXbisTtlSet As Boolean = True, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrChrtTtlTxt = "X" : 文字列を設定
    myXbisExitFlag = False
    If coXbisTtlSet = False Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrt.HasTitle = coXbisTtlVsbl
    On Error GoTo 0
    If coXbisTtlVsbl = False Then Exit Sub
    If Application.ReferenceStyle = xlA1 Then
        myXobjChrt.ChartTitle.Formula = myXstrChrtTtl
    Else
        myXobjChrt.ChartTitle.FormulaR1C1 = myXstrChrtTtl
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルの文字列を設定する
Private Sub PfixSetChartAxisTitleVisibleText( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtAxs As Object, ByVal myXstrAxsTtl As String, _
            Optional ByVal coXbisTtlSet As Boolean = True, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrAxsTtl = "X" : 文字列を設定
    myXbisExitFlag = False
    If coXbisTtlSet = False Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrtAxs.HasTitle = coXbisTtlVsbl
    On Error GoTo 0
    If coXbisTtlVsbl = False Then Exit Sub
    myXobjChrtAxs.AxisTitle.Text = myXstrAxsTtl
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルの参照式を設定する
Private Sub PfixSetChartAxisTitleVisibleFormula( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtAxs As Object, ByVal myXstrAxsTtl As String, _
            Optional ByVal coXbisTtlSet As Boolean = True, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrAxsTtl = "X" : 参照式を設定
    myXbisExitFlag = False
    If coXbisTtlSet = False Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrtAxs.HasTitle = coXbisTtlVsbl
    On Error GoTo 0
    If coXbisTtlVsbl = False Then Exit Sub
    If Application.ReferenceStyle = xlA1 Then
        myXobjChrtAxs.AxisTitle.Formula = myXstrAxsTtl
    Else
        myXobjChrtAxs.AxisTitle.FormulaR1C1 = myXstrAxsTtl
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルの表示有無を設定する
Private Sub PfixSetChartAxisTitleVisible( _
            ByVal myXobjChrtAxs As Object, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
  Const coXlonElementPrimaryValueAxisTitleAdjacentToAxis As Long = 307
    On Error GoTo ExitPath
    With myXobjChrtAxs.Parent
    If myXobjChrtAxs.Type = xlCategory And myXobjChrtAxs.AxisGroup = xlPrimary Then
        Select Case coXbisTtlVsbl
            Case False
                Call .SetElement(msoElementPrimaryCategoryAxisTitleNone)
            Case Else
                Call .SetElement(msoElementPrimaryCategoryAxisTitleAdjacentToAxis)
        End Select
    ElseIf myXobjChrtAxs.Type = xlValue And myXobjChrtAxs.AxisGroup = xlPrimary Then
        Select Case coXbisTtlVsbl
            Case False
                Call .SetElement(msoElementPrimaryValueAxisTitleNone)
            Case Else
                Call .SetElement(coXlonElementPrimaryValueAxisTitleAdjacentToAxis)
        End Select
    ElseIf myXobjChrtAxs.Type = xlValue And myXobjChrtAxs.AxisGroup = xlSecondary Then
        Select Case coXbisTtlVsbl
            Case False
                Call .SetElement(msoElementSecondaryValueAxisTitleNone)
            Case Else
                Call .SetElement(msoElementSecondaryValueAxisTitleAdjacentToAxis)
        End Select
    End If
    End With
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_グラフを標準フォーマットに調整する
Private Sub PfixSetStandardChartFormat(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisXMnrGrdLnsShw As Boolean = False, _
            Optional ByVal coXbisYMnrGrdLnsShw As Boolean = False)
'myXbisMnrGrdLnsShw = True  : 補助目盛線を表示する
'myXbisMnrGrdLnsShw = False : 補助目盛線を表示しない
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjPltArea As Object
  Dim myXobjAxsX1 As Object, myXobjAxsY1 As Object, myXobjAxsY2 As Object
    On Error Resume Next
    myXobjChrt.HasLegend = False
    myXobjChrt.HasTitle = False
    myXobjChrt.Axes(xlCategory, xlPrimary).HasTitle = False
    myXobjChrt.Axes(xlValue, xlPrimary).HasTitle = False
    myXobjChrt.Axes(xlValue, xlSecondary).HasTitle = False
'//プロットエリアの枠線を設定
    Set myXobjPltArea = myXobjChrt.PlotArea
    Call PsubSetStandardChartFormatForPlotArea(myXobjPltArea)
'//X軸主軸の枠線を設定
    Set myXobjAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    Call PsubSetStandardChartFormatForAxes(myXobjAxsX1)
'//X軸の目盛線を設定
    Call PsubSetStandardChartFormatForAxesGridlines(myXobjAxsX1)
'//Y軸主軸の枠線を設定
    Set myXobjAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    Call PsubSetStandardChartFormatForAxes(myXobjAxsY1)
'//Y軸の目盛線を設定
    Call PsubSetStandardChartFormatForAxesGridlines(myXobjAxsY1)
'//Y軸第2軸の枠線を設定
    Set myXobjAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    Call PsubSetStandardChartFormatForAxes(myXobjAxsY2)
'//X/Y軸のメモリ線を表示
'//X軸主軸
    If coXbisXMnrGrdLnsShw = False Then
        Call myXobjChrt.SetElement(msoElementPrimaryCategoryGridLinesMajor)
    Else
        Call myXobjChrt.SetElement(msoElementPrimaryCategoryGridLinesMajor)
        Call myXobjChrt.SetElement(msoElementPrimaryCategoryGridLinesMinorMajor)
    End If
'//Y軸主軸
    If coXbisYMnrGrdLnsShw = False Then
        Call myXobjChrt.SetElement(msoElementPrimaryValueGridLinesMajor)
    Else
        Call myXobjChrt.SetElement(msoElementPrimaryValueGridLinesMajor)
        Call myXobjChrt.SetElement(msoElementPrimaryValueGridLinesMinorMajor)
    End If
    On Error GoTo 0
    Set myXobjPltArea = Nothing
    Set myXobjAxsX1 = Nothing: Set myXobjAxsY1 = Nothing: Set myXobjAxsY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSetStandardChartFormatForPlotArea(ByVal myXobjTrgt As Object)
    If myXobjTrgt Is Nothing Then Exit Sub
    With myXobjTrgt
    '//枠線
        .Format.Line.Visible = True
        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
    End With
End Sub
Private Sub PsubSetStandardChartFormatForAxes(ByVal myXobjTrgt As Object)
    If myXobjTrgt Is Nothing Then Exit Sub
    With myXobjTrgt
    '//枠線
        .Format.Line.Visible = True
        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
    '//目盛
        .MajorTickMark = xlInside
        If myXobjTrgt.AxisGroup = xlPrimary Then
            .TickLabelPosition = xlLow
        ElseIf myXobjTrgt.AxisGroup = xlSecondary Then
            .TickLabelPosition = xlHigh
        End If
    End With
End Sub
Private Sub PsubSetStandardChartFormatForAxesGridlines(ByVal myXobjTrgt As Object)
    If myXobjTrgt Is Nothing Then Exit Sub
    With myXobjTrgt.MajorGridlines.Format.Line
    '//目盛線
        .Visible = msoTrue
        .ForeColor.ObjectThemeColor = msoThemeColorBackground1
        .ForeColor.Brightness = -0.15
    End With
    With myXobjTrgt.MinorGridlines.Format.Line
    '//補助目盛線
        .Visible = msoTrue
        .ForeColor.ObjectThemeColor = msoThemeColorBackground1
        .ForeColor.Brightness = -0.15
    End With
End Sub

 '定型Ｐ_グラフの凡例を標準フォーマットで設定する
Private Sub PfixSetChartStandardLegend(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
    If myXobjChrt.HasLegend = False Then myXobjChrt.HasLegend = True
    With myXobjChrt.Legend
    '//凡例の文字
        .Font.Name = "Meiryo UI"
        .Font.Size = 11
    '//凡例の位置設定
        .Position = xlLegendPositionCorner
        .IncludeInLayout = False
    '//凡例の塗りつぶし
        .Format.Fill.Visible = True
        .Format.Fill.Solid
        .Format.Fill.ForeColor.ObjectThemeColor = msoThemeColorBackground1
    '//凡例の枠線
        .Format.Line.Visible = True
        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
        .Format.Line.Weight = 0.75
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルを標準フォーマットで設定する
Private Sub PfixSetChartStandardTitle(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
    If myXobjChrt.HasTitle = False Then myXobjChrt.HasTitle = True
    With myXobjChrt.ChartTitle
    '//タイトルの文字
        .Font.Name = "Meiryo UI"
        .Font.Size = 11
        .Font.Bold = True
    '//タイトルの位置設定
        .Position = xlChartElementPositionAutomatic
        .IncludeInLayout = False
    '//タイトルの塗りつぶし
        .Format.Fill.Visible = True
        .Format.Fill.Solid
        .Format.Fill.ForeColor.ObjectThemeColor = msoThemeColorBackground1
    '//タイトルの下線
        .Format.TextFrame2.TextRange.Font.UnderlineStyle = msoUnderlineSingleLine
    '//タイトルの枠線
        .Format.Line.Visible = False
'        .Format.Line.Visible = True
'        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
'        .Format.Line.Weight = 0.75
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルの位置をグラフの中心に設定する
Private Sub PincSetChartTitleCenter(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisPltAreaCntr As Boolean = False)
'Includes PfixSetChartTargetTitleCenter
'coXbisPltAreaCntr = True  : チャートエリア中心
'coXbisPltAreaCntr = False : プロットエリア中心
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
    If myXobjChrt.HasTitle = False Then myXobjChrt.HasTitle = True
  Dim myXobjChrtTtl As Object
    Set myXobjChrtTtl = myXobjChrt.ChartTitle
  Dim myXdouSetTtlTop As Double, myXdouSetTtlLft As Double
  Dim myXlonTtlPstn As Long: myXlonTtlPstn = 1
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjChrtTtl, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjChrt.ChartTitle.Top = myXdouSetTtlTop
        myXobjChrt.ChartTitle.Left = myXdouSetTtlLft
    End If
    Set myXobjChrtTtl = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルを標準フォーマットで設定する
Private Sub PfixSetChartStandardAxisTitle(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjChrtAxsX1 As Object, myXobjChrtAxsY1 As Object, myXobjChrtAxsY2 As Object
  Dim myXobjAxsTtlX1 As Object, myXobjAxsTtlY1 As Object, myXobjAxsTtlY2 As Object
    On Error Resume Next
'//X軸主軸
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    If myXobjChrtAxsX1.HasTitle = False Then myXobjChrtAxsX1.HasTitle = True
    Set myXobjAxsTtlX1 = myXobjChrtAxsX1.AxisTitle
    Call PsubSetChartStandardAxisTitle(myXobjAxsTtlX1)
'//Y軸主軸
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    If myXobjChrtAxsY1.HasTitle = False Then myXobjChrtAxsY1.HasTitle = True
    Set myXobjAxsTtlY1 = myXobjChrtAxsY1.AxisTitle
    Call PsubSetChartStandardAxisTitle(myXobjAxsTtlY1)
'//Y軸第2軸
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    If myXobjChrtAxsY2.HasTitle = False Then myXobjChrtAxsY2.HasTitle = True
    Set myXobjAxsTtlY2 = myXobjChrtAxsY2.AxisTitle
    Call PsubSetChartStandardAxisTitle(myXobjAxsTtlY2)
    On Error GoTo 0
    Set myXobjChrtAxsX1 = Nothing
    Set myXobjChrtAxsY1 = Nothing: Set myXobjChrtAxsY2 = Nothing
    Set myXobjAxsTtlX1 = Nothing
    Set myXobjAxsTtlY1 = Nothing: Set myXobjAxsTtlY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSetChartStandardAxisTitle(ByVal myXobjAxsTtl As Object)
    If myXobjAxsTtl Is Nothing Then Exit Sub
    With myXobjAxsTtl
    '//タイトルの文字
        .Font.Name = "Meiryo UI"
        .Font.Size = 11
        .Font.Bold = True
    End With
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルの位置をグラフの中心に設定する
Private Sub PincSetChartAxisTitleCenter(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisPltAreaCntr As Boolean = False)
'Includes PfixSetChartTargetTitleCenter
'coXbisPltAreaCntr = True  : チャートエリア中心
'coXbisPltAreaCntr = False : プロットエリア中心
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjChrtAxsX1 As Object, myXobjChrtAxsY1 As Object, myXobjChrtAxsY2 As Object
  Dim myXobjAxsTtlX1 As Object, myXobjAxsTtlY1 As Object, myXobjAxsTtlY2 As Object
  Dim myXdouSetTtlTop As Double, myXdouSetTtlLft As Double
  Dim myXlonTtlPstn As Long
    On Error Resume Next
'//X軸主軸
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    If myXobjChrtAxsX1.HasTitle = False Then myXobjChrtAxsX1.HasTitle = True
    Set myXobjAxsTtlX1 = myXobjChrtAxsX1.AxisTitle
    myXlonTtlPstn = 2
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjAxsTtlX1, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjAxsTtlX1.Top = myXdouSetTtlTop
        myXobjAxsTtlX1.Left = myXdouSetTtlLft
    End If
'//Y軸主軸
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    If myXobjChrtAxsY1.HasTitle = False Then myXobjChrtAxsY1.HasTitle = True
    Set myXobjAxsTtlY1 = myXobjChrtAxsY1.AxisTitle
    myXlonTtlPstn = 3
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjAxsTtlY1, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjAxsTtlY1.Top = myXdouSetTtlTop
        myXobjAxsTtlY1.Left = myXdouSetTtlLft
    End If
'//Y軸第2軸
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    If myXobjChrtAxsY2.HasTitle = False Then myXobjChrtAxsY2.HasTitle = True
    Set myXobjAxsTtlY2 = myXobjChrtAxsY2.AxisTitle
    myXlonTtlPstn = 4
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjAxsTtlY2, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjAxsTtlY2.Top = myXdouSetTtlTop
        myXobjAxsTtlY2.Left = myXdouSetTtlLft
    End If
    On Error GoTo 0
    Set myXobjChrtAxsX1 = Nothing
    Set myXobjChrtAxsY1 = Nothing: Set myXobjChrtAxsY2 = Nothing
    Set myXobjAxsTtlX1 = Nothing
    Set myXobjAxsTtlY1 = Nothing: Set myXobjAxsTtlY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定タイトル位置がグラフの中心となる設定位置を取得する
Private Sub PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop As Double, myXdouSetTtlLft As Double, _
            ByVal myXobjChrt As Object, ByVal myXobjOrgTtl As Object, _
            ByVal myXlonTtlPstn As Long, _
            Optional ByVal coXbisPltAreaCntr As Boolean = False)
'myXlonTtlPstn = 1  : 上側のタイトル
'myXlonTtlPstn = 2  : 下側のタイトル
'myXlonTtlPstn = 3  : 左側のタイトル
'myXlonTtlPstn = 4  : 右側のタイトル
'coXbisPltAreaCntr = True  : チャートエリア中心
'coXbisPltAreaCntr = False : プロットエリア中心
    myXdouSetTtlTop = Empty: myXdouSetTtlLft = Empty
    If myXobjChrt Is Nothing Then Exit Sub
    If myXobjOrgTtl Is Nothing Then Exit Sub
  Dim myXdouOrgTtlHght As Double, myXdouOrgTtlWdth As Double, _
        myXdouChrtAreaHght As Double, myXdouChrtAreaWdth As Double, _
        myXdouPltAreaTop As Double, myXdouPltAreaHght As Double, _
        myXdouPltAreaLft As Double, myXdouPltAreaWdth As Double
  Dim myXdouCal As Double
    On Error GoTo ExitPath
    If myXlonTtlPstn = 1 Or myXlonTtlPstn = 2 Then
    '//タイトル位置が上側と下側の場合の処理
    '//タイトルのサイズ情報を取得
        myXdouOrgTtlWdth = myXobjOrgTtl.Width
    '//チャートエリアの位置サイズ情報を取得
        myXdouChrtAreaHght = myXobjChrt.ChartArea.Height
        myXdouChrtAreaWdth = myXobjChrt.ChartArea.Width
    '//プロットエリアの位置サイズ情報を取得
        myXdouPltAreaLft = myXobjChrt.PlotArea.Left
        myXdouPltAreaWdth = myXobjChrt.PlotArea.Width
    '//タイトル位置を設定
        Select Case myXlonTtlPstn
            Case 1: myXdouSetTtlTop = 0
            Case 2: myXdouSetTtlTop = myXdouChrtAreaHght
        End Select
        If coXbisPltAreaCntr = False Then
            myXdouCal = myXdouChrtAreaWdth / 2
            myXdouCal = myXdouCal - myXdouOrgTtlWdth / 2
        Else
            myXdouCal = myXdouPltAreaWdth / 2
            myXdouCal = myXdouPltAreaLft + myXdouCal
            myXdouCal = myXdouCal - myXdouOrgTtlWdth / 2
        End If
        myXdouSetTtlLft = myXdouCal
    ElseIf myXlonTtlPstn = 3 Or myXlonTtlPstn = 4 Then
    '//タイトル位置が左側と右側の場合の処理
    '//タイトルのサイズ情報を取得
        myXdouOrgTtlHght = myXobjOrgTtl.Height
    '//チャートエリアの位置サイズ情報を取得
        myXdouChrtAreaHght = myXobjChrt.ChartArea.Height
        myXdouChrtAreaWdth = myXobjChrt.ChartArea.Width
    '//プロットエリアの位置サイズ情報を取得
        myXdouPltAreaTop = myXobjChrt.PlotArea.Top
        myXdouPltAreaHght = myXobjChrt.PlotArea.Height
    '//タイトル位置を設定
        If coXbisPltAreaCntr = False Then
            myXdouCal = myXdouChrtAreaHght / 2
            myXdouCal = myXdouCal - myXdouOrgTtlHght / 2
        Else
            myXdouCal = myXdouPltAreaHght / 2
            myXdouCal = myXdouPltAreaTop + myXdouCal
            myXdouCal = myXdouCal - myXdouOrgTtlHght / 2
        End If
        myXdouSetTtlTop = myXdouCal
        Select Case myXlonTtlPstn
            Case 3: myXdouSetTtlLft = 0
            Case 4: myXdouSetTtlLft = myXdouChrtAreaWdth
        End Select
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXdouSetTtlTop = Empty: myXdouSetTtlLft = Empty
End Sub

 '定型Ｐ_グラフの数値軸を標準フォーマットで設定する
Private Sub PfixSetChartStandardAxesScale(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjChrtAxsX1 As Object, myXobjChrtAxsY1 As Object, myXobjChrtAxsY2 As Object
    On Error Resume Next
'//X軸主軸
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    Call PsubSetChartStandardAxesScale(myXobjChrtAxsX1)
'//Y軸主軸
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    Call PsubSetChartStandardAxesScale(myXobjChrtAxsY1)
'//Y軸第2軸
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    Call PsubSetChartStandardAxesScale(myXobjChrtAxsY2)
    On Error GoTo 0
    Set myXobjChrtAxsX1 = Nothing: Set myXobjChrtAxsY1 = Nothing
    Set myXobjChrtAxsY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSetChartStandardAxesScale(ByVal myXobjChrtAxs As Object)
    If myXobjChrtAxs Is Nothing Then Exit Sub
    With myXobjChrtAxs
    '//軸の文字
        .TickLabels.Font.Name = "Meiryo UI"
        .TickLabels.Font.Size = 11
    '//軸のスケール
        .MinimumScaleIsAuto = True
        .MaximumScaleIsAuto = True
        .MajorUnitIsAuto = True
        .MinorUnitIsAuto = True
    End With
End Sub

      <br></pre>
    </body>
  </html>
