<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfncbisIsMatrix
'Includes PfixArrayPartColumnNumeric
'Includes Pfix1DArrayExtractNumeric
'Includes PfixTrapezoidWaveLong
'Includes PfixSlopeIntercept
'Includes PfncdouStDevS
'Includes PfncdouArithmeticMean
'Includes PincdouMedian
'Includes PfixQuickSortOfNumeric1DArray

Option Explicit
Option Base 1

'◆ClassProc名_データ配列から標準偏差外れのデータを除去する
'Rev.003
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "COutlierFltr"
  
'//モジュール内定数
  Private Const coXlonMdnFltrQnt As Long = 3
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonStrtRow As Long, myXlonLastRow As Long
  Private myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
    'myZdouCalData(i, 1) : 計算処理後データ配列
  
'//PropertySetLet_入力制御信号
  Private myXlonCalRng As Long, myXlonCalDataRngOptn As Long
  
'//PropertySetLet_入力データ
  Private myZdouOrgData() As Double
    'myZdouOrgData(i, 1) : 元データ配列
  Private myXlonBgnRow As Long, myXlonEndRow As Long
  Private myXlonRgrsnSmplQnt As Long, myXlonRgrsnStep As Long
  Private myXlonMeanSmplQnt As Long, myXlonMeanStep As Long
  Private myXlonMedianSmplQnt As Long, myXlonMedianStep As Long
  Private myXdouSigmaLimit As Double
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonInptRowCnt As Long, myXlonInptColCnt As Long, myZdouInptData() As Double
  Private myXlonXdataCnt As Long, myZdouXdata() As Double
  Private myXlonOrgYCnt As Long, myZdouOrgY() As Double
  Private myXlonStrtNo As Long, myXlonLastNo As Long
  Private myXlonPreOfst As Long, myXlonPstOfst As Long
  Private myXlonCalStrtNo As Long, myXlonCalLastNo As Long
  
  Private myXlonRgrsnLnDevCnt As Long, myZdouRgrsnLnDev() As Double
  Private myXdouStDev As Double
  Private myXlonMeanCnt As Long, myZdouMean() As Double
  Private myXlonMedianCnt As Long, myZdouMedian() As Double
  Private myXlonFltrDataCnt As Long, myZdouFltrData() As Double

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonInptRowCnt = Empty: myXlonInptColCnt = Empty: Erase myZdouInptData
    
    myXlonXdataCnt = Empty: Erase myZdouXdata
    myXlonOrgYCnt = Empty: Erase myZdouOrgY
    myXlonStrtNo = Empty: myXlonLastNo = Empty
    myXlonPreOfst = Empty: myXlonPstOfst = Empty
    myXlonCalStrtNo = Empty: myXlonCalLastNo = Empty

    myXlonRgrsnLnDevCnt = Empty: Erase myZdouRgrsnLnDev
    myXdouStDev = Empty
    myXlonMeanCnt = Empty: Erase myZdouMean
    myXlonMedianCnt = Empty: Erase myZdouMedian
    myXlonFltrDataCnt = Empty: Erase myZdouFltrData
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getStrtRow() As Long
    getStrtRow = myXlonStrtRow
End Property

Public Property Get getLastRow() As Long
    getLastRow = myXlonLastRow
End Property

Public Property Get getCalRowCnt() As Long
    getCalRowCnt = myXlonCalRowCnt
End Property

Public Property Get getCalColCnt() As Long
    getCalColCnt = myXlonCalColCnt
End Property

Public Property Get getCalData(ByVal i As Long, ByVal j As Long) As Double
    getCalData = myZdouCalData(i, j)
'myZdouCalData(i, 1) : 計算処理後データ配列
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonStrtRow = Empty: myXlonLastRow = Empty
    myXlonCalRowCnt = Empty: myXlonCalColCnt = Empty: Erase myZdouCalData
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letCalRng(ByVal RHS As Long)
    myXlonCalRng = RHS
'myXlonCalRng = -1 : 今回値より前のデータで計算
'myXlonCalRng = 0  : 今回値の前後のデータで計算
'myXlonCalRng = 1  : 今回値より後のデータで計算
End Property

Public Property Let letCalDataRngOptn(ByVal RHS As Long)
    myXlonCalDataRngOptn = RHS
'myXlonCalDataRngOptn = 1 : 全データ範囲で取得
'myXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'myXlonCalDataRngOptn = 3 : 有効データ範囲で取得
End Property

Public Property Let letOrgData(ByRef RHS() As Double)
    myZdouOrgData() = RHS()
'myZdouOrgData(i, j) : 元データ配列
End Property

Public Property Let letBgnRow(ByVal RHS As Long)
    myXlonBgnRow = RHS
End Property

Public Property Let letEndRow(ByVal RHS As Long)
    myXlonEndRow = RHS
End Property

Public Property Let letRgrsnSmplQnt(ByVal RHS As Long)
    myXlonRgrsnSmplQnt = RHS
'myXlonRgrsnSmplQnt : 回帰直線処理するデータ数
End Property

Public Property Let letRgrsnStep(ByVal RHS As Long)
    myXlonRgrsnStep = RHS
End Property

Public Property Let letMeanSmplQnt(ByVal RHS As Long)
    myXlonMeanSmplQnt = RHS
'myXlonMeanSmplQnt : 平均値処理するデータ数
End Property

Public Property Let letMeanStep(ByVal RHS As Long)
    myXlonMeanStep = RHS
End Property

Public Property Let letMedianSmplQnt(ByVal RHS As Long)
    myXlonMedianSmplQnt = RHS
'myXlonMedianSmplQnt : メジアン処理するデータ数
End Property

Public Property Let letMedianStep(ByVal RHS As Long)
    myXlonMedianStep = RHS
End Property

Public Property Let letSigmaLimit(ByVal RHS As Double)
    myXdouSigmaLimit = RHS
'myXdouSigmaLimit : 除外する外れ値の範囲を標準偏差σで指定
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariablesMean()
    myXbisExitFlag = False
    
    If myXlonCalRng < -1 Or myXlonCalRng > 1 Then myXlonCalRng = 0
    If myXlonCalDataRngOptn < 1 Or myXlonCalDataRngOptn > 3 Then myXlonCalDataRngOptn = 1
    
    If PfncbisIsMatrix(myZdouOrgData) = False Then GoTo ExitPath
    
  Dim L As Long, i As Long, j As Long
    L = LBound(myZdouOrgData, 1)
    myXlonInptRowCnt = UBound(myZdouOrgData, 1) - L + 1
    myXlonInptColCnt = UBound(myZdouOrgData, 2) - L + 1
    ReDim myZdouInptData(myXlonInptRowCnt, myXlonInptColCnt) As Double
    For j = LBound(myZdouOrgData, 2) To UBound(myZdouOrgData, 2)
        For i = LBound(myZdouOrgData, 1) To UBound(myZdouOrgData, 1)
            myZdouInptData(i - L + 1, j - L + 1) = myZdouOrgData(i, j)
        Next i
    Next j
    
    If myXlonBgnRow < LBound(myZdouInptData, 1) Then _
        myXlonBgnRow = LBound(myZdouInptData, 1)
    If myXlonEndRow < myXlonBgnRow _
            Or myXlonEndRow > UBound(myZdouInptData, 1) Then _
        myXlonEndRow = UBound(myZdouInptData, 1)
    If myXlonRgrsnSmplQnt <= 0 _
        Or myXlonRgrsnSmplQnt > UBound(myZdouInptData, 1) Then GoTo ExitPath
    If myXlonRgrsnStep <= 0 Or myXlonRgrsnStep > UBound(myZdouInptData, 1) Then _
        myXlonRgrsnStep = 1
    If myXlonMeanSmplQnt <= 0 _
        Or myXlonMeanSmplQnt > UBound(myZdouInptData, 1) Then GoTo ExitPath
    If myXlonMeanStep <= 0 Or myXlonMeanStep > UBound(myZdouInptData, 1) Then _
        myXlonMeanStep = 1
    
    If myXdouSigmaLimit <= 0 Then myXdouSigmaLimit = 3
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'checkP_入力変数内容を確認する
Private Sub checkInputVariablesMedian()
    myXbisExitFlag = False
    
    If myXlonCalRng < -1 Or myXlonCalRng > 1 Then myXlonCalRng = 0
    If myXlonCalDataRngOptn < 1 Or myXlonCalDataRngOptn > 3 Then myXlonCalDataRngOptn = 1
    
    If PfncbisIsMatrix(myZdouOrgData) = False Then GoTo ExitPath
    
  Dim L As Long, i As Long, j As Long
    L = LBound(myZdouOrgData, 1)
    myXlonInptRowCnt = UBound(myZdouOrgData, 1) - L + 1
    myXlonInptColCnt = UBound(myZdouOrgData, 2) - L + 1
    ReDim myZdouInptData(myXlonInptRowCnt, myXlonInptColCnt) As Double
    For j = LBound(myZdouOrgData, 2) To UBound(myZdouOrgData, 2)
        For i = LBound(myZdouOrgData, 1) To UBound(myZdouOrgData, 1)
            myZdouInptData(i - L + 1, j - L + 1) = myZdouOrgData(i, j)
        Next i
    Next j
    
    If myXlonBgnRow < LBound(myZdouInptData, 1) Then _
        myXlonBgnRow = LBound(myZdouInptData, 1)
    If myXlonEndRow < myXlonBgnRow _
            Or myXlonEndRow > UBound(myZdouInptData, 1) Then _
        myXlonEndRow = UBound(myZdouInptData, 1)
    If myXlonRgrsnSmplQnt <= 0 _
        Or myXlonRgrsnSmplQnt > UBound(myZdouInptData, 1) Then GoTo ExitPath
    If myXlonRgrsnStep <= 0 Or myXlonRgrsnStep > UBound(myZdouInptData, 1) Then _
        myXlonRgrsnStep = 1
    If myXlonMedianSmplQnt <= 0 _
        Or myXlonMedianSmplQnt > UBound(myZdouInptData, 1) Then GoTo ExitPath
    If myXlonMedianStep <= 0 Or myXlonMedianStep > UBound(myZdouInptData, 1) Then _
        myXlonMedianStep = 1
    
    If myXdouSigmaLimit <= 0 Then myXdouSigmaLimit = 3
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_平均値を基準に標準偏差外れのデータを除去
Public Sub exeProcMean()
    Call ctrProcMean
End Sub

'PublicP_中央値を基準に標準偏差外れのデータを除去
Public Sub exeProcMedian()
    Call ctrProcMedian
End Sub

'CtrlP_平均値を基準に標準偏差外れのデータを除去
Private Sub ctrProcMean()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariablesMean: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//C:データ用変数を設定
    Call setDataVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//P:計算実行:Y軸データと回帰直線との差分
    Call prsCalDeviationFromRegressionLine
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
'//P:計算実行:標準偏差
    Call prsCalStandardDeviation
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
'//P:計算実行:平均値
    Call prsCalMean
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "5"     'PassFlag
    
'//P:平均値基準で標準偏差外れ値をフィルタ処理
    Call prsProcStandardDeviationOutliersFilterWithMean
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "6"     'PassFlag
    
'//C:出力データを格納
    Call setOutputVariables
    
ExitPath:
    Call initializeModuleVariables
End Sub

'CtrlP_中央値を基準に標準偏差外れのデータを除去
Private Sub ctrProcMedian()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariablesMedian: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//C:データ用変数を設定
    Call setDataVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//P:計算実行:Y軸データと回帰直線との差分
    Call prsCalDeviationFromRegressionLine
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
'//P:計算実行:標準偏差
    Call prsCalStandardDeviation
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
'//P:計算実行:中央値
    Call prsCalMedian
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "5"     'PassFlag
    
'//P:中央値基準で標準偏差外れ値をフィルタ処理
    Call prsProcStandardDeviationOutliersFilterWithMedian
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "6"     'PassFlag
    
'//C:出力データを格納
    Call setOutputVariables
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SetP_データ用変数を設定する
Private Sub setDataVariables()
    myXbisExitFlag = False
    
  Dim myXlonBgnNo As Long, myXlonEndNo As Long
    myXlonBgnNo = LBound(myZdouInptData, 1)
    myXlonEndNo = UBound(myZdouInptData, 1)
    Call PfixArrayPartColumnNumeric(myXlonOrgYCnt, myZdouOrgY, _
            myZdouInptData, 1, myXlonBgnNo, myXlonEndNo)
    If myXlonOrgYCnt <= 0 Then GoTo ExitPath
    
  Dim i As Long
    myXlonXdataCnt = myXlonOrgYCnt
    ReDim myZdouXdata(myXlonOrgYCnt) As Double
    For i = LBound(myZdouOrgY) To UBound(myZdouOrgY)
        myZdouXdata(i) = i
    Next i
    
    myXlonStrtNo = myXlonBgnNo
    myXlonLastNo = myXlonEndNo
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables( _
            ByVal myXlonCalRng As Long, _
            ByVal myXlonCalSmplQnt As Long, ByVal myXlonCalStep As Long)
    myXbisExitFlag = False
    myXlonPreOfst = Empty: myXlonPstOfst = Empty
    
'//計算するオフセット量を設定
    Select Case myXlonCalRng
        Case -1
            myXlonPreOfst = (myXlonCalSmplQnt - 1) * myXlonCalStep
            myXlonPstOfst = 0
        Case 1
            myXlonPreOfst = 0
            myXlonPstOfst = (myXlonCalSmplQnt - 1) * myXlonCalStep
        Case Else
            myXlonPreOfst = Int(myXlonCalSmplQnt / 2) * myXlonCalStep
            myXlonPstOfst = Int(myXlonCalSmplQnt / 2) * myXlonCalStep
    End Select
    
'//計算が有効な最初と最後の位置を設定
    myXlonStrtNo = myXlonStrtNo + myXlonPreOfst
    myXlonLastNo = myXlonLastNo - myXlonPstOfst
    If myXlonLastNo < myXlonStrtNo Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:Y軸データと回帰直線との差分
Private Sub prsCalDeviationFromRegressionLine()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Call setControlVariables(myXlonCalRng, myXlonRgrsnSmplQnt, myXlonRgrsnStep)
    If myXbisExitFlag = True Then GoTo ExitPath
  Dim myXlonCalStrtNo As Long, myXlonCalLastNo As Long
    myXlonCalStrtNo = LBound(myZdouOrgY) + myXlonPreOfst
    myXlonCalLastNo = UBound(myZdouOrgY) - myXlonPstOfst
    
'//台形波形を生成
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonOrgYCnt, myXlonCalStrtNo, myXlonCalLastNo, -1, -1, 0)
    If myXlonAdjstValCnt <> myXlonOrgYCnt Then GoTo ExitPath
    
'//計算処理:Y軸データと回帰直線との差分
  Dim i As Long, myXlonBgnNo As Long, myXlonEndNo As Long
  Dim myXlonXTmpCnt As Long, myZdouXTmp() As Double
  Dim myXlonYTmpCnt As Long, myZdouYTmp() As Double
  Dim myXdouSlp As Double, myXdouIntrcpt As Double, myXdouRgrsn As Double
    
    myXlonRgrsnLnDevCnt = myXlonOrgYCnt
    ReDim myZdouRgrsnLnDev(myXlonRgrsnLnDevCnt) As Double

    For i = LBound(myZdouOrgY) To UBound(myZdouOrgY)
        myXlonBgnNo = i - myXlonPreOfst + myZlonAdjstVal(i)
        myXlonEndNo = i + myXlonPstOfst + myZlonAdjstVal(i)
        Call Pfix1DArrayExtractNumeric(myXlonXTmpCnt, myZdouXTmp, _
                myZdouXdata, myXlonBgnNo, myXlonEndNo, myXlonRgrsnStep)
        If myXlonXTmpCnt <= 0 Then GoTo ExitPath
        Call Pfix1DArrayExtractNumeric(myXlonYTmpCnt, myZdouYTmp, _
                myZdouOrgY, myXlonBgnNo, myXlonEndNo, myXlonRgrsnStep)
        If myXlonYTmpCnt <= 0 Then GoTo ExitPath
        Call PfixSlopeIntercept(myXdouSlp, myXdouIntrcpt, myZdouXTmp, myZdouYTmp)
        myXdouRgrsn = myXdouSlp * myZdouXdata(i) + myXdouIntrcpt
        myZdouRgrsnLnDev(i) = myZdouOrgY(i) - myXdouRgrsn
    Next i
    
    Erase myZlonAdjstVal: Erase myZdouXTmp: Erase myZdouYTmp
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:標準偏差
Private Sub prsCalStandardDeviation()
    myXbisExitFlag = False
    
'//計算処理:不偏標本標準偏差
  Dim myXlonTmpCnt As Long, myZdouTmp() As Double
    Call Pfix1DArrayExtractNumeric(myXlonTmpCnt, myZdouTmp, _
            myZdouRgrsnLnDev, myXlonStrtNo, myXlonLastNo)
    If myXlonTmpCnt <= 0 Then GoTo ExitPath
    myXdouStDev = PfncdouStDevS(myZdouTmp)
    
    Erase myZdouTmp
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:平均値
Private Sub prsCalMean()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Call setControlVariables(myXlonCalRng, myXlonMeanSmplQnt, myXlonMeanStep)
    If myXbisExitFlag = True Then GoTo ExitPath
  Dim myXlonCalStrt As Long, myXlonCalLast As Long
    myXlonCalStrt = LBound(myZdouOrgY) + myXlonPreOfst
    myXlonCalLast = UBound(myZdouOrgY) - myXlonPstOfst
    
'//台形波形を生成
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonOrgYCnt, myXlonCalStrt, myXlonCalLast, -1, -1, 0)
    If myXlonAdjstValCnt <> myXlonOrgYCnt Then GoTo ExitPath
    
'//計算処理:平均値
  Dim i As Long, myXlonBgnNo As Long, myXlonEndNo As Long
  Dim myXlonTmpCnt As Long, myZdouTmp() As Double
    
    myXlonMeanCnt = myXlonOrgYCnt
    ReDim myZdouMean(myXlonMeanCnt) As Double

    For i = LBound(myZdouOrgY) To UBound(myZdouOrgY)
        myXlonBgnNo = i - myXlonPreOfst + myZlonAdjstVal(i)
        myXlonEndNo = i + myXlonPstOfst + myZlonAdjstVal(i)
        Call Pfix1DArrayExtractNumeric(myXlonTmpCnt, myZdouTmp, _
                myZdouRgrsnLnDev, myXlonBgnNo, myXlonEndNo, myXlonMeanStep)
        If myXlonTmpCnt <= 0 Then GoTo ExitPath
        myZdouMean(i) = PfncdouArithmeticMean(myZdouTmp)
    Next i
    
    Erase myZlonAdjstVal: Erase myZdouTmp
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:中央値
Private Sub prsCalMedian()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Call setControlVariables(myXlonCalRng, myXlonMedianSmplQnt, myXlonMedianStep)
    If myXbisExitFlag = True Then GoTo ExitPath
  Dim myXlonCalStrt As Long, myXlonCalLast As Long
    myXlonCalStrt = LBound(myZdouOrgY) + myXlonPreOfst
    myXlonCalLast = UBound(myZdouOrgY) - myXlonPstOfst
    
'//台形波形を生成
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonOrgYCnt, myXlonCalStrt, myXlonCalLast, -1, -1, 0)
    If myXlonAdjstValCnt <> myXlonOrgYCnt Then GoTo ExitPath
    
'//計算処理:中央値
  Dim i As Long, myXlonBgnNo As Long, myXlonEndNo As Long
  Dim myXlonTmpCnt As Long, myZdouTmp() As Double
    
    myXlonMedianCnt = myXlonOrgYCnt
    ReDim myZdouMedian(myXlonMedianCnt) As Double

    For i = LBound(myZdouOrgY) To UBound(myZdouOrgY)
        myXlonBgnNo = i - myXlonPreOfst + myZlonAdjstVal(i)
        myXlonEndNo = i + myXlonPstOfst + myZlonAdjstVal(i)
        Call Pfix1DArrayExtractNumeric(myXlonTmpCnt, myZdouTmp, _
                myZdouRgrsnLnDev, myXlonBgnNo, myXlonEndNo, myXlonMedianStep)
        If myXlonTmpCnt <= 0 Then GoTo ExitPath
        myZdouMedian(i) = PincdouMedian(myZdouTmp)
    Next i
    
    Erase myZlonAdjstVal: Erase myZdouTmp
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_平均値基準で標準偏差外れ値をフィルタ処理
Private Sub prsProcStandardDeviationOutliersFilterWithMean()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Call setControlVariables(0, coXlonMdnFltrQnt, 1)
    If myXbisExitFlag = True Then GoTo ExitPath
    myXlonCalStrtNo = LBound(myZdouOrgY) + myXlonPreOfst
    myXlonCalLastNo = UBound(myZdouOrgY) - myXlonPstOfst
    
'//台形波形を生成
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonOrgYCnt, myXlonCalStrtNo, myXlonCalLastNo, -1, -1, 0)
    If myXlonAdjstValCnt <> myXlonOrgYCnt Then GoTo ExitPath
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Select Case myXlonCalDataRngOptn
        Case 1
            myXlonCalStrtNo = LBound(myZdouOrgY)
            myXlonCalLastNo = UBound(myZdouOrgY)
        Case 2
            myXlonCalStrtNo = myXlonBgnRow
            myXlonCalLastNo = myXlonEndRow
        Case Else
            myXlonCalStrtNo = myXlonStrtNo
            myXlonCalLastNo = myXlonLastNo
    End Select
    
'//標準偏差外れ値のフィルタ処理
  Dim i As Long, n As Long: n = 0
  Dim myXdouCalVal As Double, myXlonBgnNo As Long, myXlonEndNo As Long
  Dim myXlonTmpCnt As Long, myZdouTmp() As Double
    
    myXlonFltrDataCnt = myXlonCalLastNo - myXlonCalStrtNo + 1
    ReDim myZdouFltrData(myXlonFltrDataCnt) As Double

    For i = myXlonCalStrtNo To myXlonCalLastNo
        n = n + 1
        
    '//計算処理:｜回帰直線との差分－平均値｜／標準偏差
        myXdouCalVal = Abs(myZdouRgrsnLnDev(i) - myZdouMean(i)) / myXdouStDev
        
    '//計算処理:標準偏差のX倍より大きいか判断
        If myXdouCalVal <= myXdouSigmaLimit Then
        '//元データを取得
            myZdouFltrData(n) = myZdouOrgY(i)
            
        Else
        '//元データの前後3データ分の中央値を取得
            myXlonBgnNo = i - myXlonPreOfst + myZlonAdjstVal(i)
            myXlonEndNo = i + myXlonPreOfst + myZlonAdjstVal(i)
            Call Pfix1DArrayExtractNumeric(myXlonTmpCnt, myZdouTmp, _
                    myZdouOrgY, myXlonBgnNo, myXlonEndNo)
            If myXlonTmpCnt <> coXlonMdnFltrQnt Then GoTo ExitPath
            myZdouFltrData(n) = PincdouMedian(myZdouTmp)
            
        End If
        
    Next i
    
    Erase myZlonAdjstVal: Erase myZdouTmp
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_中央値基準で標準偏差外れ値をフィルタ処理
Private Sub prsProcStandardDeviationOutliersFilterWithMedian()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Call setControlVariables(0, coXlonMdnFltrQnt, 1)
    If myXbisExitFlag = True Then GoTo ExitPath
    myXlonCalStrtNo = LBound(myZdouOrgY) + myXlonPreOfst
    myXlonCalLastNo = UBound(myZdouOrgY) - myXlonPstOfst
    
'//台形波形を生成
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonOrgYCnt, myXlonCalStrtNo, myXlonCalLastNo, -1, -1, 0)
    If myXlonAdjstValCnt <> myXlonOrgYCnt Then GoTo ExitPath
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Select Case myXlonCalDataRngOptn
        Case 1
            myXlonCalStrtNo = LBound(myZdouOrgY)
            myXlonCalLastNo = UBound(myZdouOrgY)
        Case 2
            myXlonCalStrtNo = myXlonBgnRow
            myXlonCalLastNo = myXlonEndRow
        Case Else
            myXlonCalStrtNo = myXlonStrtNo
            myXlonCalLastNo = myXlonLastNo
    End Select
    
'//標準偏差外れ値のフィルタ処理
  Dim i As Long, n As Long: n = 0
  Dim myXdouCalVal As Double, myXlonBgnNo As Long, myXlonEndNo As Long
  Dim myXlonTmpCnt As Long, myZdouTmp() As Double
    
    myXlonFltrDataCnt = myXlonCalLastNo - myXlonCalStrtNo + 1
    ReDim myZdouFltrData(myXlonFltrDataCnt) As Double
    
    For i = myXlonCalStrtNo To myXlonCalLastNo
        n = n + 1
    
    '//計算処理:｜回帰直線との差分－平均値｜／標準偏差
        myXdouCalVal = Abs(myZdouRgrsnLnDev(i) - myZdouMedian(i)) / myXdouStDev
        
    '//計算処理:標準偏差のX倍より大きいか判断
        If myXdouCalVal <= myXdouSigmaLimit Then
        '//元データを取得
            myZdouFltrData(n) = myZdouOrgY(i)
            
        Else
        '//元データの前後3データ分の中央値を取得
            myXlonBgnNo = i - myXlonPreOfst + myZlonAdjstVal(i)
            myXlonEndNo = i + myXlonPreOfst + myZlonAdjstVal(i)
            Call Pfix1DArrayExtractNumeric(myXlonTmpCnt, myZdouTmp, _
                    myZdouOrgY, myXlonBgnNo, myXlonEndNo)
            If myXlonTmpCnt <> coXlonMdnFltrQnt Then GoTo ExitPath
            myZdouFltrData(n) = PincdouMedian(myZdouTmp)
            
        End If
        
    Next i
    
    Erase myZlonAdjstVal: Erase myZdouTmp
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_出力データを格納する
Private Sub setOutputVariables()
    myXbisExitFlag = False
    
    myXlonStrtRow = myXlonCalStrtNo
    myXlonLastRow = myXlonCalLastNo
    
  Dim i As Long
    myXlonCalRowCnt = myXlonFltrDataCnt
    myXlonCalColCnt = 1
    ReDim myZdouCalData(myXlonCalRowCnt, myXlonCalColCnt) As Double
    For i = LBound(myZdouFltrData) To UBound(myZdouFltrData)
        myZdouCalData(i, 1) = myZdouFltrData(i)
    Next i
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｆ_指定変数が数値行列かどうかをチェックする
Private Function PfncbisIsMatrix(ByRef myZvarMtrx As Variant) As Boolean
    PfncbisIsMatrix = False
  Dim myXdouTmp As Double, L1 As Long, L2 As Long
    On Error GoTo ExitPath
    L1 = LBound(myZvarMtrx, 1): L2 = LBound(myZvarMtrx, 2)
    myXdouTmp = myZvarMtrx(L1, L2)
    On Error GoTo 0
    PfncbisIsMatrix = True
ExitPath:
End Function

 '定型Ｐ_2次元の数値配列の指定列から一部を1次元配列で取得する
Private Sub PfixArrayPartColumnNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, ByVal myXlonExtrctCol As Long, _
            ByVal myXlonBgnRow As Long, ByVal myXlonEndRow As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 抽出データ配列
'myZdouOrgData(i, j) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgnRow < LBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow > UBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow < myXlonBgnRow Then Exit Sub
    If myXlonExtrctCol < LBound(myZdouOrgData, 2) _
        Or myXlonExtrctCol > UBound(myZdouOrgData, 2) Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData, 1) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgnRow, myXlonExtrctCol)
    Li = LBound(myZdouOrgData, 1)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgnRow To myXlonEndRow Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i, myXlonExtrctCol)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_1次元の数値配列から一部を取得する
Private Sub Pfix1DArrayExtractNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, _
            ByVal myXlonBgn As Long, ByVal myXlonEnd As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 取得データ配列
'myZdouOrgData(i) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgn < LBound(myZdouOrgData) Then Exit Sub
    If myXlonEnd > UBound(myZdouOrgData) Then Exit Sub
    If myXlonEnd < myXlonBgn Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgn): Li = LBound(myZdouOrgData)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgn To myXlonEnd Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_整数で台形波形を生成する
Private Sub PfixTrapezoidWaveLong( _
            myXlonCalDataCnt As Long, myZlonCalData() As Long, _
            ByVal myXlonDataQnt As Long, _
            ByVal myXlonInflctPntBgn As Long, ByVal myXlonInflctPntEnd As Long, _
            Optional ByVal coXlonStrtStp As Long = -1, _
            Optional ByVal coXlonLastStp As Long = 1, _
            Optional ByVal coXlonCnstVal As Long = 0)
'myZlonCalData(i) : 取得データ配列
'myXlonDataQnt : 必要なデータ数
'myXlonInflctPntBgn : 開始側の変曲点
'myXlonInflctPntEnd : 終了側の変曲点
'coXlonStrtStp : 開始側のデータ変化ステップ
'coXlonLastStp : 終了側のデータ変化ステップ
'coXlonCnstVal : 一定状態の値
    myXlonCalDataCnt = Empty:  Erase myZlonCalData
    If myXlonDataQnt <= 0 Then Exit Sub
    If myXlonInflctPntBgn < 1 Then Exit Sub
    If myXlonInflctPntEnd > myXlonDataQnt Then Exit Sub
    If myXlonInflctPntBgn > myXlonInflctPntEnd Then Exit Sub
    On Error GoTo ExitPath
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    Erase myZlonTmp
    On Error GoTo 0
  Dim i As Long, n As Long
    myXlonCalDataCnt = myXlonDataQnt
    i = myXlonCalDataCnt + L - 1: ReDim myZlonCalData(i) As Long
    If myXlonInflctPntBgn = 1 Then GoTo JumpPath1
    n = myXlonInflctPntBgn
    For i = 1 To myXlonInflctPntBgn - 1
        n = n - 1
        myZlonCalData(i) = coXlonCnstVal - coXlonStrtStp * n
    Next i
JumpPath1:
    If coXlonCnstVal = 0 Then GoTo JumpPath2
    For i = myXlonInflctPntBgn To myXlonInflctPntEnd
        myZlonCalData(i) = coXlonCnstVal
    Next i
JumpPath2:
    If myXlonInflctPntEnd = 1 Then Exit Sub
    n = 0
    For i = myXlonInflctPntEnd + 1 To myXlonDataQnt
        n = n + 1
        myZlonCalData(i) = coXlonCnstVal + coXlonLastStp * n
    Next i
ExitPath:
End Sub

 '定型Ｐ_配列計算-2配列の傾き＆切片
Private Sub PfixSlopeIntercept( _
            myXdouSlp As Double, myXdouIntcpt As Double, _
            ByRef myZdouOrgX() As Double, ByRef myZdouOrgY() As Double)
'myZdouOrgX(i) : X軸の元データ配列
'myZdouOrgY(i) : Y軸の元データ配列
    myXdouSlp = Empty: myXdouIntcpt = Empty
    If UBound(myZdouOrgX) <> UBound(myZdouOrgY) Then Exit Sub
  Dim n As Long: n = UBound(myZdouOrgX) - LBound(myZdouOrgX) + 1
  Dim i As Long, SY2 As Double, SX2 As Double, SY As Double, SXY As Double, SX As Double
    For i = LBound(myZdouOrgX) To UBound(myZdouOrgX)
        SY2 = SY2 + (myZdouOrgY(i)) ^ 2
        SX2 = SX2 + (myZdouOrgX(i)) ^ 2
        SY = SY + myZdouOrgY(i)
        SXY = SXY + myZdouOrgX(i) * myZdouOrgY(i)
        SX = SX + myZdouOrgX(i)
    Next i
    myXdouSlp = (n * SXY - SY * SX) / (n * SX2 - SX ^ 2)
    myXdouIntcpt = (SX2 * SY - SXY * SX) / (n * SX2 - SX ^ 2)
End Sub

 '定型Ｆ_配列計算-不偏標本標準偏差
Private Function PfncdouStDevS(ByRef myZdouOrgData() As Double) As Double
'myZdouOrgData(i) : 元データ配列
    PfncdouStDevS = Empty
  Dim myXdouSumSq As Double: myXdouSumSq = 0
  Dim myXdouSum As Double: myXdouSum = 0
  Dim i As Long, n As Long: n = 0
    For i = LBound(myZdouOrgData) To UBound(myZdouOrgData)
        n = n + 1
        myXdouSumSq = myXdouSumSq + (myZdouOrgData(i)) ^ 2
        myXdouSum = myXdouSum + myZdouOrgData(i)
    Next i
    If n <= 1 Then Exit Function
    PfncdouStDevS = ((myXdouSumSq - (myXdouSum ^ 2) / n) / (n - 1)) ^ 0.5
End Function

 '定型Ｆ_配列計算-相加平均
Private Function PfncdouArithmeticMean(ByRef myZdouOrgData() As Double) As Double
'myZdouOrgData(i) : 元データ配列
    PfncdouArithmeticMean = Empty
  Dim myXdouAveTmp As Double: myXdouAveTmp = 0
  Dim i As Long, n As Long: n = 0
    For i = LBound(myZdouOrgData) To UBound(myZdouOrgData)
        n = n + 1: myXdouAveTmp = myXdouAveTmp + myZdouOrgData(i)
    Next i
    PfncdouArithmeticMean = myXdouAveTmp / n
End Function

 '定型Ｆ_配列計算-中央値
Private Function PincdouMedian(ByRef myZdouOrgData() As Double) As Double
'Includes PfixQuickSortOfNumeric1DArray
'myZdouOrgData(i) : 元データ配列
    PincdouMedian = Empty
  Dim myXlonLbnd As Long: myXlonLbnd = LBound(myZdouOrgData)
  Dim myXlonUbnd As Long: myXlonUbnd = UBound(myZdouOrgData)
  Dim myXlonCnt As Long: myXlonCnt = myXlonUbnd - myXlonLbnd + 1
  Dim myZdouTmp() As Double, i As Long
    ReDim myZdouTmp(myXlonUbnd) As Double
    For i = myXlonLbnd To myXlonUbnd
        myZdouTmp(i) = myZdouOrgData(i)
    Next i
    Call PfixQuickSortOfNumeric1DArray(myZdouTmp, myXlonLbnd, myXlonUbnd, 1)
    If myXlonCnt Mod 2 = 1 Then
        PincdouMedian = myZdouTmp(Int((myXlonLbnd + myXlonUbnd) / 2))
    Else
      Dim m As Long, n As Long, myXdouM1 As Double, myXdouM2 As Double
        m = Int((myXlonLbnd + myXlonUbnd) / 2): n = m + 1
        myXdouM1 = myZdouTmp(m): myXdouM2 = myZdouTmp(n)
        PincdouMedian = (myXdouM1 + myXdouM2) / 2
    End If
    Erase myZdouTmp
End Function

 '定型Ｐ_1次元の数値配列のクイックソート
Private Sub PfixQuickSortOfNumeric1DArray( _
            myZdouSrtData() As Double, _
            ByVal myXlonLbnd As Long, ByVal myXlonUbnd As Long, _
            Optional ByVal coXlonOrder As Long = 1)
'myZdouSrtData(i) : 数値情報
'coXlonOrder = 1    : 昇順 (Ascending Order)
'coXlonOrder = -1   : 降順 (Descending Order)
    If myXlonUbnd <= myXlonLbnd Then Exit Sub
  Dim i As Long, j As Long
  Dim myXdouMed As Double, myXdouTmp As Double
    myXdouMed = myZdouSrtData(Int((myXlonLbnd + myXlonUbnd) / 2))
    i = myXlonLbnd: j = myXlonUbnd
    Do
        Do While coXlonOrder * (myZdouSrtData(i) - myXdouMed) < 0
            i = i + 1
        Loop
        Do While coXlonOrder * (myZdouSrtData(j) - myXdouMed) > 0
            j = j - 1
        Loop
        If i >= j Then Exit Do
        myXdouTmp = myZdouSrtData(i)
        myZdouSrtData(i) = myZdouSrtData(j)
        myZdouSrtData(j) = myXdouTmp
        i = i + 1: j = j - 1
    Loop
    If myXlonLbnd < i - 1 Then _
        Call PfixQuickSortOfNumeric1DArray(myZdouSrtData, myXlonLbnd, i - 1, coXlonOrder)
    If myXlonUbnd > j + 1 Then _
        Call PfixQuickSortOfNumeric1DArray(myZdouSrtData, j + 1, myXlonUbnd, coXlonOrder)
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_データ配列から標準偏差外れのデータを除去する
'Private Sub instCOutlierFltrMean()
''平均値を基準に標準偏差外れのデータを除去
'  Const coXlonCalRng As Long = 0
'    'coXlonCalRng = -1 : 今回値より前のデータで平均化
'    'coXlonCalRng = 0  : 今回値の前後のデータで平均化
'    'coXlonCalRng = 1  : 今回値より後のデータで平均化
'  Const coXlonCalDataRngOptn As Long = 3
'    'coXlonCalDataRngOptn = 1 : 全データ範囲で取得
'    'coXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'    'coXlonCalDataRngOptn = 3 : 有効データ範囲で取得
'  Const coXlonBgnRow As Long = 1
'  Const coXlonEndRow As Long = 0
'  Const coXlonRgrsnSmplQnt As Long = 11
'    'coXlonRgrsnSmplQnt : 回帰直線処理するデータ数
'  Const coXlonRgrsnStep As Long = 1
'  Const coXlonMeanSmplQnt As Long = 11
'    'coXlonMeanSmplQnt : 平均値処理するデータ数
'  Const coXlonMeanStep As Long = 1
'  Const coXdouSigmaLimit As Long = 3
'    'coXdouSigmaLimit : 除外する外れ値の範囲を標準偏差σで指定
''  Dim myXlonOrgRowCnt As Long, myXlonOrgColCnt As Long, myZdouOrgData() As Double
''    'myZdouOrgData(i, 1) : 元データ配列
''    myXlonOrgRowCnt = UBound(myZdouOrgData, 1)
''    myXlonOrgColCnt = 1
'  Dim Lc As Long
''  Dim myXlonStrtRow As Long, myXlonLastRow As Long
''  Dim myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
''    'myZdouCalData(i, 1) : 計算処理後データ配列
''//COutlierFltrクラスモジュールのインスタンス生成
'  Dim myXinsOutlierFltr As COutlierFltr: Set myXinsOutlierFltr = New COutlierFltr
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long, j As Long
'    With myXinsOutlierFltr
'    '//クラス内変数への入力
'        .letCalRng = coXlonCalRng
'        .letCalDataRngOptn = coXlonCalDataRngOptn
'        .letOrgData = myZdouOrgData
'        .letBgnRow = coXlonBgnRow
'        .letEndRow = coXlonEndRow
'        .letRgrsnSmplQnt = coXlonRgrsnSmplQnt
'        .letRgrsnStep = coXlonRgrsnStep
'        .letMeanSmplQnt = coXlonMeanSmplQnt
'        .letMeanStep = coXlonMeanStep
'        .letSigmaLimit = coXdouSigmaLimit
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProcMean
'        myXlonStrtRow = .getStrtRow
'        myXlonLastRow = .getLastRow
'        myXlonCalRowCnt = .getCalRowCnt
'        myXlonCalColCnt = .getCalColCnt
'        If myXlonCalRowCnt <= 0 Or myXlonCalColCnt <= 0 Then GoTo JumpPath
'        ReDim myZdouCalData(myXlonCalRowCnt, myXlonCalColCnt) As Double
'        Lc = .getOptnBase
'        For i = 1 To myXlonCalRowCnt
'            myZdouCalData(i + Lo - 1, Lo) = .getCalData(i, Lc)
'        Next i
'    End With
'JumpPath:
'    Set myXinsOutlierFltr = Nothing
''    Call variablesOfCOutlierFltr(myXlonCalRowCnt, myZdouCalData)    'Debug.Print
'End Sub
'
''◆ClassProc名_データ配列から標準偏差外れのデータを除去する
'Private Sub instCOutlierFltrMedian()
''中央値を基準に標準偏差外れのデータを除去
'  Const coXlonCalRng As Long = 0
'    'coXlonCalRng = -1 : 今回値より前のデータで平均化
'    'coXlonCalRng = 0  : 今回値の前後のデータで平均化
'    'coXlonCalRng = 1  : 今回値より後のデータで平均化
'  Const coXlonCalDataRngOptn As Long = 3
'    'coXlonCalDataRngOptn = 1 : 全データ範囲で取得
'    'coXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'    'coXlonCalDataRngOptn = 3 : 有効データ範囲で取得
'  Const coXlonBgnRow As Long = 1
'  Const coXlonEndRow As Long = 0
'  Const coXlonRgrsnSmplQnt As Long = 11
'    'coXlonRgrsnSmplQnt : 回帰直線処理するデータ数
'  Const coXlonRgrsnStep As Long = 1
'  Const coXlonMedianSmplQnt As Long = 11
'    'coXlonMedianSmplQnt : メジアン処理するデータ数
'  Const coXlonMedianStep As Long = 1
'  Const coXdouSigmaLimit As Long = 3
'    'coXdouSigmaLimit : 除外する外れ値の範囲を標準偏差σで指定
''  Dim myXlonOrgRowCnt As Long, myXlonOrgColCnt As Long, myZdouOrgData() As Double
'    'myZdouOrgData(i, 1) : 元データ配列
''    myXlonOrgRowCnt = UBound(myZdouOrgData, 1)
''    myXlonOrgColCnt = 1
'  Dim Lc As Long
''  Dim myXlonStrtRow As Long, myXlonLastRow As Long
''  Dim myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
'    'myZdouCalData(i, 1) : 計算処理後データ配列
''//COutlierFltrクラスモジュールのインスタンス生成
'  Dim myXinsOutlierFltr As COutlierFltr: Set myXinsOutlierFltr = New COutlierFltr
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long, j As Long
'    With myXinsOutlierFltr
'    '//クラス内変数への入力
'        .letCalRng = coXlonCalRng
'        .letCalDataRngOptn = coXlonCalDataRngOptn
'        .letOrgData = myZdouOrgData
'        .letBgnRow = coXlonBgnRow
'        .letEndRow = coXlonEndRow
'        .letRgrsnSmplQnt = coXlonRgrsnSmplQnt
'        .letRgrsnStep = coXlonRgrsnStep
'        .letMedianSmplQnt = coXlonMedianSmplQnt
'        .letMedianStep = coXlonMedianStep
'        .letSigmaLimit = coXdouSigmaLimit
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProcMedian
'        myXlonStrtRow = .getStrtRow
'        myXlonLastRow = .getLastRow
'        myXlonCalRowCnt = .getCalRowCnt
'        myXlonCalColCnt = .getCalColCnt
'        If myXlonCalRowCnt <= 0 Or myXlonCalColCnt <= 0 Then GoTo JumpPath
'        ReDim myZdouCalData(myXlonCalRowCnt, myXlonCalColCnt) As Double
'        Lc = .getOptnBase
'        For i = 1 To myXlonCalRowCnt
'            myZdouCalData(i + Lo - 1, Lo) = .getCalData(i + Lc - 1, Lc)
'        Next i
'    End With
'JumpPath:
'    Set myXinsOutlierFltr = Nothing
''    Call variablesOfCOutlierFltr(myXlonCalRowCnt, myZdouCalData)    'Debug.Print
'End Sub
'
'Private Sub variablesOfCOutlierFltr( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//COutlierFltrクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim i As Long, j As Long
'    For i = LBound(myZvarField, 1) To UBound(myZvarField, 1)
'        For j = LBound(myZvarField, 2) To UBound(myZvarField, 2)
'            Debug.Print "データ" & i & "," & j & ": " & myZvarField(i, j)
'        Next j
'    Next i
'End Sub

      <br></pre>
    </body>
  </html>
