<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PincGetNameSortedFolderTreeWithComeBack
'Includes PincGetAndSortSubFolderArray
'Includes PfixGetSubFolderArray
'Includes PincGetNotHiddenSubFileArray
'Includes PfncbisCheckFileAttribute
'Includes PincSortFileArray
'Includes PfixGetFolderFileStringInformationAndObject2DArray
'Includes PfixQuickSortOfStringObject2DArray
'Includes PfixGetFolderFileStringInformationFor1DArray

Option Explicit
Option Base 1

'◆ClassProc名_指定ディレクトリ階層内のフォルダファイル一覧をファイル名でソートして取得する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CFldrrFileLstNameSort"
  
'//モジュール内定数
  Private Const coXlonAtrbt As Long = 2
  Private Const coXlonOrder As Long = 1
  Private Const coXlonMode As Long = 1
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonFldrFileCnt As Long, myZobjFldrFile() As Object, _
            myZstrFldrFileName() As String, myZstrFldrFilePath() As String, _
            myZlonFldrFileLyrNo() As Long
  
'//PropertySetLet_入力制御信号
  Private myXbisNotOutFileInfo As Boolean
  
'//PropertySetLet_入力データ
  Private myXstrDirPath As String, myXlonLyrs As Long
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getFldrFileCnt() As Long
    getFldrFileCnt = myXlonFldrFileCnt
End Property

Public Property Get getFldrFileAry(ByVal i As Long) As Object
    Set getFldrFileAry = myZobjFldrFile(i)
'myZobjFldrFile(i) : フォルダファイル
End Property

Public Property Get getFldrFileNameAry(ByVal i As Long) As String
    getFldrFileNameAry = myZstrFldrFileName(i)
'myZstrFldrFileName() : フォルダファイル名
End Property

Public Property Get getFldrFilePathAry(ByVal i As Long) As String
    getFldrFilePathAry = myZstrFldrFilePath(i)
'myZstrFldrFilePath(i) : フォルダファイルパス
End Property

Public Property Get getFldrFileLyrNoAry(ByVal i As Long) As Long
    getFldrFileLyrNoAry = myZlonFldrFileLyrNo(i)
'myZlonFldrFileLyrNo(i) : フォルダファイル階層
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonFldrFileCnt = Empty: Erase myZobjFldrFile
    Erase myZstrFldrFileName: Erase myZstrFldrFilePath
    Erase myZlonFldrFileLyrNo
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letNotOutFileInfo(ByVal RHS As Boolean)
    myXbisNotOutFileInfo = RHS
'myXbisNotOutFileInfo = False : ファイルオブジェクとファイル情報を両方出力する
'myXbisNotOutFileInfo = True  : ファイルオブジェクトのみ出力してファイル情報は出力しない
End Property

Public Property Let letDirPath(ByVal RHS As String)
    myXstrDirPath = RHS
End Property

Public Property Let letLyrs(ByVal RHS As Long)
    myXlonLyrs = RHS
'myXlonLyrs = 0   : 全フォルダ階層取得
'myXlonLyrs = 1～ : 取得したいフォルダ階層数
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXstrDirPath = "" Then GoTo ExitPath
    If myXlonLyrs < 0 Then myXlonLyrs = 0
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//S:ディレクトリ階層内のファイル一覧を取得
    Call snsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//P:ファイル情報取得
    Call prsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SnsP_ディレクトリ階層内のファイル一覧をファイル名でソートして取得
Private Sub snsProc()
    myXbisExitFlag = False
    
'//フォルダ階層一覧を階層ごとにフォルダ名でソートして取得
  Dim myXlonFldrTreeCnt As Long, myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long
    Call PincGetNameSortedFolderTreeWithComeBack( _
            myXlonFldrTreeCnt, myZobjFldrTree, myZlonFldrLyrNo, _
            myXstrDirPath, myXlonLyrs)
    
'//ディレクトリ階層内の隠しファイル以外のファイル一覧をファイル名でソートして取得
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim k As Long, n As Long: k = 0: n = L - 1
  Dim i As Long, j As Long
  Dim myXobjDir As Object
  Dim myXlonTmpCnt As Long, myZobjTmpFile() As Object, myZobjBufFile() As Object
    
    For i = LBound(myZobjFldrTree) To UBound(myZobjFldrTree)
        k = k + 1
        Set myXobjDir = Nothing
        Set myXobjDir = myZobjFldrTree(i)
        
        n = n + 1
        ReDim Preserve myZobjFldrFile(n) As Object
        ReDim Preserve myZlonFldrFileLyrNo(n) As Long
        Set myZobjFldrFile(n) = myXobjDir
        myZlonFldrFileLyrNo(n) = myZlonFldrLyrNo(i)
 
    '//ディレクトリ階層内の隠しファイル以外のファイル一覧を取得
        Call PincGetNotHiddenSubFileArray(myXlonTmpCnt, myZobjTmpFile, myXobjDir, coXlonAtrbt)
        If myXlonTmpCnt <= 0 Then GoTo NextPath
        
    '//ファイル一覧をファイル名順にソート
        Call PincSortFileArray(myXlonTmpCnt, myZobjBufFile, myZobjTmpFile, coXlonOrder, coXlonMode)
        If myXlonTmpCnt <= 0 Then GoTo NextPath
        
        For j = LBound(myZobjBufFile) To UBound(myZobjBufFile)
            n = n + 1
            ReDim Preserve myZobjFldrFile(n) As Object
            ReDim Preserve myZlonFldrFileLyrNo(n) As Long
            Set myZobjFldrFile(n) = myZobjBufFile(j)
            myZlonFldrFileLyrNo(n) = myZlonFldrLyrNo(i)
        Next j
        
NextPath:
    Next i
    myXlonFldrFileCnt = n - L + 1
    
    myXlonFldrTreeCnt = Empty: Erase myZobjFldrTree: Erase myZlonFldrLyrNo
    Set myXobjDir = Nothing: Erase myZobjTmpFile: Erase myZobjBufFile
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_ファイル情報取得
Private Sub prsProc()
    myXbisExitFlag = False
    
    If myXbisNotOutFileInfo = True Then Exit Sub
    
'//ファイル一覧のファイル名を取得
  Dim myXlonInfoCnt As Long
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrFldrFileName, _
            myZobjFldrFile, 1)
    If myXlonInfoCnt <> myXlonFldrFileCnt Then GoTo ExitPath
    
'//ファイル一覧のファイルパスを取得
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrFldrFilePath, _
            myZobjFldrFile, 2)
     If myXlonInfoCnt <> myXlonFldrFileCnt Then GoTo ExitPath
   
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｐ_フォルダ階層一覧を階層ごとにフォルダ名でソートして取得する
Private Sub PincGetNameSortedFolderTreeWithComeBack( _
            myXlonFldrTreeCnt As Long, _
            myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long, _
            ByVal myXstrDirPath As String, _
            Optional ByVal coXlonLyrs As Long = 0)
'Includes PincGetAndSortSubFolderArray
'myZobjFldrTree(i) : フォルダ階層のオブジェクト一覧
'myZlonFldrLyrNo(i) : フォルダ階層のオブジェクト一覧の階層No
'myXstrDirPath : フォルダ階層を取得したいディレクトリパス
'coXlonLyrs = 0   : 全フォルダ階層取得
'coXlonLyrs = 1～ : 取得したいフォルダ階層数
    myXlonFldrTreeCnt = Empty: Erase myZobjFldrTree: Erase myZlonFldrLyrNo
    If myXstrDirPath = "" Then Exit Sub
'//初期ディレクトリをセット
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXobjDir As Object: Set myXobjDir = myXobjFSO.GetFolder(myXstrDirPath)
    Set myXobjFSO = Nothing
'//再帰プロシージャ開始
  Dim myXlonFldrCnt As Long, myXlonLyrNo As Long
    myXlonFldrCnt = 0: myXlonLyrNo = 0
    Call PsubFolderTreeComeBack( _
            myZobjFldrTree, myZlonFldrLyrNo, myXlonFldrCnt, _
            myXlonLyrNo, coXlonLyrs, myXobjDir)
    Set myXobjDir = Nothing
    myXlonFldrTreeCnt = myXlonFldrCnt
End Sub
Private Sub PsubFolderTreeComeBack( _
            myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long, _
            ByRef myXlonFldrCnt As Long, _
            ByVal myXlonLyrNo As Long, ByVal myXlonLyrs As Long, _
            ByVal myXobjFldr As Object)
'//再起用サブプロシージャ
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
'//フォルダを配列変数に格納
    myXlonFldrCnt = myXlonFldrCnt + 1
    myXlonLyrNo = myXlonLyrNo + 1
  Dim n As Long: n = myXlonFldrCnt + L - 1
    ReDim Preserve myZobjFldrTree(n) As Object
    ReDim Preserve myZlonFldrLyrNo(n) As Long
    Set myZobjFldrTree(n) = myXobjFldr
    myZlonFldrLyrNo(n) = myXlonLyrNo
    If (myXlonLyrs >= 1 And myXlonLyrNo >= myXlonLyrs) Then Exit Sub
'//サブフォルダ一覧を取得してソート
  Dim myXlonSubFldrCnt As Long, myZobjSubFldr() As Object
    Call PincGetAndSortSubFolderArray(myXlonSubFldrCnt, myZobjSubFldr, myXobjFldr)
    If myXlonSubFldrCnt <= 0 Then Exit Sub
'//再帰を使用してサブフォルダを探索
  Dim myZvarTmpFldr As Variant
    For Each myZvarTmpFldr In myZobjSubFldr
        Call PsubFolderTreeComeBack( _
                myZobjFldrTree, myZlonFldrLyrNo, myXlonFldrCnt, _
                myXlonLyrNo, myXlonLyrs, myZvarTmpFldr)
    Next myZvarTmpFldr
    Erase myZobjSubFldr: myZvarTmpFldr = Empty
End Sub

 '定型Ｐ_指定ディレクトリ内のサブフォルダ一覧を取得してフォルダ名でソートする
Private Sub PincGetAndSortSubFolderArray( _
            myXlonFldrCnt As Long, myZobjFldr() As Object, _
            ByVal myXobjDir As Object)
'Includes PfixGetSubFolderArray
'Includes PfixGetFolderFileStringInformationAndObject2DArray
'Includes PfixQuickSortOfStringObject2DArray
'myZobjFldr(i) : サブフォルダ
'//サブフォルダオブジェクトを取得
  Dim myZobjSubFldr() As Object
    Call PfixGetSubFolderArray(myXlonFldrCnt, myZobjSubFldr, myXobjDir)
    If myXlonFldrCnt <= 0 Then Exit Sub
'//文字列情報一覧を取得
  Dim myXlonInfoCnt As Long, myZvarStrAndObj() As Variant
    Call PfixGetFolderFileStringInformationAndObject2DArray( _
            myXlonInfoCnt, myZvarStrAndObj, _
            myZobjSubFldr, 1)
    If myXlonInfoCnt <> myXlonFldrCnt Then myXlonFldrCnt = 0
    If myXlonFldrCnt <= 0 Then Exit Sub
'//文字列によるクイックソート
  Dim L As Long, U As Long
    L = LBound(myZvarStrAndObj, 1)
    U = UBound(myZvarStrAndObj, 1)
    Call PfixQuickSortOfStringObject2DArray(myZvarStrAndObj, L, U, 1, 1)
'//オブジェクトを配列変数に格納
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long: i = myXlonFldrCnt + Lo - 1
    ReDim myZobjFldr(i) As Object
    For i = LBound(myZvarStrAndObj, 1) To UBound(myZvarStrAndObj, 1)
        Set myZobjFldr(i + Lo - L) = myZvarStrAndObj(i, L + 1)
    Next i
    Erase myZobjSubFldr: Erase myZvarStrAndObj
End Sub

 '定型Ｐ_指定ディレクトリ内のサブフォルダオブジェクト一覧を取得する
Private Sub PfixGetSubFolderArray( _
            myXlonFldrCnt As Long, myZobjFldr() As Object, _
            ByVal myXobjDir As Object)
'myZobjFldr(i) : サブフォルダ
    myXlonFldrCnt = Empty: Erase myZobjFldr
    If myXobjDir Is Nothing Then Exit Sub
    myXlonFldrCnt = myXobjDir.SubFolders.Count
    If myXlonFldrCnt <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim n As Long: n = myXlonFldrCnt + L - 1
    ReDim myZobjFldr(n) As Object
  Dim myXobjSubFldr As Object: n = L - 1
    For Each myXobjSubFldr In myXobjDir.SubFolders
        n = n + 1: Set myZobjFldr(n) = myXobjSubFldr
    Next myXobjSubFldr
    Set myXobjSubFldr = Nothing
End Sub

 '定型Ｐ_指定ディレクトリ内の隠しファイル以外のサブファイルオブジェクト一覧を取得する
Private Sub PincGetNotHiddenSubFileArray( _
            myXlonFileCnt As Long, myZobjNotHdnFile() As Object, _
            ByVal myXobjDir As Object, _
            Optional ByVal coXlonAtrbt As Long = 2)
'Includes PfncbisCheckFileAttribute
'myZobjNotHdnFile(i) : サブファイル
'coXlonAtrbt = 2 : 隠しファイル
    myXlonFileCnt = Empty: Erase myZobjNotHdnFile
    If myXobjDir Is Nothing Then Exit Sub
    If myXobjDir.Files.Count <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXobjSubFile As Object, myXbisAtrChck As Boolean, n As Long: n = L - 1
    For Each myXobjSubFile In myXobjDir.Files
        myXbisAtrChck = PfncbisCheckFileAttribute(myXobjSubFile, coXlonAtrbt, True)
        If myXbisAtrChck = True Then GoTo NextPath
        n = n + 1: ReDim Preserve myZobjNotHdnFile(n) As Object
        Set myZobjNotHdnFile(n) = myXobjSubFile
NextPath:
    Next myXobjSubFile
    myXlonFileCnt = n - L + 1
    Set myXobjSubFile = Nothing
End Sub

 '定型Ｆ_指定ファイルが指定属性であることを確認する(WFDec2Bin使用)
Private Function PfncbisCheckFileAttribute( _
            ByVal myXobjFile As Object, ByVal myXlonAtrbt As Long, _
            Optional ByVal coXbisInStrOptn As Boolean = False) As Boolean
'myXlonAtrbt = 0   : 0000 0000 : 標準ファイル
'myXlonAtrbt = 1   : 0000 0001 : 読み取り専用ファイル
'myXlonAtrbt = 2   : 0000 0010 : 隠しファイル
'myXlonAtrbt = 4   : 0000 0100 : システムファイル
'myXlonAtrbt = 8   : 0000 1000 : ディスクドライブボリュームラベル(取得のみ可能)
'myXlonAtrbt = 16  : 0001 0000 : フォルダまたはディレクトリ(取得のみ可能)
'myXlonAtrbt = 32  : 0010 0000 : アーカイブファイル
'myXlonAtrbt = 64  : 0100 0000 : リンクまたはショートカット(取得のみ可能)
'myXlonAtrbt = 128 : 1000 0000 : 圧縮ファイル(取得のみ可能)
'※複数の属性を持つ場合には数値を足し合わせる
'coXbisInStrOptn = False : 値が一致すれば、myXbisCheck=True
'coXbisInStrOptn = True  : 値が含まれれば、myXbisCheck=True
    PfncbisCheckFileAttribute = False
    If myXobjFile Is Nothing Then Exit Function
    If myXlonAtrbt < 0 Then Exit Function
  Dim myXlonFileAtrbt As Long, myXlonFileAtrbtBin As String
    With myXobjFile
        myXlonFileAtrbt = .Attributes
        myXlonFileAtrbtBin = Application.WorksheetFunction.Dec2Bin(myXlonFileAtrbt, 8)
    End With
  Dim myXstrDsgntAtrbtBin As String
    Select Case coXbisInStrOptn
        Case True
            myXstrDsgntAtrbtBin = Application.WorksheetFunction.Dec2Bin(myXlonAtrbt, 8)
          Dim i As Long
            For i = Len(myXstrDsgntAtrbtBin) To 1 Step -1
                If Mid(myXstrDsgntAtrbtBin, i, 1) = 1 Then _
                    If Mid(myXlonFileAtrbtBin, i, 1) <> 1 Then Exit Function
            Next i
        Case Else
            If myXlonFileAtrbt <> myXlonAtrbt Then Exit Function
    End Select
    PfncbisCheckFileAttribute = True
End Function

 '定型Ｐ_ファイル一覧をファイル名順にソートする
Private Sub PincSortFileArray( _
            myXlonFileCnt As Long, myZobjFile() As Object, _
            ByRef myZobjOrgFile() As Object, _
            Optional ByVal coXlonOrder As Long = 1, _
            Optional ByVal coXlonMode As Long = 1)
'Includes PfixGetFolderFileStringInformationAndObject2DArray
'Includes PfixQuickSortOfStringObject2DArray
'myZobjOrgFile(i) : 元ファイル
'coXlonOrder = 1  : 昇順 (Ascending Order)
'coXlonOrder = -1 : 降順 (Descending Order)
'coXlonMode = vbBinaryCompare  = 0 : バイナリモードの比較
'coXlonMode = vbTextCompare    = 1 : テキストモードの比較
    myXlonFileCnt = Empty: Erase myZobjFile
'//文字列情報一覧を取得
  Dim myXlonInfoCnt As Long, myZvarStrAndObj() As Variant
    Call PfixGetFolderFileStringInformationAndObject2DArray( _
            myXlonInfoCnt, myZvarStrAndObj, _
            myZobjOrgFile, 1)
    If myXlonInfoCnt <= 0 Then Exit Sub
    myXlonFileCnt = myXlonInfoCnt
'//文字列によるクイックソート
  Dim L As Long, U As Long
    L = LBound(myZvarStrAndObj, 1)
    U = UBound(myZvarStrAndObj, 1)
    Call PfixQuickSortOfStringObject2DArray(myZvarStrAndObj, L, U, coXlonOrder, coXlonMode)
'//オブジェクトを配列変数に格納
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long: i = myXlonFileCnt + Lo - 1
    ReDim myZobjFile(i) As Object
    For i = LBound(myZvarStrAndObj, 1) To UBound(myZvarStrAndObj, 1)
        Set myZobjFile(i + Lo - L) = myZvarStrAndObj(i, L + 1)
    Next i
    Erase myZvarStrAndObj
End Sub

 '定型Ｐ_フォルダファイルオブジェクト一覧の文字列情報とオブジェクト併記の配列を取得する
Private Sub PfixGetFolderFileStringInformationAndObject2DArray( _
            myXlonInfoCnt As Long, myZvarStrInfoAndObj() As Variant, _
            ByRef myZobjFldrFile() As Object, _
            Optional ByVal coXlonStrOptn As Long = 1)
'myZvarStrInfoAndObj(i, 1) : 文字列情報
'myZvarStrInfoAndObj(i, 2) : 元フォルダor元ファイル
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZvarStrInfoAndObj
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZstrBuf() As String, myZobjBuf() As Object
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1
        ReDim Preserve myZstrBuf(n) As String
        ReDim Preserve myZobjBuf(n) As Object
        myZstrBuf(n) = myXstrTmp
        Set myZobjBuf(n) = myZobjFldrFile(i)
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
    ReDim myZvarStrInfoAndObj(n, Lo + 1) As Variant
    For i = LBound(myZstrBuf) To UBound(myZstrBuf)
        myZvarStrInfoAndObj(i, Lo + 0) = myZstrBuf(i)
        Set myZvarStrInfoAndObj(i, Lo + 1) = myZobjBuf(i)
    Next i
    Erase myZstrBuf: Erase myZobjBuf
ExitPath:
End Sub

 '定型Ｐ_オブジェクト配列の文字列によるクイックソート
Private Sub PfixQuickSortOfStringObject2DArray( _
            myZvarStrAndObj As Variant, _
            ByVal myXlonLbnd As Long, ByVal myXlonUbnd As Long, _
            Optional ByVal coXlonOrder As Long = 1, _
            Optional ByVal coXlonMode As Long = 1)
'myZvarStrAndObj(i, 1) : 文字列情報
'myZvarStrAndObj(i, 2) : オブジェクト
'coXlonOrder = 1  : 昇順 (Ascending Order)
'coXlonOrder = -1 : 降順 (Descending Order)
'coXlonMode = vbBinaryCompare  = 0 : バイナリモードの比較
'coXlonMode = vbTextCompare    = 1 : テキストモードの比較
'→SetProc:定型プロシージャ使用前のコード部分に記述
'    myXlonLbnd = LBound(myZvarStrAndObj, 1)
'    myXlonUbnd = UBound(myZvarStrAndObj, 1)
'    If myXlonUbnd <= myXlonLbnd Then Exit Sub
'←EndProc
  Dim Li As Long, myXlonMed As Long, myXstrMed As String
    Li = LBound(myZvarStrAndObj, 2)
    myXlonMed = Int((myXlonLbnd + myXlonUbnd) / 2)
    myXstrMed = myZvarStrAndObj(myXlonMed, Li)
  Dim i As Long, j As Long
    i = myXlonLbnd: j = myXlonUbnd
  Dim myXstrBufi As String, myXstrBufj As String, myXstrTmp As String, myXobjTmp As Object
    Do
        myXstrBufi = myZvarStrAndObj(i, Li)
        Do While StrComp(myXstrBufi, myXstrMed, coXlonMode) = -1 * coXlonOrder
            i = i + 1: myXstrBufi = myZvarStrAndObj(i, Li)
        Loop
        myXstrBufj = myZvarStrAndObj(j, Li)
        Do While StrComp(myXstrBufj, myXstrMed, coXlonMode) = coXlonOrder
            j = j - 1: myXstrBufj = myZvarStrAndObj(j, Li)
        Loop
        If i >= j Then Exit Do
        myXstrTmp = CStr(myZvarStrAndObj(i, Li + 0))
        myZvarStrAndObj(i, Li + 0) = myZvarStrAndObj(j, Li + 0)
        myZvarStrAndObj(j, Li + 0) = myXstrTmp
        Set myXobjTmp = myZvarStrAndObj(i, Li + 1)
        Set myZvarStrAndObj(i, Li + 1) = myZvarStrAndObj(j, Li + 1)
        Set myZvarStrAndObj(j, Li + 1) = myXobjTmp
        i = i + 1: j = j - 1
    Loop
    If myXlonLbnd < i - 1 Then _
        Call PfixQuickSortOfStringObject2DArray( _
                myZvarStrAndObj, myXlonLbnd, i - 1, coXlonOrder, coXlonMode)
    If myXlonUbnd > j + 1 Then _
        Call PfixQuickSortOfStringObject2DArray( _
                myZvarStrAndObj, j + 1, myXlonUbnd, coXlonOrder, coXlonMode)
ExitPath:
End Sub

 '定型Ｐ_1次元配列のフォルダファイルオブジェクト一覧の文字列情報を取得する
Private Sub PfixGetFolderFileStringInformationFor1DArray( _
                myXlonInfoCnt As Long, myZstrInfo() As String, _
                ByRef myZobjFldrFile() As Object, _
                Optional ByVal coXlonStrOptn As Long = 1)
'myZstrInfo(i) : 抽出フォルダ情報
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZstrInfo
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1: ReDim Preserve myZstrInfo(n) As String
        myZstrInfo(n) = myXstrTmp
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
ExitPath:
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_指定ディレクトリ階層内のファイル一覧をファイル名でソートして取得する
'Private Sub PforNewInstanceOfCFldrrFileLstNameSort()
'  Dim Lc As Long
'  Dim myXlonFldrFileCnt As Long, myZobjFldrFile() As Object, _
'        myZstrFldrFileName() As String, myZstrFldrFilePath() As String, _
'        myZlonFldrFileLyrNo() As Long
'    'myZobjFldrFile(i) : フォルダファイル
'    'myZstrFldrFileName(i) : フォルダファイル名
'    'myZstrFldrFilePath(i) : フォルダファイルパス
'    'myZlonFldrFileLyrNo(i) : フォルダファイル階層
''//クラス内変数への入力
'  Dim myXbisNotOutFileInfo As Boolean
'    'myXbisNotOutFileInfo = False : ファイルオブジェクとファイル情報を両方出力する
'    'myXbisNotOutFileInfo = True  : ファイルオブジェクトのみ出力してファイル情報は出力しない
'    myXbisNotOutFileInfo = False
'  Dim myXstrDirPath As String
'    myXstrDirPath = "C:\_Cdrive\_VBA\11 プログラムデータベース\04 Classモジュール"
'  Dim myXlonLyrs As Long
'    'myXlonLyrs = 0   : 全フォルダ階層取得
'    'myXlonLyrs = 1～ : 取得したいフォルダ階層数
'    myXlonLyrs = 0
''//CFldrrFileLstNameSortクラスモジュールのインスタンス生成
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long
'  Dim myXinsSubLyrFileLst As CFldrrFileLstNameSort
'    Set myXinsSubLyrFileLst = New CFldrrFileLstNameSort
'    With myXinsSubLyrFileLst
'    '//クラス内変数への入力
'        .letNotOutFileInfo = myXbisNotOutFileInfo
'        .letDirPath = myXstrDirPath
'        .letLyrs = myXlonLyrs
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXlonFldrFileCnt = .getFldrFileCnt
'        If myXlonFldrFileCnt <= 0 Then GoTo JumpPath
'        i = myXlonFldrFileCnt + Lo - 1
'        ReDim myZobjFile(i) As Object
'        ReDim myZstrFldrFileName(i) As String
'        ReDim myZstrFldrFilePath(i) As String
'        ReDim myZlonFldrFileLyrNo(i) As Long
'        Lc = .getOptnBase
'        For i = 1 To myXlonFldrFileCnt
'            Set myZobjFile(i + Lo - 1) = .getFldrFileAry(i + Lc - 1)
'            myZstrFldrFileName(i + Lo - 1) = .getFldrFileNameAry(i + Lc - 1)
'            myZstrFldrFilePath(i + Lo - 1) = .getFldrFilePathAry(i + Lc - 1)
'            myZlonFldrFileLyrNo(i + Lo - 1) = .getFldrFileLyrNoAry(i + Lc - 1)
'        Next i
'    End With
'JumpPath:
'    Set myXinsSubLyrFileLst = Nothing
'    Call PforDisplayVariablesOfCFldrrFileLstNameSort(myXlonFldrFileCnt, myZstrFldrFileName) 'Debug.Print
'ExitPath:
'End Sub
'Private Sub PforDisplayVariablesOfCFldrrFileLstNameSort( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CFldrrFileLstNameSortクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim k As Long
'    For k = LBound(myZvarField) To UBound(myZvarField)
'        Debug.Print "データ" & k & ": " & myZvarField(k)
'    Next k
'End Sub

      <br></pre>
    </body>
  </html>
