<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PfixSetStandardChartFormat
'Includes PfixSetChartStandardLegend
'Includes PfixSetChartStandardTitle
'Includes PincSetChartTitleCenter
'Includes PfixSetChartStandardAxisTitle
'Includes PincSetChartAxisTitleCenter
'Includes PfixSetChartTargetTitleCenter
'Includes PfixSetChartStandardAxesScale

Option Explicit
Option Base 1

'◆ClassProc名_グラフを標準グラフに設定する
'Rev.002
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CStndrdChrt"
  
'//PropertyGet_出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//PropertySetLet_入力制御信号
  Private myXbisMsgBoxON As Boolean
  Private myXbisXMnrGrdLnsShw As Boolean, myXbisYMnrGrdLnsShw As Boolean
  Private myXbisTtlPltAreaCntr As Boolean, myXbisAxsTtlPltAreaCntr As Boolean
  
'//PropertySetLet_入力データ
  Private myXobjChrt As Object
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getCmpltFlag() As Boolean
    getCmpltFlag = myXbisCmpltFlag
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letMsgBoxON(ByVal RHS As Boolean)
    myXbisMsgBoxON = RHS
'myXbisMsgBxON = False : 標準設定実行有無のMsgBoxを表示しない
'myXbisMsgBxON = True  : 標準設定実行有無のMsgBoxを表示する
End Property

Public Property Let letMnrGrdLnsShw(ByVal k As Long, ByVal RHS As Boolean)
    Select Case k
        Case 1: myXbisXMnrGrdLnsShw = RHS
        Case 2: myXbisYMnrGrdLnsShw = RHS
    End Select
'myXbisMnrGrdLnsShw = True  : 補助目盛線を表示する
'myXbisMnrGrdLnsShw = False : 補助目盛線を表示しない
End Property

Public Property Let letPltAreaCntr(ByVal k As Long, ByVal RHS As Boolean)
    Select Case k
        Case 1: myXbisTtlPltAreaCntr = RHS
        Case 2: myXbisAxsTtlPltAreaCntr = RHS
    End Select
'myXbisTtlPltAreaCntr = True  : チャートエリア中心
'myXbisTtlPltAreaCntr = False : プロットエリア中心
End Property

Public Property Set setChrt(ByVal RHS As Object)
    Set myXobjChrt = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXobjChrt Is Nothing Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicF_
Public Function fncbisCmpltFlag() As Boolean
    fncbisCmpltFlag = Empty
    Call ctrProc
    fncbisCmpltFlag = myXbisCmpltFlag
End Function

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag

  Dim myXstrTitle As String, myXstrPrompt As String, myXlonMsgBox As Long
    If myXbisMsgBoxON = True Then
        myXstrTitle = "OK/キャンセルの選択"
        myXstrPrompt = "グラフのフォーマットを自動調整しますか？"
        myXlonMsgBox = MsgBox(myXstrPrompt, vbQuestion + vbOKCancel, myXstrTitle)
        If myXlonMsgBox = vbCancel Then Exit Sub
    End If

'//グラフを標準フォーマットに調整
    Call PfixSetStandardChartFormat(myXbisExitFlag, _
            myXobjChrt, myXbisXMnrGrdLnsShw, myXbisYMnrGrdLnsShw)
    If myXbisExitFlag = True Then GoTo ExitPath

'//グラフの凡例を標準フォーマットで設定
    Call PfixSetChartStandardLegend(myXbisExitFlag, myXobjChrt)
    If myXbisExitFlag = True Then GoTo ExitPath

''//グラフのタイトルを標準フォーマットで設定
'    Call PfixSetChartStandardTitle(myXbisExitFlag, myXobjChrt)
'    If myXbisExitFlag = True Then GoTo ExitPath
    
''//グラフのタイトルの位置をグラフの中心に設定
'    Call PincSetChartTitleCenter(myXbisExitFlag, myXobjChrt, myXbisTtlPltAreaCntr)
'    If myXbisExitFlag = True Then GoTo ExitPath

'//グラフのX/Y軸のタイトルを標準フォーマットで設定
    Call PfixSetChartStandardAxisTitle(myXbisExitFlag, myXobjChrt)
    If myXbisExitFlag = True Then GoTo ExitPath

'//グラフのX/Y軸のタイトルの位置をグラフの中心に設定
    Call PincSetChartAxisTitleCenter(myXbisExitFlag, myXobjChrt, myXbisAxsTtlPltAreaCntr)
    If myXbisExitFlag = True Then GoTo ExitPath

'//グラフの数値軸を標準フォーマットで設定
    Call PfixSetChartStandardAxesScale(myXbisExitFlag, myXobjChrt)
    If myXbisExitFlag = True Then GoTo ExitPath
    
    myXbisCmpltFlag = True
ExitPath:
    Call initializeModuleVariables
End Sub

'===============================================================================================

 '定型Ｐ_グラフを標準フォーマットに調整する
Private Sub PfixSetStandardChartFormat(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisXMnrGrdLnsShw As Boolean = False, _
            Optional ByVal coXbisYMnrGrdLnsShw As Boolean = False)
'myXbisMnrGrdLnsShw = True  : 補助目盛線を表示する
'myXbisMnrGrdLnsShw = False : 補助目盛線を表示しない
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjPltArea As Object
  Dim myXobjAxsX1 As Object, myXobjAxsY1 As Object, myXobjAxsY2 As Object
    On Error Resume Next
    myXobjChrt.HasLegend = False
    myXobjChrt.HasTitle = False
    myXobjChrt.Axes(xlCategory, xlPrimary).HasTitle = False
    myXobjChrt.Axes(xlValue, xlPrimary).HasTitle = False
    myXobjChrt.Axes(xlValue, xlSecondary).HasTitle = False
'//プロットエリアの枠線を設定
    Set myXobjPltArea = myXobjChrt.PlotArea
    Call PsubSetStandardChartFormatForPlotArea(myXobjPltArea)
'//X軸主軸の枠線を設定
    Set myXobjAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    Call PsubSetStandardChartFormatForAxes(myXobjAxsX1)
'//X軸の目盛線を設定
    Call PsubSetStandardChartFormatForAxesGridlines(myXobjAxsX1)
'//Y軸主軸の枠線を設定
    Set myXobjAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    Call PsubSetStandardChartFormatForAxes(myXobjAxsY1)
'//Y軸の目盛線を設定
    Call PsubSetStandardChartFormatForAxesGridlines(myXobjAxsY1)
'//Y軸第2軸の枠線を設定
    Set myXobjAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    Call PsubSetStandardChartFormatForAxes(myXobjAxsY2)
'//X/Y軸のメモリ線を表示
'//X軸主軸
    If coXbisXMnrGrdLnsShw = False Then
        Call myXobjChrt.SetElement(msoElementPrimaryCategoryGridLinesMajor)
    Else
        Call myXobjChrt.SetElement(msoElementPrimaryCategoryGridLinesMajor)
        Call myXobjChrt.SetElement(msoElementPrimaryCategoryGridLinesMinorMajor)
    End If
'//Y軸主軸
    If coXbisYMnrGrdLnsShw = False Then
        Call myXobjChrt.SetElement(msoElementPrimaryValueGridLinesMajor)
    Else
        Call myXobjChrt.SetElement(msoElementPrimaryValueGridLinesMajor)
        Call myXobjChrt.SetElement(msoElementPrimaryValueGridLinesMinorMajor)
    End If
    On Error GoTo 0
    Set myXobjPltArea = Nothing
    Set myXobjAxsX1 = Nothing: Set myXobjAxsY1 = Nothing: Set myXobjAxsY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSetStandardChartFormatForPlotArea(ByVal myXobjTrgt As Object)
    If myXobjTrgt Is Nothing Then Exit Sub
    With myXobjTrgt
    '//枠線
        .Format.Line.Visible = True
        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
    End With
End Sub
Private Sub PsubSetStandardChartFormatForAxes(ByVal myXobjTrgt As Object)
    If myXobjTrgt Is Nothing Then Exit Sub
    With myXobjTrgt
    '//枠線
        .Format.Line.Visible = True
        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
    '//目盛
        .MajorTickMark = xlInside
        If myXobjTrgt.AxisGroup = xlPrimary Then
            .TickLabelPosition = xlLow
        ElseIf myXobjTrgt.AxisGroup = xlSecondary Then
            .TickLabelPosition = xlHigh
        End If
    End With
End Sub
Private Sub PsubSetStandardChartFormatForAxesGridlines(ByVal myXobjTrgt As Object)
    If myXobjTrgt Is Nothing Then Exit Sub
    With myXobjTrgt.MajorGridlines.Format.Line
    '//目盛線
        .Visible = msoTrue
        .ForeColor.ObjectThemeColor = msoThemeColorBackground1
        .ForeColor.Brightness = -0.15
    End With
    With myXobjTrgt.MinorGridlines.Format.Line
    '//補助目盛線
        .Visible = msoTrue
        .ForeColor.ObjectThemeColor = msoThemeColorBackground1
        .ForeColor.Brightness = -0.15
    End With
End Sub

 '定型Ｐ_グラフの凡例を標準フォーマットで設定する
Private Sub PfixSetChartStandardLegend(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
    If myXobjChrt.HasLegend = False Then myXobjChrt.HasLegend = True
    With myXobjChrt.Legend
    '//凡例の文字
        .Font.Name = "Meiryo UI"
        .Font.Size = 11
    '//凡例の位置設定
        .Position = xlLegendPositionCorner
        .IncludeInLayout = False
    '//凡例の塗りつぶし
        .Format.Fill.Visible = True
        .Format.Fill.Solid
        .Format.Fill.ForeColor.ObjectThemeColor = msoThemeColorBackground1
    '//凡例の枠線
        .Format.Line.Visible = True
        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
        .Format.Line.Weight = 0.75
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルを標準フォーマットで設定する
Private Sub PfixSetChartStandardTitle(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
    If myXobjChrt.HasTitle = False Then myXobjChrt.HasTitle = True
    With myXobjChrt.ChartTitle
    '//タイトルの文字
        .Font.Name = "Meiryo UI"
        .Font.Size = 11
        .Font.Bold = True
    '//タイトルの位置設定
        .Position = xlChartElementPositionAutomatic
        .IncludeInLayout = False
    '//タイトルの塗りつぶし
        .Format.Fill.Visible = True
        .Format.Fill.Solid
        .Format.Fill.ForeColor.ObjectThemeColor = msoThemeColorBackground1
    '//タイトルの下線
        .Format.TextFrame2.TextRange.Font.UnderlineStyle = msoUnderlineSingleLine
    '//タイトルの枠線
        .Format.Line.Visible = False
'        .Format.Line.Visible = True
'        .Format.Line.ForeColor.ObjectThemeColor = msoThemeColorText1
'        .Format.Line.Weight = 0.75
    End With
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのタイトルの位置をグラフの中心に設定する
Private Sub PincSetChartTitleCenter(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisPltAreaCntr As Boolean = False)
'Includes PfixSetChartTargetTitleCenter
'coXbisPltAreaCntr = True  : チャートエリア中心
'coXbisPltAreaCntr = False : プロットエリア中心
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
    If myXobjChrt.HasTitle = False Then myXobjChrt.HasTitle = True
  Dim myXobjChrtTtl As Object
    Set myXobjChrtTtl = myXobjChrt.ChartTitle
  Dim myXdouSetTtlTop As Double, myXdouSetTtlLft As Double
  Dim myXlonTtlPstn As Long: myXlonTtlPstn = 1
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjChrtTtl, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjChrt.ChartTitle.Top = myXdouSetTtlTop
        myXobjChrt.ChartTitle.Left = myXdouSetTtlLft
    End If
    Set myXobjChrtTtl = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルを標準フォーマットで設定する
Private Sub PfixSetChartStandardAxisTitle(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjChrtAxsX1 As Object, myXobjChrtAxsY1 As Object, myXobjChrtAxsY2 As Object
  Dim myXobjAxsTtlX1 As Object, myXobjAxsTtlY1 As Object, myXobjAxsTtlY2 As Object
    On Error Resume Next
'//X軸主軸
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    If myXobjChrtAxsX1.HasTitle = False Then myXobjChrtAxsX1.HasTitle = True
    Set myXobjAxsTtlX1 = myXobjChrtAxsX1.AxisTitle
    Call PsubSetChartStandardAxisTitle(myXobjAxsTtlX1)
'//Y軸主軸
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    If myXobjChrtAxsY1.HasTitle = False Then myXobjChrtAxsY1.HasTitle = True
    Set myXobjAxsTtlY1 = myXobjChrtAxsY1.AxisTitle
    Call PsubSetChartStandardAxisTitle(myXobjAxsTtlY1)
'//Y軸第2軸
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    If myXobjChrtAxsY2.HasTitle = False Then myXobjChrtAxsY2.HasTitle = True
    Set myXobjAxsTtlY2 = myXobjChrtAxsY2.AxisTitle
    Call PsubSetChartStandardAxisTitle(myXobjAxsTtlY2)
    On Error GoTo 0
    Set myXobjChrtAxsX1 = Nothing
    Set myXobjChrtAxsY1 = Nothing: Set myXobjChrtAxsY2 = Nothing
    Set myXobjAxsTtlX1 = Nothing
    Set myXobjAxsTtlY1 = Nothing: Set myXobjAxsTtlY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSetChartStandardAxisTitle(ByVal myXobjAxsTtl As Object)
    If myXobjAxsTtl Is Nothing Then Exit Sub
    With myXobjAxsTtl
    '//タイトルの文字
        .Font.Name = "Meiryo UI"
        .Font.Size = 11
        .Font.Bold = True
    End With
End Sub

 '定型Ｐ_グラフのX/Y軸のタイトルの位置をグラフの中心に設定する
Private Sub PincSetChartAxisTitleCenter(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object, _
            Optional ByVal coXbisPltAreaCntr As Boolean = False)
'Includes PfixSetChartTargetTitleCenter
'coXbisPltAreaCntr = True  : チャートエリア中心
'coXbisPltAreaCntr = False : プロットエリア中心
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjChrtAxsX1 As Object, myXobjChrtAxsY1 As Object, myXobjChrtAxsY2 As Object
  Dim myXobjAxsTtlX1 As Object, myXobjAxsTtlY1 As Object, myXobjAxsTtlY2 As Object
  Dim myXdouSetTtlTop As Double, myXdouSetTtlLft As Double
  Dim myXlonTtlPstn As Long
    On Error Resume Next
'//X軸主軸
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    If myXobjChrtAxsX1.HasTitle = False Then myXobjChrtAxsX1.HasTitle = True
    Set myXobjAxsTtlX1 = myXobjChrtAxsX1.AxisTitle
    myXlonTtlPstn = 2
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjAxsTtlX1, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjAxsTtlX1.Top = myXdouSetTtlTop
        myXobjAxsTtlX1.Left = myXdouSetTtlLft
    End If
'//Y軸主軸
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    If myXobjChrtAxsY1.HasTitle = False Then myXobjChrtAxsY1.HasTitle = True
    Set myXobjAxsTtlY1 = myXobjChrtAxsY1.AxisTitle
    myXlonTtlPstn = 3
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjAxsTtlY1, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjAxsTtlY1.Top = myXdouSetTtlTop
        myXobjAxsTtlY1.Left = myXdouSetTtlLft
    End If
'//Y軸第2軸
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    If myXobjChrtAxsY2.HasTitle = False Then myXobjChrtAxsY2.HasTitle = True
    Set myXobjAxsTtlY2 = myXobjChrtAxsY2.AxisTitle
    myXlonTtlPstn = 4
    Call PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop, myXdouSetTtlLft, _
            myXobjChrt, myXobjAxsTtlY2, myXlonTtlPstn, coXbisPltAreaCntr)
    If myXdouSetTtlTop <> 0 Or myXdouSetTtlLft <> 0 Then
        myXobjAxsTtlY2.Top = myXdouSetTtlTop
        myXobjAxsTtlY2.Left = myXdouSetTtlLft
    End If
    On Error GoTo 0
    Set myXobjChrtAxsX1 = Nothing
    Set myXobjChrtAxsY1 = Nothing: Set myXobjChrtAxsY2 = Nothing
    Set myXobjAxsTtlX1 = Nothing
    Set myXobjAxsTtlY1 = Nothing: Set myXobjAxsTtlY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

 '定型Ｐ_指定タイトル位置がグラフの中心となる設定位置を取得する
Private Sub PfixSetChartTargetTitleCenter( _
            myXdouSetTtlTop As Double, myXdouSetTtlLft As Double, _
            ByVal myXobjChrt As Object, ByVal myXobjOrgTtl As Object, _
            ByVal myXlonTtlPstn As Long, _
            Optional ByVal coXbisPltAreaCntr As Boolean = False)
'myXlonTtlPstn = 1  : 上側のタイトル
'myXlonTtlPstn = 2  : 下側のタイトル
'myXlonTtlPstn = 3  : 左側のタイトル
'myXlonTtlPstn = 4  : 右側のタイトル
'coXbisPltAreaCntr = True  : チャートエリア中心
'coXbisPltAreaCntr = False : プロットエリア中心
    myXdouSetTtlTop = Empty: myXdouSetTtlLft = Empty
    If myXobjChrt Is Nothing Then Exit Sub
    If myXobjOrgTtl Is Nothing Then Exit Sub
  Dim myXdouOrgTtlHght As Double, myXdouOrgTtlWdth As Double, _
        myXdouChrtAreaHght As Double, myXdouChrtAreaWdth As Double, _
        myXdouPltAreaTop As Double, myXdouPltAreaHght As Double, _
        myXdouPltAreaLft As Double, myXdouPltAreaWdth As Double
  Dim myXdouCal As Double
    On Error GoTo ExitPath
    If myXlonTtlPstn = 1 Or myXlonTtlPstn = 2 Then
    '//タイトル位置が上側と下側の場合の処理
    '//タイトルのサイズ情報を取得
        myXdouOrgTtlWdth = myXobjOrgTtl.Width
    '//チャートエリアの位置サイズ情報を取得
        myXdouChrtAreaHght = myXobjChrt.ChartArea.Height
        myXdouChrtAreaWdth = myXobjChrt.ChartArea.Width
    '//プロットエリアの位置サイズ情報を取得
        myXdouPltAreaLft = myXobjChrt.PlotArea.Left
        myXdouPltAreaWdth = myXobjChrt.PlotArea.Width
    '//タイトル位置を設定
        Select Case myXlonTtlPstn
            Case 1: myXdouSetTtlTop = 0
            Case 2: myXdouSetTtlTop = myXdouChrtAreaHght
        End Select
        If coXbisPltAreaCntr = False Then
            myXdouCal = myXdouChrtAreaWdth / 2
            myXdouCal = myXdouCal - myXdouOrgTtlWdth / 2
        Else
            myXdouCal = myXdouPltAreaWdth / 2
            myXdouCal = myXdouPltAreaLft + myXdouCal
            myXdouCal = myXdouCal - myXdouOrgTtlWdth / 2
        End If
        myXdouSetTtlLft = myXdouCal
    ElseIf myXlonTtlPstn = 3 Or myXlonTtlPstn = 4 Then
    '//タイトル位置が左側と右側の場合の処理
    '//タイトルのサイズ情報を取得
        myXdouOrgTtlHght = myXobjOrgTtl.Height
    '//チャートエリアの位置サイズ情報を取得
        myXdouChrtAreaHght = myXobjChrt.ChartArea.Height
        myXdouChrtAreaWdth = myXobjChrt.ChartArea.Width
    '//プロットエリアの位置サイズ情報を取得
        myXdouPltAreaTop = myXobjChrt.PlotArea.Top
        myXdouPltAreaHght = myXobjChrt.PlotArea.Height
    '//タイトル位置を設定
        If coXbisPltAreaCntr = False Then
            myXdouCal = myXdouChrtAreaHght / 2
            myXdouCal = myXdouCal - myXdouOrgTtlHght / 2
        Else
            myXdouCal = myXdouPltAreaHght / 2
            myXdouCal = myXdouPltAreaTop + myXdouCal
            myXdouCal = myXdouCal - myXdouOrgTtlHght / 2
        End If
        myXdouSetTtlTop = myXdouCal
        Select Case myXlonTtlPstn
            Case 3: myXdouSetTtlLft = 0
            Case 4: myXdouSetTtlLft = myXdouChrtAreaWdth
        End Select
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXdouSetTtlTop = Empty: myXdouSetTtlLft = Empty
End Sub

 '定型Ｐ_グラフの数値軸を標準フォーマットで設定する
Private Sub PfixSetChartStandardAxesScale(myXbisExitFlag As Boolean, _
            ByVal myXobjChrt As Object)
    myXbisExitFlag = False
    If myXobjChrt Is Nothing Then GoTo ExitPath
  Dim myXobjChrtAxsX1 As Object, myXobjChrtAxsY1 As Object, myXobjChrtAxsY2 As Object
    On Error Resume Next
'//X軸主軸
    Set myXobjChrtAxsX1 = myXobjChrt.Axes(xlCategory, xlPrimary)
    Call PsubSetChartStandardAxesScale(myXobjChrtAxsX1)
'//Y軸主軸
    Set myXobjChrtAxsY1 = myXobjChrt.Axes(xlValue, xlPrimary)
    Call PsubSetChartStandardAxesScale(myXobjChrtAxsY1)
'//Y軸第2軸
    Set myXobjChrtAxsY2 = myXobjChrt.Axes(xlValue, xlSecondary)
    Call PsubSetChartStandardAxesScale(myXobjChrtAxsY2)
    On Error GoTo 0
    Set myXobjChrtAxsX1 = Nothing: Set myXobjChrtAxsY1 = Nothing
    Set myXobjChrtAxsY2 = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSetChartStandardAxesScale(ByVal myXobjChrtAxs As Object)
    If myXobjChrtAxs Is Nothing Then Exit Sub
    With myXobjChrtAxs
    '//軸の文字
        .TickLabels.Font.Name = "Meiryo UI"
        .TickLabels.Font.Size = 11
    '//軸のスケール
        .MinimumScaleIsAuto = True
        .MaximumScaleIsAuto = True
        .MajorUnitIsAuto = True
        .MinorUnitIsAuto = True
    End With
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_グラフを標準グラフに設定する
'Private Sub instCStndrdChrt()
'  Dim myXbisCmpltFlag As Boolean
''//クラス内変数への入力
'  Dim myXbisMsgBoxON As Boolean
'    'myXbisMsgBxON = False : 標準設定実行有無のMsgBoxを表示しない
'    'myXbisMsgBxON = True  : 標準設定実行有無のMsgBoxを表示しする
'    myXbisMsgBoxON = False
'  Dim myXbisXMnrGrdLnsShw As Boolean, myXbisYMnrGrdLnsShw As Boolean
'    'myXbisMnrGrdLnsShw = True  : 補助目盛線を表示する
'    'myXbisMnrGrdLnsShw = False : 補助目盛線を表示しない
'    myXbisXMnrGrdLnsShw = False
'    myXbisYMnrGrdLnsShw = False
'  Dim myXbisTtlPltAreaCntr As Boolean, myXbisAxsTtlPltAreaCntr As Boolean
'    'myXbisTtlPltAreaCntr = True  : チャートエリア中心
'    'myXbisTtlPltAreaCntr = False : プロットエリア中心
'    myXbisTtlPltAreaCntr = False
'    myXbisAxsTtlPltAreaCntr = False
'  Dim myXobjChrt As Object
'    Set myXobjChrt = ActiveChart
''//CStndrdChrtクラスモジュールのインスタンス生成
'  Dim myXinsStndrdChrt As CStndrdChrt: Set myXinsStndrdChrt = New CStndrdChrt
'    With myXinsStndrdChrt
'    '//クラス内変数への入力
'        .letMsgBoxON = myXbisMsgBoxON
'        .letMnrGrdLnsShw(1) = myXbisXMnrGrdLnsShw
'        .letMnrGrdLnsShw(2) = myXbisYMnrGrdLnsShw
'        .letPltAreaCntr(1) = myXbisTtlPltAreaCntr
'        .letPltAreaCntr(2) = myXbisAxsTtlPltAreaCntr
'        Set .setChrt = myXobjChrt
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        myXbisCmpltFlag = .fncbisCmpltFlag
'    End With
'    Set myXinsStndrdChrt = Nothing
'End Sub

      <br></pre>
    </body>
  </html>

.html
