<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfixGetPicturePlaceSizeRange

Option Explicit
Option Base 1

'◆ModuleProc名_スクリーンショットを撮るたびに自動でシートに貼り付ける
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "ScrnShtToSht"

  Private Declare PtrSafe Function OpenClipboard Lib "USER32" ( _
        Optional ByVal hwnd As Long = 0) As Long
  Private Declare PtrSafe Function EmptyClipboard Lib "USER32" () As Long
  Private Declare PtrSafe Function CloseClipboard Lib "USER32" () As Long
  
'//モジュール内定数
  Private Const coXlonPstRow As Long = 2
  Private Const coXlonPstCol As Long = 2
  
'//モジュール内変数_データ
  Private myZvarClpBrdFrmt As Variant
  Private myXobjPstSht As Object, myXobjPstRng As Object
  Private myXobjPicture As Object

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myZvarClpBrdFrmt = Empty
    Set myXobjPstSht = Nothing: Set myXobjPstRng = Nothing
    Set myXobjPicture = Nothing
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeStartCapture()
    Call initializeModuleVariables
    
    MsgBox "AutoCaptureを開始します。" & vbCrLf & vbCrLf & _
            "終了するにはSTOPボタンを押して下さい。", vbInformation
    
    Set myXobjPstSht = Worksheets.Add(after:=Worksheets(Worksheets.Count))
    Application.Caption = "AutoCapture中"
    
'//処理実行
    Call subAutoCapture
    
End Sub

Private Sub subAutoCapture()

    If Right(Application.Caption, 3) = "停止中" Then GoTo ExitPath
    
    myZvarClpBrdFrmt = Application.ClipboardFormats
    If myZvarClpBrdFrmt(1) = -1 Then GoTo JumpPath
    
  Dim myXlonPicTopRow As Long, myXlonPicLftCol As Long, _
        myXlonPicBttmRow As Long, myXlonPicRghtCol As Long, _
        myXlonPicHghtRows As Long, myXlonPicWdthCols As Long
  Dim myXlonPicCnt As Long
  Dim i As Long
    For i = LBound(myZvarClpBrdFrmt) To UBound(myZvarClpBrdFrmt)
        Debug.Print myZvarClpBrdFrmt(i)
        If myZvarClpBrdFrmt(i) <> xlClipboardFormatBitmap Then GoTo NextPath
        
        myXlonPicCnt = myXobjPstSht.Pictures.Count
        If myXlonPicCnt = 0 Then
            Set myXobjPstRng = myXobjPstSht.Cells(coXlonPstRow, coXlonPstCol)
            
        Else
            Set myXobjPicture = myXobjPstSht.Pictures(myXlonPicCnt)
            
            Call PfixGetPicturePlaceSizeRange( _
                    myXlonPicTopRow, myXlonPicLftCol, myXlonPicBttmRow, myXlonPicRghtCol, _
                    myXlonPicHghtRows, myXlonPicWdthCols, _
                    myXobjPicture)
            Set myXobjPstRng = myXobjPstSht.Cells(myXlonPicBttmRow + 2, coXlonPstCol)
            
        End If
        
        myXobjPstSht.Paste Destination:=myXobjPstRng
        
        OpenClipboard
        EmptyClipboard
        CloseClipboard
        
NextPath:
    Next i
    
JumpPath:
    DoEvents
    Application.OnTime DateAdd("s", 1, Now), "subAutoCapture"
    
    Exit Sub
ExitPath:
    Application.Caption = ""
    MsgBox "AutoCaptureを停止しました。", vbInformation
    Call initializeModuleVariables
End Sub

'PublicP_
Public Sub exeStopCapture()
    
    Application.Caption = "停止中"
    
End Sub

'===============================================================================================

 '定型Ｐ_画像の位置とサイズをセル範囲で取得する
Private Sub PfixGetPicturePlaceSizeRange( _
            myXlonPicTopRow As Long, myXlonPicLftCol As Long, _
            myXlonPicBttmRow As Long, myXlonPicRghtCol As Long, _
            myXlonPicHghtRows As Long, myXlonPicWdthCols As Long, _
            ByVal myXobjPicture As Object)
    myXlonPicTopRow = Empty: myXlonPicLftCol = Empty
    myXlonPicBttmRow = Empty: myXlonPicRghtCol = Empty
    myXlonPicHghtRows = Empty: myXlonPicWdthCols = Empty
    On Error GoTo ExitPath
    With myXobjPicture
        myXlonPicTopRow = .TopLeftCell.Row
        myXlonPicLftCol = .TopLeftCell.Column
        myXlonPicBttmRow = .BottomRightCell.Row
        myXlonPicRghtCol = .BottomRightCell.Column
    End With
    On Error GoTo 0
    myXlonPicHghtRows = myXlonPicBttmRow - myXlonPicTopRow + 1
    myXlonPicWdthCols = myXlonPicRghtCol - myXlonPicLftCol + 1
ExitPath:
End Sub

 '抽象Ｐ_OnTimeループ
Private Sub PabsOnTime()
    If Right(Application.Caption, 3) = "停止中" Then GoTo ExitPath
    Debug.Print Now
    Application.OnTime DateAdd("s", 2, Now), "PabsOnTime"
    Exit Sub
ExitPath:
    Application.Caption = ""
End Sub
Private Sub PsubOnTime()
    Application.Caption = "停止中"
End Sub

'DummyＰ_
Private Sub MsubDummy()
End Sub

      <br></pre>
    </body>
  </html>
