<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfncbisIsMatrixNumeric
'Includes PfixArrayPartColumnNumeric
'Includes Pfix1DArrayExtractNumeric
'Includes PfixTrapezoidWaveLong
'Includes PfncdouSlope
'Includes PfixSlopeIntercept

Option Explicit
Option Base 1

'◆ClassProc名_データ配列の指定区間ごとで回帰直線処理をしてデータを取得する
'Rev.005
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CIntrvlRgrsnLn"
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonStrtRow As Long, myXlonLastRow As Long
  Private myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
    'myZdouCalData(i, 1) : 計算処理後データ配列
  
'//PropertySetLet_入力制御信号
  Private myXlonCalRng As Long
    'myXlonCalRng = -1 : 今回値より前のデータで計算
    'myXlonCalRng = 0  : 今回値の前後のデータで計算
    'myXlonCalRng = 1  : 今回値より後のデータで計算
  Private myXlonCalDataRngOptn As Long
    'myXlonCalDataRngOptn = 1 : 全データ範囲で取得
    'myXlonCalDataRngOptn = 2 : 指定データ範囲で取得
    'myXlonCalDataRngOptn = 3 : 有効データ範囲で取得
  
'//PropertySetLet_入力データ
  Private myZdouXOrgData() As Double, myZdouYOrgData() As Double
    'myZdouXOrgData(i, 1) : X軸の元データ配列
    'myZdouYOrgData(i, 1) : Y軸の元データ配列
  Private myXlonBgnRow As Long, myXlonEndRow As Long
  Private myXlonCalSmplQnt As Long, myXlonCalStep As Long
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonXInptRowCnt As Long, myXlonXInptColCnt As Long, myZdouXInptData() As Double
  Private myXlonYInptRowCnt As Long, myXlonYInptColCnt As Long, myZdouYInptData() As Double
  Private myXlonStrt As Long, myXlonLast As Long
  Private myXlonXTmpDataCnt As Long, myZdouXTmpData() As Double
  Private myXlonYTmpDataCnt As Long, myZdouYTmpData() As Double
  Private myXlonPreOfst As Long, myXlonPstOfst As Long
  Private myXlonCalStrt As Long, myXlonCalLast As Long
  Private myXlonCalTmpCnt As Long, myZdouCalTmp() As Double

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonXInptRowCnt = Empty: myXlonXInptColCnt = Empty: Erase myZdouXInptData
    myXlonYInptRowCnt = Empty: myXlonYInptColCnt = Empty: Erase myZdouYInptData
    myXlonStrt = Empty: myXlonLast = Empty
    myXlonXTmpDataCnt = Empty: Erase myZdouXTmpData
    myXlonYTmpDataCnt = Empty: Erase myZdouYTmpData
    myXlonPreOfst = Empty: myXlonPstOfst = Empty
    myXlonCalStrt = Empty: myXlonCalLast = Empty
    myXlonCalTmpCnt = Empty: Erase myZdouCalTmp
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getStrtRow() As Long
    getStrtRow = myXlonStrtRow
End Property

Public Property Get getLastRow() As Long
    getLastRow = myXlonLastRow
End Property

Public Property Get getCalRowCnt() As Long
    getCalRowCnt = myXlonCalRowCnt
End Property

Public Property Get getCalColCnt() As Long
    getCalColCnt = myXlonCalColCnt
End Property

Public Property Get getCalData(ByVal i As Long, ByVal j As Long) As Double
    getCalData = myZdouCalData(i, j)
'myZdouCalData(i, 1) : 計算処理後データ配列
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonStrtRow = Empty: myXlonLastRow = Empty
    myXlonCalRowCnt = Empty: myXlonCalColCnt = Empty: Erase myZdouCalData
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letCalRng(ByVal RHS As Long)
    myXlonCalRng = RHS
'myXlonCalRng = -1 : 今回値より前のデータで計算
'myXlonCalRng = 0  : 今回値の前後のデータで計算
'myXlonCalRng = 1  : 今回値より後のデータで計算
End Property

Public Property Let letCalDataRngOptn(ByVal RHS As Long)
    myXlonCalDataRngOptn = RHS
'myXlonCalDataRngOptn = 1 : 全データ範囲で取得
'myXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'myXlonCalDataRngOptn = 3 : 有効データ範囲で取得
End Property

Public Property Let letXOrgData(ByRef RHS() As Double)
    myZdouXOrgData() = RHS()
'myZdouXOrgData(i, 1) : X軸の元データ配列
End Property

Public Property Let letYOrgData(ByRef RHS() As Double)
    myZdouYOrgData() = RHS()
'myZdouYOrgData(i, 1) : Y軸の元データ配列
End Property

Public Property Let letBgnRow(ByVal RHS As Long)
    myXlonBgnRow = RHS
End Property

Public Property Let letEndRow(ByVal RHS As Long)
    myXlonEndRow = RHS
End Property

Public Property Let letCalSmplQnt(ByVal RHS As Long)
    myXlonCalSmplQnt = RHS
End Property

Public Property Let letCalStep(ByVal RHS As Long)
    myXlonCalStep = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXlonCalRng < -1 Or myXlonCalRng > 1 Then myXlonCalRng = 0
    If myXlonCalDataRngOptn < 1 Or myXlonCalDataRngOptn > 3 Then myXlonCalDataRngOptn = 1
    
    If PfncbisIsMatrixNumeric(myZdouXOrgData) = False Then GoTo ExitPath
    If PfncbisIsMatrixNumeric(myZdouYOrgData) = False Then GoTo ExitPath
    
    If UBound(myZdouXOrgData, 1) <> UBound(myZdouYOrgData, 1) Then GoTo ExitPath
    If UBound(myZdouXOrgData, 2) <> UBound(myZdouYOrgData, 2) Then GoTo ExitPath
    
  Dim L As Long, i As Long, j As Long
    
    L = LBound(myZdouXOrgData, 1)
    myXlonXInptRowCnt = UBound(myZdouXOrgData, 1) - L + 1
    myXlonXInptColCnt = UBound(myZdouXOrgData, 2) - L + 1
    ReDim myZdouXInptData(myXlonXInptRowCnt, myXlonXInptColCnt) As Double
    For j = LBound(myZdouXOrgData, 2) To UBound(myZdouXOrgData, 2)
        For i = LBound(myZdouXOrgData, 1) To UBound(myZdouXOrgData, 1)
            myZdouXInptData(i - L + 1, j - L + 1) = myZdouXOrgData(i, j)
        Next i
    Next j
    
    L = LBound(myZdouYOrgData, 1)
    myXlonYInptRowCnt = UBound(myZdouYOrgData, 1) - L + 1
    myXlonYInptColCnt = UBound(myZdouYOrgData, 2) - L + 1
    ReDim myZdouYInptData(myXlonYInptRowCnt, myXlonYInptColCnt) As Double
    For j = LBound(myZdouYOrgData, 2) To UBound(myZdouYOrgData, 2)
        For i = LBound(myZdouYOrgData, 1) To UBound(myZdouYOrgData, 1)
            myZdouYInptData(i - L + 1, j - L + 1) = myZdouYOrgData(i, j)
        Next i
    Next j
    
    If myXlonBgnRow < LBound(myZdouXInptData, 1) Then myXlonBgnRow = LBound(myZdouXInptData, 1)
    If myXlonEndRow < myXlonBgnRow Or myXlonEndRow > UBound(myZdouXInptData, 1) Then _
            myXlonEndRow = UBound(myZdouXInptData, 1)
    If myXlonCalSmplQnt <= 0 Or myXlonCalSmplQnt > UBound(myZdouXInptData, 1) Then GoTo ExitPath
    If myXlonCalStep <= 0 Or myXlonCalStep > UBound(myZdouXInptData, 1) Then myXlonCalStep = 1
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_回帰直線の傾き取得
Public Sub exeProcCalSlope()
    Call ctrProcCalSlope
End Sub

'PublicP_Y軸データと回帰直線との差分取得
Public Sub exeProcCalDeviationFromRegressionLine()
    Call ctrProcCalDeviationFromRegressionLine
End Sub

'CtrlP_回帰直線の傾き取得
Private Sub ctrProcCalSlope()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//C:データ用変数を設定
    Call setDataVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//C:制御用変数を設定
    Call setControlVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag

'//回帰直線の傾き取得
    Call prsCalSlope
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
'//C:出力データを格納
    Call setOutputVariables
    
ExitPath:
    Call initializeModuleVariables
End Sub

'CtrlP_Y軸データと回帰直線との差分取得
Private Sub ctrProcCalDeviationFromRegressionLine()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//C:データ用変数を設定
    Call setDataVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//C:制御用変数を設定
    Call setControlVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag

'//Y軸データと回帰直線との差分取得
    Call prsCalDeviationFromRegressionLine
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
'//C:出力データを格納
    Call setOutputVariables
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SetP_データ用変数を設定する
Private Sub setDataVariables()
    myXbisExitFlag = False
    
    myXlonStrt = LBound(myZdouXInptData, 1)
    myXlonLast = UBound(myZdouXInptData, 1)
    
'//2次元の数値配列の指定列から一部を1次元配列で取得
  Const coXlonCalCol As Long = 1
    Call PfixArrayPartColumnNumeric( _
            myXlonXTmpDataCnt, myZdouXTmpData, _
            myZdouXInptData, coXlonCalCol, myXlonStrt, myXlonLast, myXlonCalStep)
    Call PfixArrayPartColumnNumeric( _
            myXlonYTmpDataCnt, myZdouYTmpData, _
            myZdouYInptData, coXlonCalCol, myXlonStrt, myXlonLast, myXlonCalStep)
    If myXlonXTmpDataCnt <= 0 Or myXlonYTmpDataCnt <= 0 Then GoTo ExitPath
    If myXlonXTmpDataCnt <> myXlonYTmpDataCnt Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables()
    myXbisExitFlag = False
    
'//計算するオフセット量を設定
    Select Case myXlonCalRng
        Case -1
            myXlonPreOfst = (myXlonCalSmplQnt - 1) * myXlonCalStep
            myXlonPstOfst = 0
        Case 1
            myXlonPreOfst = 0
            myXlonPstOfst = (myXlonCalSmplQnt - 1) * myXlonCalStep
        Case Else
            myXlonPreOfst = Int(myXlonCalSmplQnt / 2) * myXlonCalStep
            myXlonPstOfst = Int(myXlonCalSmplQnt / 2) * myXlonCalStep
    End Select
    
'//計算が有効な最初と最後の位置を設定
    myXlonStrt = myXlonStrt + myXlonPreOfst
    myXlonLast = myXlonLast - myXlonPstOfst
    If myXlonLast < myXlonStrt Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:回帰直線の傾き
Private Sub prsCalSlope()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Select Case myXlonCalDataRngOptn
        Case 1
            myXlonCalStrt = LBound(myZdouXTmpData)
            myXlonCalLast = UBound(myZdouXTmpData)
        Case 2
            myXlonCalStrt = myXlonBgnRow
            myXlonCalLast = myXlonEndRow
        Case Else
            myXlonCalStrt = myXlonStrt
            myXlonCalLast = myXlonLast
    End Select
    
'//台形波形を生成
  Const L As Long = 1
  Const coXlonStrtStp As Long = -1
  Const coXlonCnstVal As Long = 0
  Const coXlonLastStp As Long = -1
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonXTmpDataCnt, myXlonStrt, myXlonLast, _
            L, coXlonStrtStp, coXlonCnstVal, coXlonLastStp)
    If myXlonAdjstValCnt <> myXlonXTmpDataCnt Then GoTo ExitPath
    
'//計算処理:回帰直線の傾き
  Dim i As Long, n As Long: n = 0
  Dim myXlonBgn As Long, myXlonEnd As Long
  Dim myXlonTmpXCnt As Long, myZdouTmpX() As Double
  Dim myXlonTmpYCnt As Long, myZdouTmpY() As Double
    
    myXlonCalTmpCnt = myXlonCalLast - myXlonCalStrt + 1
    ReDim myZdouCalTmp(myXlonCalTmpCnt) As Double

    For i = myXlonCalStrt To myXlonCalLast
        myXlonBgn = i - myXlonPreOfst + myZlonAdjstVal(i)
        myXlonEnd = i + myXlonPstOfst + myZlonAdjstVal(i)
        Call Pfix1DArrayExtractNumeric( _
                myXlonTmpXCnt, myZdouTmpX, _
                myZdouXTmpData, myXlonBgn, myXlonEnd, myXlonCalStep)
        If myXlonTmpXCnt <= 0 Then GoTo ExitPath
        Call Pfix1DArrayExtractNumeric( _
                myXlonTmpYCnt, myZdouTmpY, _
                myZdouYTmpData, myXlonBgn, myXlonEnd, myXlonCalStep)
        If myXlonTmpYCnt <= 0 Then GoTo ExitPath
        n = n + 1
        myZdouCalTmp(n) = PfncdouSlope(myZdouTmpX, myZdouTmpY)
    Next i
    If n <> myXlonCalTmpCnt Then GoTo ExitPath
    
    Erase myZlonAdjstVal: Erase myZdouTmpX: Erase myZdouTmpY
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:Y軸データと回帰直線との差分
Private Sub prsCalDeviationFromRegressionLine()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Select Case myXlonCalDataRngOptn
        Case 1
            myXlonCalStrt = LBound(myZdouXTmpData)
            myXlonCalLast = UBound(myZdouXTmpData)
        Case 2
            myXlonCalStrt = myXlonBgnRow
            myXlonCalLast = myXlonEndRow
        Case Else
            myXlonCalStrt = myXlonStrt
            myXlonCalLast = myXlonLast
    End Select
    
'//台形波形を生成
  Const L As Long = 1
  Const coXlonStrtStp As Long = -1
  Const coXlonCnstVal As Long = 0
  Const coXlonLastStp As Long = -1
  Dim myXlonAdjstValCnt As Long, myZlonAdjstVal() As Long
    Call PfixTrapezoidWaveLong( _
            myXlonAdjstValCnt, myZlonAdjstVal, _
            myXlonXTmpDataCnt, myXlonStrt, myXlonLast, _
            L, coXlonStrtStp, coXlonCnstVal, coXlonLastStp)
    If myXlonAdjstValCnt <> myXlonXTmpDataCnt Then GoTo ExitPath
    
'//計算処理:Y軸データと回帰直線との差分
  Dim i As Long, n As Long: n = 0
  Dim myXlonBgn As Long, myXlonEnd As Long
  Dim myXlonTmpXCnt As Long, myZdouTmpX() As Double
  Dim myXlonTmpYCnt As Long, myZdouTmpY() As Double
  Dim myXdouSlp As Double, myXdouIntrcpt As Double, myXdouRgrsn As Double
    
    myXlonCalTmpCnt = myXlonCalLast - myXlonCalStrt + 1
    ReDim myZdouCalTmp(myXlonCalTmpCnt) As Double

    For i = myXlonCalStrt To myXlonCalLast
        myXlonBgn = i - myXlonPreOfst + myZlonAdjstVal(i)
        myXlonEnd = i + myXlonPstOfst + myZlonAdjstVal(i)
        Call Pfix1DArrayExtractNumeric( _
                myXlonTmpXCnt, myZdouTmpX, _
                myZdouXTmpData, myXlonBgn, myXlonEnd, myXlonCalStep)
        If myXlonTmpXCnt <= 0 Then GoTo ExitPath
        Call Pfix1DArrayExtractNumeric( _
                myXlonTmpYCnt, myZdouTmpY, _
                myZdouYTmpData, myXlonBgn, myXlonEnd, myXlonCalStep)
        If myXlonTmpYCnt <= 0 Then GoTo ExitPath
        Call PfixSlopeIntercept(myXdouSlp, myXdouIntrcpt, myZdouTmpX, myZdouTmpY)
        myXdouRgrsn = myXdouSlp * myZdouXTmpData(i) + myXdouIntrcpt
        n = n + 1
        myZdouCalTmp(n) = myZdouYTmpData(i) - myXdouRgrsn
    Next i
    If n <> myXlonCalTmpCnt Then GoTo ExitPath
    
    Erase myZlonAdjstVal: Erase myZdouTmpX: Erase myZdouTmpY
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_出力データを格納する
Private Sub setOutputVariables()
    myXbisExitFlag = False
    
    myXlonStrtRow = myXlonCalStrt
    myXlonLastRow = myXlonCalLast
    
  Dim i As Long
    myXlonCalRowCnt = myXlonCalTmpCnt
    myXlonCalColCnt = 1
    ReDim myZdouCalData(myXlonCalRowCnt, myXlonCalColCnt) As Double
    For i = LBound(myZdouCalTmp) To UBound(myZdouCalTmp)
        myZdouCalData(i, 1) = myZdouCalTmp(i)
    Next i
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｆ_指定行列が数値行列かどうかをチェックする
Private Function PfncbisIsMatrixNumeric(ByRef myZvarMtrx As Variant) As Boolean
    PfncbisIsMatrixNumeric = False
  Dim myXvarTmp As Variant, myXdouTmp As Double, L1 As Long, L2 As Long
    On Error GoTo ExitPath
    L1 = LBound(myZvarMtrx, 1): L2 = LBound(myZvarMtrx, 2)
    myXdouTmp = myZvarMtrx(L1, L2)
    For Each myXvarTmp In myZvarMtrx
        If IsNumeric(myXvarTmp) = False Then GoTo ExitPath
    Next myXvarTmp
    On Error GoTo 0
    PfncbisIsMatrixNumeric = True
ExitPath:
End Function

 '定型Ｐ_2次元の数値配列の指定列から一部を1次元配列で取得する
Private Sub PfixArrayPartColumnNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, ByVal myXlonExtrctCol As Long, _
            ByVal myXlonBgnRow As Long, ByVal myXlonEndRow As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 抽出データ配列
'myZdouOrgData(i, j) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgnRow < LBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow > UBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow < myXlonBgnRow Then Exit Sub
    If myXlonExtrctCol < LBound(myZdouOrgData, 2) _
        Or myXlonExtrctCol > UBound(myZdouOrgData, 2) Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData, 1) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgnRow, myXlonExtrctCol)
    Li = LBound(myZdouOrgData, 1)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgnRow To myXlonEndRow Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i, myXlonExtrctCol)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_1次元の数値配列から一部を取得する
Private Sub Pfix1DArrayExtractNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, _
            ByVal myXlonBgn As Long, ByVal myXlonEnd As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 取得データ配列
'myZdouOrgData(i) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgn < LBound(myZdouOrgData) Then Exit Sub
    If myXlonEnd > UBound(myZdouOrgData) Then Exit Sub
    If myXlonEnd < myXlonBgn Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgn): Li = LBound(myZdouOrgData)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgn To myXlonEnd Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_整数で台形波形を生成する
Private Sub PfixTrapezoidWaveLong( _
            myXlonCalDataCnt As Long, myZlonCalData() As Long, _
            ByVal myXlonDataQnt As Long, _
            ByVal myXlonInflctPntBgn As Long, ByVal myXlonInflctPntEnd As Long, _
            Optional ByVal L As Long = 1, _
            Optional ByVal coXlonStrtStp As Long = -1, _
            Optional ByVal coXlonCnstVal As Long = 0, _
            Optional ByVal coXlonLastStp As Long = 1)
'myZlonCalData(i) : 取得データ配列
'myXlonDataQnt : 必要なデータ数
'myXlonInflctPntBgn : 開始側の変曲点
'myXlonInflctPntEnd : 終了側の変曲点
'L : Option Base
'coXlonStrtStp : 開始側のデータ変化ステップ
'coXlonCnstVal : 一定状態の値
'coXlonLastStp : 終了側のデータ変化ステップ
    myXlonCalDataCnt = Empty:  Erase myZlonCalData
    If myXlonDataQnt <= 0 Then Exit Sub
    If myXlonInflctPntBgn < L Then Exit Sub
    If myXlonInflctPntEnd > myXlonDataQnt + L - 1 Then Exit Sub
    If myXlonInflctPntBgn > myXlonInflctPntEnd Then Exit Sub
    If L < 0 Or 1 < L Then Exit Sub
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long
    myXlonCalDataCnt = myXlonDataQnt
    i = myXlonCalDataCnt + Lo - 1: ReDim myZlonCalData(i) As Long
    If myXlonInflctPntBgn = L Then GoTo JumpPath
    n = myXlonInflctPntBgn
    For i = L To myXlonInflctPntBgn - 1
        n = n - 1
        myZlonCalData(i) = coXlonCnstVal - coXlonStrtStp * n
    Next i
JumpPath:
    For i = myXlonInflctPntBgn To myXlonInflctPntEnd
        myZlonCalData(i) = coXlonCnstVal
    Next i
    If myXlonInflctPntEnd = myXlonDataQnt + L - 1 Then Exit Sub
    n = 0
    For i = myXlonInflctPntEnd + 1 To myXlonDataQnt + L - 1
        n = n + 1
        myZlonCalData(i) = coXlonCnstVal + coXlonLastStp * n
    Next i
End Sub

 '定型Ｆ_配列計算-2配列の傾き
Private Function PfncdouSlope( _
            ByRef myZdouInptX() As Double, ByRef myZdouInptY() As Double) As Double
'myZdouInptX(i) : X軸用データ配列
'myZdouInptY(i) : Y軸用データ配列
    PfncdouSlope = Empty
    If UBound(myZdouInptX) <> UBound(myZdouInptY) Then Exit Function
  Dim n As Long: n = UBound(myZdouInptX) - LBound(myZdouInptX) + 1
  Dim i As Long, SY2 As Double, SX2 As Double, SY As Double, SXY As Double, SX As Double
    For i = LBound(myZdouInptX) To UBound(myZdouInptX)
        SY2 = SY2 + (myZdouInptY(i)) ^ 2
        SX2 = SX2 + (myZdouInptX(i)) ^ 2
        SY = SY + myZdouInptY(i)
        SXY = SXY + myZdouInptX(i) * myZdouInptY(i)
        SX = SX + myZdouInptX(i)
    Next i
    PfncdouSlope = (n * SXY - SY * SX) / (n * SX2 - SX ^ 2)
End Function

 '定型Ｐ_配列計算-2配列の傾き＆切片
Private Sub PfixSlopeIntercept( _
            myXdouSlp As Double, myXdouIntcpt As Double, _
            ByRef myZdouInptX() As Double, ByRef myZdouInptY() As Double)
'myZdouInptX(i) : X軸の元データ配列
'myZdouInptY(i) : Y軸の元データ配列
    myXdouSlp = Empty: myXdouIntcpt = Empty
    If UBound(myZdouInptX) <> UBound(myZdouInptY) Then Exit Sub
  Dim n As Long: n = UBound(myZdouInptX) - LBound(myZdouInptX) + 1
  Dim i As Long, SY2 As Double, SX2 As Double, SY As Double, SXY As Double, SX As Double
    For i = LBound(myZdouInptX) To UBound(myZdouInptX)
        SY2 = SY2 + (myZdouInptY(i)) ^ 2
        SX2 = SX2 + (myZdouInptX(i)) ^ 2
        SY = SY + myZdouInptY(i)
        SXY = SXY + myZdouInptX(i) * myZdouInptY(i)
        SX = SX + myZdouInptX(i)
    Next i
    myXdouSlp = (n * SXY - SY * SX) / (n * SX2 - SX ^ 2)
    myXdouIntcpt = (SX2 * SY - SXY * SX) / (n * SX2 - SX ^ 2)
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_データ配列の指定区間ごとで回帰直線処理をしてデータを取得する
'Private Sub instCIntrvlRgrsnLn()
'  Const coXlonCalRng As Long = 1
'    'coXlonCalRng = -1 : 今回値より前のデータで計算
'    'coXlonCalRng = 0  : 今回値の前後のデータで計算
'    'coXlonCalRng = 1  : 今回値より後のデータで計算
'  Const coXlonCalDataRngOptn As Long = 3
'    'coXlonCalRng = 1 : 全データ範囲で取得
'    'coXlonCalRng = 2 : 指定データ範囲で取得
'    'coXlonCalRng = 3 : 有効データ範囲で取得
'  Const coXlonBgnRow As Long = 1
'  Const coXlonEndRow As Long = 0
'  Const coXlonCalSmplQnt As Long = 11
'  Const coXlonCalStep As Long = 1
''  Dim myXlonOrgRowCnt As Long, myXlonOrgColCnt As Long, _
''        myZdouXOrgData() As Double, myZdouYOrgData() As Double
''    'myZdouXOrgData(i, 1) : X軸の元データ配列
''    'myZdouYOrgData(i, 1) : Y軸の元データ配列
'  Dim Lc As Long
''  Dim myXlonStrtRow As Long, myXlonLastRow As Long
''  Dim myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
''    'myZdouCalData(i, 1) : 計算処理後データ配列
''//CIntrvlRgrsnLnクラスモジュールのインスタンス生成
'  Dim myXinsIntrvlRgrsnLn As CIntrvlRgrsnLn: Set myXinsIntrvlRgrsnLn = New CIntrvlRgrsnLn
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long, j As Long
'    With myXinsIntrvlRgrsnLn
'    '//クラス内変数への入力
'        .letCalRng = coXlonCalRng
'        .letCalDataRngOptn = coXlonCalDataRngOptn
'        .letXOrgData = myZdouXOrgData
'        .letYOrgData = myZdouYOrgData
'        .letBgnRow = coXlonBgnRow
'        .letEndRow = coXlonEndRow
'        .letCalSmplQnt = coXlonCalSmplQnt
'        .letCalStep = coXlonCalStep
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProcCalSlope                        '回帰直線の傾き取得
''        .exeProcCalDeviationFromRegressionLine  'Y軸データと回帰直線との差分取得
'        myXlonStrtRow = .getStrtRow
'        myXlonLastRow = .getLastRow
'        myXlonCalRowCnt = .getCalRowCnt
'        myXlonCalColCnt = .getCalColCnt
'        If myXlonCalRowCnt <= 0 Or myXlonCalColCnt <= 0 Then GoTo JumpPath
'        i = myXlonCalRowCnt + Lo - 1: j = myXlonCalColCnt + Lo - 1
'        ReDim myZdouCalData(i, j) As Double
'        Lc = .getOptnBase
'        For i = 1 To myXlonCalRowCnt
'            myZdouCalData(i + Lo - 1, Lo) = .getCalData(i + Lc - 1, Lc)
'        Next i
'    End With
'JumpPath:
'    Set myXinsIntrvlRgrsnLn = Nothing
'    Call variablesOfCIntrvlRgrsnLn(myXlonCalRowCnt, myZdouCalData)   'Debug.Print
'End Sub
'Private Sub variablesOfCIntrvlRgrsnLn( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CIntrvlRgrsnLnクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim i As Long, j As Long
'    For i = LBound(myZvarField, 1) To UBound(myZvarField, 1)
'        For j = LBound(myZvarField, 2) To UBound(myZvarField, 2)
'            Debug.Print "データ" & i & "," & j & ": " & myZvarField(i, j)
'        Next j
'    Next i
'End Sub

      <br></pre>
    </body>
  </html>
