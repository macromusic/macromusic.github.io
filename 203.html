<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PfixGetFolderTreeWithComeBack
'Includes incPfixGetShortCutSubFileArray
'Includes PfncbisCheckShortCutFile
'Includes PfixGetFolderFileStringInformationFor1DArray

Option Explicit
Option Base 1

'◆ClassProc名_指定ディレクトリ階層内のショートカットファイル一覧を取得する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CSubLyrShrtCutLst"
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonFileCnt As Long, myZobjShrtCutFile() As Object, _
            myZstrShrtCutFileName() As String, myZstrShrtCutFilePath() As String
  
'//PropertySetLet_入力制御信号
  Private myXbisNotOutFileInfo As Boolean
  
'//PropertySetLet_入力データ
  Private myXstrDirPath As String, myXlonLyrs As Long
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonFldrTreeCnt As Long, myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonFldrTreeCnt = Empty: Erase myZobjFldrTree: Erase myZlonFldrLyrNo
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getFileCnt() As Long
    getFileCnt = myXlonFileCnt
End Property

Public Property Get getShrtCutFileAry(ByVal i As Long) As Object
    Set getShrtCutFileAry = myZobjShrtCutFile(i)
'myZobjShrtCutFile(i) : ショートカットファイル
End Property

Public Property Get getShrtCutFileNameAry(ByVal i As Long) As String
    getShrtCutFileNameAry = myZstrShrtCutFileName(i)
'myZstrShrtCutFileName() : ショートカットファイル名
End Property

Public Property Get getShrtCutFilePathAry(ByVal i As Long) As String
    getShrtCutFilePathAry = myZstrShrtCutFilePath(i)
'myZstrShrtCutFilePath(i) : ショートカットファイルパス
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonFileCnt = Empty: Erase myZobjShrtCutFile
    Erase myZstrShrtCutFileName: Erase myZstrShrtCutFilePath
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letNotOutFileInfo(ByVal RHS As Boolean)
    myXbisNotOutFileInfo = RHS
'myXbisNotOutFileInfo = False : ファイルオブジェクとファイル情報を両方出力する
'myXbisNotOutFileInfo = True  : ファイルオブジェクトのみ出力してファイル情報は出力しない
End Property

Public Property Let letDirPath(ByVal RHS As String)
    myXstrDirPath = RHS
End Property

Public Property Let letLyrs(ByVal RHS As Long)
    myXlonLyrs = RHS
'myXlonLyrs = 0   : 全フォルダ階層取得
'myXlonLyrs = 1～ : 取得したいフォルダ階層数
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXstrDirPath = "" Then GoTo ExitPath
    If myXlonLyrs < 0 Then myXlonLyrs = 0
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//S:指定ディレクトリのフォルダ階層一覧を取得
    Call snsProc1
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//S:ディレクトリ階層内のショートカットファイル一覧を取得
    Call snsProc2
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
'//P:ファイル情報取得
    Call prsProc
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SnsP_指定ディレクトリのフォルダ階層一覧を取得
Private Sub snsProc1()
    myXbisExitFlag = False
    
    Call PfixGetFolderTreeWithComeBack( _
            myXlonFldrTreeCnt, myZobjFldrTree, myZlonFldrLyrNo, _
            myXstrDirPath, myXlonLyrs)
    If myXlonFldrTreeCnt <= 0 Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SnsP_ディレクトリ階層内のショートカットファイル一覧を取得
Private Sub snsProc2()
    myXbisExitFlag = False

  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim n As Long: n = L - 1
  Dim myXvarDir As Variant, k As Long: k = 0
  Dim myXobjDir As Object, myXlonTmpCnt As Long, myZobjTmpFile() As Object
    
    For Each myXvarDir In myZobjFldrTree
        k = k + 1
        Set myXobjDir = Nothing
        Set myXobjDir = myXvarDir
 
        Call incPfixGetShortCutSubFileArray(myXlonTmpCnt, myZobjTmpFile, myXobjDir)
        If myXlonTmpCnt <= 0 Then GoTo NextPath
'        Debug.Print "PassFlag: " & meMstrMdlName & "3-" & k   'PassFlag
        
        Call snsSubProc2(myZobjTmpFile, n)
        
NextPath:
    Next myXvarDir
    myXlonFileCnt = n - L + 1
    
    Set myXobjDir = Nothing: Erase myZobjTmpFile
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SnsP_ショートカットファイルを配列変数に格納
Private Sub snsSubProc2(ByRef myZobjTmpFile() As Object, ByRef n As Long)

  Dim myXvarFile As Variant
    For Each myXvarFile In myZobjTmpFile
        n = n + 1: ReDim Preserve myZobjShrtCutFile(n) As Object
        Set myZobjShrtCutFile(n) = myXvarFile
    Next myXvarFile
    
End Sub

'PrcsP_ファイル情報取得
Private Sub prsProc()
    myXbisExitFlag = False
    
    If myXbisNotOutFileInfo = True Then Exit Sub
    
'//ショートカットファイル一覧のファイル名を取得
  Dim myXlonInfoCnt As Long
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrShrtCutFileName, _
            myZobjShrtCutFile, 1)
    If myXlonInfoCnt <> myXlonFileCnt Then GoTo ExitPath
    
'//ショートカットファイル一覧のファイルパスを取得
    Call PfixGetFolderFileStringInformationFor1DArray( _
            myXlonInfoCnt, myZstrShrtCutFilePath, _
            myZobjShrtCutFile, 2)
     If myXlonInfoCnt <> myXlonFileCnt Then GoTo ExitPath
   
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｐ_フォルダ階層一覧を取得する
Private Sub PfixGetFolderTreeWithComeBack( _
            myXlonFldrTreeCnt As Long, _
            myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long, _
            ByVal myXstrDirPath As String, _
            Optional ByVal coXlonLyrs As Long = 0)
'myZobjFldrTree(i) : フォルダ階層のオブジェクト一覧
'myZlonFldrLyrNo(i) : フォルダ階層のオブジェクト一覧の階層No
'myXstrDirPath : フォルダ階層を取得したいディレクトリパス
'coXlonLyrs = 0   : 全フォルダ階層取得
'coXlonLyrs = 1～ : 取得したいフォルダ階層数
    myXlonFldrTreeCnt = Empty: Erase myZobjFldrTree: Erase myZlonFldrLyrNo
    If myXstrDirPath = "" Then Exit Sub
'//初期ディレクトリをセット
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXobjDir As Object: Set myXobjDir = myXobjFSO.GetFolder(myXstrDirPath)
    Set myXobjFSO = Nothing
'//再帰プロシージャ開始
  Dim myXlonFldrCnt As Long, myXlonLyrNo As Long
    myXlonFldrCnt = 0: myXlonLyrNo = 0
    Call PsubFolderTreeComeBack( _
            myZobjFldrTree, myZlonFldrLyrNo, myXlonFldrCnt, _
            myXlonLyrNo, coXlonLyrs, myXobjDir)
    Set myXobjDir = Nothing
    myXlonFldrTreeCnt = myXlonFldrCnt
End Sub
Private Sub PsubFolderTreeComeBack( _
            myZobjFldrTree() As Object, myZlonFldrLyrNo() As Long, _
            ByRef myXlonFldrCnt As Long, _
            ByVal myXlonLyrNo As Long, ByVal myXlonLyrs As Long, _
            ByVal myXobjFldr As Object)
'//再起用サブプロシージャ
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
'//フォルダを配列変数に格納
    myXlonFldrCnt = myXlonFldrCnt + 1
    myXlonLyrNo = myXlonLyrNo + 1
  Dim n As Long: n = myXlonFldrCnt + L - 1
    ReDim Preserve myZobjFldrTree(n) As Object
    ReDim Preserve myZlonFldrLyrNo(n) As Long
    Set myZobjFldrTree(n) = myXobjFldr
    myZlonFldrLyrNo(n) = myXlonLyrNo
    If (myXlonLyrs >= 1 And myXlonLyrNo >= myXlonLyrs) Then Exit Sub
'//サブフォルダを確認
  Dim myXlonSubFldrCnt As Long
    myXlonSubFldrCnt = myXobjFldr.SubFolders.Count
    If myXlonSubFldrCnt <= 0 Then Exit Sub
'//再帰を使用してサブフォルダを探索
  Dim myZobjSubFldr As Object
    For Each myZobjSubFldr In myXobjFldr.SubFolders
        Call PsubFolderTreeComeBack( _
                myZobjFldrTree, myZlonFldrLyrNo, myXlonFldrCnt, _
                myXlonLyrNo, myXlonLyrs, myZobjSubFldr)
    Next myZobjSubFldr
    Set myZobjSubFldr = Nothing
End Sub

 '定型Ｐ_指定ディレクトリ内のショートカットのサブファイル一覧を取得する
Private Sub incPfixGetShortCutSubFileArray( _
            myXlonFileCnt As Long, myZobjShrtCutFile() As Object, _
            ByVal myXobjDir As Object)
'Includes PfncbisCheckShortCutFile
'myZobjShrtCutFile(i) : ショートカットファイル
    myXlonFileCnt = Empty: Erase myZobjShrtCutFile
    If myXobjDir Is Nothing Then Exit Sub
    If myXobjDir.Files.Count <= 0 Then Exit Sub
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim myXobjSubFile As Object, myXstrFilePath As String
  Dim myXbisShrtCutChck As Boolean, n As Long: n = L - 1
    For Each myXobjSubFile In myXobjDir.Files
        myXstrFilePath = myXobjSubFile.Path
        myXbisShrtCutChck = PfncbisCheckShortCutFile(myXstrFilePath)
        If myXbisShrtCutChck = False Then GoTo NextPath
        n = n + 1: ReDim Preserve myZobjShrtCutFile(n) As Object
        Set myZobjShrtCutFile(n) = myXobjSubFile
NextPath:
    Next myXobjSubFile
    myXlonFileCnt = n - L + 1
    Set myXobjSubFile = Nothing
End Sub

 '定型Ｆ_指定ショートカットファイルの存在を確認する
Private Function PfncbisCheckShortCutFile(ByVal myXstrFilePath As String) As Boolean
  Const coXstrLnkExtsn As String = "lnk"
    PfncbisCheckShortCutFile = False
    If myXstrFilePath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXstrExist As Boolean, myXstrExtsn As String
    myXstrExist = myXobjFSO.FileExists(myXstrFilePath)
    If myXstrExist = False Then Exit Function
    myXstrExtsn = myXobjFSO.GetExtensionName(myXstrFilePath)
    If LCase(myXstrExtsn) = LCase(coXstrLnkExtsn) Then PfncbisCheckShortCutFile = True
    Set myXobjFSO = Nothing
End Function

 '定型Ｐ_1次元配列のフォルダファイルオブジェクト一覧の文字列情報を取得する
Private Sub PfixGetFolderFileStringInformationFor1DArray( _
                myXlonInfoCnt As Long, myZstrInfo() As String, _
                ByRef myZobjFldrFile() As Object, _
                Optional ByVal coXlonStrOptn As Long = 1)
'myZstrInfo(i) : 抽出フォルダ情報
'myZobjFldrFile(i) : 元フォルダor元ファイル
'coXlonStrOptn = 1  : 名前 (Name)
'coXlonStrOptn = 2  : パス (Path)
'coXlonStrOptn = 3  : 親フォルダ (ParentFolder)
'coXlonStrOptn = 4  : 属性 (Attributes)
'coXlonStrOptn = 5  : 種類 (Type)
    myXlonInfoCnt = Empty: Erase myZstrInfo
  Dim myXobjTmp As Object, Li As Long
    On Error GoTo ExitPath
    Li = LBound(myZobjFldrFile): Set myXobjTmp = myZobjFldrFile(Li)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXstrTmp As String, i As Long, n As Long: n = Lo - 1
    On Error GoTo NextPath
    For i = LBound(myZobjFldrFile) To UBound(myZobjFldrFile)
        myXstrTmp = Empty
        Select Case coXlonStrOptn
            Case 1: myXstrTmp = myZobjFldrFile(i).Name
            Case 2: myXstrTmp = myZobjFldrFile(i).Path
            Case 3: myXstrTmp = myZobjFldrFile(i).ParentFolder
            Case 4: myXstrTmp = myZobjFldrFile(i).Attributes
            Case 5: myXstrTmp = myZobjFldrFile(i).Type
        End Select
        n = n + 1: ReDim Preserve myZstrInfo(n) As String
        myZstrInfo(n) = myXstrTmp
NextPath:
    Next i
    On Error GoTo 0
    myXlonInfoCnt = n - Lo + 1
ExitPath:
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_指定ディレクトリ階層内のショートカットファイル一覧を取得する
'Private Sub PforNewInstanceOfCSubLyrShrtCutLst()
'  Dim Lc As Long
'  Dim myXlonFileCnt As Long, myZobjShrtCutFile() As Object, _
'        myZstrShrtCutFileName() As String, myZstrShrtCutFilePath() As String
'    'myZobjShrtCutFile(i) : ショートカットファイル
'    'myZstrShrtCutFileName() : ショートカットファイル名
'    'myZstrShrtCutFilePath(i) : ショートカットファイルパス
''//クラス内変数への入力
'  Dim myXbisNotOutFileInfo As Boolean
'    'myXbisNotOutFileInfo = False : ファイルオブジェクとファイル情報を両方出力する
'    'myXbisNotOutFileInfo = True  : ファイルオブジェクトのみ出力してファイル情報は出力しない
'    myXbisNotOutFileInfo = False
'  Dim myXstrDirPath As String
'    myXstrDirPath = "C:\_Cdrive"
'  Dim myXlonLyrs As Long
'    'myXlonLyrs = 0   : 全フォルダ階層取得
'    'myXlonLyrs = 1～ : 取得したいフォルダ階層数
'    myXlonLyrs = 0
''//CSubLyrShrtCutLstクラスモジュールのインスタンス生成
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long
'  Dim myXinsSubLyrShrtCutLst As CSubLyrShrtCutLst
'    Set myXinsSubLyrShrtCutLst = New CSubLyrShrtCutLst
'    With myXinsSubLyrShrtCutLst
'    '//クラス内変数への入力
'        .letNotOutFileInfo = myXbisNotOutFileInfo
'        .letDirPath = myXstrDirPath
'        .letLyrs = myXlonLyrs
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXlonFileCnt = .getFileCnt
'        If myXlonFileCnt <= 0 Then GoTo JumpPath
'        i = myXlonFileCnt + Lo - 1
'        ReDim myZobjShrtCutFile(i) As Object
'        ReDim myZstrShrtCutFileName(i) As String
'        ReDim myZstrShrtCutFilePath(i) As String
'        Lc = .getOptnBase
'        For i = 1 To myXlonFileCnt
'            Set myZobjShrtCutFile(i + Lo - 1) = .getShrtCutFileAry(i + Lc - 1)
'            myZstrShrtCutFileName(i + Lo - 1) = .getShrtCutFileNameAry(i + Lc - 1)
'            myZstrShrtCutFilePath(i + Lo - 1) = .getShrtCutFilePathAry(i + Lc - 1)
'        Next i
'    End With
'JumpPath:
'    Set myXinsSubLyrShrtCutLst = Nothing
'    Call PforDisplayVariablesOfCSubLyrShrtCutLst(myXlonFileCnt, myZstrShrtCutFileName)  'Debug.Print
'ExitPath:
'End Sub
'Private Sub PforDisplayVariablesOfCSubLyrShrtCutLst( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CSubLyrShrtCutLstクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim k As Long
'    For k = LBound(myZvarField) To UBound(myZvarField)
'        Debug.Print "データ" & k & ": " & myZvarField(k)
'    Next k
'End Sub

      <br></pre>
    </body>
  </html>
