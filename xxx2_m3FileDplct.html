<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes CFileDplct
'Includes PfnclonArrayDimension
'Includes PfncbisCheckFileExist
'Includes PfncbisCheckFolderExist
'Includes PfixGetFileNameInformationByFSO
'Includes PfncstrFilePathReplaceParentBaseExtension

Option Explicit
Option Base 1

'◆ModuleProc名_ファイルを複製する
'Rev.002
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "m3FileDplct"
  
'//出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//出力データ
  Private myXlonExeFileCnt As Long, _
            myZstrExeFilePath() As String, myZstrExeFileName() As String
    'myZstrExeFilePath(k) : 実行ファイルパス
    'myZstrExeFileName(k) : 実行ファイル名
  
'//入力データ
  Private myXstrOrgFilePath As String
  Private myXlonOrgInfoCnt As Long, myZstrOrgInfo() As String
    'myZstrOrgInfo(i) : 元情報
  Private myXstrSaveDirPath As String
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonRunInfoNo As Long, myXstrRunInfo As String
  Private myXlonDplctFileCnt As Long, myZstrDplctFilePath() As String

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonRunInfoNo = Empty: myXstrRunInfo = Empty
    myXlonDplctFileCnt = Empty: Erase myZstrDplctFilePath
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub callProc( _
            myXbisCmpltFlagOUT As Boolean, _
            myXlonExeFileCntOUT As Long, _
            myZstrExeFilePathOUT() As String, myZstrExeFileNameOUT() As String, _
            ByVal myXstrOrgFilePathIN As String, _
            ByVal myXlonOrgInfoCntIN As Long, ByRef myZstrOrgInfoIN() As String, _
            ByVal myXstrSaveDirPathIN As String)
    
'//入力変数を初期化
    myXstrOrgFilePath = Empty
    myXlonOrgInfoCnt = Empty
    Erase myZstrOrgInfo
    myXstrSaveDirPath = Empty

'//入力変数を取り込み
    myXstrOrgFilePath = myXstrOrgFilePathIN
    If myXlonOrgInfoCntIN <= 0 Then Exit Sub
    myXlonOrgInfoCnt = myXlonOrgInfoCntIN
    myZstrOrgInfo() = myZstrOrgInfoIN()
    myXstrSaveDirPath = myXstrSaveDirPathIN
    
'//出力変数を初期化
    myXbisCmpltFlagOUT = False
    
    myXlonExeFileCntOUT = Empty
    Erase myZstrExeFilePathOUT
    Erase myZstrExeFileNameOUT
    
'//処理実行
    Call ctrProc
    If myXbisCmpltFlag = False Then Exit Sub
    
'//出力変数に格納
    myXbisCmpltFlagOUT = myXbisCmpltFlag
    
    myXlonExeFileCntOUT = myXlonExeFileCnt
    myZstrExeFilePathOUT() = myZstrExeFilePath()
    myZstrExeFileNameOUT() = myZstrExeFileName()
    
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"    'PassFlag
    
'//指定ファイルのファイル名情報を取得(FileSystemObject使用)
  Dim myXstrPrntPath As String, myXstrFileName As String, _
        myXstrBaseName As String, myXstrExtsn As String
    Call PfixGetFileNameInformationByFSO( _
            myXstrPrntPath, myXstrFileName, myXstrBaseName, myXstrExtsn, _
            myXstrOrgFilePath)
    
'//C:情報一覧を順次実行
  Dim myXstrOrgPrnt As String, myXstrOrgBase As String, myXstrOrgExt As String
  Dim myXstrNewPrnt As String, myXstrNewBase As String, myXstrNewExt As String
  Dim myXstrDplctFilePath As String
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim n As Long: n = Lo - 1
  Dim k As Long
    For k = LBound(myZstrOrgInfo) To UBound(myZstrOrgInfo)
        myXstrRunInfo = Empty
        myXlonRunInfoNo = k
        myXstrRunInfo = myZstrOrgInfo(k)
        If myXstrRunInfo = "" Then GoTo NextPath
        
    '//ファイルパス内のディレクトリパスorベース名or拡張子を置換
        myXstrOrgPrnt = Empty: myXstrNewPrnt = Empty
        myXstrOrgBase = Empty: myXstrNewBase = Empty
        
        myXstrOrgPrnt = myXstrPrntPath
        myXstrNewPrnt = myXstrSaveDirPath
        myXstrDplctFilePath = PfncstrFilePathReplaceParentBaseExtension( _
                                myXstrOrgFilePath, _
                                myXstrOrgPrnt, myXstrOrgBase, myXstrOrgExt, _
                                myXstrNewPrnt, myXstrNewBase, myXstrNewExt)
        myXstrOrgPrnt = Empty: myXstrNewPrnt = Empty
        
        myXstrOrgBase = myXstrBaseName
        myXstrNewBase = myXstrRunInfo
        myXstrDplctFilePath = PfncstrFilePathReplaceParentBaseExtension( _
                                myXstrDplctFilePath, _
                                myXstrOrgPrnt, myXstrOrgBase, myXstrOrgExt, _
                                myXstrNewPrnt, myXstrNewBase, myXstrNewExt)
        
        n = n + 1
        ReDim Preserve myZstrDplctFilePath(n) As String
        myZstrDplctFilePath(n) = myXstrDplctFilePath
NextPath:
    Next k
    myXlonDplctFileCnt = n - Lo + 1
'    Debug.Print "PassFlag: " & meMstrMdlName & "7"    'PassFlag

'//ファイルを複製
    Call instCFileDplct
    If myXbisExitFlag = True Then GoTo ExitPath
    
    myXbisCmpltFlag = True
ExitPath:
    Call initializeModuleVariables
End Sub

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
    myXlonExeFileCnt = Empty: Erase myZstrExeFilePath: Erase myZstrExeFileName
End Sub

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
'//指定ファイルの存在を確認
    If PfncbisCheckFileExist(myXstrOrgFilePath) = False Then GoTo ExitPath
    
'//指定フォルダの存在を確認
    If PfncbisCheckFolderExist(myXstrSaveDirPath) = False Then GoTo ExitPath
    
'//配列変数の次元数を取得
  Dim myXlonAryDmnsn As Long
    myXlonAryDmnsn = PfnclonArrayDimension(myZstrOrgInfo)
    
  Dim Li As Long, myXstrTmp As String
  
    On Error GoTo ExitPath
    Select Case myXlonAryDmnsn
        Case 1
            Li = LBound(myZstrOrgInfo): myXstrTmp = myZstrOrgInfo(Li)
            Exit Sub
        Case 2
            Li = LBound(myZstrOrgInfo, 1): myXstrTmp = myZstrOrgInfo(Li, Li)
        Case Else: GoTo ExitPath
    End Select
    On Error GoTo 0

  Dim myZstrOrgInfoINT() As String
    myZstrOrgInfoINT() = myZstrOrgInfo()
    
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long
    i = UBound(myZstrOrgInfoINT, 1)
    ReDim myZstrOrgInfo(i) As String
    For i = LBound(myZstrOrgInfoINT) To UBound(myZstrOrgInfoINT)
        myZstrOrgInfo(i + Lo - Li) = myZstrOrgInfoINT(i)
    Next i
    
    Erase myZstrOrgInfoINT
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

'◆ClassProc名_ファイルを複製する
Private Sub instCFileDplct()
  Dim Lc As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
  Dim i As Long
  Dim myXinsFileDplct As CFileDplct: Set myXinsFileDplct = New CFileDplct
    With myXinsFileDplct
    '//クラス内変数への入力
        .letOrgFilePath = myXstrOrgFilePath
        .letDplctFilePathAry() = myZstrDplctFilePath()
        .letAutoNaming = True
    '//クラス内プロシージャの実行とクラス内変数からの出力
        .exeProc
        myXbisExitFlag = Not .getCmpltFlag
        myXlonExeFileCnt = .getExeFileCnt
        If myXlonExeFileCnt <= 0 Then GoTo JumpPath
        i = myXlonExeFileCnt + L - 1
        ReDim myZstrExeFilePath(i) As String
        ReDim myZstrExeFileName(i) As String
        Lc = .getOptnBase
        For i = 1 To myXlonExeFileCnt
            myZstrExeFilePath(i + L - 1) = .getExeFilePathAry(i + Lc - 1)
            myZstrExeFileName(i + L - 1) = .getExeFileNameAry(i + Lc - 1)
        Next i
    End With
JumpPath:
    Set myXinsFileDplct = Nothing
End Sub

'===============================================================================================
 
 '定型Ｆ_配列変数の次元数を取得する
Private Function PfnclonArrayDimension(ByRef myZvarOrgData As Variant) As Long
    PfnclonArrayDimension = Empty
    If IsArray(myZvarOrgData) = False Then Exit Function
  Dim myXvarTmp As Variant, k As Long: k = 0
    On Error Resume Next
    Do
        k = k + 1: myXvarTmp = UBound(myZvarOrgData, k)
    Loop While Err.Number = 0
    On Error GoTo 0
    PfnclonArrayDimension = k - 1
End Function

 '定型Ｆ_指定ファイルの存在を確認する
Private Function PfncbisCheckFileExist(ByVal myXstrFilePath As String) As Boolean
    PfncbisCheckFileExist = False
    If myXstrFilePath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfncbisCheckFileExist = myXobjFSO.FileExists(myXstrFilePath)
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_指定フォルダの存在を確認する
Private Function PfncbisCheckFolderExist(ByVal myXstrDirPath As String) As Boolean
    PfncbisCheckFolderExist = False
    If myXstrDirPath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfncbisCheckFolderExist = myXobjFSO.FolderExists(myXstrDirPath)
    Set myXobjFSO = Nothing
End Function

 '定型Ｐ_指定ファイルのファイル名情報を取得する(FileSystemObject使用)
Private Sub PfixGetFileNameInformationByFSO( _
            myXstrPrntPath As String, myXstrFileName As String, _
            myXstrBaseName As String, myXstrExtsn As String, _
            ByVal myXstrFilePath As String)
    myXstrPrntPath = Empty: myXstrFileName = Empty
    myXstrBaseName = Empty: myXstrExtsn = Empty
    If myXstrFilePath = "" Then Exit Sub
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
  Dim myXbisFileExist As Boolean
    With myXobjFSO
        myXbisFileExist = .FileExists(myXstrFilePath)
        If myXbisFileExist = False Then Exit Sub
        myXstrPrntPath = .GetParentFolderName(myXstrFilePath)   '親フォルダパス
        myXstrFileName = .GetFileName(myXstrFilePath)           'ファイル名
        myXstrBaseName = .GetBaseName(myXstrFilePath)           'ファイルベース名
        myXstrExtsn = .GetExtensionName(myXstrFilePath)         'ファイル拡張子
    End With
    Set myXobjFSO = Nothing
End Sub

 '定型Ｆ_ファイルパス内のディレクトリパスorベース名or拡張子を置換する
Private Function PfncstrFilePathReplaceParentBaseExtension( _
            ByVal myXstrOrgFilePath As String, _
            ByVal myXstrOrgPrnt As String, ByVal myXstrOrgBase As String, _
            ByVal myXstrOrgExt As String, _
            ByVal myXstrNewPrnt As String, ByVal myXstrNewBase As String, _
            ByVal myXstrNewExt As String) As String
    PfncstrFilePathReplaceParentBaseExtension = Empty
    If myXstrOrgFilePath = "" Then Exit Function
    If myXstrNewPrnt = "" And myXstrNewBase = "" And myXstrNewExt = "" Then Exit Function
  Dim myXstrNewFilePath As String
    If InStr(myXstrOrgFilePath, myXstrOrgPrnt) > 0 And myXstrNewPrnt <> "" Then _
        myXstrNewFilePath = Replace(myXstrOrgFilePath, myXstrOrgPrnt, myXstrNewPrnt)
    If InStr(myXstrOrgFilePath, myXstrOrgBase) > 0 And myXstrNewBase <> "" Then _
        myXstrNewFilePath = Replace(myXstrOrgFilePath, myXstrOrgBase, myXstrNewBase)
    If InStr(myXstrOrgFilePath, myXstrOrgExt) > 0 And myXstrNewExt <> "" Then _
        myXstrNewFilePath = Replace(myXstrOrgFilePath, myXstrOrgExt, myXstrNewExt)
    PfncstrFilePathReplaceParentBaseExtension = myXstrNewFilePath
End Function

'DummyＰ_
Private Sub MsubDummy()
End Sub

      <br></pre>
    </body>
  </html>
