<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PfixGetPathBookSheetRangeAddress
'Includes PfixGetRowColumnInRangeAddress

Option Explicit
Option Base 1

'◆ClassProc名_グラフ用のセル範囲アドレスからセル情報を取得する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CGetRngAdrsInfo"
  
'//PropertyGet_出力データ
  Private myXstrPathBkSht As String, myXstrRngAdrs As String, _
            myXstrDirPath As String, myXstrBookName As String, myXstrShtName As String
  Private myXlonBgnRow As Long, myXlonBgnCol As Long, myXlonEndRow As Long, myXlonEndCol As Long
  
'//PropertySetLet_入力データ
  Private myXstrRngAdrsWEx As String
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getPathBkSht() As String
    getPathBkSht = myXstrPathBkSht
End Property

Public Property Get getRngAdrs() As String
    getRngAdrs = myXstrRngAdrs
End Property

Public Property Get getDirPath() As String
    getDirPath = myXstrDirPath
End Property

Public Property Get getBookName() As String
    getBookName = myXstrBookName
End Property

Public Property Get getShtName() As String
    getShtName = myXstrShtName
End Property

Public Property Get getRowCol(ByVal k As Long) As Long
    Select Case k
        Case 1: getRowCol = myXlonBgnRow
        Case 2: getRowCol = myXlonBgnCol
        Case 3: getRowCol = myXlonEndRow
        Case 4: getRowCol = myXlonEndCol
    End Select
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXstrPathBkSht = Empty: myXstrRngAdrs = Empty
    myXstrDirPath = Empty: myXstrBookName = Empty: myXstrShtName = Empty
    myXlonBgnRow = Empty: myXlonBgnCol = Empty
    myXlonEndRow = Empty: myXlonEndCol = Empty
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letRngAdrsWEx(ByVal RHS As String)
    myXstrRngAdrsWEx = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXstrRngAdrsWEx = "" Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//セル範囲アドレスからパス[ブック]シート!セル範囲情報を分離
    Call PfixGetPathBookSheetRangeAddress( _
            myXstrPathBkSht, myXstrRngAdrs, myXstrDirPath, myXstrBookName, myXstrShtName, _
            myXstrRngAdrsWEx)
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//セル範囲アドレスから行列情報を取得
    Call PfixGetRowColumnInRangeAddress( _
            myXlonBgnRow, myXlonBgnCol, myXlonEndRow, myXlonEndCol, _
            myXstrRngAdrsWEx)
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
ExitPath:
    Call initializeModuleVariables
End Sub

'===============================================================================================

 '定型Ｐ_セル範囲アドレスからパス[ブック]シート!セル範囲情報を分離する
Private Sub PfixGetPathBookSheetRangeAddress( _
            myXstrPathBkSht As String, myXstrRngAdrs As String, _
            myXstrDirPath As String, myXstrBookName As String, myXstrShtName As String, _
            ByVal myXstrRngAdrsWEx As String)
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    myXstrPathBkSht = Empty: myXstrRngAdrs = Empty
    myXstrDirPath = Empty: myXstrBookName = Empty: myXstrShtName = Empty
    If myXstrRngAdrsWEx = "" Then Exit Sub
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(0)
        myXstrRngAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//パス[ブック]シート部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrTmp, "'") + 1
    myXlonEnd = InStrRev(myXstrTmp, "'") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then
        myXstrPathBkSht = myXstrTmp
    Else
        myXstrPathBkSht = Mid(myXstrTmp, myXlonBgn, myXlonLen)
    End If
'//シート部分を取得
    myZvarSplt = Split(myXstrPathBkSht, "]")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 1 Then
        myXstrShtName = myZvarSplt(0)
    ElseIf myXlonSpltCnt = 2 Then
        myXstrShtName = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//ブック部分を取得
    myXlonBgn = InStr(myXstrPathBkSht, "[") + 1
    myXlonEnd = InStrRev(myXstrPathBkSht, "]") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrBookName = Mid(myXstrPathBkSht, myXlonBgn, myXlonLen)
'//パス部分を取得
    myZvarSplt = Split(myXstrPathBkSht, "[")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrDirPath = myZvarSplt(0)
        If myXstrDirPath <> "" Then _
            myXstrDirPath = Left(myXstrDirPath, Len(myXstrDirPath) - 1)
    Else
        Exit Sub
    End If
End Sub

 '定型Ｐ_セル範囲アドレスから行列情報を取得する
Private Sub PfixGetRowColumnInRangeAddress( _
            myXlonBgnRow As Long, myXlonBgnCol As Long, _
            myXlonEndRow As Long, myXlonEndCol As Long, _
            ByVal myXstrRngAdrsWEx As String)
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    myXlonBgnRow = Empty: myXlonBgnCol = Empty: myXlonEndRow = Empty: myXlonEndCol = Empty
    If myXstrRngAdrsWEx = "" Then Exit Sub
  Dim myXstrRngAdrs As String, myXstrBgnAdrs As String, myXstrEndAdrs As String
  Dim myXstrBgnRow As String, myXstrBgnCol As String, _
        myXstrEndRow As String, myXstrEndCol As String
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(0)
        myXstrRngAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//セル範囲をR1C1形式に変換
    On Error Resume Next
    myXstrRngAdrs = Application.ConvertFormula(Formula:=myXstrRngAdrs, _
                        FromReferenceStyle:=xlA1, ToReferenceStyle:=xlR1C1, ToAbsolute:=True)
    On Error GoTo 0
    If myXstrRngAdrs = "" Then Exit Sub
'//セル範囲の前半部分と後半部分を分離
    myZvarSplt = Split(myXstrRngAdrs, ":")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 1 Then
        myXstrBgnAdrs = myZvarSplt(0)
    ElseIf myXlonSpltCnt = 2 Then
        myXstrBgnAdrs = myZvarSplt(0)
        myXstrEndAdrs = myZvarSplt(1)
    Else
        Exit Sub
    End If
'//最初の行と最初の列を取得
    myZvarSplt = Split(myXstrBgnAdrs, "R")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(1)
    Else
        Exit Sub
    End If
    myZvarSplt = Split(myXstrTmp, "C")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrBgnRow = myZvarSplt(0)
        myXstrBgnCol = myZvarSplt(1)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrBgnRow) = True Then myXlonBgnRow = CLng(myXstrBgnRow)
    If IsNumeric(myXstrBgnCol) = True Then myXlonBgnCol = CLng(myXstrBgnCol)
'//最後の行と最後の列を取得
    myZvarSplt = Split(myXstrEndAdrs, "R")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrTmp = myZvarSplt(1)
    Else
        Exit Sub
    End If
    myZvarSplt = Split(myXstrTmp, "C")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrEndRow = myZvarSplt(0)
        myXstrEndCol = myZvarSplt(1)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrEndRow) = True Then myXlonEndRow = CLng(myXstrEndRow)
    If IsNumeric(myXstrEndCol) = True Then myXlonEndCol = CLng(myXstrEndCol)
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_グラフ用のセル範囲アドレスからセル情報を取得する
'Private Sub instCGetRngAdrsInfo()
'  Dim myXstrPathBkSht As String, myXstrRngAdrs As String, _
'        myXstrDirPath As String, myXstrBookName As String, myXstrShtName As String
'  Dim myXlonBgnRow As Long, myXlonBgnCol As Long, myXlonEndRow As Long, myXlonEndCol As Long
''//クラス内変数への入力
'  Dim myXstrRngAdrsWEx As String
'    myXstrRngAdrsWEx = "'パス\[ブック]シート'!R1C1:R2C2"
''//CGetRngAdrsInfoクラスモジュールのインスタンス生成
'  Dim myXinsGetRngAdrsInfo As CGetRngAdrsInfo
'    Set myXinsGetRngAdrsInfo = New CGetRngAdrsInfo
'    With myXinsGetRngAdrsInfo
'    '//クラス内変数への入力
'        .letRngAdrsWEx = myXstrRngAdrsWEx
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXstrPathBkSht = .getPathBkSht
'        myXstrRngAdrs = .getRngAdrs
'        myXstrDirPath = .getDirPath
'        myXstrBookName = .getBookName
'        myXstrShtName = .getShtName
'        myXlonBgnRow = .getRowCol(1)
'        myXlonBgnCol = .getRowCol(2)
'        myXlonEndRow = .getRowCol(3)
'        myXlonEndCol = .getRowCol(4)
'    End With
'    Set myXinsGetRngAdrsInfo = Nothing
''    Debug.Print "データ: " & myXstrPathBkSht
''    Debug.Print "データ: " & myXstrRngAdrs
''    Debug.Print "データ: " & myXstrDirPath
''    Debug.Print "データ: " & myXstrBookName
''    Debug.Print "データ: " & myXstrShtName
''    Debug.Print "データ: " & myXlonBgnRow
''    Debug.Print "データ: " & myXlonBgnCol
''    Debug.Print "データ: " & myXlonEndRow
''    Debug.Print "データ: " & myXlonEndCol
'End Sub

      <br></pre>
    </body>
  </html>
