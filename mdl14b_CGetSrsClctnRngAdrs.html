<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfixGetSeriesCollectionFormula
'Includes PfixGetDataRangeAddressInXYScatter

Option Explicit
Option Base 1

'◆ClassProc名_散布図グラフの指定データ系列のセル範囲アドレスを取得する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CGetSrsClctnRngAdrs"
  
'//PropertyGet_出力データ
  Private myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String
  Private myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
            myXlonSrsClctnNo As Long
  Private myXobjSrsNameRng As Object, myXobjSrsXvalRng As Object, myXobjSrsYvalRng As Object
  
'//PropertySetLet_入力データ
  Private myXobjSrsClctn As Object
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getSrsFrml(ByVal k As Long) As String
    Select Case k
        Case 1: getSrsFrml = myXstrSrsFrmlA1
        Case 2: getSrsFrml = myXstrSrsFrmlR1C1
    End Select
End Property

Public Property Get getDataAdrs(ByVal k As Long) As String
    Select Case k
        Case 1: getDataAdrs = myXstrNameAdrs
        Case 2: getDataAdrs = myXstrXvalAdrs
        Case 3: getDataAdrs = myXstrYvalAdrs
    End Select
End Property

Public Property Get getSrsClctnNo() As Long
    getSrsClctnNo = myXlonSrsClctnNo
End Property

Public Property Get getSrsRng(ByVal k As Long) As Object
    Select Case k
        Case 1: Set getSrsRng = myXobjSrsNameRng
        Case 2: Set getSrsRng = myXobjSrsXvalRng
        Case 3: Set getSrsRng = myXobjSrsYvalRng
    End Select
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXstrSrsFrmlA1 = Empty: myXstrSrsFrmlR1C1 = Empty
    myXstrNameAdrs = Empty: myXstrXvalAdrs = Empty: myXstrYvalAdrs = Empty
    myXlonSrsClctnNo = Empty
    Set myXobjSrsNameRng = Nothing
    Set myXobjSrsXvalRng = Nothing: Set myXobjSrsYvalRng = Nothing
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Set setSrsClctn(ByVal RHS As Object)
    Set myXobjSrsClctn = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXobjSrsClctn Is Nothing Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//グラフの指定系列の参照範囲を取得
    Call PfixGetSeriesCollectionFormula(myXstrSrsFrmlA1, myXstrSrsFrmlR1C1, myXobjSrsClctn)
    If myXstrSrsFrmlA1 = "" Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//散布図グラフのデータ系列の参照式からセル範囲アドレスを取得
    Call PfixGetDataRangeAddressInXYScatter( _
            myXstrNameAdrs, myXstrXvalAdrs, myXstrYvalAdrs, myXlonSrsClctnNo, _
            myXstrSrsFrmlA1)
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
'//セル範囲を設定
    On Error Resume Next
    Set myXobjSrsNameRng = Range(myXstrNameAdrs)
    Set myXobjSrsXvalRng = Range(myXstrXvalAdrs)
    Set myXobjSrsYvalRng = Range(myXstrYvalAdrs)
    On Error GoTo 0
    
ExitPath:
    Call initializeModuleVariables
End Sub

'===============================================================================================

 '定型Ｐ_グラフの指定系列の参照範囲を取得する
Private Sub PfixGetSeriesCollectionFormula( _
            myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String, _
            ByVal myXobjSrsClctn As Object)
    myXstrSrsFrmlA1 = Empty: myXstrSrsFrmlR1C1 = Empty
    On Error GoTo ExitPath
    myXstrSrsFrmlA1 = myXobjSrsClctn.Formula
    myXstrSrsFrmlR1C1 = myXobjSrsClctn.FormulaR1C1
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_散布図グラフのデータ系列の参照式からセル範囲アドレスを取得する
Private Sub PfixGetDataRangeAddressInXYScatter( _
            myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
            myXlonSrsClctnNo As Long, _
            ByVal myXstrSrsFrml As String)
'objSrsClctn.Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    myXstrNameAdrs = Empty: myXstrXvalAdrs = Empty: myXstrYvalAdrs = Empty
    If myXstrSrsFrml = "" Then Exit Sub
'//参照式からアドレス部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrSrsFrml, "(") + 1
    myXlonEnd = InStrRev(myXstrSrsFrml, ")") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrSndwchd = Mid(myXstrSrsFrml, myXlonBgn, myXlonLen)
'//項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号を取得
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long
    myZvarSplt = Split(myXstrSndwchd, ",")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
  Dim myXstrSCNo As String
    If myXlonSpltCnt = 4 Then
        myXstrNameAdrs = myZvarSplt(0)
        myXstrXvalAdrs = myZvarSplt(1)
        myXstrYvalAdrs = myZvarSplt(2)
        myXstrSCNo = myZvarSplt(3)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrSCNo) = True Then myXlonSrsClctnNo = CLng(myXstrSCNo)
    myZvarSplt = Empty
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_散布図グラフの指定データ系列のセル範囲アドレスを取得する
'Private Sub instCGetSrsClctnRngAdrs()
'  Dim myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String
'  Dim myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
'        myXlonSrsClctnNo As Long
'  Dim myXobjSrsNameRng As Object, myXobjSrsXvalRng As Object, myXobjSrsYvalRng As Object
''//クラス内変数への入力
'  Dim myXobjSrsClctn As Object
'    Set myXobjSrsClctn = ActiveSheet.ChartObjects(1).Chart.SeriesCollection(1)
''//CGetSrsClctnRngAdrsクラスモジュールのインスタンス生成
'  Dim myXinsGetSrsClctnRngAdrs As CGetSrsClctnRngAdrs
'    Set myXinsGetSrsClctnRngAdrs = New CGetSrsClctnRngAdrs
'    With myXinsGetSrsClctnRngAdrs
'    '//クラス内変数への入力
'        Set .setSrsClctn = myXobjSrsClctn
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXstrSrsFrmlA1 = .getSrsFrml(1)
'        myXstrSrsFrmlR1C1 = .getSrsFrml(2)
'        myXstrNameAdrs = .getDataAdrs(1)
'        myXstrXvalAdrs = .getDataAdrs(2)
'        myXstrYvalAdrs = .getDataAdrs(3)
'        myXlonSrsClctnNo = .getSrsClctnNo
'        Set myXobjSrsNameRng = .getSrsRng(1)
'        Set myXobjSrsXvalRng = .getSrsRng(2)
'        Set myXobjSrsYvalRng = .getSrsRng(3)
'    End With
'    Set myXinsGetSrsClctnRngAdrs = Nothing
'    Set myXobjSrsClctn = Nothing
''    Debug.Print "データ: " & myXstrSrsFrmlA1
''    Debug.Print "データ: " & myXstrSrsFrmlR1C1
''    Debug.Print "データ: " & myXstrNameAdrs
''    Debug.Print "データ: " & myXstrXvalAdrs
''    Debug.Print "データ: " & myXstrYvalAdrs
''    myXobjSrsNameRng.Select
''    myXobjSrsXvalRng.Select
''    myXobjSrsYvalRng.Select
'End Sub

      <br></pre>
    </body>
  </html>
