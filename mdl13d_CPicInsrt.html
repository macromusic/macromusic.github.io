<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfncbisCheckFileExist
'Includes PfixInsertPicture
'Includes PfncobjCutPasteSpecialPicture

Option Explicit
Option Base 1

'◆ClassProc名_画像をシート上に挿入する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CPicInsrt"
  
'//PropertyGet_出力データ
  Private myXobjPicture As Object
  
'//PropertySetLet_入力データ
  Private myXobjSheet As Object, myXstrFilePath As String
  Private myXlonPstFrmt As Long
    
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXobjPicShp As Object

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    Set myXobjPicShp = Nothing
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getPicture() As Object
    Set getPicture = myXobjPicture
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    Set myXobjPicture = Nothing
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Set setSheet(ByVal RHS As Object)
    Set myXobjSheet = RHS
End Property

Public Property Let letFilePath(ByVal RHS As String)
    myXstrFilePath = RHS
End Property

Public Property Let letPstFrmt(ByVal RHS As Long)
    myXlonPstFrmt = RHS
'myXlonPstFrmt = 1 : "図 (PNG)"
'myXlonPstFrmt = 2 : "図 (JPEG)"
'myXlonPstFrmt = 3 : "図 (GIF)"
'myXlonPstFrmt = 4 : "図 (拡張メタファイル)"
'myXlonPstFrmt = 5 : "ビットマップ"
'myXlonPstFrmt = 6 : "Microsoft Office 描画オブジェクト"
'myXlonPstFrmt = ? : 形式変更しない
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False

    If myXobjSheet Is Nothing Then GoTo ExitPath
    If PfncbisCheckFileExist(myXstrFilePath) = False Then GoTo ExitPath
        
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicF_
Public Function fncobjPicture() As Object
    Set fncobjPicture = Nothing
    Call ctrProc
    Set fncobjPicture = myXobjPicture
End Function

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag

'//画像をエクセルシート上に挿入
    Set myXobjPicShp = PfncobjInsertPicture(myXobjSheet, myXstrFilePath)
    If myXobjPicShp Is Nothing Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag

'//シート上の画像を切り取って形式を指定して貼り付け
    Set myXobjPicShp = PfncobjCutPasteSpecialPicture(myXobjPicShp, myXlonPstFrmt)
    If myXobjPicShp Is Nothing Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag
    
    Set myXobjPicture = myXobjPicShp
    
ExitPath:
    Call initializeModuleVariables
End Sub

'===============================================================================================
 
 '定型Ｆ_指定ファイルの存在を確認する
Private Function PfncbisCheckFileExist(ByVal myXstrFilePath As String) As Boolean
    PfncbisCheckFileExist = False
    If myXstrFilePath = "" Then Exit Function
  Dim myXobjFSO As Object: Set myXobjFSO = CreateObject("Scripting.FileSystemObject")
    PfncbisCheckFileExist = myXobjFSO.FileExists(myXstrFilePath)
    Set myXobjFSO = Nothing
End Function

 '定型Ｆ_シート上に画像を挿入する
Private Function PfncobjInsertPicture( _
            ByVal myXobjSheet As Object, ByVal myXstrFilePath As String) As Object
    Set PfncobjInsertPicture = Nothing
    If myXobjSheet Is Nothing Then Exit Function
    If myXstrFilePath = "" Then Exit Function
    On Error GoTo ExitPath
    Set PfncobjInsertPicture = myXobjSheet.Pictures.Insert(myXstrFilePath)
    On Error GoTo 0
ExitPath:
End Function

 '定型Ｆ_シート上の画像を切り取って形式を指定して貼り付ける
Private Function PfncobjCutPasteSpecialPicture( _
            ByVal myXobjPicShp As Object, _
            Optional ByVal coXlonPstFrmt As Long = 1) As Object
'coXlonPstFrmt = 1 : "図 (PNG)"
'coXlonPstFrmt = 2 : "図 (JPEG)"
'coXlonPstFrmt = 3 : "図 (GIF)"
'coXlonPstFrmt = 4 : "図 (拡張メタファイル)"
'coXlonPstFrmt = 5 : "ビットマップ"
'coXlonPstFrmt = 6 : "Microsoft Office 描画オブジェクト"
'coXlonPstFrmt = ? : 形式変更しない
    Set PfncobjCutPasteSpecialPicture = Nothing
    If myXobjPicShp Is Nothing Then Exit Function
  Dim myXstrPstFrmt As String
    If coXlonPstFrmt = 1 Then
        myXstrPstFrmt = "図 (PNG)"
    ElseIf coXlonPstFrmt = 2 Then
        myXstrPstFrmt = "図 (JPEG)"
    ElseIf coXlonPstFrmt = 3 Then
        myXstrPstFrmt = "図 (GIF)"
    ElseIf coXlonPstFrmt = 4 Then
        myXstrPstFrmt = "図 (拡張メタファイル)"
    ElseIf coXlonPstFrmt = 5 Then
        myXstrPstFrmt = "ビットマップ"
    ElseIf coXlonPstFrmt = 6 Then
        myXstrPstFrmt = "Microsoft Office 描画オブジェクト"
    Else
        Set PfncobjCutPasteSpecialPicture = myXobjPicShp
        Exit Function
    End If
    On Error GoTo ExitPath
  Dim myXobjPstSht As Object
    Set myXobjPstSht = myXobjPicShp.Parent
    myXobjPstSht.Activate
    myXobjPicShp.Cut
    Call myXobjPstSht.PasteSpecial(Format:=myXstrPstFrmt)
    Set PfncobjCutPasteSpecialPicture = myXobjPstSht.Pictures(myXobjPstSht.Pictures.Count)
    On Error GoTo 0
    Set myXobjPstSht = Nothing
ExitPath:
End Function

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_画像をシート上に挿入する
'Private Sub instCPicInsrt()
'  Dim myXobjPicture As Object
''//クラス内変数への入力
'  Const coXlonPstFrmt As Long = 0
'    'coXlonPstFrmt = 1 : "図 (PNG)"
'    'coXlonPstFrmt = 2 : "図 (JPEG)"
'    'coXlonPstFrmt = 3 : "図 (GIF)"
'    'coXlonPstFrmt = 4 : "図 (拡張メタファイル)"
'    'coXlonPstFrmt = 5 : "ビットマップ"
'    'coXlonPstFrmt = 6 : "Microsoft Office 描画オブジェクト"
'    'coXlonPstFrmt = ? : 形式変更しない
'  Dim myXobjSheet As Object, myXstrFilePath As String
'    Set myXobjSheet = ThisWorkbook.Worksheets(1)
'    myXstrFilePath = ""
''//CPicInsrtクラスモジュールのインスタンス生成
'  Dim myXinsPicInsrt As CPicInsrt: Set myXinsPicInsrt = New CPicInsrt
'    With myXinsPicInsrt
'    '//クラス内変数への入力
'        Set .setSheet = myXobjSheet
'        .letFilePath = myXstrFilePath
'        .letPstFrmt = coXlonPstFrmt
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        Set myXobjPicture = .fncobjPicture
'    End With
'    Set myXinsPicInsrt = Nothing
'End Sub

      <br></pre>
    </body>
  </html>
