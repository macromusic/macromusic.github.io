<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PfncbisIsMatrixNumeric
'Includes PfixArrayPartColumnNumeric
'Includes Pfix1DArrayExtractNumeric
'Includes PfixTrapezoidWaveLong
'Includes PfixSlopeIntercept

Option Explicit
Option Base 1

'◆ClassProc名_データ配列の指定区間ごとで回帰直線間の差分データを取得する
'Rev.004
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CIntrvlRgrsnLnDev"
  
'//PropertyGet_出力データ
  Private Lc As Long
  Private myXlonStrtRow As Long, myXlonLastRow As Long
  Private myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
    'myZdouCalData(i, 1) : 計算処理後データ配列
  
'//PropertySetLet_入力制御信号
  Private myXlonCalDataRngOptn As Long
  
'//PropertySetLet_入力データ
  Private myZdouOrgData() As Double
    'myZdouOrgData(i, 2) : 元データ配列
  Private myXlonXCalCol As Long, myXlonYCalCol As Long
  Private myXlonCalSmplQnt As Long, myXlonCalStep As Long
  Private myXlonBgnRow As Long, myXlonEndRow As Long
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXlonInptRowCnt As Long, myXlonInptColCnt As Long, myZdouInptData() As Double
  Private myXlonInptXCnt As Long, myZdouInptX() As Double
  Private myXlonInptYCnt As Long, myZdouInptY() As Double
  Private myXlonStrtNo As Long, myXlonLastNo As Long
  Private myXlonPreOfstNeg As Long, myXlonPstOfstNeg As Long
  Private myXlonPreOfstPos As Long, myXlonPstOfstPos As Long
  Private myXlonCalStrtNo As Long, myXlonCalLastNo As Long
  Private myXlonCalTmpCnt As Long, myZdouCalTmp() As Double

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    myXlonInptRowCnt = Empty: myXlonInptColCnt = Empty: Erase myZdouInptData
    myXlonInptXCnt = Empty: Erase myZdouInptX
    myXlonInptYCnt = Empty: Erase myZdouInptY
    myXlonStrtNo = Empty: myXlonLastNo = Empty
    myXlonPreOfstNeg = Empty: myXlonPstOfstNeg = Empty
    myXlonPreOfstPos = Empty: myXlonPstOfstPos = Empty
    myXlonCalStrtNo = Empty: myXlonCalLastNo = Empty
    myXlonCalTmpCnt = Empty: Erase myZdouCalTmp
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getStrtRow() As Long
    getStrtRow = myXlonStrtRow
End Property

Public Property Get getLastRow() As Long
    getLastRow = myXlonLastRow
End Property

Public Property Get getCalRowCnt() As Long
    getCalRowCnt = myXlonCalRowCnt
End Property

Public Property Get getCalColCnt() As Long
    getCalColCnt = myXlonCalColCnt
End Property

Public Property Get getCalData(ByVal i As Long, ByVal j As Long) As Double
    getCalData = myZdouCalData(i, j)
'myZdouCalData(i, 1) : 計算処理後データ配列
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXlonStrtRow = Empty: myXlonLastRow = Empty
    myXlonCalRowCnt = Empty: myXlonCalColCnt = Empty: Erase myZdouCalData
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Let letCalDataRngOptn(ByVal RHS As Long)
    myXlonCalDataRngOptn = RHS
'myXlonCalDataRngOptn = 1 : 全データ範囲で取得
'myXlonCalDataRngOptn = 2 : 指定データ範囲で取得
'myXlonCalDataRngOptn = 3 : 有効データ範囲で取得
End Property

Public Property Let letOrgData(ByRef RHS() As Double)
    myZdouOrgData() = RHS()
'myZdouOrgData(i, 2) : 元データ配列
End Property

Public Property Let letXCalCol(ByVal RHS As Long)
    myXlonXCalCol = RHS
End Property

Public Property Let letYCalCol(ByVal RHS As Long)
    myXlonYCalCol = RHS
End Property

Public Property Let letCalSmplQnt(ByVal RHS As Long)
    myXlonCalSmplQnt = RHS
End Property

Public Property Let letCalStep(ByVal RHS As Long)
    myXlonCalStep = RHS
End Property

Public Property Let letBgnRow(ByVal RHS As Long)
    myXlonBgnRow = RHS
End Property

Public Property Let letEndRow(ByVal RHS As Long)
    myXlonEndRow = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXlonCalDataRngOptn < 1 Or myXlonCalDataRngOptn > 3 Then myXlonCalDataRngOptn = 1
    
    If PfncbisIsMatrixNumeric(myZdouOrgData) = False Then GoTo ExitPath
    
  Dim L As Long, i As Long, j As Long
    L = LBound(myZdouOrgData, 1)
    myXlonInptRowCnt = UBound(myZdouOrgData, 1) - L + 1
    myXlonInptColCnt = UBound(myZdouOrgData, 2) - L + 1
    ReDim myZdouInptData(myXlonInptRowCnt, myXlonInptColCnt) As Double
    For j = LBound(myZdouOrgData, 2) To UBound(myZdouOrgData, 2)
        For i = LBound(myZdouOrgData, 1) To UBound(myZdouOrgData, 1)
            myZdouInptData(i + (1 - L), j + (1 - L)) = myZdouOrgData(i, j)
        Next i
    Next j
    
    If myXlonBgnRow < LBound(myZdouInptData, 1) Then myXlonBgnRow = LBound(myZdouInptData, 1)
    If myXlonEndRow < myXlonBgnRow Or myXlonEndRow > UBound(myZdouInptData, 1) Then _
        myXlonEndRow = UBound(myZdouInptData, 1)
    If myXlonXCalCol < LBound(myZdouInptData, 2) _
        Or myXlonXCalCol > UBound(myZdouInptData, 2) Then GoTo ExitPath
    If myXlonYCalCol < LBound(myZdouInptData, 2) _
        Or myXlonYCalCol > UBound(myZdouInptData, 2) Then GoTo ExitPath
    If myXlonCalSmplQnt <= 0 Or myXlonCalSmplQnt > UBound(myZdouInptData, 1) Then GoTo ExitPath
    If myXlonCalStep <= 0 Or myXlonCalStep > UBound(myZdouInptData, 1) Then myXlonCalStep = 1
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//C:データ用変数を設定
    Call setDataVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//C:制御用変数を設定
    Call setControlVariables
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "3"     'PassFlag

'//P:計算実行:回帰直線間の差分
    Call prsCal
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "4"     'PassFlag
    
'//C:出力データを格納
    Call setOutputVariables
    
ExitPath:
    Call initializeModuleVariables
End Sub

'SetP_データ用変数を設定する
Private Sub setDataVariables()
    myXbisExitFlag = False
    
  Dim myXlonBgnNo As Long, myXlonEndNo As Long
    myXlonBgnNo = LBound(myZdouInptData, 1)
    myXlonEndNo = UBound(myZdouInptData, 1)
    Call PfixArrayPartColumnNumeric(myXlonInptXCnt, myZdouInptX, _
            myZdouInptData, myXlonXCalCol, myXlonBgnNo, myXlonEndNo)
    Call PfixArrayPartColumnNumeric(myXlonInptYCnt, myZdouInptY, _
            myZdouInptData, myXlonYCalCol, myXlonBgnNo, myXlonEndNo)
    If myXlonInptXCnt <= 0 Or myXlonInptYCnt <= 0 Then GoTo ExitPath
    If myXlonInptXCnt <> myXlonInptYCnt Then GoTo ExitPath
    
    myXlonStrtNo = myXlonBgnNo
    myXlonLastNo = myXlonEndNo
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'SetP_制御用変数を設定する
Private Sub setControlVariables()
    myXbisExitFlag = False
    
'//計算するオフセット量を設定
    myXlonPreOfstNeg = (myXlonCalSmplQnt - 1) * myXlonCalStep
    myXlonPstOfstNeg = 0
    
    myXlonPreOfstPos = 0
    myXlonPstOfstPos = (myXlonCalSmplQnt - 1) * myXlonCalStep
    
'//計算が有効な最初と最後の位置を設定
    myXlonStrtNo = myXlonStrtNo + myXlonPreOfstNeg
    myXlonLastNo = myXlonLastNo - myXlonPstOfstPos
    If myXlonLastNo < myXlonStrtNo Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'PrcsP_計算実行:回帰直線間の差分
Private Sub prsCal()
    myXbisExitFlag = False
    
'//計算する最初のデータ位置と最後のデータ位置を設定
    Select Case myXlonCalDataRngOptn
        Case 1
            myXlonCalStrtNo = LBound(myZdouInptX)
            myXlonCalLastNo = UBound(myZdouInptX)
        Case 2
            myXlonCalStrtNo = myXlonBgnRow
            myXlonCalLastNo = myXlonEndRow
        Case Else
            myXlonCalStrtNo = myXlonStrtNo
            myXlonCalLastNo = myXlonLastNo
    End Select
    
'//台形波形を生成
  Dim myXlonAdjstNegCnt As Long, myZlonAdjstNeg() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstNegCnt, myZlonAdjstNeg, _
            myXlonInptXCnt, myXlonStrtNo, UBound(myZdouInptX), 1, -1, 0, 0)
    If myXlonAdjstNegCnt <> myXlonInptXCnt Then GoTo ExitPath
    
  Dim myXlonAdjstPosCnt As Long, myZlonAdjstPos() As Long
    Call PfixTrapezoidWaveLong(myXlonAdjstPosCnt, myZlonAdjstPos, _
            myXlonInptXCnt, LBound(myZdouInptX), myXlonLastNo, 1, 0, -1, 0)
    If myXlonAdjstPosCnt <> myXlonInptXCnt Then GoTo ExitPath
    
'//計算処理:回帰直線間の差分
  Dim i As Long, n As Long: n = 0
  Dim myXlonBgnNo As Long, myXlonEndNo As Long
  Dim myXdouSlpNeg As Double, myXdouIntrcptNeg As Double
  Dim myXdouSlpPos As Double, myXdouIntrcptPos As Double
  Dim myXdouRgrsnNeg As Double, myXdouRgrsnPos As Double
    
    myXlonCalTmpCnt = myXlonCalLastNo - myXlonCalStrtNo + 1
    ReDim myZdouCalTmp(myXlonCalTmpCnt) As Double

    For i = myXlonCalStrtNo To myXlonCalLastNo
        n = n + 1
    '//今回値より前のデータで計算
        myXlonBgnNo = i - myXlonPreOfstNeg + myZlonAdjstNeg(i)
        myXlonEndNo = i + myXlonPstOfstNeg
        Call CsubCalRegressionLineSlopeIntercept( _
                myXdouSlpNeg, myXdouIntrcptNeg, _
                myXlonBgnNo, myXlonEndNo)
        If myXdouSlpNeg = 0 And myXdouIntrcptNeg = 0 Then GoTo ExitPath
    '//今回値より後のデータで計算
        myXlonBgnNo = i - myXlonPreOfstPos
        myXlonEndNo = i + myXlonPstOfstPos + myZlonAdjstPos(i)
        Call CsubCalRegressionLineSlopeIntercept( _
                myXdouSlpPos, myXdouIntrcptPos, _
                myXlonBgnNo, myXlonEndNo)
        If myXdouSlpPos = 0 And myXdouIntrcptPos = 0 Then GoTo ExitPath
    '//回帰直線間の差分を計算
        myXdouRgrsnNeg = myXdouSlpNeg * myZdouInptX(i) + myXdouIntrcptNeg
        myXdouRgrsnPos = myXdouSlpPos * myZdouInptX(i) + myXdouIntrcptPos
        myZdouCalTmp(n) = myXdouRgrsnPos - myXdouRgrsnNeg
    Next i
    
    Erase myZlonAdjstNeg: Erase myZlonAdjstPos
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'クラス内Ｐ_2配列の傾き＆切片を計算
Private Sub CsubCalRegressionLineSlopeIntercept( _
            myXdouSlp As Double, myXdouIntrcpt As Double, _
            ByVal myXlonBgnNo As Long, ByVal myXlonEndNo As Long)
    myXdouSlp = Empty: myXdouIntrcpt = Empty
    
  Dim myXlonTmpXCnt As Long, myZdouTmpX() As Double
  Dim myXlonTmpYCnt As Long, myZdouTmpY() As Double

    If myXlonBgnNo = myXlonEndNo Then
        If myXlonBgnNo * myXlonEndNo = 1 Then
            myXlonBgnNo = myXlonBgnNo
            myXlonEndNo = myXlonEndNo + 1
        Else
            myXlonBgnNo = myXlonBgnNo - 1
            myXlonEndNo = myXlonEndNo
        End If
    End If
    
    Call Pfix1DArrayExtractNumeric(myXlonTmpXCnt, myZdouTmpX, _
            myZdouInptX, myXlonBgnNo, myXlonEndNo, myXlonCalStep)
    If myXlonTmpXCnt <= 0 Then GoTo ExitPath
    Call Pfix1DArrayExtractNumeric(myXlonTmpYCnt, myZdouTmpY, _
            myZdouInptY, myXlonBgnNo, myXlonEndNo, myXlonCalStep)
    If myXlonTmpYCnt <= 0 Then GoTo ExitPath
    
    Call PfixSlopeIntercept(myXdouSlp, myXdouIntrcpt, myZdouTmpX, myZdouTmpY)
    
    Erase myZdouTmpX: Erase myZdouTmpY
ExitPath:
End Sub

'SetP_出力データを格納する
Private Sub setOutputVariables()
    myXbisExitFlag = False
    
    myXlonStrtRow = myXlonCalStrtNo
    myXlonLastRow = myXlonCalLastNo
    
  Dim i As Long
    myXlonCalRowCnt = myXlonCalTmpCnt
    myXlonCalColCnt = 1
    ReDim myZdouCalData(myXlonCalRowCnt, myXlonCalColCnt) As Double
    For i = LBound(myZdouCalTmp) To UBound(myZdouCalTmp)
        myZdouCalData(i, 1) = myZdouCalTmp(i)
    Next i
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｆ_指定行列が数値行列かどうかをチェックする
Private Function PfncbisIsMatrixNumeric(ByRef myZvarMtrx As Variant) As Boolean
    PfncbisIsMatrixNumeric = False
  Dim myXvarTmp As Variant, myXdouTmp As Double, L1 As Long, L2 As Long
    On Error GoTo ExitPath
    L1 = LBound(myZvarMtrx, 1): L2 = LBound(myZvarMtrx, 2)
    myXdouTmp = myZvarMtrx(L1, L2)
    For Each myXvarTmp In myZvarMtrx
        If IsNumeric(myXvarTmp) = False Then GoTo ExitPath
    Next myXvarTmp
    On Error GoTo 0
    PfncbisIsMatrixNumeric = True
ExitPath:
End Function

 '定型Ｐ_2次元の数値配列の指定列から一部を1次元配列で取得する
Private Sub PfixArrayPartColumnNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, ByVal myXlonExtrctCol As Long, _
            ByVal myXlonBgnRow As Long, ByVal myXlonEndRow As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 抽出データ配列
'myZdouOrgData(i, j) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgnRow < LBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow > UBound(myZdouOrgData, 1) Then Exit Sub
    If myXlonEndRow < myXlonBgnRow Then Exit Sub
    If myXlonExtrctCol < LBound(myZdouOrgData, 2) _
        Or myXlonExtrctCol > UBound(myZdouOrgData, 2) Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData, 1) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgnRow, myXlonExtrctCol)
    Li = LBound(myZdouOrgData, 1)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgnRow To myXlonEndRow Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i, myXlonExtrctCol)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_1次元の数値配列から一部を取得する
Private Sub Pfix1DArrayExtractNumeric( _
            myXlonExtrctCnt As Long, myZdouExtrctData() As Double, _
            ByRef myZdouOrgData() As Double, _
            ByVal myXlonBgn As Long, ByVal myXlonEnd As Long, _
            Optional ByVal coXlonStep As Long = 1)
'myZdouExtrctData(i) : 取得データ配列
'myZdouOrgData(i) : 元データ配列
    myXlonExtrctCnt = Empty: Erase myZdouExtrctData
  Dim myXdouTmp As Double, Li As Long
    On Error GoTo ExitPath
    If myXlonBgn < LBound(myZdouOrgData) Then Exit Sub
    If myXlonEnd > UBound(myZdouOrgData) Then Exit Sub
    If myXlonEnd < myXlonBgn Then Exit Sub
    If coXlonStep <= 0 Or coXlonStep > UBound(myZdouOrgData) Then Exit Sub
    myXdouTmp = myZdouOrgData(myXlonBgn): Li = LBound(myZdouOrgData)
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long: n = Lo - 1
    For i = myXlonBgn To myXlonEnd Step coXlonStep
        n = n + 1: ReDim Preserve myZdouExtrctData(n) As Double
        myZdouExtrctData(n) = myZdouOrgData(i)
    Next i
    myXlonExtrctCnt = n - Lo + 1
ExitPath:
End Sub

 '定型Ｐ_整数で台形波形を生成する
Private Sub PfixTrapezoidWaveLong( _
            myXlonCalDataCnt As Long, myZlonCalData() As Long, _
            ByVal myXlonDataQnt As Long, _
            ByVal myXlonInflctPntBgn As Long, ByVal myXlonInflctPntEnd As Long, _
            Optional ByVal L As Long = 1, _
            Optional ByVal coXlonStrtStp As Long = -1, _
            Optional ByVal coXlonLastStp As Long = 1, _
            Optional ByVal coXlonCnstVal As Long = 0)
'myZlonCalData(i) : 取得データ配列
'myXlonDataQnt : 必要なデータ数
'myXlonInflctPntBgn : 開始側の変曲点
'myXlonInflctPntEnd : 終了側の変曲点
'L : Option Base
'coXlonStrtStp : 開始側のデータ変化ステップ
'coXlonLastStp : 終了側のデータ変化ステップ
'coXlonCnstVal : 一定状態の値
    myXlonCalDataCnt = Empty:  Erase myZlonCalData
    If myXlonDataQnt <= 0 Then Exit Sub
    If myXlonInflctPntBgn < L Then Exit Sub
    If myXlonInflctPntEnd > myXlonDataQnt + L - 1 Then Exit Sub
    If myXlonInflctPntBgn > myXlonInflctPntEnd Then Exit Sub
    If L < 0 Or 1 < L Then Exit Sub
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim i As Long, n As Long
    myXlonCalDataCnt = myXlonDataQnt
    i = myXlonCalDataCnt + Lo - 1: ReDim myZlonCalData(i) As Long
    If myXlonInflctPntBgn = L Then GoTo JumpPath1
    n = myXlonInflctPntBgn
    For i = L To myXlonInflctPntBgn - 1
        n = n - 1
        myZlonCalData(i) = coXlonCnstVal - coXlonStrtStp * n
    Next i
JumpPath1:
    For i = myXlonInflctPntBgn To myXlonInflctPntEnd
        myZlonCalData(i) = coXlonCnstVal
    Next i
JumpPath2:
    If myXlonInflctPntEnd = myXlonDataQnt + L - 1 Then Exit Sub
    n = 0
    For i = myXlonInflctPntEnd + 1 To myXlonDataQnt + L - 1
        n = n + 1
        myZlonCalData(i) = coXlonCnstVal + coXlonLastStp * n
    Next i
ExitPath:
End Sub

 '定型Ｐ_配列計算-2配列の傾き＆切片
Private Sub PfixSlopeIntercept( _
            myXdouSlp As Double, myXdouIntcpt As Double, _
            ByRef myZdouInptX() As Double, ByRef myZdouInptY() As Double)
'myZdouInptX(i) : X軸の元データ配列
'myZdouInptY(i) : Y軸の元データ配列
    myXdouSlp = Empty: myXdouIntcpt = Empty
    If UBound(myZdouInptX) <> UBound(myZdouInptY) Then Exit Sub
  Dim n As Long: n = UBound(myZdouInptX) - LBound(myZdouInptX) + 1
  Dim i As Long, SY2 As Double, SX2 As Double, SY As Double, SXY As Double, SX As Double
    For i = LBound(myZdouInptX) To UBound(myZdouInptX)
        SY2 = SY2 + (myZdouInptY(i)) ^ 2
        SX2 = SX2 + (myZdouInptX(i)) ^ 2
        SY = SY + myZdouInptY(i)
        SXY = SXY + myZdouInptX(i) * myZdouInptY(i)
        SX = SX + myZdouInptX(i)
    Next i
    myXdouSlp = (n * SXY - SY * SX) / (n * SX2 - SX ^ 2)
    myXdouIntcpt = (SX2 * SY - SXY * SX) / (n * SX2 - SX ^ 2)
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_データ配列の指定区間ごとで回帰直線間の差分データを取得する
'Private Sub instCIntrvlRgrsnLnDev()
'  Const coXlonCalDataRngOptn As Long = 1
'    'coXlonCalRng = 1 : 全データ範囲で取得
'    'coXlonCalRng = 2 : 指定データ範囲で取得
'    'coXlonCalRng = 3 : 有効データ範囲で取得
'  Const coXlonXCalCol As Long = 1
'  Const coXlonYCalCol As Long = 2
'  Const coXlonCalSmplQnt As Long = 11
'  Const coXlonCalStep As Long = 1
'  Const coXlonBgnRowCol As Long = 1
'  Const coXlonEndRowCol As Long = 0
''  Dim myXlonOrgRowCnt As Long, myXlonOrgColCnt As Long, myZdouOrgData() As Double
''    'myZdouOrgData(i, 2) : 元データ配列
''    myXlonOrgRowCnt = UBound(myZdouOrgData, 1)
''    myXlonOrgColCnt = UBound(myZdouOrgData, 2)
'  Dim Lc As Long
''  Dim myXlonStrtRow As Long, myXlonLastRow As Long
''  Dim myXlonCalRowCnt As Long, myXlonCalColCnt As Long, myZdouCalData() As Double
''    'myZdouCalData(i, 1) : 計算処理後データ配列
''//CIntrvlRgrsnLnDevクラスモジュールのインスタンス生成
'  Dim myXinsIntrvlRgrsnLnDev As CIntrvlRgrsnLnDev
'    Set myXinsIntrvlRgrsnLnDev = New CIntrvlRgrsnLnDev
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long, j As Long
'    With myXinsIntrvlRgrsnLnDev
'    '//クラス内変数への入力
'        .letCalDataRngOptn = coXlonCalDataRngOptn
'        .letOrgData = myZdouOrgData
'        .letXCalCol = coXlonXCalCol
'        .letYCalCol = coXlonYCalCol
'        .letCalSmplQnt = coXlonCalSmplQnt
'        .letCalStep = coXlonCalStep
'        .letBgnRow = coXlonBgnRowCol
'        .letEndRow = coXlonEndRowCol
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXlonStrtRow = .getStrtRow
'        myXlonLastRow = .getLastRow
'        myXlonCalRowCnt = .getCalRowCnt
'        myXlonCalColCnt = .getCalColCnt
'        If myXlonCalRowCnt <= 0 Or myXlonCalColCnt <= 0 Then GoTo JumpPath
'        i = myXlonCalRowCnt + Lo - 1: j = myXlonCalColCnt + Lo - 1
'        ReDim myZdouCalData(i, j) As Double
'        Lc = .getOptnBase
'        For i = 1 To myXlonCalRowCnt
'            myZdouCalData(i + Lo - 1, Lo) = .getCalData(i + Lc - 1, Lc)
'        Next i
'    End With
'JumpPath:
'    Set myXinsIntrvlRgrsnLnDev = Nothing
'    Call variablesOfCIntrvlRgrsnLnDev(myXlonCalRowCnt, myZdouCalData)    'Debug.Print
'End Sub
'Private Sub variablesOfCIntrvlRgrsnLnDev( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CIntrvlRgrsnLnDevクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim i As Long, j As Long
'    For i = LBound(myZvarField, 1) To UBound(myZvarField, 1)
'        For j = LBound(myZvarField, 2) To UBound(myZvarField, 2)
'            Debug.Print "データ" & i & "," & j & ": " & myZvarField(i, j)
'        Next j
'    Next i
'End Sub

      <br></pre>
    </body>
  </html>
