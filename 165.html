<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>macro sample</div>
      <pre>
'Includes PfncstrRangeAddress
'Includes PfncobjInputBoxRange
'Includes PfixSetChartAxisTitleVisibleFormula

Option Explicit
Option Base 1

'◆ClassProc名_グラフのX/Y軸のタイトルに表示させる参照範囲を指定する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CChrtAxsTtlRngSlct"
  
'//モジュール内定数
  Private Const coXstrTitle As String = "グラフのX/Y軸タイトル文字列のセル指定"
  Private Const coXstrPrompt As String = "グラフのX/Y軸タイトルに表示させる文字列のセル範囲を指定して下さい。"
  
'//PropertyGet_出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//PropertySetLet_入力データ
  Private myXobjChrt As Object
  Private myXstrTrgtChrtAxs As String
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean
  
'//モジュール内変数_データ
  Private myXobjDfltRng As Object, myXobjRltvRng As Object
  Private myXstrDfltAdrs As String
    
  Private myXobjSlctRng As Object
  Private myXobjChrtAxs As Object, myXstrAxsTtl As String

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
    
    Set myXobjDfltRng = Nothing: Set myXobjRltvRng = Nothing
    myXstrDfltAdrs = Empty
    
    Set myXobjSlctRng = Nothing
    Set myXobjChrtAxs = Nothing: myXstrAxsTtl = Empty
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getCmpltFlag() As Boolean
    getCmpltFlag = myXbisCmpltFlag
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Set setChrt(ByVal RHS As Object)
    Set myXobjChrt = RHS
End Property

Public Property Let letTrgtChrtAxs(ByVal RHS As String)
    myXstrTrgtChrtAxs = RHS
'myXstrTrgtChrtAxs = "X1" : X軸主軸のタイトルを設定
'myXstrTrgtChrtAxs = "Y1" : Y軸主軸のタイトルを設定
'myXstrTrgtChrtAxs = "Y2" : Y軸第2軸のタイトルを設定
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXobjChrt Is Nothing Then GoTo ExitPath
    
    Select Case myXstrTrgtChrtAxs
        Case "X1"
            Set myXobjChrtAxs = myXobjChrt.Axes(xlCategory, xlPrimary)
        Case "Y1"
            Set myXobjChrtAxs = myXobjChrt.Axes(xlValue, xlPrimary)
        Case "Y2"
            On Error GoTo ExitPath
            Set myXobjChrtAxs = myXobjChrt.Axes(xlValue, xlSecondary)
            On Error GoTo 0
    End Select
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicF_
Public Function fncbisCmpltFlag() As Boolean
    fncbisCmpltFlag = Empty
    Call ctrProc
    fncbisCmpltFlag = myXbisCmpltFlag
End Function

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag
    
'//セル範囲のアドレスを取得
    If myXobjDfltRng Is Nothing Then Set myXobjDfltRng = Selection
    myXstrDfltAdrs = PfncstrRangeAddress(myXobjDfltRng, myXobjRltvRng, True, True, False)
    
'//InputBoxメソッドでセル範囲の入力を促す
  Dim myXbisExitLpFlag As Boolean: myXbisExitLpFlag = False
    Do While myXbisExitLpFlag = False
        Set myXobjSlctRng = PfncobjInputBoxRange( _
                                coXstrPrompt, coXstrTitle, myXstrDfltAdrs)
        If myXobjSlctRng Is Nothing Then GoTo ExitPath
        If InStr(myXobjSlctRng.Address, ",") <= 0 Then
            myXbisExitLpFlag = True
        Else
            MsgBox "処理範囲は複数個所に分散させずに、連続範囲で指定して下さい。"
        End If
    Loop
    If myXobjSlctRng Is Nothing Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
'//セル範囲のアドレスを取得
    myXstrAxsTtl = PfncstrRangeAddress(myXobjSlctRng, myXobjRltvRng, True, True, True)
    myXstrAxsTtl = "=" & myXstrAxsTtl

'//定型Ｐ_グラフのX/Y軸のタイトルの参照式を設定する
    Call PfixSetChartAxisTitleVisibleFormula(myXbisExitFlag, _
            myXobjChrtAxs, myXstrAxsTtl, True, True)
    If myXbisExitFlag = True Then GoTo ExitPath
    
    myXbisCmpltFlag = True
ExitPath:
    Call initializeModuleVariables
End Sub

'===============================================================================================

 '定型Ｆ_セル範囲のアドレスを取得する
Private Function PfncstrRangeAddress( _
            ByVal myXobjRng As Object, ByVal myXobjRltvRng As Object, _
            Optional ByVal coXbisRowAbslt As Boolean = True, _
            Optional ByVal coXbisColAbslt As Boolean = True, _
            Optional ByVal coXbisExternal As Boolean = False) As String
'coXbisRowAbslt = True  : 行の参照を絶対参照として返す
'coXbisRowAbslt = False : 行の参照を相対参照として返す
'coXbisColAbslt = True  : 列の参照を絶対参照として返す
'coXbisColAbslt = False : 列の参照を相対参照として返す
'coXbisExternal = True  : 外部参照を返す_[ブック名]シート名!アドレス
'coXbisExternal = False : ローカル参照を返す
'myXobjRltvRng          : 相対参照の開始位置
    PfncstrRangeAddress = Empty
    If myXobjRng Is Nothing Then Exit Function
    If Application.ReferenceStyle = xlR1C1 And _
            (coXbisRowAbslt = False Or coXbisColAbslt = False) Then
        If myXobjRltvRng Is Nothing Then GoTo ExitPath
    '//参照形式がxlR1C1で相対参照する場合
        PfncstrRangeAddress = myXobjRng.Address( _
                                ReferenceStyle:=Application.ReferenceStyle, _
                                RowAbsolute:=coXbisRowAbslt, _
                                ColumnAbsolute:=coXbisColAbslt, _
                                external:=coXbisExternal, _
                                RelativeTo:=myXobjRltvRng)
    Else
    '//相対参照先が必要ない場合
        PfncstrRangeAddress = myXobjRng.Address( _
                                ReferenceStyle:=Application.ReferenceStyle, _
                                RowAbsolute:=coXbisRowAbslt, _
                                ColumnAbsolute:=coXbisColAbslt, _
                                external:=coXbisExternal)
    End If
    Exit Function
ExitPath:
'    MsgBox "相対参照の開始位置を指定して下さい。"   'Debug.Print
End Function

 '定型Ｆ_InputBoxメソッドでセル範囲の入力を促す
Private Function PfncobjInputBoxRange( _
                    ByVal myXstrPrompt As String, ByVal myXstrTitle As String, _
                    ByVal myXstrDefault As String) As Object
    Set PfncobjInputBoxRange = Nothing
    If myXstrTitle = "" Then myXstrTitle = "セル範囲の選択"
    If myXstrPrompt = "" Then myXstrPrompt = myXstrTitle
    On Error Resume Next
    Set PfncobjInputBoxRange = Application.InputBox( _
                                prompt:=myXstrPrompt, Title:=myXstrTitle, _
                                Default:=myXstrDefault, Type:=8)
    On Error GoTo 0
'    Debug.Print "入力値: " & PfncobjInputBoxRange.Address( _
'                            ReferenceStyle:=Application.ReferenceStyle, _
'                            RowAbsolute:=True, ColumnAbsolute:=True, external:=True)
End Function

 '定型Ｐ_グラフのX/Y軸のタイトルの参照式を設定する
Private Sub PfixSetChartAxisTitleVisibleFormula( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjChrtAxs As Object, ByVal myXstrAxsTtl As String, _
            Optional ByVal coXbisTtlSet As Boolean = True, _
            Optional ByVal coXbisTtlVsbl As Boolean = True)
'coXbisTtlSet = False : 設定処理しない
'coXbisTtlVsbl = True  : タイトル表示
'coXbisTtlVsbl = False : タイトル非表示
'myXstrAxsTtl = "X" : 参照式を設定
    myXbisExitFlag = False
    If coXbisTtlSet = False Then Exit Sub
    On Error GoTo ExitPath
    myXobjChrtAxs.HasTitle = coXbisTtlVsbl
    On Error GoTo 0
    If coXbisTtlVsbl = False Then Exit Sub
    If Application.ReferenceStyle = xlA1 Then
        myXobjChrtAxs.AxisTitle.Formula = myXstrAxsTtl
    Else
        myXobjChrtAxs.AxisTitle.FormulaR1C1 = myXstrAxsTtl
    End If
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_グラフのX/Y軸のタイトルに表示させる参照範囲を指定する
'Private Sub instCChrtAxsTtlRngSlct()
'  Dim myXbisCmpltFlag As Boolean
''//クラス内変数への入力
'  Dim myXobjChrt As Object
'  Dim myXstrTrgtChrtAxs As String
'    'myXstrTrgtChrtAxs = "X1" : X軸主軸のタイトルを設定
'    'myXstrTrgtChrtAxs = "Y1" : Y軸主軸のタイトルを設定
'    'myXstrTrgtChrtAxs = "Y2" : Y軸第2軸のタイトルを設定
'    Set myXobjChrt = ActiveSheet.ChartObjects(1).Chart
'    myXstrTrgtChrtAxs = "Y2"
''//CChrtAxsTtlRngSlctクラスモジュールのインスタンス生成
'  Dim myXinsChrtAxsTtlRngSlct As CChrtAxsTtlRngSlct
'    Set myXinsChrtAxsTtlRngSlct = New CChrtAxsTtlRngSlct
'    With myXinsChrtAxsTtlRngSlct
'    '//クラス内変数への入力
'        Set .setChrt = myXobjChrt
'        .letTrgtChrtAxs = myXstrTrgtChrtAxs
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        myXbisCmpltFlag = .fncbisCmpltFlag
'    End With
'    Set myXinsChrtAxsTtlRngSlct = Nothing
'ExitPath:
'End Sub

      <br></pre>
    </body>
  </html>
