<!DOCTYPE html>
  <html lang="ja">
    <head>
      <meta charset="UTF-8" />
      <meta name="viewport" content="width=device-width, initial-scale=1.0" />
      <meta http-equiv="X-UA-Compatible" content="ie=edge" />
      <title>Document</title>
    </head>
    <body>
      <div>MacroSample</div>
      <pre>
'Includes PabsSeriesCollectionOperation
'Includes PfixGetSeriesCollectionFormula
'Includes PfixGetDataRangeAddressInXYScatter
'Includes PfncstrReplacePathBookSheetToSheet
'Includes PfncstrSetSeriesFormulaInXYScatter
'Includes PfixSetSeriesCollectionFormulaInXYScatter

Option Explicit
Option Base 1

'◆ClassProc名_グラフの全系列に対してパス[ブック]シート情報を指定シート名に置換する
'Rev.001
  
'//モジュールメモリ
  Private Const meMstrMdlName As String = "CChrtRngAdrsWExToShtName"
  
'//PropertyGet_出力制御信号
  Private myXbisCmpltFlag As Boolean
  
'//PropertyGet_出力データ
  Private myXlonSrsClctnCnt As Long, myXlonErrSrsClctnCnt As Long, myZobjErrSrsClctn() As Object
    'myZobjErrSrsClctn(i) : エラー発生系列
  
'//PropertySetLet_入力データ
  Private myXobjChart As Object, myXstrShtName As String
  
'//モジュール内変数_制御信号
  Private myXbisExitFlag As Boolean

'iniP_モジュール内変数を初期化する
Private Sub initializeModuleVariables()
    myXbisExitFlag = False
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Get getOptnBase() As Long
  Dim myZlonTmp(1) As Long, L As Long: L = LBound(myZlonTmp)
    getOptnBase = L
End Property

Public Property Get getCmpltFlag() As Boolean
    getCmpltFlag = myXbisCmpltFlag
End Property

Public Property Get getSrsClctnCnt() As Long
    getSrsClctnCnt = myXlonSrsClctnCnt
End Property

Public Property Get getErrSrsClctnCnt() As Long
    getErrSrsClctnCnt = myXlonErrSrsClctnCnt
End Property

Public Property Get getErrSrsClctnAry(ByVal i As Long) As Object
    Set getErrSrsClctnAry = myZobjErrSrsClctn(i)
'myZobjErrSrsClctn(i) : エラー発生系列
End Property

'iniP_出力変数を初期化する
Private Sub initializeOutputVariables()
    myXbisCmpltFlag = False
    myXlonSrsClctnCnt = Empty: myXlonErrSrsClctnCnt = Empty: Erase myZobjErrSrsClctn
End Sub

'-----------------------------------------------------------------------------------------------

Public Property Set setChart(ByVal RHS As Object)
    Set myXobjChart = RHS
End Property

Public Property Let letShtName(ByVal RHS As String)
    myXstrShtName = RHS
End Property

'checkP_入力変数内容を確認する
Private Sub checkInputVariables()
    myXbisExitFlag = False
    
    If myXobjChart Is Nothing Then GoTo ExitPath
    If myXstrShtName = "" Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'-----------------------------------------------------------------------------------------------

'PublicP_
Public Sub exeProc()
    Call ctrProc
End Sub

'CtrlP_
Private Sub ctrProc()
    Call initializeOutputVariables
    Call initializeModuleVariables
    Call checkInputVariables: If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "1"     'PassFlag

'//エクセルグラフ内の全系列に対して処理を実行
    Call PabsSeriesCollectionOperation( _
            myXbisExitFlag, _
            myXlonSrsClctnCnt, myXlonErrSrsClctnCnt, myZobjErrSrsClctn, _
            myXobjChart)
    If myXbisExitFlag = True Then GoTo ExitPath
'    Debug.Print "PassFlag: " & meMstrMdlName & "2"     'PassFlag
    
    myXbisCmpltFlag = True
ExitPath:
    Call initializeModuleVariables
End Sub

 '抽象Ｐ_エクセルグラフ内の全系列に対して処理を実行する
Private Sub PabsSeriesCollectionOperation( _
            myXbisExitFlag As Boolean, myXlonSrsClctnCnt As Long, _
            myXlonErrSrsClctnCnt As Long, myZobjErrSrsClctn() As Object, _
            ByVal myXobjChart As Object)
    myXlonSrsClctnCnt = Empty: myXlonErrSrsClctnCnt = Empty: Erase myZobjErrSrsClctn
    On Error GoTo ExitPath
  Dim k As Long: k = myXobjChart.SeriesCollection.Count
    If k <= 0 Then GoTo ExitPath
    On Error GoTo 0
  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
  Dim myXobjSrsClctn As Object, n As Long, e As Long: n = 0: e = Lo - 1
    For Each myXobjSrsClctn In myXobjChart.SeriesCollection
        Call PsubSeriesCollectionOperation(myXbisExitFlag, myXobjSrsClctn)
        If myXbisExitFlag = True Then
            e = e + 1: ReDim Preserve myZobjErrSrsClctn(e) As Object
            Set myZobjErrSrsClctn(e) = myXobjSrsClctn
        Else
            n = n + 1
        End If
    Next myXobjSrsClctn
    myXlonSrsClctnCnt = n: myXlonErrSrsClctnCnt = e - Lo + 1
    If myXlonErrSrsClctnCnt >= 1 Then
        myXbisExitFlag = True
    Else
        myXbisExitFlag = False
    End If
    Set myXobjSrsClctn = Nothing
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub
Private Sub PsubSeriesCollectionOperation(myXbisExitFlag As Boolean, _
            ByVal myXobjSrsClctn As Object)
    myXbisExitFlag = False
'//グラフ内の全系列に対する処理
    
'//グラフの指定系列の参照範囲を取得
  Dim myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String
    Call PfixGetSeriesCollectionFormula(myXstrSrsFrmlA1, myXstrSrsFrmlR1C1, myXobjSrsClctn)
    If myXstrSrsFrmlA1 = "" Or myXstrSrsFrmlR1C1 = "" Then GoTo ExitPath
    
'//散布図グラフのデータ系列の参照式からセル範囲アドレスを取得
  Dim myXstrSrsFrml As String
    If Application.ReferenceStyle = xlA1 Then
        myXstrSrsFrml = myXstrSrsFrmlA1
    Else
        myXstrSrsFrml = myXstrSrsFrmlR1C1
    End If
  Dim myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
        myXlonSrsClctnNo As Long
    Call PfixGetDataRangeAddressInXYScatter( _
            myXstrNameAdrs, myXstrXvalAdrs, myXstrYvalAdrs, myXlonSrsClctnNo, _
            myXstrSrsFrml)
    If myXstrNameAdrs = "" Or myXstrXvalAdrs = "" Or myXstrYvalAdrs = "" Then GoTo ExitPath
    
'//セル範囲アドレスのパス[ブック]シート情報を指定シート名に置換
  Dim myXstrNewNameAdrs As String, myXstrNewXvalAdrs As String, myXstrNewYvalAdrs As String
    myXstrNewNameAdrs = PfncstrReplacePathBookSheetToSheet(myXstrNameAdrs, myXstrShtName)
    If myXstrNewNameAdrs = "" Then GoTo ExitPath
    myXstrNewXvalAdrs = PfncstrReplacePathBookSheetToSheet(myXstrXvalAdrs, myXstrShtName)
    If myXstrNewXvalAdrs = "" Then GoTo ExitPath
    myXstrNewYvalAdrs = PfncstrReplacePathBookSheetToSheet(myXstrYvalAdrs, myXstrShtName)
    If myXstrNewYvalAdrs = "" Then GoTo ExitPath
    
'//散布図グラフの指定データ系列設定用のSERIES関数式を設定
  Dim myXstrNewSrsFrml As String
    myXstrNewSrsFrml = PfncstrSetSeriesFormulaInXYScatter( _
                        myXlonSrsClctnNo, myXstrNewNameAdrs, myXstrNewXvalAdrs, myXstrNewYvalAdrs)
    If myXstrNewSrsFrml = "" Then GoTo ExitPath

'//散布図グラフの指定データ系列にSERIES関数でデータ参照範囲を設定
    Call PfixSetSeriesCollectionFormulaInXYScatter(myXbisExitFlag, myXobjSrsClctn, myXstrNewSrsFrml)
    If myXbisExitFlag = True Then GoTo ExitPath
    
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'===============================================================================================

 '定型Ｐ_グラフの指定系列の参照範囲を取得する
Private Sub PfixGetSeriesCollectionFormula( _
            myXstrSrsFrmlA1 As String, myXstrSrsFrmlR1C1 As String, _
            ByVal myXobjSrsClctn As Object)
    myXstrSrsFrmlA1 = Empty: myXstrSrsFrmlR1C1 = Empty
    On Error GoTo ExitPath
    myXstrSrsFrmlA1 = myXobjSrsClctn.Formula
    myXstrSrsFrmlR1C1 = myXobjSrsClctn.FormulaR1C1
    On Error GoTo 0
ExitPath:
End Sub

 '定型Ｐ_散布図グラフのデータ系列の参照式からセル範囲アドレスを取得する
Private Sub PfixGetDataRangeAddressInXYScatter( _
            myXstrNameAdrs As String, myXstrXvalAdrs As String, myXstrYvalAdrs As String, _
            myXlonSrsClctnNo As Long, _
            ByVal myXstrSrsFrml As String)
'objSrsClctn.Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    myXstrNameAdrs = Empty: myXstrXvalAdrs = Empty: myXstrYvalAdrs = Empty
    If myXstrSrsFrml = "" Then Exit Sub
'//参照式からアドレス部分を取得
  Dim myXlonBgn As Long, myXlonEnd As Long, myXlonLen As Long, myXstrSndwchd As String
    myXlonBgn = InStr(myXstrSrsFrml, "(") + 1
    myXlonEnd = InStrRev(myXstrSrsFrml, ")") - 1
    myXlonLen = myXlonEnd - myXlonBgn + 1
    If myXlonBgn <= 0 Or myXlonEnd <= 0 Or myXlonLen < 0 Then Exit Sub
    myXstrSndwchd = Mid(myXstrSrsFrml, myXlonBgn, myXlonLen)
'//項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号を取得
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long
    myZvarSplt = Split(myXstrSndwchd, ",")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
  Dim myXstrSCNo As String
    If myXlonSpltCnt = 4 Then
        myXstrNameAdrs = myZvarSplt(0)
        myXstrXvalAdrs = myZvarSplt(1)
        myXstrYvalAdrs = myZvarSplt(2)
        myXstrSCNo = myZvarSplt(3)
    Else
        Exit Sub
    End If
    If IsNumeric(myXstrSCNo) = True Then myXlonSrsClctnNo = CLng(myXstrSCNo)
    myZvarSplt = Empty
End Sub

 '定型Ｆ_セル範囲アドレスのパス[ブック]シート情報を指定シート名に置換する
Private Function PfncstrReplacePathBookSheetToSheet( _
            ByVal myXstrRngAdrsWEx As String, ByVal myXstrShtName As String) As String
'objRange.Address(external:=True) : 'パス\[ブック]シート'!セル範囲
    PfncstrReplacePathBookSheetToSheet = Empty
    If myXstrRngAdrsWEx = "" Then Exit Function
    If myXstrShtName = "" Then Exit Function
  Dim myXstrPathBkSht As String, myXstrNewAdrs As String
'//パス[ブック]シート部分とセル範囲部分を分離
  Dim myZvarSplt As Variant, myXlonSpltCnt As Long, myXstrTmp As String
    myZvarSplt = Split(myXstrRngAdrsWEx, "!")
    myXlonSpltCnt = UBound(myZvarSplt) - LBound(myZvarSplt) + 1
    If myXlonSpltCnt = 2 Then
        myXstrPathBkSht = myZvarSplt(0)
    Else
        Exit Function
    End If
'//パス[ブック]シート部分を置換
    myXstrNewAdrs = Replace(myXstrRngAdrsWEx, myXstrPathBkSht, myXstrShtName)
    PfncstrReplacePathBookSheetToSheet = myXstrNewAdrs
End Function

 '定型Ｆ_散布図グラフの指定データ系列設定用のSERIES関数式を設定する
Private Function PfncstrSetSeriesFormulaInXYScatter( _
            ByVal myXlonSrsClctnNo As Long, _
            ByVal myXstrNameAdrs As String, _
            ByVal myXstrXvalAdrs As String, ByVal myXstrYvalAdrs As String) As String
'Formula =“=SERIES(項目名,項目軸ラベルのセル範囲,グラフデータのセル範囲,系列番号)”
    PfncstrSetSeriesFormulaInXYScatter = Empty
  Dim myXstrSrsFrml As String
    myXstrSrsFrml = myXstrNameAdrs & "," & myXstrXvalAdrs & "," & _
                    myXstrYvalAdrs & "," & myXlonSrsClctnNo
    myXstrSrsFrml = "=SERIES(" & myXstrSrsFrml & ")"
    PfncstrSetSeriesFormulaInXYScatter = myXstrSrsFrml
ExitPath:
End Function

 '定型Ｐ_散布図グラフの指定データ系列にSERIES関数でデータ参照範囲を設定する
Private Sub PfixSetSeriesCollectionFormulaInXYScatter( _
            myXbisExitFlag As Boolean, _
            ByVal myXobjSrsClctn As Object, ByVal myXstrSrsFrml As String)
    myXbisExitFlag = False
    If myXobjSrsClctn Is Nothing Then Exit Sub
    If myXstrSrsFrml = "" Then Exit Sub
    On Error GoTo ExitPath
    If Application.ReferenceStyle = xlA1 Then
        myXobjSrsClctn.Formula = myXstrSrsFrml
    Else
        myXobjSrsClctn.FormulaR1C1 = myXstrSrsFrml
    End If
    On Error GoTo 0
    Exit Sub
ExitPath:
    myXbisExitFlag = True
End Sub

'DummyＰ_
Private Sub CsubDummy()
End Sub

'===============================================================================================

''◆ClassProc名_グラフの全系列に対してパス[ブック]シート情報を指定シート名に置換する
'Private Sub instCChrtRngAdrsWExToShtName()
'  Dim Lc As Long
'  Dim myXbisCmpltFlag As Boolean
'  Dim myXlonSrsClctnCnt As Long, myXlonErrSrsClctnCnt As Long, myZobjErrSrsClctn() As Object
'    'myZobjErrSrsClctn(i) : エラー発生系列
''//クラス内変数への入力
'  Dim myXobjChart As Object
'    Set myXobjChart = ActiveChart
'  Dim myXstrShtName As String
'    myXstrShtName = myXobjChart.Parent.Parent.Name
''//CChrtRngAdrsWExToShtNameクラスモジュールのインスタンス生成
'  Dim myZlonTmp(1) As Long, Lo As Long: Lo = LBound(myZlonTmp)
'  Dim i As Long
'  Dim myXinsChrtRngAdrsWExToShtName As CChrtRngAdrsWExToShtName
'    Set myXinsChrtRngAdrsWExToShtName = New CChrtRngAdrsWExToShtName
'    With myXinsChrtRngAdrsWExToShtName
'    '//クラス内変数への入力
'        Set .setChart = myXobjChart
'        .letShtName = myXstrShtName
'    '//クラス内プロシージャの実行とクラス内変数からの出力
'        .exeProc
'        myXbisCmpltFlag = .getCmpltFlag
'        myXlonSrsClctnCnt = .getSrsClctnCnt
'        myXlonErrSrsClctnCnt = .getErrSrsClctnCnt
'        If myXlonErrSrsClctnCnt <= 0 Then GoTo JumpPath
'        i = myXlonErrSrsClctnCnt + Lo - 1
'        ReDim myZobjErrSrsClctn(i) As Object
'        Lc = .getOptnBase
'        For i = 1 To myXlonErrSrsClctnCnt
'            Set myZobjErrSrsClctn(i + Lo - 1) = .getErrSrsClctnAry(i + Lc - 1)
'        Next i
'    End With
'JumpPath:
'    Set myXinsChrtRngAdrsWExToShtName = Nothing
''    Call variablesCChrtRngAdrsWExToShtName(myXlonErrSrsClctnCnt, myZobjErrSrsClctn) 'Debug.Print
'End Sub
'Private Sub variablesCChrtRngAdrsWExToShtName( _
'            ByVal myXlonDataCnt As Long, ByRef myZvarField As Variant)
''//CChrtRngAdrsWExToShtNameクラス内から出力した変数の内容確認
'    Debug.Print "データ数: " & myXlonDataCnt
'    If myXlonDataCnt <= 0 Then Exit Sub
'  Dim k As Long
'    For k = LBound(myZvarField) To UBound(myZvarField)
'        Debug.Print "データ" & k & ": " & myZvarField(k).Formula
'    Next k
'End Sub

      <br></pre>
    </body>
  </html>
